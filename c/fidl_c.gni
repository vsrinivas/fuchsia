# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/fidl/toolchain.gni")

# C simple client bindings for a FIDL library.
#
# The parameters for this template are defined in //build/fidl/fidl.gni. The
# relevant parameters in this template are:
#   - name;

template("fidl_c_client") {
  assert(is_fuchsia, "This template can only be used in $target_toolchain.")

  not_needed(invoker, [ "sources" ])

  main_target_name = target_name

  library_name = target_name
  if (defined(invoker.name)) {
    library_name = invoker.name
  }

  c_stem = exec_script("//build/fidl/c_stem_from_library.py",
                       [
                         "--name",
                         library_name,
                       ],
                       "trim string")
  fidl_target_gen_dir =
      get_label_info(":$target_name($fidl_toolchain)", "target_gen_dir")
  fidl_root_gen_dir =
      get_label_info(":$target_name($fidl_toolchain)", "root_gen_dir")
  c_header = "$fidl_root_gen_dir/$c_stem.h"
  c_client = "$fidl_root_gen_dir/$c_stem.client.c"
  coding_tables = "$fidl_target_gen_dir/$target_name.fidl.tables.cc"

  # The C simple client code is generated by the frontend, so we just need to
  # produce a target with the generated file name and configuration information.
  source_set(main_target_name + "_c_client") {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "visibility",
                           ])

    sources = [
      c_client,
      coding_tables,
    ]
    public = [
      c_header,
    ]

    # Let dependencies use `#include "$file_stem.h"`.
    public_configs = [ "//build/cpp:fidl_gen_config" ]

    deps = [
      ":${main_target_name}_compile($fidl_toolchain)",
    ]
    public_deps = [
      "//zircon/public/lib/fidl",
    ]
    libs = [ "zircon" ]
  }
}
