# Copyright 2016 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//gnbuild/config.gni")

# Generate a raw binary file for a given file.
#
# Parameters
#
#   input (required)
#     Input file name
#
#   output (required)
#     Output file name
#
#   deps (optional)
template("convert_to_raw") {
  assert(defined(invoker.input), "must specify \"input\" value")
  assert(defined(invoker.output), "must specify \"output\" value")
  forward_variables_from(invoker,
                         [
                           "input",
                           "output",
                         ])

  action(target_name) {
    forward_variables_from(invoker, [ "deps" ])

    script = "//gnbuild/convert_to_raw.sh"
    sources = [
      input,
    ]
    outputs = [
      output,
    ]

    args = [
      toolprefix,
      rebase_path(input),
      rebase_path(output),
    ]
  }
}
