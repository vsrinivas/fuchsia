# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/fidl_cpp.gni")
import("//build/fidl/fidl_library.gni")
import("//build/fidl/toolchain.gni")

# Declares a FIDL library.
#
# Depending on the toolchain in which this targets is expanded, it will yield
# different results:
#   - in the FIDL toolchain, it will compile its source files into an
#     intermediate representation consumable by language bindings generators;
#   - in the target or shared toolchain, this will produce a source_set
#     containing C++ bindings.
#
# Parameters
#
#   sources (required)
#     List of paths to library source files.
#
#   name (optional)
#     Name of the library.
#     Defaults to the target's name.

template("fidl") {

if (current_toolchain == fidl_toolchain) {

fidl_library(target_name) {
  forward_variables_from(invoker, "*")
}

} else if (current_toolchain == target_toolchain ||
    current_toolchain == shlib_toolchain) {

fidl_cpp(target_name) {
  forward_variables_from(invoker, "*")
}

} else {
  assert(false, "Unable to process FIDL target in toolchain $current_toolchain")
}

}
