# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/fidl/fidl.gni")
import("//build/test.gni")

if (is_fuchsia) {
  group("test") {
    testonly = true
    deps = [ ":service-llcpp-test" ]
  }

  test("service-llcpp-test") {
    configs += [ "//build/unification/config:zircon-migrated" ]
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
    sources = [
      "llcpp/client_test.cc",
      "llcpp/server_test.cc",
    ]
    deps = [
      ":fidl.service.test_llcpp",
      "//sdk/lib/fdio",
      "//zircon/public/lib/zxtest",
      "//zircon/system/ulib/async-loop:async-loop-cpp",
      "//zircon/system/ulib/async-loop:async-loop-default",
      "//zircon/system/ulib/fidl-async",
      "//zircon/system/ulib/fidl-async:fidl-async-cpp",
      "//zircon/system/ulib/fs",
      "//zircon/system/ulib/service:service-llcpp",
    ]
  }
}

# Generate C bindings that the LLCPP bindings depend on.
fidl("fidl.service.test") {
  testonly = true
  visibility = [ ":*" ]
  sources = [ "test.test.fidl" ]
}
