# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Common pieces.
source_set("common") {
  testonly = true
  visibility = [ ":*" ]
  sources = [
    "engine_tests.cc",
    "event_tests.c",
    "event_tests.cc",
    "event_tests_ntrace.c",
    "event_tests_ntrace.cc",
    "fields_tests.cc",
    "no_optimization.c",
    "record_tests.cc",
  ]
  deps = [
    "$zx/system/ulib/async",
    "$zx/system/ulib/async:async-cpp",
    "$zx/system/ulib/async:async-default",
    "$zx/system/ulib/async-loop",
    "$zx/system/ulib/async-loop:async-loop-cpp",
    "$zx/system/ulib/async-loop:async-loop-default.static",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/trace",

    # The engine is chosen by the binary, we just need engine headers here.
    "$zx/system/ulib/trace-engine:headers",
    "$zx/system/ulib/trace-reader",
    "$zx/system/ulib/trace-test-utils",
    "$zx/system/ulib/unittest",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",

    # fdio is here so that things like printf work.
    # Otherwise they silently fail (output is dropped).
    "$zx/system/ulib/fdio",
  ]

  # TODO(41900): UBSan has found an instance of undefined behavior in this target.
  # Disable UBSan for this target temporarily until it is migrated into CI/CQ.
  configs +=
      [ "$zx_build/public/gn/config:temporarily_disable_ubsan_do_not_use" ]
}

# Version of test that uses libtrace-engine.so.
zx_test("trace") {
  deps = [
    ":common",
    "$zx/system/ulib/trace-engine",
  ]
  data_deps = [
    ":trace-static-engine",
  ]
}

# And again using libtrace-engine.a.
zx_test("trace-static-engine") {
  deps = [
    ":common",
    "$zx/system/ulib/trace-engine:trace-engine-static",
  ]
}
