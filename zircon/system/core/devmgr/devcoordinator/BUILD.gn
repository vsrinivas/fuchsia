# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

executable("devcoordinator") {
  sources = [
    "main.cc",
  ]

  deps = [
    ":common",
    "$zx/system/fidl/fuchsia-boot:c",
    "$zx/system/ulib/devmgr-launcher",
    "../shared",
  ]

  # Bring the auxiliaries into the build and the image.  devhost isn't
  # listed here because it's implicitly in data_deps of every driver().
  data_deps = [
    ":internal-drivers",
    "../fshost",
    "$zx/system/core/miscsvc",
    "$zx/system/core/netsvc",
    "$zx/system/core/svchost",
    "$zx/system/core/virtcon:virtual-console",
    "$zx/system/core/pwrbtn-monitor",
  ]
}

source_set("internal-drivers") {
  deps = [
    "$zx/system/core/devmgr/component",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  sources = [
    "binding-internal.h",
    "binding.cc",
    "boot-args.cc",
    "boot-args.h",
    "composite-device.cc",
    "coordinator.cc",
    "coordinator.h",
    "devfs.cc",
    "devfs.h",
    "devhost-loader-service.cc",
    "devhost-loader-service.h",
    "device.cc",
    "device.h",
    "driver-test-reporter.cc",
    "driver-test-reporter.h",
    "driver.cc",
    "driver.h",
    "fidl.cc",
    "suspend-task.cc",
    "task.cc",
    "unbind-task.cc",
    "vmo-writer.cc",
    "vmo-writer.h",
  ]
  public_deps = [
    "$zx/system/fidl/fuchsia-boot:c",
    "$zx/system/fidl/fuchsia-device-manager:c",
    "$zx/system/fidl/fuchsia-driver-test:c",
    "$zx/system/fidl/fuchsia-io:c",
    "$zx/system/fidl/fuchsia-mem:c",
    "$zx/system/ulib/async-loop:async-loop-cpp",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/driver-info",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fdio",
    "$zx/system/ulib/fidl",
    "$zx/system/ulib/fit",
    "$zx/system/ulib/fs",
    "$zx/system/ulib/fzl",
    "$zx/system/ulib/launchpad",
    "$zx/system/ulib/libzbi",
    "$zx/system/ulib/loader-service",
    "$zx/system/ulib/memfs",
    "$zx/system/ulib/svc",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zircon-internal",
    "$zx/system/ulib/zx",
  ]

  deps = [
    "$zx/system/ulib/fidl-async",
  ]
}

test("devcoordinator-test") {
  output_name = "devcoordinator-test"
  test_group = "ddk"
  sources = [
    "binding-test.cc",
    "boot-args-test.cc",
    "coordinator-test.cc",
    "task-test.cc",
  ]
  deps = [
    ":common",
    "$zx/system/ulib/zxtest",
    "../shared:env",
  ]
}
