# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

source_set("fshost-registry") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "admin-server.cc",
    "fs-manager.cc",
    "registry.cc",
    "registry_vnode.cc",
  ]
  public_deps = [
    ":metrics",
    "../shared",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/memfs",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-fshost:llcpp",
  ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/loader-service",
    "//zircon/public/lib/memfs-cpp",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/fidl/fuchsia-boot:llcpp",
  ]
}

source_set("metrics") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "metrics.cc",
    "metrics.h",
  ]
  public_deps = [
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/cobalt-client",
    "//zircon/public/lib/metrics-cobalt",
  ]
  deps = [
    "//zircon/public/lib/cobalt-client",
    "//zircon/public/lib/metrics-cobalt",
  ]
}

source_set("block-watcher") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "block-device.cc",
    "block-watcher.cc",
    "encrypted-volume.cc",
    "filesystem-mounter.cc",
    "fshost-fs-provider.cc",
    "pkgfs-launcher.cc",
  ]
  public_deps = [
    ":metrics",
    "../shared",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fdio-caller",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/memfs",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-fshost:llcpp",
  ]
  deps = [
    ":fshost-registry",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/fs-management",
    "//zircon/public/lib/gpt",
    "//zircon/public/lib/loader-service",
    "//zircon/public/lib/minfs",
    "//zircon/public/lib/trace",
    "//zircon/public/lib/trace-engine",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxcrypt-fdio",
    "//zircon/system/fidl/fuchsia-boot:c",
    "//zircon/system/fidl/fuchsia-boot:llcpp",
    "//zircon/system/fidl/fuchsia-device:c",
    "//zircon/system/fidl/fuchsia-device:llcpp",
    "//zircon/system/fidl/fuchsia-hardware-block:c",
    "//zircon/system/fidl/fuchsia-hardware-block-encrypted:c",
    "//zircon/system/fidl/fuchsia-hardware-block-partition:c",
  ]
}

executable("fshost") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "main.cc" ]
  deps = [
    ":block-watcher",
    ":fshost-registry",
    ":metrics",
    "//zircon/public/lib/cobalt-client",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/fs-management",
    "//zircon/public/lib/hermetic-decompressor",
    "//zircon/public/lib/loader-service",
    "//zircon/public/lib/ramdevice-client",
    "//zircon/system/fidl/fuchsia-boot:c",
    "//zircon/system/fidl/fuchsia-boot:llcpp",
    "//zircon/system/fidl/fuchsia-fshost:c",
    "//zircon/system/fidl/fuchsia-fshost:llcpp",
    "//zircon/system/fidl/fuchsia-ldsvc:c",
  ]
}

test("fshost-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  if (is_fuchsia) {
    testonly = false
  }
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  test_group = "ddk"
  sources = [
    "delayed-outdir-test.cc",
    "fshost-boot-args-test.cc",
    "fshost-test.cc",
  ]
  deps = [
    ":block-watcher",
    ":fshost-registry",
    ":metrics",
    "//zircon/public/lib/cobalt-client",
    "//zircon/public/lib/fidl-async",
    "//zircon/public/lib/in-memory-logger",
    "//zircon/public/lib/loader-service",
    "//zircon/public/lib/mock-boot-arguments",
    "//zircon/public/lib/zxtest",
    "//zircon/system/fidl/fuchsia-boot:llcpp",
    "//zircon/system/fidl/fuchsia-io:c",
  ]

  # TODO(46432): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

test("fshost-metrics-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  if (is_fuchsia) {
    testonly = false
  }
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  test_group = "ddk"
  sources = [ "metrics-test.cc" ]
  deps = [
    ":metrics",
    "//zircon/public/lib/async-testing",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/in-memory-logger",
    "//zircon/public/lib/metrics-cobalt",
    "//zircon/public/lib/zxtest",
    "//zircon/system/fidl/fuchsia-boot:llcpp",
  ]
}

test("block-watcher-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  if (is_fuchsia) {
    testonly = false
  }
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  test_group = "ddk"
  sources = [
    "block-device-test.cc",
    "block-watcher-test.cc",
    "filesystem-mounter-test.cc",
  ]
  deps = [
    ":block-watcher",
    "//zircon/public/lib/fidl-async",
    "//zircon/public/lib/fs-management",
    "//zircon/public/lib/in-memory-logger",
    "//zircon/public/lib/loader-service",
    "//zircon/public/lib/metrics-cobalt",
    "//zircon/public/lib/minfs",
    "//zircon/public/lib/ramdevice-client",
    "//zircon/public/lib/zxtest",
    "//zircon/system/fidl/fuchsia-boot:llcpp",
  ]
}

migrated_manifest("fshost-manifest") {
  deps = [ ":fshost" ]
}

migrated_manifest("fshost-test-manifest") {
  deps = [ ":fshost-test" ]
}

migrated_manifest("fshost-metrics-test-manifest") {
  deps = [ ":fshost-metrics-test" ]
}

migrated_manifest("block-watcher-test-manifest") {
  deps = [ ":block-watcher-test" ]
}
