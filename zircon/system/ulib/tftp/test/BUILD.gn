# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//build/testing/bootfs_test.gni")

group("tests") {
  testonly = true
  deps = [ ":tftp-fuzzers" ]
}

test("tftp-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "tftp-test"
  sources = [
    "tftp-file-test.cc",
    "tftp-test.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/tftp",
  ]
  include_dirs = [ ".." ]

  # TODO(fxbug.dev/47447): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

bootfs_test("tftp-test-bootfs-test") {
  name = "tftp-test"
  deps = [ ":tftp-test" ]
}

fuzzer("tftp-fuzzer") {
  sources = [ "tftp-fuzztest.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/tftp",
  ]
  include_dirs = [ ".." ]
}

fuzzers_package("tftp-fuzzers") {
  cpp_fuzzers = [ ":tftp-fuzzer" ]
}
