# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

group("test") {
  testonly = true
  deps = [
    ":hid-parser",
    ":hid-parser-fuzzer",
  ]
}

test("hid-parser") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "hid-descriptor-test.cc",
    "hid-helper-test.cc",
    "hid-parser-test.cc",
    "hid-report-data.cc",
    "hid-units-test.cc",
    "hid-utest-data.cc",
  ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/hid-parser",
  ]

  # TODO(47348): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

fuzzer("hid-parser-fuzzer") {
  sources = [ "hid-parser-fuzztest.cc" ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/hid-parser",
  ]
}

unittest_package("hid-parser-package") {
  package_name = "hid-parser"
  deps = [ ":hid-parser" ]

  tests = [
    {
      name = "hid-parser"
      dest = "hid-parser-test"
    },
  ]
}
