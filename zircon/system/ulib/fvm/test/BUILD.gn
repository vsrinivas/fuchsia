# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/unification/zbi/bootfs_test.gni")

group("test") {
  testonly = true
  deps = [ ":fvm-unit" ]
}

test("fvm-unit") {
  output_name = "fvm-unit-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "format-info-test.cc",
    "integrity-validation-test.cc",
    "partition-entry-test.cc",
    "slice-entry-test.cc",
  ]
  deps = [
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fvm",
  ]
  if (is_fuchsia) {
    sources += [ "client-test.cc" ]
    deps += [
      "//sdk/lib/fdio",
      "//sdk/lib/syslog/cpp:backend_legacy",
      "//zircon/public/lib/zx",
      "//zircon/system/ulib/block-client",
      "//zircon/system/ulib/fdio-caller",
    ]
  }
}

bootfs_test("fvm-unit-bootfs-test") {
  name = "fvm-unit-test"
  deps = [ ":fvm-unit" ]
}
