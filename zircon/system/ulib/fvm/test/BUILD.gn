# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//src/sys/build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":test-package" ]
}

test("fvm-unit") {
  output_name = "fvm-unit-test"
  sources = [
    "format-info-test.cc",
    "integrity-validation-test.cc",
    "partition-entry-test.cc",
    "slice-entry-test.cc",
  ]
  deps = [
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fvm",
  ]
  if (is_fuchsia) {
    sources += [ "client-test.cc" ]
    deps += [
      "//sdk/lib/fdio",
      "//zircon/public/lib/zx",
      "//zircon/system/ulib/block-client",
      "//zircon/system/ulib/fdio-caller",
    ]
  }
}

fuchsia_component("fvm-unit-test-component") {
  component_name = "fvm-unit-test"
  testonly = true

  manifest = "fvm-unit-test.cmx"

  deps = [ ":fvm-unit" ]
}

fuchsia_test_package("test-package") {
  package_name = "fvm-tests"
  test_components = [ ":fvm-unit-test-component" ]
}
