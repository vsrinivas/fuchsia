# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36548.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/unification/zx_library.gni")

zx_library("fvm-host") {
  sdk = "source"
  sdk_headers = [
    "fvm-host/file-wrapper.h",
    "fvm-host/container.h",
    "fvm-host/format.h",
    "fvm-host/fvm-info.h",
    "fvm-host/sparse-paver.h",
  ]
  host = true
  sources = [
    "container/container.cc",
    "container/fvm.cc",
    "container/sparse.cc",
    "file-wrapper.cc",
    "format.cc",
    "format/blobfs.cc",
    "format/format.cc",
    "format/minfs.cc",
    "fvm-info.cc",
    "sparse-paver.cc",
  ]
  public_deps = [
    # <fvm/format.h> has #include <fs-management/mount.h> though that
    # library is not really meant for host code.
    "//zircon/system/ulib/fs-management",

    # <fvm-host/format.h> has #include <blobfs/format.h>.
    "//src/storage/blobfs",

    # <fvm-host/format.h> has #include <minfs/bcache.h>.
    "//src/storage/minfs",

    # <fvm-host/container.h> has #include <fvm/sparse-reader.h>.
    "//zircon/system/ulib/fvm",
  ]
  deps = [
    "//src/storage/blobfs",
    "//src/storage/minfs",
    "//third_party/boringssl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/lz4",
    "//zircon/public/lib/safemath",
    "//zircon/system/ulib/digest",
    "//zircon/system/ulib/fs",
    "//zircon/system/ulib/fs/transaction",
    "//zircon/system/ulib/fvm",
    "//zircon/third_party/ulib/cksum",
  ]
}

test("fvm-host-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "fvm-host-test.cc" ]
  deps = [
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fvm",
    "//zircon/system/ulib/fvm-host",
  ]
}
