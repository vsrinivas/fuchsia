# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

zx_library("zxcrypt-dev") {
  static = true
  sdk = true
  sdk_headers = [
    "zxcrypt/ddk-volume.h",
    "zxcrypt/volume.h",
  ]
  sources = [
    "ddk-volume.cc",
    "volume.cc",
  ]

  public_deps = [
    # <zxcrypt/volume.h> has #include <crypto/aead.h> (among others).
    "$zx/system/ulib/zircon-crypto:zircon-crypto-static.headers",

    # <zxcrypt/ddk-volume.h> has #include <ddk/device.h>.
    "$zx/system/ulib/ddk:headers",
  ]

  configs += [ "$zx_build/public/gn/config:integer-paranoia" ]

  # To minimize the set of libraries that we depend on that need to be loaded
  # from /boot/lib in early boot, we statically link most of our dependencies.
  deps = [
    "$zx/system/banjo/ddk.protocol.block",
    "$zx/system/banjo/ddk.protocol.block.volume",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/driver",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fs-management:headers",  # only used for zxcrypt_magic
    "$zx/system/ulib/sync",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zircon-crypto:zircon-crypto-static",
    "$zx/system/ulib/zircon-internal",
    "$zx/system/ulib/zx",
    "$zx/third_party/ulib/boringssl",
  ]
  configs += [ "$zx_build/public/gn/config:static-libc++" ]
}

zx_library("zxcrypt-fdio") {
  shared = true
  sdk = true
  sdk_headers = [
    "zxcrypt/fdio-volume.h",
    "zxcrypt/volume.h",
  ]
  sources = [
    "fdio-volume.cc",
    "volume.cc",
  ]
  public_deps = [
    # <zxcrypt/fdio-volume.h> has #include <lib/fzl/fdio.h>
    # <zxcrypt/volume.h> has #include <crypto/aead.h> (among others).
    "$zx/system/ulib/fzl:headers",
    "$zx/system/ulib/zircon-crypto:headers",
  ]
  configs += [ "$zx_build/public/gn/config:integer-paranoia" ]
  deps = [
    "$zx/system/fidl/fuchsia-device:llcpp",
    "$zx/system/fidl/fuchsia-hardware-block:c",
    "$zx/system/fidl/fuchsia-hardware-block-encrypted:c",
    "$zx/system/fidl/fuchsia-hardware-block-volume:c",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fdio",
    "$zx/system/ulib/fs-management:headers",  # only used for zxcrypt_magic
    "$zx/system/ulib/fzl",
    "$zx/system/ulib/kms-stateless",
    "$zx/system/ulib/ramdevice-client:static",
    "$zx/system/ulib/sync",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zircon-crypto",
    "$zx/system/ulib/zircon-internal",
    "$zx/system/ulib/zx",
    "$zx/third_party/ulib/boringssl",
  ]
  configs += [ "$zx_build/public/gn/config:static-libc++" ]
}
