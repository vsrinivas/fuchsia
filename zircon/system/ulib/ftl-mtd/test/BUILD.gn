# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")

group("tests") {
  testonly = true
  if (current_os == "linux") {
    deps = [
      ":ftl-volume-wrapper($host_toolchain)",
      ":nand-volume-driver($host_toolchain)",
    ]
  }
}

source_set("common") {
  testonly = true
  sources = [ "fake-nand-interface.cc" ]
  deps = [ "//zircon/system/ulib/mtd" ]
}

test("nand-volume-driver") {
  # Restrict visbility to ensure OS is checked.
  visibility = [ ":tests" ]
  sources = [ "nand-volume-driver-tests.cc" ]
  deps = [
    ":common",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/ftl-mtd",
    "//zircon/system/ulib/mtd",
  ]
}

test("ftl-volume-wrapper") {
  # Restrict visbility to ensure OS is checked.
  visibility = [ ":tests" ]
  sources = [ "ftl-volume-wrapper-tests.cc" ]
  deps = [
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/ftl-mtd",
    "//zircon/system/ulib/fvm-host",
    "//zircon/system/ulib/mtd",
  ]
}
