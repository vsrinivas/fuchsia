# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/test/test_package.gni")

group("test") {
  testonly = true
  deps = [ ":gpt-unit" ]
}

test("gpt-unit") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "gpt-tests.cc",
    "main.cc",
  ]
  include_dirs = [
    "//zircon/system/ulib/gpt",
    "..",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//zircon/public/lib/cksum",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/gpt",
    "//zircon/system/ulib/mbr",
    "//zircon/system/ulib/ramdevice-client",
  ]
}

unittest_package("gpt-unit-package") {
  package_name = "gpt-unit"
  deps = [ ":gpt-unit" ]

  tests = [
    {
      name = "gpt-unit"
      dest = "gpt-unit-test"
    },
  ]
}
