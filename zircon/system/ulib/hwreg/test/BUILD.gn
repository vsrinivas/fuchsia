# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//src/sys/build/fuchsia_unittest_package.gni")

group("test") {
  testonly = true
  deps = [
    ":libhwreg",
    ":libhwreg-i2c",
  ]
}

test("libhwreg") {
  output_name = "libhwreg-test"
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "asm-test.cc",
    "bitfields-test.cc",
    "indirect-test.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/hwreg-mock",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

test("libhwreg-i2c") {
  output_name = "libhwreg-i2c-test"
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "i2c-hwreg-test.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.i2c",
    "//sdk/lib/fdio",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/hwreg-i2c",
  ]
}

group("tests") {
  testonly = true

  # NOTE: The other tests in this file are zbi tests which are
  # included from //bundles/bringup:tests. The list below should
  # instead be reachable from //src:tests.
  deps = [
    ":libhwreg-i2c-test-pkg",
    ":libhwreg-test-pkg",
    "asm:tests",
  ]
}

fuchsia_unittest_package("libhwreg-test-pkg") {
  package_name = "libhwreg-test"
  deps = [ ":libhwreg" ]
}

fuchsia_unittest_package("libhwreg-i2c-test-pkg") {
  package_name = "libhwreg-i2c-test"
  deps = [ ":libhwreg-i2c" ]
}
