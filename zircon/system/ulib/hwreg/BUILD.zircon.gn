# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

zx_library("hwreg") {
  sdk = "source"
  sdk_headers = [
    "hwreg/asm.h",
    "hwreg/bitfields.h",
    "hwreg/indirect.h",
    "hwreg/internal.h",
    "hwreg/mmio.h",
    "hwreg/pio.h",
  ]
  kernel = true
  static = true
  host = true
  sources = [ "printers.cc" ]
  if (is_kernel) {
    # TODO(mcgrathr): remove old multiboot environment
    if (toolchain.environment != "multiboot") {
      public_deps = [
        "$zx/kernel/lib/ktl",
        "$zx/kernel/lib/libc",
      ]
    }
  } else {
    sources += [ "asm.cc" ]
    deps = [ "$zx/system/ulib/fit" ]
  }

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "$zx_build_config:Wno-conversion" ]
}

zx_library("hwreg-mock") {
  testonly = true
  sdk = "source"
  sdk_headers = [ "hwreg/mock.h" ]
  kernel = true
  static = true
  host = true
  sources = []
  deps = [ "$zx/system/ulib/mock-function" ]
  public_deps = [
    # <hwreg/mock.h> has #include <lib/mock-function/mock-function.h>.
    "$zx/system/ulib/mock-function:headers",
  ]
}
