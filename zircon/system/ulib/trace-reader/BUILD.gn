# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

zx_library("trace-reader") {
  sdk = "source"
  sdk_headers = [
    "trace-reader/file_reader.h",
    "trace-reader/reader.h",
    "trace-reader/reader_internal.h",
    "trace-reader/records.h",
  ]
  host = true
  sources = [
    "file_reader.cc",
    "reader.cc",
    "reader_internal.cc",
    "records.cc",
  ]
  public_deps = [
    # <trace-reader/records.h> has #include <trace-engine/types.h>.
    "$zx/system/ulib/trace-engine:trace-engine-headers-for-reader.headers",

    # <trace-reader/records.h> has #include <fbl/array.h>.
    "$zx/system/ulib/fbl:headers",

    # <trace-reader/records.h> has #include <lib/fit/variant.h>.
    "$zx/system/ulib/fit:headers",
  ]
  deps = [
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fit",
  ]

  # TODO(41892): UBSan has found an instance of undefined behavior in this target.
  # Disable UBSan for this target temporarily until it is migrated into CI/CQ.
  configs +=
      [ "$zx_build/public/gn/config:temporarily_disable_ubsan_do_not_use" ]
}

zx_test("trace-reader-test") {
  sources = [
    "file_reader_tests.cc",
    "reader_tests.cc",
    "records_tests.cc",
  ]
  deps = [
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fit",
    "$zx/system/ulib/trace-engine:trace-engine-headers-for-reader",
    "$zx/system/ulib/trace-reader",
    "$zx/system/ulib/zxtest",
  ]
  if (is_fuchsia) {
    deps += [
      "$zx/system/ulib/fdio",
      "$zx/system/ulib/zircon",
      "$zx/system/ulib/zx",
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":trace-reader-test",
  ]
}
