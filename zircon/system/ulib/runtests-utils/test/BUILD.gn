# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
group("test") {
  testonly = true
  deps = [ ":service-proxy-dir" ]
}

test("service-proxy-dir") {
  output_name = "service-proxy-dir-test"

  configs += [ "//build/unification/config:zircon-migrated" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  fdio_config = [ "//build/config/fuchsia:fdio_config" ]
  if (configs + fdio_config - fdio_config != configs) {
    configs -= fdio_config
  }

  sources = [ "service-proxy-dir.cc" ]
  deps = [
    ":fidl.test.echo_c",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//src/lib/storage/vfs/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/runtests-utils",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fidl("fidl.test.echo") {
  visibility = [ ":*" ]
  sources = [ "echo.test.fidl" ]
}

fuchsia_unittest_package("service-proxy-dir-test-pkg") {
  package_name = "service-proxy-dir-test"
  deps = [ ":service-proxy-dir" ]
}

group("tests") {
  testonly = true
  deps = [ ":service-proxy-dir-test-pkg" ]
}
