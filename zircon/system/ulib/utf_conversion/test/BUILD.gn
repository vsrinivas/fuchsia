# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//src/sys/build/fuchsia_unittest_package.gni")

group("tests") {
  testonly = true
  deps = [
    ":utf-conversion-fuzzers",
    ":utf-conversion-test-pkg",
  ]
}

test("utf_conversion-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "main.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/utf_conversion",
  ]
}

fuzzer("utf-conversion-fuzzer") {
  sources = [ "utf_conversion-fuzzer.cc" ]
  deps = [ "//zircon/system/ulib/utf_conversion" ]
}

fuzzers_package("utf-conversion-fuzzers") {
  cpp_fuzzers = [ ":utf-conversion-fuzzer" ]
}

fuchsia_unittest_package("utf-conversion-test-pkg") {
  package_name = "utf_conversion-test"
  deps = [ ":utf_conversion-test" ]
}
