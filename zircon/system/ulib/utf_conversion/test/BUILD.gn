# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//build/testing/bootfs_test.gni")

group("tests") {
  testonly = true
  deps = [ ":utf-conversion-fuzzers" ]
}

test("utf_conversion-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "main.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/utf_conversion",
  ]
}

bootfs_test("utf_conversion-test-bootfs-test") {
  name = "utf_conversion-test"
  deps = [ ":utf_conversion-test" ]
}

fuzzer("utf-conversion-fuzzer") {
  sources = [ "utf_conversion-fuzzer.cc" ]
  deps = [ "//zircon/system/ulib/utf_conversion" ]
}

fuzzers_package("utf-conversion-fuzzers") {
  cpp_fuzzers = [ ":utf-conversion-fuzzer" ]
}
