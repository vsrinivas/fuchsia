# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//src/sys/build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":digest-unit-tests" ]
}

test("digest") {
  output_name = "digest-unit-tests"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "digest.cc",
    "hash-list.cc",
    "merkle-tree.cc",
    "node-digest.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//third_party/boringssl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/digest",
  ]
}

fuchsia_component("digest-unit-tests-component") {
  component_name = "digest-unit-tests"
  testonly = true

  manifest = "digest-unit-tests.cmx"

  deps = [ ":digest" ]
}

fuchsia_test_package("digest-unit-tests") {
  test_components = [ ":digest-unit-tests-component" ]
}
