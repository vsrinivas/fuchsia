# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//src/sys/build/fuchsia_unittest_package.gni")

group("tests") {
  testonly = true
  deps = [
    ":backtrace-request-integration-test-pkg",
    ":backtrace-request-test-pkg",
  ]
}

test("backtrace-request-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "backtrace-request-test.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/backtrace-request:backtrace-request-utils",
  ]
}

fuchsia_unittest_package("backtrace-request-test-pkg") {
  package_name = "backtrace-request-test"
  executable_path = "test/backtrace-request-test"
  deps = [ ":backtrace-request-test" ]
}

# Integration test for requesting a backtrace.
#
# Related unittests exist alongside the backtrace and crashsvc code, but
# this verifies that the pieces are working together correctly.
test("backtrace-request-integration-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "backtrace-request-integration-test.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/backtrace-request",
  ]
}

fuchsia_unittest_package("backtrace-request-integration-test-pkg") {
  package_name = "backtrace-request-integration-test"
  executable_path = "test/backtrace-request-integration-test"
  deps = [ ":backtrace-request-integration-test" ]
}
