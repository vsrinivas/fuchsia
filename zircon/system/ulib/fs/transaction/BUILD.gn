# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36548.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/unification/zx_library.gni")

zx_library("transaction") {
  host = true
  sources = []
  static = true

  sdk = "source"
  sdk_headers = [
    "fs/transaction/buffered_operations_builder.h",
    "fs/transaction/transaction_handler.h",
    "fs/transaction/writeback.h",
  ]

  public_deps = [
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/storage/buffer",
    "//zircon/system/ulib/storage/operation",
  ]

  if (is_fuchsia) {
    sources = [
      "buffered_operations_builder.cc",
      "device_transaction_handler.cc",
      "legacy_transaction_handler.cc",
      "transaction_handler.cc",
      "writeback.cc",
    ]

    public_deps += [
      "//zircon/public/lib/fit",
      "//zircon/system/ulib/block-client",
    ]

    deps = [
      "//sdk/lib/fdio",
      "//zircon/public/lib/fbl",
      "//zircon/public/lib/zircon-internal",
      "//zircon/public/lib/zx",
      "//zircon/system/ulib/storage/operation",
    ]
  } else {
    sources = [
      "buffered_operations_builder.cc",
      "transaction_handler.cc",
    ]

    deps = [
      "//zircon/public/lib/fbl",
      "//zircon/system/ulib/storage/operation",
    ]
  }
}
