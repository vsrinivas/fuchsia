# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/testing/bootfs_test.gni")

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//src/sys/build/fuchsia_component.gni")
import("//src/sys/build/fuchsia_test_package.gni")

group("test") {
  testonly = true
  deps = [ ":journal-unit-tests" ]
}

test("journal-unit") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "background_executor_test.cc",
    "data_streamer_test.cc",
    "disk_struct_test.cc",
    "entry_view_test.cc",
    "header_view_test.cc",
    "inspector_parser_test.cc",
    "inspector_test.cc",
    "journal_test.cc",
    "operation_tracker_test.cc",
    "replay_test.cc",
    "replay_tree_test.cc",
    "superblock_test.cc",
  ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/safemath",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fs/journal",
    "//zircon/third_party/ulib/cksum",
  ]

  include_dirs = [ ".." ]
}

source_set("fuzz-utils") {
  sources = [ "fuzzer_utils.cc" ]
  public_deps = [
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fs/journal",
    "//zircon/third_party/ulib/cksum",
  ]
}

fuzzer("journal-fuzzer") {
  sources = [ "journal_fuzztest.cc" ]
  deps = [ ":fuzz-utils" ]
  include_dirs = [ "test" ]
}

fuzzer("journal-entry-fuzzer") {
  sources = [ "entry_parse_fuzztest.cc" ]
  deps = [ ":fuzz-utils" ]
  include_dirs = [ "test" ]
}

fuzzer("journal-replay-fuzzer") {
  sources = [ "replay_fuzztest.cc" ]
  deps = [ ":fuzz-utils" ]
  include_dirs = [ "test" ]
}

fuchsia_component("journal-unit-tests-component") {
  component_name = "journal-unit-tests"
  testonly = true
  manifest = "meta/journal-unit-tests.cml"
  deps = [ ":journal-unit" ]
}

fuchsia_test_package("journal-unit-tests") {
  test_components = [ ":journal-unit-tests-component" ]
}
