# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("$zx_build/public/gn/fuzzer.gni")

zx_library("journal") {
  host = true
  sources = [
    "header_view.cc",
  ]
  deps = [
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fit",
    "$zx/third_party/ulib/cksum",
  ]
  static = true

  if (is_fuchsia) {
    sources += [
      "background_executor.cc",
      "data_streamer.cc",
      "entry_view.cc",
      "inspector_journal.cc",
      "inspector_journal_entries.cc",
      "journal.cc",
      "journal_writer.cc",
      "operation_tracker.cc",
      "replay.cc",
      "replay_tree.cc",
      "superblock.cc",
    ]
    public_deps = [
      "$zx/system/ulib/disk-inspector:headers",
      "$zx/system/ulib/fs:headers",
      "$zx/system/ulib/fs/transaction:headers",
      "$zx/system/ulib/fzl:headers",
      "$zx/system/ulib/range:headers",
      "$zx/system/ulib/storage/buffer:headers",
      "$zx/system/ulib/storage/operation:headers",
    ]
    deps += [
      "$zx/system/ulib/disk-inspector",
      "$zx/system/ulib/fbl",
      "$zx/system/ulib/fdio",
      "$zx/system/ulib/fs/transaction",
      "$zx/system/ulib/range",
      "$zx/system/ulib/storage/buffer",
      "$zx/system/ulib/storage/operation",
      "$zx/system/ulib/sync",
      "$zx/system/ulib/zircon",
    ]
  }
}

group("test") {
  testonly = true
  deps = [
    ":journal-fuzzer",
    ":journal-unit",
  ]
}

zx_test("journal-unit") {
  test_group = "fs"
  sources = [
    "test/background_executor_test.cc",
    "test/data_streamer_test.cc",
    "test/entry_view_test.cc",
    "test/header_view_test.cc",
    "test/inspector_test.cc",
    "test/journal_test.cc",
    "test/operation_tracker_test.cc",
    "test/replay_test.cc",
    "test/replay_tree_test.cc",
    "test/superblock_test.cc",
  ]
  deps = [
    ":journal",
    "$zx/system/ulib/sync",
    "$zx/system/ulib/zx",
    "$zx/system/ulib/zxtest",
    "$zx/third_party/ulib/cksum",
  ]

  include_dirs = [ "." ]
}

source_set("fuzz-utils") {
  sources = [
    "test/fuzzer_utils.cc",
  ]
  public_deps = [
    ":journal",
    "$zx/system/ulib/sync",
    "$zx/system/ulib/zx",
    "$zx/third_party/ulib/cksum",
  ]
}

fuzzer("journal-fuzzer") {
  sources = [
    "test/journal_fuzztest.cc",
  ]
  deps = [
    ":fuzz-utils",
  ]
  include_dirs = [ "test" ]
}
