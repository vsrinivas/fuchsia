# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//src/sys/build/fuchsia_unittest_package.gni")

group("tests") {
  testonly = true
  deps = [
    ":bitmap-fuzzers",
    ":bitmap-test-pkg",
  ]
}

test("bitmap") {
  output_name = "bitmap-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "raw-bitmap-tests.cc",
    "rle-bitmap-tests.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/bitmap",
  ]
}

fuchsia_unittest_package("bitmap-test-pkg") {
  package_name = "bitmap-test"
  executable_path = "test/bitmap-test"
  deps = [ ":bitmap" ]
}

fuzzer("raw-bitmap-fuzzer") {
  sources = [ "raw-bitmap-fuzzer.cc" ]
  deps = [ "//zircon/system/ulib/bitmap" ]
}

fuzzers_package("bitmap-fuzzers") {
  cpp_fuzzers = [ ":raw-bitmap-fuzzer" ]
}
