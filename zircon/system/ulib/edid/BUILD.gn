# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36548.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/zx_library.gni")

zx_library("edid") {
  sdk = "source"
  sdk_headers = [
    "lib/edid/edid.h",
    "lib/edid/timings.h",
  ]
  sources = [
    "edid.cc",
    "eisa_vid_lut.cc",
    "timings.cc",
  ]
  deps = [
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
  ]
  public_deps = [ "//zircon/public/lib/hwreg" ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("edid-test") {
  sources = [ "unit-tests.cc" ]
  deps = [
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/edid",
  ]
}

unittest_package("edid-test-package") {
  package_name = "edid-test"
  deps = [ ":edid-test" ]
  tests = [
    {
      name = "edid-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":edid-test-package" ]
}
