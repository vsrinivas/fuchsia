# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("test") {
  testonly = true
  deps = [ ":async-test-pkg" ]
}

test("async") {
  output_name = "async-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [
    "default_tests.cc",
    "dispatcher_tests.cc",
    "executor_tests.cc",
    "irq_tests.cc",
    "paged_vmo_tests.cc",
    "receiver_tests.cc",
    "task_tests.cc",
    "time_tests.cc",
    "trap_tests.cc",
    "wait_tests.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/async-testing",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("async-test-pkg") {
  deps = [ ":async" ]
}
