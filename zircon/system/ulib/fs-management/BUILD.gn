# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36548.                         #
##########################################

# Tests for this library are in //src/storage/fs-management-tests.

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/unification/zx_library.gni")

if (is_fuchsia) {
  zx_library("fs-management") {
    sdk = "shared"
    sdk_headers = [
      "fs-management/admin.h",
      "fs-management/format.h",
      "fs-management/fvm.h",
      "fs-management/launch.h",
      "fs-management/mount.h",
    ]
    shared = true
    configs += [ "//build/config:all_source" ]
    sources = [
      "admin.cc",
      "fsck.cc",
      "fvm.cc",
      "launch.cc",
      "mkfs.cc",
      "mount.cc",
      "path.cc",
    ]
    public_deps = [ "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_c" ]
    deps = [
      "//sdk/fidl/fuchsia.boot:fuchsia.boot_llcpp",
      "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_llcpp",
      "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
      "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_llcpp",
      "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_c",
      "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_c",
      "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
      "//third_party/boringssl",
      "//zircon/public/lib/fbl",
      "//zircon/public/lib/pretty",
      "//zircon/public/lib/zx",
      "//zircon/public/lib/zxc",
      "//zircon/system/ulib/digest",
      "//zircon/system/ulib/fdio-caller",
      "//zircon/system/ulib/fs",
      "//zircon/system/ulib/fvm",
      "//zircon/system/ulib/fzl",
      "//zircon/system/ulib/gpt",
    ]
    configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

    data_deps = [
      "//src/storage/bin/blobfs",
      "//src/storage/bin/minfs",
      "//zircon/third_party/uapp/fsck-msdosfs",
      "//zircon/third_party/uapp/mkfs-msdosfs",
    ]
  }
} else {
  config("headers_config") {
    include_dirs = [ "include" ]
  }

  source_set("fs-management") {
    public = [
      "include/fs-management/admin.h",
      "include/fs-management/format.h",
      "include/fs-management/fvm.h",
      "include/fs-management/launch.h",
      "include/fs-management/mount.h",
    ]

    public_configs = [ ":headers_config" ]
  }
}
