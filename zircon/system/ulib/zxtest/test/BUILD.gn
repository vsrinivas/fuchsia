# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/testing/bootfs_test.gni")

group("tests") {
  testonly = true
  deps = [
    ":zxtest($host_toolchain)",
    "integration:tests",
    "regression:tests",
  ]
}

test("zxtest") {
  output_name = "zxtest-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "assertion_test.cc",
    "event-broadcaster_test.cc",
    "main.cc",
    "parameterized-value_test.cc",
    "prod_test.cc",
    "reporter_test.cc",
    "runner_test.cc",
    "test-case_test.cc",
    "test-info_test.cc",
    "test_test.cc",
    "values_test.cc",
  ]
  deps = [
    "//src/zircon/lib/zircon:zircon-headers",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest:prod",
  ]
  if (is_fuchsia) {
    sources += [ "death-statement_test.cc" ]

    deps += [
      "//sdk/lib/fdio",
      "//sdk/lib/fit",
      "//zircon/public/lib/zx",
    ]
  }
}

bootfs_test("zxtest-bootfs-test") {
  name = "zxtest-test"
  deps = [ ":zxtest" ]
}
