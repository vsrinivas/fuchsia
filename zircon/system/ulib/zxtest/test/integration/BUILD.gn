# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/test/test_package.gni")

group("integration") {
  testonly = true
  deps = [ ":zxtest-integration" ]
}

test("zxtest-integration") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "assertions_test.c",
    "assertions_test.cc",
    "helper.cc",
    "main.cc",
    "print_test.c",
    "print_test.cc",
    "printvalue_test.cc",
    "register_test.c",
    "register_test.cc",
  ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zxtest",
  ]
  if (is_fuchsia) {
    deps += [
      "//zircon/public/lib/fdio",
      "//zircon/public/lib/zx",
    ]
  }
}

unittest_package("zxtest-integration-package") {
  package_name = "zxtest-integration"
  deps = [ ":zxtest-integration" ]

  tests = [
    {
      name = "zxtest-integration"
      dest = "zxtest-integration-test"
    },
  ]
}
