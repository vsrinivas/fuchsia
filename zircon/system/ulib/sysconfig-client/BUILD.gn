# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36548.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/unification/zx_library.gni")

group("sysconfig-client") {
  deps = [
    ":sysconfig-sync-client",
    ":sysconfig-sync-client-c",
  ]
}

group("tests") {
  testonly = true
  deps = [ "test:tests" ]
}

zx_library("sysconfig-sync-client") {
  sdk = "source"
  sdk_headers = [ "lib/sysconfig/sync-client.h" ]

  sources = [
    "shared/abr-wear-leveling.c",
    "shared/sysconfig-header.c",
    "sync-client.cc",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.skipblock:fuchsia.hardware.skipblock_llcpp",
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/fzl",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.skipblock:fuchsia.hardware.skipblock_llcpp",
    "//sdk/fidl/fuchsia.sysinfo:fuchsia.sysinfo_llcpp",
    "//sdk/lib/fdio",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/third_party/ulib/cksum",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

zx_library("sysconfig-sync-client-c") {
  shared = true
  sdk = "shared"
  sdk_headers = [ "lib/sysconfig/sync-client-c.h" ]

  sources = [ "sync-client-c.cc" ]
  deps = [
    ":sysconfig-sync-client",
    "//zircon/public/lib/zx",
  ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}
