# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

zx_library("async-loop") {
  sdk = "source"
  sdk_publishable = true
  sdk_headers = [ "lib/async-loop/loop.h" ]
  sources = [
    "loop.c",
  ]
  public_deps = [
    # <lib/async-loop/loop.h> has #include <lib/async/dispatcher.h>.
    "$zx/system/ulib/async:headers",
  ]
  deps = [
    "$zx/system/ulib/async",
    "$zx/system/ulib/zircon",
  ]
}

zx_library("async-loop-cpp") {
  sdk = "source"
  sdk_publishable = true
  sdk_headers = [ "lib/async-loop/cpp/loop.h" ]
  sources = [
    "loop_wrapper.cc",
  ]
  deps = [
    ":async-loop",
    "$zx/system/ulib/async",
    "$zx/system/ulib/zircon",
  ]
  public_deps = [
    ":headers",

    # <lib/async-loop/cpp/loop.h> has #include <lib/zx/time.h>.
    "$zx/system/ulib/zx:headers",
  ]
}

zx_library("async-loop-default") {
  sdk = "static"
  sdk_publishable = true
  sdk_headers = [ "lib/async-loop/default.h" ]
  shared = false
  sources = [
    "default.c",
  ]
  deps = [
    "$zx/system/ulib/async:async-default",
  ]
  public_deps = [
    # <lib/async-loop/default.h> has #include <lib/async-loop/loop.h>.
    ":async-loop.headers",
  ]
}
