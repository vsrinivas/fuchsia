# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/testing/host_test_data.gni")

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

group("test") {
  testonly = true
  deps = [
    ":zbitl-test-package",
    ":zbitl-unittests($host_toolchain)",
  ]
}

# Test data ZBIs created by data/gen.py.
test_data_zbis = [
  "data/empty.zbi",
  "data/one-item.zbi",
  "data/bad-crc-item.zbi",
  "data/multiple-small-items.zbi",
  "data/second-item-on-page-boundary.zbi",
]

if (is_host) {
  host_test_data("host-test-data") {
    sources = test_data_zbis
    outputs = [ "$root_out_dir/test_data/zbitl/{{source_file_part}}" ]
  }
}

test("zbitl-unittests") {
  sources = [
    "checking-tests.cc",
    "fd-tests.cc",
    "json-tests.cc",
    "memory-tests.cc",
    "stdio-tests.cc",
    "tests.cc",
    "view-tests.cc",
  ]
  deps = [
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/lib/files",
    "//src/lib/fxl",
    "//zircon/public/lib/zbitl",
    "//zircon/public/lib/zxtest",

    # TODO(57392): Move it back to //third_party once unification completes.
    "//zircon/third_party/rapidjson",
  ]
  if (is_fuchsia) {
    sources += [ "vmo-tests.cc" ]
    configs += [ "//build/unification/config:zircon-migrated" ]
  } else if (is_host) {
    deps += [ ":host-test-data" ]
  }
}

test_package("zbitl-test-package") {
  package_name = "zbitl-tests"
  deps = [ ":zbitl-unittests" ]

  tests = [
    {
      name = "zbitl-unittests"
    },
  ]

  resources = []
  foreach(zbi, test_data_zbis) {
    resources += [
      {
        path = zbi
        dest = get_path_info(zbi, "file")
      },
    ]
  }
}
