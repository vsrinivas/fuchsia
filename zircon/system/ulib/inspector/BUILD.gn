# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36548.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/unification/zx_library.gni")

zx_library("inspector") {
  # This should only ever be used as a shared library because it hides
  # the otherwise problematic ngunwind symbols.
  shared = true
  static = false
  sdk = "shared"
  sdk_headers = [ "inspector/inspector.h" ]
  sources = [
    "backtrace.cc",
    "debug-info.cc",
    "dso-list.cc",
    "memory.cc",
    "registers.cc",
    "utils.cc",
  ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/pretty",
    "//zircon/system/ulib/backtrace-request:backtrace-request-utils",
    "//zircon/system/ulib/elf-search",
    "//zircon/third_party/ulib/ngunwind",
  ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  # Compile this with frame pointers so that if we crash
  # the simplistic unwinder will work.
  if (configs + [ "//build/config:default_frame_pointers" ] -
      [ "//build/config:default_frame_pointers" ] != configs) {
    configs -= [ "//build/config:default_frame_pointers" ]
    configs += [ "//build/config:frame_pointers" ]
  }

  configs += [
    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}
