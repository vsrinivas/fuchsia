# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

zx_library("buffer") {
  host = true
  sources = []
  static = true

  if (is_fuchsia) {
    sources += [
      "blocking_ring_buffer.cc",
      "ring_buffer.cc",
      "vmo_buffer.cc",
    ]
    public_deps = [
      "$zx/system/ulib/fbl:headers",
      "$zx/system/ulib/fzl:headers",
      "$zx/system/ulib/storage/operation:headers",
      "$zx/system/ulib/zx:headers",
    ]
    deps = [
      "$zx/system/ulib/fbl",
      "$zx/system/ulib/fdio",

      # TODO(38240): Clean up any remaining 'fs' dependencies. Only for fs/trace
      "$zx/system/ulib/fs:headers",
      "$zx/system/ulib/fzl",
      "$zx/system/ulib/storage/operation",
      "$zx/system/ulib/zircon",
      "$zx/system/ulib/zx",
    ]
  }
}

group("test") {
  testonly = true
  deps = [
    ":storage_buffer",
  ]
}

zx_test("storage_buffer") {
  output_name = "storage-buffer-test"
  test_group = "storage"
  sources = [
    "test/block_buffer_view_test.cc",
    "test/blocking_ring_buffer_test.cc",
    "test/ring_buffer_test.cc",
    "test/vmo_buffer_test.cc",
  ]
  deps = [
    ":buffer",
    "$zx/system/ulib/fdio",
    "$zx/system/ulib/zxtest",
  ]
}
