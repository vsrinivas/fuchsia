# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/unification/zbi/bootfs_test.gni")
import("//src/sys/build/fuchsia_component.gni")
import("//src/sys/build/fuchsia_test_package.gni")

group("test") {
  testonly = true
  deps = [ ":storage-buffer-tests" ]
}

test("storage_buffer") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "block_buffer_view_test.cc",
    "blocking_ring_buffer_test.cc",
    "owned_vmoid_test.cc",
    "ring_buffer_test.cc",
    "vmo_buffer_test.cc",
    "vmoid_registry_test.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/storage/buffer",
  ]
}

fuchsia_component("storage-buffer-tests-component") {
  component_name = "storage-buffer-tests"
  testonly = true
  manifest = "meta/storage-buffer-tests.cml"
  deps = [ ":storage_buffer" ]
}

fuchsia_test_package("storage-buffer-tests") {
  test_components = [ ":storage-buffer-tests-component" ]
}
