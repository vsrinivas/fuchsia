# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

testonly = true

simple_drivers = [
  "child-1",
  "child-2",
  "child-2.proxy",
  "child-3",
  "parent",
  "composite",
]

group("test") {
  deps = [
    ":test-board",
    ":test-clock",
    ":test-gpio",
    ":test-i2c",
    ":test-power",
  ]
  foreach(driver, simple_drivers) {
    deps += [ ":test-$driver" ]
  }
}

driver("test-board") {
  sources = [
    "test-board.cpp",
    "test-clock.cpp",
    "test-gpio.cpp",
    "test-i2c.cpp",
    "test-power.cpp",
    "test.cpp",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zxcpp",
  ]
}

driver("test-gpio") {
  sources = [
    "test/gpio.cpp",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.gpio",
    "$zx/system/banjo/ddk.protocol.gpioimpl",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
    "$zx/system/ulib/zxcpp",
  ]
}

driver("test-power") {
  sources = [
    "test/power.cpp",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/banjo/ddk.protocol.power",
    "$zx/system/banjo/ddk.protocol.powerimpl",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
    "$zx/system/ulib/zxcpp",
  ]
}

driver("test-clock") {
  sources = [
    "test/clock.cpp",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.clock",
    "$zx/system/banjo/ddk.protocol.clockimpl",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
    "$zx/system/ulib/zxcpp",
  ]
}

driver("test-i2c") {
  sources = [
    "test/i2c.cpp",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.i2c",
    "$zx/system/banjo/ddk.protocol.i2cimpl",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
    "$zx/system/ulib/zxcpp",
  ]
}

foreach(driver, simple_drivers) {
  driver("test-$driver") {
    sources = [
      "test/$driver.c",
    ]
    deps = [
      "$zx/system/banjo/ddk.protocol.clock",
      "$zx/system/banjo/ddk.protocol.composite",
      "$zx/system/banjo/ddk.protocol.gpio",
      "$zx/system/banjo/ddk.protocol.platform.device",
      "$zx/system/banjo/ddk.protocol.power",
      "$zx/system/ulib/ddk",
      "$zx/system/ulib/sync",
      "$zx/system/ulib/zircon",
    ]
  }
}
