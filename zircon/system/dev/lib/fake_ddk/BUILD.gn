# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # Enable printing of in driver logs in unittests.
  enable_driver_unittest_logs = true

  # Log levels to be printed when logs are enabled. Default is ERROR, WARNING, & INFO.
  # Refer to zircon/system/public/zircon/syscalls/log.h for levels.
  driver_unittest_log_flags = "0x7"
}

zx_library("fake_ddk") {
  sdk = "static"
  sdk_headers = [ "lib/fake_ddk/fake_ddk.h" ]
  testonly = true
  sources = [ "fake_ddk.cc" ]
  if (enable_driver_unittest_logs) {
    defines = [ "LOG_FLAGS=${driver_unittest_log_flags}" ]
  } else {
    defines = [ "LOG_FLAGS=0" ]
  }
  deps = [
    "$zx/system/ulib/async",
    "$zx/system/ulib/async:async-cpp",
    "$zx/system/ulib/async-loop",
    "$zx/system/ulib/async-loop:async-loop-cpp",
    "$zx/system/ulib/async-loop:async-loop-default.static",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fdio",
    "$zx/system/ulib/fidl-async",
    "$zx/system/ulib/sync",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
    "$zx/system/ulib/zxtest",
  ]
  public_deps = [
    # <lib/fake_ddk/fidl-helper.h> has #include <lib/async-loop/cpp/loop.h>.
    "$zx/system/ulib/async-loop:headers",

    # <lib/fake_ddk/fake_ddk.h> has #include <ddk/device.h>.
    "$zx/system/ulib/ddk:headers",

    # <lib/fake_ddk/fake_ddk.h> has #include <fbl/array.h>
    "$zx/system/ulib/fbl:headers",

    # <lib/fake_ddk/fidl-helper.h> has #include <lib/fidl-async/bind.h>.
    "$zx/system/ulib/fidl-async:headers",

    # <lib/fake_ddk/fake_ddk.h> has #include <lib/sync/completion.h>.
    "$zx/system/ulib/sync:headers",
  ]
}
