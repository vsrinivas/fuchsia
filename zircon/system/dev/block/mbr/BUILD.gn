# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("mbr") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "mbr-device.cc",
    "mbr-device.h",
    "mbr.cc",
    "mbr.h",
  ]
  deps = [
    "//src/devices:driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/gpt",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.block",
    "//zircon/system/banjo/ddk.protocol.block.partition",
  ]
}

test("mbr-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "mbr-test"
  sources = [
    "mbr-device-test.cc",
    "mbr-device.cc",
    "mbr-test.cc",
    "mbr.cc",
  ]
  deps = [
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/gpt",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/banjo/ddk.protocol.block",
    "//zircon/system/banjo/ddk.protocol.block.partition",
  ]
}

migrated_manifest("mbr-manifest") {
  deps = [ ":mbr" ]
}

migrated_manifest("mbr-test-manifest") {
  deps = [ ":mbr-test" ]
}
