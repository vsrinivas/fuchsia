# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("hid") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "hid-fifo.c",
    "hid-instance.cc",
    "hid-parser-lib.cc",
    "hid.cc",
  ]
  deps = [
    "//src/devices:driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hid",
    "//zircon/public/lib/hid-parser",
    "//zircon/public/lib/trace-driver",
    "//zircon/system/banjo/ddk.protocol.hidbus",
    "//zircon/system/banjo/ddk.protocol.hiddevice",
    "//zircon/system/fidl/fuchsia-hardware-input:llcpp",
  ]
}

test("hid-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "hid-fifo.c",
    "hid-instance.cc",
    "hid-parser-lib.cc",
    "hid-test.cc",
    "hid.cc",
  ]
  deps = [
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hid",
    "//zircon/public/lib/hid-parser",
    "//zircon/public/lib/trace-driver",
    "//zircon/public/lib/zxtest",
    "//zircon/system/banjo/ddk.protocol.hidbus",
    "//zircon/system/banjo/ddk.protocol.hiddevice",
    "//zircon/system/fidl/fuchsia-hardware-input:llcpp",
  ]
}

migrated_manifest("hid-manifest") {
  deps = [ ":hid" ]
}

migrated_manifest("hid-test-manifest") {
  deps = [ ":hid-test" ]
}
