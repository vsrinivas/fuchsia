# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("hid-buttons") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = []
  deps = [
  ":common",
  "//src/devices:driver",
  ]
}

source_set("common") {
  defines = [ "_ALL_SOURCE" ]
  sources = [ "hid-buttons.cc" ]

  public_deps = [
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/hid",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.buttons",
    "//zircon/system/banjo/ddk.protocol.composite",
    "//zircon/system/banjo/ddk.protocol.gpio",
    "//zircon/system/banjo/ddk.protocol.hidbus",
    "//zircon/system/fidl/fuchsia-buttons:llcpp",
  ]
}

test("hid-buttons-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  testonly = false
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "hid-buttons-test"
  sources = [ "hid-buttons-test.cc" ]
  deps = [
    ":common",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/zxtest",
  ]
}

migrated_manifest("hid-buttons-manifest") {
  deps = [ ":hid-buttons" ]
}

migrated_manifest("hid-buttons-test-manifest") {
  deps = [ ":hid-buttons-test" ]
}
