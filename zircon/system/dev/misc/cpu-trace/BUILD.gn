# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

source_set("perf-mon") {
  visibility = [ ":*" ]

  sources = [
    "perf-mon.cc",
    "perf-mon.h",
  ]

  if (current_cpu == "arm64") {
    sources += [ "arm64-pm.cc" ]
  } else if (current_cpu == "x64") {
    sources += [ "intel-pm.cc" ]
  }

  public_deps = [
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
  ]

  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/banjo/ddk.protocol.platform.device",
    "//zircon/system/fidl/fuchsia-perfmon-cpu:llcpp",
  ]
}

driver_module("cpu-trace") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "cpu-trace.cc" ]

  if (current_cpu == "x64") {
    sources += [ "intel-pt.cc" ]
  }

  deps = [
    ":perf-mon",
    "//src/devices:driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.platform.device",
    "//zircon/system/fidl/fuchsia-hardware-cpu-insntrace:llcpp",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":perf-mon-tests" ]
}

test("perf-mon-tests") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "tests/perf-mon-tests.cc" ]
  deps = [
    ":perf-mon",
    "//src/devices:driver",
    "//zircon/public/lib/fake-bti",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxtest",
    "//zircon/system/fidl/fuchsia-perfmon-cpu:llcpp",
  ]
}

migrated_manifest("cpu-trace-manifest") {
  deps = [ ":cpu-trace" ]
}

migrated_manifest("perf-mon-tests-manifest") {
  deps = [ ":perf-mon-tests" ]
}
