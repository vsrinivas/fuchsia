# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("perf-mon") {
  visibility = [ ":*" ]

  sources = [
    "perf-mon.cc",
    "perf-mon.h",
  ]

  if (current_cpu == "arm64") {
    sources += [ "arm64-pm.cc" ]
  } else if (current_cpu == "x64") {
    sources += [ "intel-pm.cc" ]
  }

  public_deps = [
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
  ]

  deps = [
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/fidl/fuchsia-perfmon-cpu:llcpp",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zircon-internal",
  ]
}

zx_driver("cpu-trace") {
  sources = [ "cpu-trace.cc" ]

  if (current_cpu == "x64") {
    sources += [ "intel-pt.cc" ]
  }

  deps = [
    ":perf-mon",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/fidl/fuchsia-hardware-cpu-insntrace:llcpp",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fidl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zircon-internal",
    "$zx/system/ulib/zx",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":perf-mon-tests" ]
}

zx_test("perf-mon-tests") {
  sources = [ "tests/perf-mon-tests.cc" ]
  deps = [
    ":perf-mon",
    "$zx/system/dev/lib/fake-bti",
    "$zx/system/dev/lib/fake_ddk",
    "$zx/system/fidl/fuchsia-perfmon-cpu:llcpp",
    "$zx/system/ulib/driver",
    "$zx/system/ulib/fdio",
    "$zx/system/ulib/fit",
    "$zx/system/ulib/zircon-internal",
    "$zx/system/ulib/zxtest",
  ]
}
