# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

zx_driver("aml-securemem") {
  sources = []
  deps = [ ":common" ]

  # TODO(32708): (ZX-2863) This driver presently violates the allowed shlib deps policy:
  # + ../../prebuilt/third_party/gn/linux-x64/gn path . --root=../../zircon/ '//system/dev/securemem/aml-securemem:aml-securemem(//public/gn/toolchain:user-arm64-clang.shlib)' '//system/ulib/fdio:fdio.shared(//public/gn/toolchain:user-arm64-clang.shlib)'
  # //system/dev/securemem/aml-securemem:aml-securemem --[public]-->
  # //system/dev/securemem/aml-securemem:aml-securemem._build --[private]-->
  # //system/dev/securemem/aml-securemem:common --[public]-->
  # //system/ulib/tee-client-api:static --[public]-->
  # //system/ulib/tee-client-api:tee-client-api.static --[private]-->
  # //system/ulib/tee-client-api:tee-client-api._sources --[private]-->
  # //system/ulib/fdio:fdio --[public]-->
  # //system/ulib/fdio:fdio.shared
  #
  # TODO(36236): See bug for discussion of potential ways to resolve this.
  deprecated_inhibit_driver_shlib_allowlist = true
}

source_set("common") {
  sources = [
    "device.cc",
    "device.h",
    "secmem-client-session.cc",
    "sysmem-secure-mem-server.cc",
  ]

  deps = [
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
  ]

  public_deps = [
    "$zx/system/banjo/ddk.protocol.composite",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/banjo/ddk.protocol.sysmem",
    "$zx/system/banjo/ddk.protocol.tee",
    "$zx/system/dev/lib/device-protocol-platform-device",
    "$zx/system/fidl/fuchsia-hardware-securemem:llcpp",
    "$zx/system/fidl/fuchsia-sysmem:llcpp",
    "$zx/system/ulib/async:async-cpp.static",
    "$zx/system/ulib/async-loop:async-loop-cpp.static",
    "$zx/system/ulib/async-loop:async-loop-default.static",
    "$zx/system/ulib/closure-queue",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/fidl-async:fidl-async-cpp.static",
    "$zx/system/ulib/fit",
    "$zx/system/ulib/tee-client-api:static",
    "$zx/third_party/ulib/safemath",
  ]
}

zx_test("aml-securemem-test") {
  output_name = "aml-securemem-test"
  sources = [ "test/aml-securemem-test.cc" ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "$zx/system/dev/lib/fake-bti",
    "$zx/system/dev/lib/fake_ddk",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zxtest",
  ]
}
