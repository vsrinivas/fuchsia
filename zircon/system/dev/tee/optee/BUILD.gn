# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("optee") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [
    "optee-client.cc",
    "optee-controller.cc",
    "optee-message.cc",
    "shared-memory.cc",
    "util.cc",
  ]
  deps = [
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/device-protocol-platform-device",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/fidl-utils",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/mmio",
    "//zircon/public/lib/region-alloc",
    "//zircon/public/lib/tee-client-api-headers",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.composite",
    "//zircon/system/banjo/ddk.protocol.platform.device",
    "//zircon/system/banjo/ddk.protocol.sysmem",
    "//zircon/system/banjo/ddk.protocol.tee",
    "//zircon/system/fidl/fuchsia-hardware-tee:llcpp",
    "//zircon/system/fidl/fuchsia-tee:llcpp",
    "//zircon/system/fidl/fuchsia-tee-manager:llcpp",
  ]
}

group("test") {
  testonly = true
  deps = [ ":optee-message-test" ]
}

test("optee-message-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  if (is_fuchsia) {
    testonly = false
  }
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  output_name = "optee-message-test"
  sources = [
    "optee-client.cc",
    "optee-controller.cc",
    "optee-message.cc",
    "shared-memory.cc",
    "test/optee-message-test.cc",
    "util.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/device-protocol-platform-device",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/fidl-utils",
    "//zircon/public/lib/mmio",
    "//zircon/public/lib/region-alloc",
    "//zircon/public/lib/tee-client-api-headers",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxtest",
    "//zircon/system/banjo/ddk.protocol.composite",
    "//zircon/system/banjo/ddk.protocol.sysmem",
    "//zircon/system/banjo/ddk.protocol.tee",
    "//zircon/system/fidl/fuchsia-hardware-tee:llcpp",
    "//zircon/system/fidl/fuchsia-tee:llcpp",
  ]
}

migrated_manifest("optee-manifest") {
  deps = [ ":optee" ]
}

migrated_manifest("optee-message-test-manifest") {
  deps = [ ":optee-message-test" ]
}
