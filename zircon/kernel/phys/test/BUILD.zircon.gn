# Copyright 2020 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("$zx/kernel/phys/phys_executable.gni")
import("$zx/kernel/phys/qemu.gni")
import("$zx/kernel/phys/zbi_executable.gni")
import("$zx/public/gn/resource.gni")
import("$zx/public/gn/test/zbi_test.gni")

assert(toolchain.environment == "kernel.phys" ||
       toolchain.environment_label == qemu_environment)

# This provides a PhysMain function appropriate for zbi_test() targets.
source_set("qemu-test-main") {
  testonly = true
  sources = [ "qemu-test-main.cc" ]
  defines = [ "ZBI_TEST_SUCCESS_STRING=\"$zbi_test_success_string\"" ]
  deps = [
    "$zx/kernel/lib/arch",
    "$zx/kernel/lib/libc",
    "$zx/system/ulib/uart",
  ]
  public_deps = [
    # This is public to propagate its public_deps so dependents
    # of qemu-test-main can use #include "test-main.h".
    ":test-main",
  ]
}

source_set("zbi-test-main") {
  testonly = true
  sources = [ "zbi-test-main.cc" ]
  defines = [ "ZBI_TEST_SUCCESS_STRING=\"$zbi_test_success_string\"" ]
  deps = [
    ":test-main",
    "$zx/kernel/lib/libc",
  ]
}

source_set("test-main") {
  testonly = true
  sources = [ "test-main.h" ]
  public_deps = [
    "$zx/kernel/lib/arch:headers",
    "..:main",
  ]
}

# Build a test that runs directly as a QEMU kernel.  This is wired up as a
# zbi_test() for infrastructure purposes though there is nothing in the ZBI.
template("phys_qemu_test") {
  main_target = target_name
  executable_target = "_qemu_phys_test.$target_name.executable"
  zbi_target = "_qemu_phys_test.$target_name.zbi_test"

  phys_executable(executable_target) {
    testonly = true
    visibility = [ ":*" ]
    deps = []
    forward_variables_from(invoker,
                           "*",
                           [
                             "linker_script",
                             "output_dir",
                             "output_extension",
                             "output_name",
                             "tags",
                             "testonly",
                           ])
    linker_script = "../phys.ld"
    deps += qemu_deps + [ ":qemu-test-main" ]
  }

  zbi_test(zbi_target) {
    visibility = [ ":*" ]

    # The ZBI is empty other than the boilerplate cmdline, and does not
    # contain a ZBI-bootable kernel.
    args = []
    cpu = ""
    deps = []
    tags = [ "incomplete" ]
    if (defined(invoker.tags)) {
      tags += invoker.tags
    }

    # The special kernel is all that really matters for this test.
    qemu_kernel = ":$executable_target"

    # Set a short timeout since the tests don't necessarily shut down QEMU
    # after printing the success string.
    timeout = qemu_short_timeout
  }

  # The "null redirect" is needed to perform variant selection consistent
  # with what the phys_executable will do.  That way the $executable_target
  # label is in the right toolchain to be found in images.json.
  environment_redirect(main_target) {
    testonly = true
    forward_variables_from(invoker, [ "visibility" ])
    environment_label = qemu_environment
    deps = [ ":$zbi_target" ]
  }
}

# TODO(fxbug.dev/57245): We provide a dummy resource to contrive 'completeness'
# in the following ZBI tests, which is a requirement for bootability under the
# current policy of zedboot, gigaboot, and the boot-shims using the old
# `zbi_check`. Once that is no longer a requirement, remove this target.
generated_resource("dummy.txt") {
  testonly = true
  visibility = [ ":*" ]
  contents = "This space is intentionally left blank."
  outputs = [ "$target_gen_dir/dummy.txt" ]
}

# Build these tests in both phys_qemu_test() and zbi_executable()/zbi_test()
# incarnations.
common_tests = [
  "hello-world-test",
  "backtrace-test",
  "phys-unittests",
]

foreach(test, common_tests) {
  phys_qemu_test("qemu-$test") {
    deps = [ ":$test" ]
  }

  if (current_cpu == "arm64") {  # TODO(mcgrathr): enable for x86
    zbi_executable("zbi-$test.executable") {
      testonly = true
      deps = [
        ":$test",
        ":zbi-test-main",
      ]
    }

    zbi_test("zbi-$test") {
      args = []
      deps = [
        ":dummy.txt",
        ":zbi-$test.executable",
      ]

      # TODO(mcgrathr): enable on hardware too
      device_types = [ "QEMU" ]

      # Set a short timeout since the tests don't reliably reboot the machine.
      timeout = qemu_short_timeout
    }
  }
}

source_set("hello-world-test") {
  testonly = true
  sources = [ "hello-world-test.cc" ]
  deps = [
    ":test-main",
    "$zx/kernel/lib/libc",
  ]
}

source_set("backtrace-test") {
  testonly = true
  sources = [ "backtrace-test.cc" ]
  deps = [
    ":test-main",
    "$zx/kernel/lib/arch",
    "$zx/kernel/lib/libc",
  ]
}

# Ideally we'd test all four combinations on arm64 and both combinations on
# x86.  But the common library code is always compiled with shadow-call-stack
# on arm64 so we can't disable the ABI support for it and with safe-stack on
# x86 so we can't disable the ABI support for it there.  The start.S code only
# enables extra stack support conditionally so as not to waste space, so there
# isn't a good way to test non-default configurations.
source_set("phys-unittests") {
  testonly = true
  sources = [
    "phys-unittests.cc",
    "stack-tests.cc",
  ]
  deps = [
    ":test-main",
    "$zx/kernel/lib/libc",
    "$zx/kernel/lib/unittest",
    "$zx/kernel/tests",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "$zx_build_config:Wno-conversion" ]
}

group("test") {
  testonly = true

  deps = []
  foreach(test, common_tests) {
    deps += [ ":qemu-$test" ]

    if (current_cpu == "arm64") {  # TODO(mcgrathr): enable for x86
      deps += [ ":zbi-$test" ]
    }
  }
}
