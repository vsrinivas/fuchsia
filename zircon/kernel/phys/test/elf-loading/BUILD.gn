# Copyright 2022 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/dist/resource.gni")
import("//build/testing/zbi_test.gni")
import("//zircon/kernel/kernel_package.gni")
import("//zircon/kernel/phys/kernel_elf_interp.gni")
import("//zircon/kernel/phys/zbi_executable.gni")

zbi_executable("basic-elf-loading-test") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "basic-test.cc" ]

  deps = [
    "..:zbi-test-main",
    "//src/lib/elfldltl",
    "//src/lib/zbitl",
    "//zircon/kernel/lib/ktl",
    "//zircon/kernel/lib/libc",
    "//zircon/kernel/phys:elf-image",
    "//zircon/kernel/phys:kernel-package",
    "//zircon/kernel/phys:zbi-memory",
    "//zircon/kernel/phys:zbitl-allocation",
  ]
}

kernel_elf_interp("basic-elf-loading-test.interp") {
  visibility = [ ":*" ]
  testonly = true

  deps = [ ":basic-elf-loading-test" ]
}

loadable_module("get-int") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "get-int.cc" ]
  ldflags = [ "-Wl,-e,GetInt" ]

  deps = [ ":basic-elf-loading-test.interp" ]

  metadata = {
    # This is the same metadata as a resource() target would have.
    distribution_entries = [
      {
        source = rebase_path("$root_out_dir/$target_name.so", root_build_dir)
        destination = target_name
        label = get_label_info(":$target_name", "label_with_toolchain")
      },
    ]
  }
}

kernel_package("basic-elf-loading-test-data") {
  visibility = [ ":*" ]
  testonly = true

  deps = [ ":get-int($phys_toolchain)" ]
}

zbi_test("zbi-basic-elf-loading-test") {
  deps = [
    ":basic-elf-loading-test",
    ":basic-elf-loading-test-data",
  ]
}

group("elf-loading") {
  testonly = true

  deps = [ ":zbi-basic-elf-loading-test" ]
}
