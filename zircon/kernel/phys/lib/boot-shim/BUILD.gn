# Copyright 2021 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/components.gni")
import("//build/test.gni")
import("//build/zircon/migrated_targets.gni")

zx_library("boot-shim") {
  sdk = "source"
  sdk_headers = [
    "lib/boot-shim/acpi.h",
    "lib/boot-shim/boot-shim.h",
    "lib/boot-shim/efi.h",
    "lib/boot-shim/debugdata.h",
    "lib/boot-shim/devicetree-boot-shim.h",
    "lib/boot-shim/efi-boot-shim.h",
    "lib/boot-shim/item-base.h",
    "lib/boot-shim/pool-mem-config.h",
    "lib/boot-shim/test-serial-number.h",
    "lib/boot-shim/uart.h",
  ]

  public_deps = [
    # <lib/boot-shim/item-base.h> has #include <lib/stdcompat/span.h>.
    "//sdk/lib/stdcompat",

    # <lib/boot-shim/uart.h> has #include <lib/uart/all.h>.
    "//zircon/system/ulib/uart",

    # <lib/boot-shim/item-base.h> has #include <lib/zbitl/image.h>.
    "//src/lib/zbitl",

    # <lib/boot-shim/item-base.h> has #include <lib/fit/result.h>
    "//zircon/system/ulib/zxc",

    # <lib/boot-shim/efi.h> #include <efi/system-table.h>
    "//zircon/kernel/lib/efi",

    # <lib/boot-shim/devicetree-boot-shim> has include <lib/devictree/matcher.h>
    # <lib/devicetree/devicetree.h>
    "//zircon/kernel/lib/devicetree",

    # <lib/boot-shim/devicetree-boot-shim> has include <fbl/macros.h>
    "//zircon/system/ulib/fbl",
  ]

  sources = [
    "acpi.cc",
    "boot-shim.cc",
    "cmdline.cc",
    "debugdata.cc",
    "efi-boot-shim.cc",
    "efi.cc",
    "pool-mem-config.cc",
    "test-serial-number.cc",
    "uart.cc",
  ]

  deps = [
    "//zircon/kernel/lib/acpi_lite",
    "//zircon/kernel/lib/boot-options",
    "//zircon/kernel/phys/lib/memalloc",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/uart",
  ]
}

if (!is_kernel) {
  zx_library("boot-shim-test") {
    testonly = true

    sdk = "source"
    sdk_headers = [ "lib/boot-shim/test-helper.h" ]

    sources = [ "test-helper.cc" ]
    deps = [
      "//src/lib/zbitl",
      "//zircon/system/ulib/zxtest",
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":boot-shim-tests",
    ":boot-shim-unittests($host_toolchain)",
  ]
}

test("boot-shim-unittests") {
  sources = [
    "acpi-tests.cc",
    "boot-shim-tests.cc",
    "debugdata-tests.cc",
    "devicetree-boot-shim-tests.cc",
    "efi-boot-shim-tests.cc",
    "efi-tests.cc",
    "pool-mem-config-tests.cc",
    "test-serial-number-tests.cc",
    "uart-tests.cc",
  ]
  deps = [
    ":boot-shim",
    ":boot-shim-test",
    "//zircon/kernel/lib/acpi_lite",
    "//zircon/kernel/lib/devicetree/tests:test_helper",
    "//zircon/kernel/phys/lib/memalloc",
    "//zircon/system/ulib/zxtest",
  ]
  if (is_host) {
    deps += [ "//zircon/kernel/lib/devicetree/tests:host-test-data" ]
  }
}

fuchsia_unittest_package("boot-shim-tests") {
  deps = [
    ":boot-shim-unittests",
    "//zircon/kernel/lib/devicetree/tests:dtb_resources",
  ]
}
