# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("$zx/kernel/params.gni")

source_set("kernel") {
  sources = [
    "brwlock.cc",
    "deadline.cc",
    "debug.cc",
    "dpc.cc",
    "event.cc",
    "init.cc",
    "mp.cc",
    "mutex.cc",
    "owned_wait_queue.cc",
    "percpu.cc",
    "semaphore.cc",
    "thread.cc",
    "timer.cc",
    "wait.cc",
  ]

  if (select_scheduler == "legacy") {
    sources += [ "legacy_scheduler.cc" ]
  } else if (select_scheduler == "fair") {
    sources += [ "fair_scheduler.cc" ]
  } else if (select_scheduler == "unified") {
    sources += [ "scheduler.cc" ]
  } else {
    assert(false, "Invalid scheduler selection!")
  }

  deps = [
    ":tests",
    "$zx/kernel/dev/interrupt",
    "$zx/kernel/lib/abi_type_validator",
    "$zx/kernel/lib/cmdline",
    "$zx/kernel/lib/console",
    "$zx/kernel/lib/counters",
    "$zx/kernel/lib/fbl",
    "$zx/kernel/lib/heap",
    "$zx/kernel/lib/ktl",
    "$zx/kernel/lib/libc",
    "$zx/kernel/lib/topology",
    "$zx/kernel/lib/version",
    "$zx/kernel/object",
    "$zx/kernel/vm",
    "$zx/system/ulib/affine",
    "$zx/system/ulib/explicit-memory",
    "$zx/system/ulib/zircon-internal",
  ]

  public_configs = [ "$zx/public/gn/config:Wno-unused-function" ]
}

source_set("tests") {
  # TODO: testonly = true
  sources = [
    "mutex_tests.cc",
    "semaphore_tests.cc",
    "thread_test.cc",
  ]
  deps = [
    "$zx/kernel/lib/console",
    "$zx/kernel/lib/unittest",
  ]
}
