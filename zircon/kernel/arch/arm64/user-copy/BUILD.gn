# Copyright 2022 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/components.gni")
import("//build/components/fuchsia_shell_package.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/components/fuchsia_unittest_component.gni")
import("//build/test.gni")

source_set("user-copy-asm") {
  visibility = [ ":*" ]
  public = [ "user-copy-asm.h" ]
  public_deps = [ "//zircon/kernel/lib/arch" ]
}

if (is_kernel) {
  source_set("user-copy") {
    sources = [
      "user-copy.S",
      "user-copy.cc",
    ]
    deps = [
      "//zircon/kernel/lib/arch",
      "//zircon/kernel/lib/libc",
      "//zircon/kernel/lib/user_copy",
    ]
  }
}

if (is_fuchsia) {
  template("arm64_user_copy_test") {
    test_target_name = "${target_name}-test"
    main_target_name = target_name

    extra_deps = []
    if (defined(invoker.deps)) {
      extra_deps = invoker.deps
    }

    test(test_target_name) {
      visibility = [ ":*" ]
      sources = [ "user-copy-tests.cc" ] + invoker.sources
      defines = [ "ARM64_USERCOPY_FN=${main_target_name}" ]
      deps = [
               ":user-copy-asm",
               "//sdk/lib/stdcompat",
               "//src/lib/fxl/test:gtest_main",
               "//third_party/googletest:gtest",
               "//zircon/kernel/lib/arch",
             ] + extra_deps
    }

    benchmark_name = "$target_name-bench"
    executable(benchmark_name) {
      output_name = main_target_name
      visibility = [ ":*" ]
      sources = [ "user-copy-bench.cc" ] + invoker.sources
      defines = [ "ARM64_USERCOPY_FN=${main_target_name}" ]
      deps = [
               ":user-copy-asm",
               "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp_wire",
               "//sdk/lib/stdcompat",
               "//zircon/kernel/lib/arch",
               "//zircon/system/ulib/fbl",
               "//zircon/system/ulib/service:service-llcpp",
               "//zircon/system/ulib/zx",
             ] + extra_deps
    }

    fuchsia_shell_package("${main_target_name}.shell") {
      deps = [ ":$benchmark_name" ]
    }

    fuchsia_unittest_component(main_target_name) {
      deps = [ ":$test_target_name" ]
    }
  }

  arm64_user_copy_test("_arm64_user_copy") {
    sources = [ "user-copy.S" ]
    deps = [
      # unsanitized memcpy symbol.
      "//zircon/kernel/lib/libc/string/arch/arm64",
    ]
  }

  arm64_user_copy_test("_arm64_user_copy_from_user") {
    sources = [ "user-copy-from-user.S" ]
  }

  arm64_user_copy_test("_arm64_user_copy_to_user") {
    sources = [ "user-copy-to-user.S" ]
  }

  fuchsia_test_package("tests") {
    package_name = "arm64-user-copy-tests"
    test_components = [
      ":_arm64_user_copy",
      ":_arm64_user_copy_to_user",
      ":_arm64_user_copy_from_user",
    ]
  }

  # Elf binaries that will produce data that describes the usercopy behaviors.
  group("benchmarks") {
    deps = [
      ":_arm64_user_copy.shell",
      ":_arm64_user_copy_from_user.shell",
      ":_arm64_user_copy_to_user.shell",
    ]
  }
}
