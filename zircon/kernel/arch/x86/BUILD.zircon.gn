# Copyright 2019 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("$zx_build/public/gn/migrated_targets.gni")
if (zx == "/") {
  import("$zx/kernel/params.gni")
}

# These set the ABI contract between C++ and assembly code.
# This is kept consistent between the kernel proper and phys
# so (pure PIC) code should be callable from the kernel proper.
config("abi") {
  cflags = [
    "-mno-red-zone",

    # context switch routines conditionally use these instructions.
    "-mfsgsbase",

    # Hard disable floating point in the kernel.
    "-msoft-float",
    "-mno-mmx",
    "-mno-sse",
    "-mno-sse2",
    "-mno-3dnow",
    "-mno-avx",
    "-mno-avx2",
  ]

  if (is_gcc) {
    cflags += [
      "-falign-jumps=1",
      "-falign-loops=1",
      "-falign-functions=4",

      # Optimization: Since FPU is disabled, do not pass flag in %rax to
      # varargs routines that floating point args are in use.
      "-mskip-rax-setup",
    ]
  } else {
    # Clang needs -mcmodel=kernel to tell it to use the right safe-stack
    # ABI for the kernel.
    cflags += [ "-mcmodel=kernel" ]
  }

  # This is not part of the ABI contract per se, but it's part of the
  # "expected resilience of generated code" contract that should be uniform
  # across all the privileged code.
  configs = [ ":retpoline" ]
}

config("retpoline") {
  visibility = [ ":*" ]
  if (is_gcc) {
    cflags = [
      "-mindirect-branch=thunk-inline",
      "-mindirect-branch-register",
    ]
  } else {
    cflags = [ "-mretpoline" ]
    if (toolchain.environment == "kernel") {
      # The kernel proper uses its own optimized thunks, but phys and
      # multiboot can just use the compiler's safe default thunks.
      cflags += [ "-mretpoline-external-thunk" ]
    }
  }
  defines = [ "KERNEL_RETPOLINE=1" ]
}

# This is used pervasively throughout the kernel on x86.
config("kernel") {
  defines = [
    "ARCH_X86",

    "KERNEL_LOAD_OFFSET=0x00100000",  # 1MB
  ]

  # For #include <arch/foo.h>.
  include_dirs = [ "include" ]

  # TODO(digit): Once the dependencies below have been migrated to the Fuchsia
  # build, only use configs to add the include paths.
  if (zx == "/") {
    configs = [
      # <arch/aspace.h> has #include <bitmap/rle-bitmap.h>
      "$zx/system/ulib/bitmap:headers.config",

      # <arch/aspace.h> has #include <arch/x86/page_tables/constants.h>.
      "page_tables:headers.config",

      # <arch/x86/page_tables/page_tables.h> has #include <hwreg/bitfields.h>.
      "$zx/system/ulib/hwreg:headers.config",

      # <arch/x86/idle_states.h> has #include <ktl/atomic.h>.
      "$zx/kernel/lib/ktl:headers.config",

      # <arch/x86.h> has #include <lib/arch/intrin.h>.
      "$zx/kernel/lib/arch/x86:headers.config",
    ]
  } else {
    include_dirs += [
      # <arch/aspace.h> has #include <bitmap/rle-bitmap.h>
      "$zx/system/ulib/bitmap/include",

      # <arch/aspace.h> has #include <arch/x86/page_tables/constants.h>.
      "page_tables/include",

      # <arch/x86/page_tables/page_tables.h> has #include <hwreg/bitfields.h>.
      "$zx/system/ulib/hwreg/include",

      # <arch/x86/idle_states.h> has #include <ktl/atomic.h>.
      "$zx/kernel/lib/ktl/include",

      # <arch/x86.h> has #include <lib/arch/intrin.h>.
      "$zx/kernel/lib/arch/x86/include",
    ]
  }
}

# TODO(fxbug.dev/58463): Remove conditional when all dependencies below have been
# migrated to the Fuchsia build.
# NOTE: Bad indentation is intentional, to make code reviews easier.
if (zx == "/") {
  zx_library("x86") {
    kernel = true
    sources = [
      "amd.cc",
      "arch.cc",
      "asm.S",
      "bootstrap16.cc",
      "cache.cc",
      "debugger.cc",
      "descriptor.cc",
      "exceptions.S",
      "faults.cc",
      "feature.cc",
      "gdt.S",
      "hwp.cc",
      "idle_states.cc",
      "idt.cc",
      "intel.cc",
      "ioapic.cc",
      "ioport.cc",
      "lapic.cc",
      "mexec.S",
      "mmu.cc",
      "mmu_mem_types.cc",
      "mp.cc",
      "msr.cc",
      "ops.S",
      "perf_mon.cc",
      "proc_trace.cc",
      "pv.cc",
      "registers.cc",
      "retpoline.S",
      "smp.cc",
      "spinlock.cc",
      "start.S",
      "start16.S",
      "system_topology.cc",
      "thread.cc",
      "timer_freq.cc",
      "user_copy.S",
      "user_copy.cc",
      "uspace_entry.S",
    ]
    deps = [
      ":syscall",
      ":tests",
      "$zx/kernel/arch/x86/cpuid",
      "$zx/kernel/dev/hw_rng",
      "$zx/kernel/dev/iommu/dummy",
      "$zx/kernel/dev/iommu/intel",
      "$zx/kernel/lib/acpi_tables",
      "$zx/kernel/lib/arch",
      "$zx/kernel/lib/cmdline",
      "$zx/kernel/lib/code_patching",
      "$zx/kernel/lib/console",
      "$zx/kernel/lib/counters",
      "$zx/kernel/lib/crashlog",
      "$zx/kernel/lib/fbl",
      "$zx/kernel/lib/init",
      "$zx/kernel/lib/instrumentation",
      "$zx/kernel/lib/kpci",
      "$zx/kernel/lib/ktrace",
      "$zx/kernel/lib/perfmon",
      "$zx/kernel/lib/root_resource_filter",
      "$zx/kernel/lib/topology",
      "$zx/kernel/object",
      "$zx/kernel/platform/pc",
      "$zx/system/ulib/bitmap",
      "$zx/system/ulib/pretty",
      "cpuid",
    ]
    public_deps = [
      # arch/x86/system_topology.h has #include <arch/x86/cpuid.h>.
      "$zx/kernel/arch/x86/cpuid:headers",

      # arch/x86/apic.h has #include <dev/interrupt.h>.
      "$zx/kernel/dev/interrupt:headers",

      # arch/x86/hypervisor.h has #include <hypervisor/guest_physical_address_space.h>.
      "$zx/kernel/hypervisor:headers",

      # <arch/aspace.h> has #include <bitmap/rle-bitmap.h>.
      "$zx/system/ulib/bitmap:headers",

      # <arch/aspace.h> uses various fbl things
      "$zx/system/ulib/fbl:headers",

      # <arch/x86/acpi.h> has #include <acpica/acpi.h>.
      "$zx/third_party/lib/acpica:headers",

      # <arch/x86/ioport.h> has #include <bitmap/rle-bitmap.h>.
      "$zx/system/ulib/bitmap:headers",

      # <arch/x86/apic.h> has #include <dev/interrupt.h>.
      "$zx/kernel/dev/interrupt:headers",

      # <arch/x86/idle_states.h> has #include <ktl/atomic.h>.
      "$zx/kernel/lib/ktl:headers",

      # <arch/x86.h> has #include <lib/arch/intrin.h>.
      "$zx/kernel/lib/arch:headers",
    ]
  }

  source_set("syscall") {
    sources = [ "syscall.S" ]
    deps = [
      "$zx/kernel/lib/code_patching",
      "$zx/kernel/lib/syscalls",
    ]
  }

  source_set("tests") {
    sources = [
      "hwp_tests.cc",
      "idle_states_test.cc",
      "mmu_tests.cc",
      "platform_tests.cc",
      "system_topology_test.cc",
    ]
    deps = [
      ":headers",
      "$zx/kernel/lib/acpi_tables:headers",
      "$zx/kernel/lib/cmdline",
      "$zx/kernel/lib/console:headers",
      "$zx/kernel/lib/topology:headers",
      "$zx/kernel/lib/unittest",
    ]

    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    configs += [ "$zx_build_config:Wno-conversion" ]
  }
}  # zx == "/"
