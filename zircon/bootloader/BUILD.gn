# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("$zx/public/gn/toolchain/environment.gni")
import("$zx/public/gn/toolchain/environment_redirect.gni")

# Gigaboot gets its own toolchains to build EFI code.
if (current_toolchain == default_toolchain) {
  environment("efi") {
    cpu = "x64"
    os = "win"
    globals = {
      is_kernel = true
    }
    configs += standard_fuchsia_configs + [ ":efi_config" ]

    # TODO: harmless, but just to match build.mk
    configs -= [ "$zx/public/gn/config:default_frame_pointers" ]

    # TODO: strip = true

    # There isn't a GCC toolchain that supports the Windows target directly
    # the way we use it.
    exclude_variant_tags = [ "gcc" ]
  }
} else if (toolchain.environment == "efi") {
  # This is the top config for all code in the efi_toolchain.
  config("efi_config") {
    configs = [
      "$zx/kernel:standalone",
      "$zx/public/gn/config:no_sanitizers",

      # TODO: "$zx/kernel:warnings",
    ]
    include_dirs = [ "include" ]
    cflags = [
      "-std=c99",
      "-fshort-wchar",
    ]
    if (current_cpu == "x64") {
      cflags += [ "-mno-red-zone" ]
    }
  }

  executable("bootloader") {
    output_dir = root_out_dir
    output_name = "boot${current_cpu}"
    output_extension = "efi"
    deps = [
      "lib",
      "src",
    ]
    ldflags = [
      "-nostdlib",
      "-Wl,/subsystem:efi_application",
      "-Wl,/entry:efi_main",
    ]

    # TODO(mcgrathr): Temporary kludge for legacy Fuchsia GN build integration.
    metadata = {
      images = [
        {
          name = "bootloader"
          type = "efi"
          path = rebase_path("$root_out_dir/$output_name.$output_extension",
                             root_build_dir)
        },
      ]
    }
  }
} else {
  # In any other toolchain, just redirect to the proper toolchain.
  environment_redirect("bootloader") {
    environment_label = ":efi"
    deps = [
      ":bootloader",
    ]
  }
}
