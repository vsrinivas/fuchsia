# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("flutter-runner-launch-smoke-test") {
  testonly = true
  deps = [ ":tests" ]
}

group("tests") {
  testonly = true
  deps = [ ":flutter-runner-launch-smoke-test-unittests" ]
}

rustc_binary("bin") {
  edition = "2021"
  output_name = "flutter_runner_launch_smoke_test"

  # Generates a GN target for unit-tests with the label `bin_test`, and
  # a binary named `flutter_runner_launch_smoke_test_bin_test`.
  with_unit_tests = true

  testonly = true

  deps = [
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_rust",
    "//src/flutter/tests/fidl:fidl_rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//src/lib/ui/fuchsia-scenic",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_component("component") {
  testonly = true
  component_name = "flutter-runner-launch-smoke-test"
  manifest = "meta/flutter-runner-launch-smoke-test.cml"
  deps = [ ":bin" ]
}

# Run with `fx test flutter-runner-launch-smoke-test-unittests`.
fuchsia_unittest_package("flutter-runner-launch-smoke-test-unittests") {
  manifest = "meta/flutter-runner-launch-smoke-test.cml"
  deps = [
    ":bin_test",
    "//src/flutter/tests/bin/pingable-flutter-component",
  ]
}
