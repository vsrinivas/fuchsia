# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//src/sys/build/components.gni")

group("all") {
  deps = [
    ":graphical_session",
    "//src/ui/scenic:scenic_pkg",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":graphical_session_tests" ]
}

rustc_binary("graphical_session_bin") {
  name = "graphical_session"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.images:fuchsia.images-rustc",
    "//sdk/fidl/fuchsia.ui.gfx:fuchsia.ui.gfx-rustc",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/ui/fuchsia-scenic",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:png",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [
    "src/app.rs",
    "src/graphics_util.rs",
    "src/main.rs",
    "src/view.rs",
  ]
}

resource("whale") {
  sources = [ "resources/whale.png" ]
  outputs = [ "data/whale.png" ]
}

fuchsia_package_with_single_component("graphical_session") {
  manifest = "meta/graphical_session.cml"
  deps = [
    ":graphical_session_bin",
    ":whale",
  ]
}

fuchsia_unittest_package("graphical_session_tests") {
  manifest = "meta/graphical_session_bin_test.cml"
  deps = [ ":graphical_session_bin_test" ]
}
