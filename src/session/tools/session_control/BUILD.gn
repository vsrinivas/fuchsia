# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")

rustc_binary("bin") {
  name = "session_control"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-component",
    "//sdk/fidl/fuchsia.session:fuchsia.session-rustc",
    "//src/lib/argh",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]
}

package("session_control") {
  deps = [
    ":bin",
  ]

  meta = [
    {
      path = rebase_path("meta/session_control.cml")
      dest = "session_control.cm"
    },
  ]

  binaries = [
    {
      name = "session_control"
      shell = true
    },
  ]
}

unittest_package("session_control_tests") {
  deps = [
    ":bin_test",
  ]

  tests = [
    {
      name = "session_control_bin_test"
      dest = "session_control_tests"
      environments = basic_envs
    },
  ]
}
