# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/dist/component_manifest_resources.gni")
import("//build/package.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

group("bootfs") {
  deps = [
    ":meta",
    ":virtual-console",
  ]
}

component_manifest_resources("meta") {
  sources = [ "meta/virtual-console.cml" ]
}

# This can't be a source_set() because the test compiles with a #define.
common_sources = [
  "args.cc",
  "keyboard-vt100.cc",
  "keyboard.cc",
  "textcon.cc",
  "vc-device.cc",
  "vc-gfx.cc",
  "vc-input.cc",
]

group("common") {
  visibility = [ ":*" ]

  public_deps = [
    "//sdk/banjo/ddk.protocol.display.controller",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_llcpp",
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_llcpp",
    "//sdk/fidl/fuchsia.hardware.input:fuchsia.hardware.input_c",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_llcpp",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_c",
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_llcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_c",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//sdk/fidl/fuchsia.virtualconsole:fuchsia.virtualconsole_llcpp",
    "//sdk/lib/fdio",
    "//sdk/lib/svc",
    "//src/lib/listnode",
    "//src/storage/deprecated-fs-fidl-handler",
    "//src/ui/input/lib/hid-input-report",
    "//src/ui/lib/key_util",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/gfx",
    "//zircon/public/lib/gfx-font-data",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fs",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/image-format:image-format-llcpp",
    "//zircon/system/ulib/statusor",
    "//zircon/system/ulib/svc",
  ]
}

executable("virtual-console") {
  sources = common_sources + [
              "main.cc",
              "vc-display.cc",
              "vc-log.cc",
              "session-manager.cc",
            ]
  deps = [
    ":common",
    "//sdk/lib/syslog/cpp",
    "//src/sys/lib/stdout-to-debuglog/cpp",
    "//zircon/public/lib/zxc",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("virtual-console-test-display") {
  defines = [ "BUILD_FOR_DISPLAY_TEST" ]
  sources = [
    "vc-display-test.cc",
    "vc-display.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("virtual-console-test-text") {
  testonly = true

  defines = [ "BUILD_FOR_TEST" ]

  sources = common_sources + [
              "keyboard-test.cc",
              "textcon-test.cc",
            ]

  deps = [
    ":common",
    "//zircon/public/lib/zxtest",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("virtual-console-session-manager-test") {
  sources = [
    "keyboard-vt100.cc",
    "session-manager-test.cc",
    "session-manager.cc",
    "textcon.cc",
    "vc-device.cc",
    "vc-input.cc",
  ]

  deps = [
    ":common",
    "//zircon/public/lib/zxc",
    "//zircon/public/lib/zxtest",
  ]
}

test("virtual-console-test-integration") {
  sources = [ "vc-integration-test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.virtualconsole:fuchsia.virtualconsole_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

test("virtual-console-args-test") {
  sources = [
    "args-test.cc",
    "args.cc",
  ]

  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_llcpp",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
  ]
}

test_package("test") {
  package_name = "virtual-console-test"
  deps = [
    ":virtual-console-args-test",
    ":virtual-console-session-manager-test",
    ":virtual-console-test-display",
    ":virtual-console-test-integration",
    ":virtual-console-test-text",
  ]
  tests = [
    {
      name = "virtual-console-test-text"
      environments = basic_envs
    },
    {
      name = "virtual-console-test-integration"
      environments = basic_envs
    },
    {
      name = "virtual-console-test-display"
      environments = basic_envs
    },
    {
      name = "virtual-console-args-test"
      environments = basic_envs
    },
    {
      name = "virtual-console-session-manager-test"
      environments = basic_envs
    },
  ]
}
