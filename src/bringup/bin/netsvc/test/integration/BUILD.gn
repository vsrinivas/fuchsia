# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("bin") {
  edition = "2021"
  output_name = "netsvc_integration_test"
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_rust",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//sdk/fidl/fuchsia.mem:fuchsia.mem_rust",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces_rust",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_rust",
    "//sdk/fidl/fuchsia.sysinfo:fuchsia.sysinfo_rust",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/testing/netemul/fidl:fuchsia.netemul_rust",
    "//src/connectivity/network/testing/netemul/rust:lib",
    "//src/connectivity/network/tests/integration/common:netstack_testing_common",
    "//src/connectivity/network/tests/integration/macros:netstack_testing_macros",
    "//src/lib/async-utils",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/network/netsvc-proto",
    "//src/lib/network/packet",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:arrayvec",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:test-case",
    "//third_party/rust_crates:zerocopy",
  ]
  sources = [ "src/lib.rs" ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_test_component("netsvc-integration-test") {
  component_name = "netsvc-integration-test"
  manifest = "meta/netsvc-integration-test.cml"
  deps = [
    ":bin",
    "//src/bringup/bin/device-name-provider:device-name-provider-component",
    "//src/bringup/bin/netsvc:test-component",

    # netemul-sandbox-v2 is used to create hermetic test realms.
    "//src/connectivity/network/testing/netemul/service:netemul-sandbox",

    # We inject a network stack to be able to communicate with netsvc.
    "//src/connectivity/network/netstack:component-debug",
  ]
}

fuchsia_test_package("integration") {
  package_name = "netsvc-integration-test"
  test_components = [ ":netsvc-integration-test" ]
}
