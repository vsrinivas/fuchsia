# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/testing/bootfs_test.gni")
import("//build/unification/zx_library.gni")

zx_library("crashsvc") {
  sdk = "source"

  sdk_headers = [
    "crashsvc/crashsvc.h",
    "crashsvc/exception_handler.h",
    "crashsvc/logging.h",
  ]

  sources = [
    "crashsvc.cc",
    "exception_handler.cc",
    "logging.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.exception:fuchsia.exception_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/fsl",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/backtrace-request:backtrace-request-utils",
    "//zircon/system/ulib/inspector",
  ]

  public_deps = [ "//zircon/public/lib/zx" ]

  configs += [ "//build/config:all_source" ]
}

executable("svchost") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "svchost.cc",
    "sysmem.cc",
  ]
  deps = [
    ":crashsvc",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_llcpp",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_llcpp",
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.device.manager:fuchsia.device.manager_llcpp",
    "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_llcpp",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_llcpp",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_llcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_c",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_llcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_c",
    "//sdk/fidl/fuchsia.mem:fuchsia.mem_c",
    "//sdk/fidl/fuchsia.net:fuchsia.net_llcpp",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_llcpp",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_llcpp",
    "//sdk/fidl/fuchsia.process:fuchsia.process_llcpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_c",
    "//sdk/fidl/fuchsia.sysinfo:fuchsia.sysinfo_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
    "//sdk/fidl/fuchsia.virtualconsole:fuchsia.virtualconsole_llcpp",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fs",
    "//zircon/system/ulib/kcounter",
    "//zircon/system/ulib/kernel-debug",
    "//zircon/system/ulib/kernel-mexec",
    "//zircon/system/ulib/ktrace",
    "//zircon/system/ulib/logger",
    "//zircon/system/ulib/profile",
    "//zircon/system/ulib/svc",
    "//zircon/system/ulib/sysmem-connector",
  ]
  output_dir = "$root_out_dir/zircon-migrated"
}

test("crashsvc-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "crashsvc-test"
  sources = [ "crashsvc-test.cc" ]
  deps = [
    ":crashsvc",
    "//sdk/fidl/fuchsia.exception:fuchsia.exception_llcpp",
    "//sdk/lib/fdio",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/fs",
    "//zircon/system/ulib/mini-process",
  ]
}

bootfs_test("crashsvc-test-bootfs-test") {
  name = "crashsvc-test"
  deps = [ ":crashsvc-test" ]
}
