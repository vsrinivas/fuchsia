# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")

group("tests") {
  testonly = true

  deps = []

  if (host_os == "linux") {
    deps += [ ":limited_netsvc_tests($host_toolchain)" ]
  }
}

if (is_linux) {
  go_library("lib") {
    testonly = true

    name = "fuchsia.googlesource.com/tests/limited_netsvc"

    deps = [ "//src/testing/qemu" ]
  }

  group("main_zbi") {
    metadata = {
      test_runtime_deps = [ "$root_build_dir/fuchsia.zbi" ]
    }
  }

  copy("copy_netsvc_tools") {
    visibility = [ ":*" ]

    sources = [
      "$zircon_root_build_dir/tools/loglistener",
      "$zircon_root_build_dir/tools/netaddr",
      "$zircon_root_build_dir/tools/netcp",
      "$zircon_root_build_dir/tools/netls",
      "$zircon_root_build_dir/tools/netruncmd",
    ]
    outputs = [ "$root_out_dir/test_data/limited_netsvc/{{source_file_part}}" ]
  }

  group("netsvc_user_tools") {
    deps = [ ":copy_netsvc_tools" ]

    metadata = {
      test_runtime_deps = [
        "$root_out_dir/test_data/limited_netsvc/loglistener",
        "$root_out_dir/test_data/limited_netsvc/netaddr",
        "$root_out_dir/test_data/limited_netsvc/netcp",
        "$root_out_dir/test_data/limited_netsvc/netls",
        "$root_out_dir/test_data/limited_netsvc/netruncmd",
      ]
    }
  }

  # TODO(scottmg): Colocate this test with netsvc when it moves out of //zircon.
  go_test("limited_netsvc_tests") {
    gopackages = [ "fuchsia.googlesource.com/tests/limited_netsvc" ]
    deps = [
      ":lib",
      "//tools/net/netutil",
      "//tools/net/tftp",
    ]
    non_go_deps = [
      ":main_zbi",
      ":netsvc_user_tools",
    ]
  }
}
