# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//src/tests/benchmarks/fidl/benchmark_suite/benchmark_suite.gni")
import("//tools/fidl/gidl/gidl.gni")

gidl_deps = []
foreach(suite, benchmark_suites) {
  suite_target = "benchmark_suite__${suite.name}"
  gidl_deps += [ ":" + suite_target ]

  gidl_walker_benchmarks(suite_target) {
    testonly = true
    inputs = suite.gidl
    fidl = suite.fidl_target
    deps = [
      "//sdk/lib/fidl/cpp",
      "//sdk/lib/fidl/cpp:handle_util",
      "//zircon/public/lib/fbl",
      "//zircon/public/lib/fidl",
      "//zircon/system/ulib/async-loop:async-loop-cpp",
      "//zircon/system/ulib/async-loop:async-loop-default",
      "//zircon/system/ulib/perftest",
    ]
    extra_args = [
      "-cpp-benchmarks-fidl-library",
      suite.fidl_library_suffix,
    ]
  }
}

executable("bin") {
  output_name = "walker_fidl_microbenchmarks"
  testonly = true
  sources = [
    "main.cc",
    "walker_benchmark_util.cc",
  ]
  deps = [
           "//src/zircon/lib/zircon",
           "//zircon/public/lib/fbl",
           "//zircon/public/lib/fidl",
           "//zircon/public/lib/zx",
           "//zircon/system/ulib/perftest",
         ] + gidl_deps
}

package("walker") {
  testonly = true

  deps = [ ":bin" ]

  binaries = [
    {
      name = "walker_fidl_microbenchmarks"
      shell = true
    },
  ]
}
