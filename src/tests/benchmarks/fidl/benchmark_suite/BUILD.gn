import("//build/fidl/fidl.gni")
import("//build/go/go_library.gni")
import("//tools/fidl/gidl/gidl.gni")

benchmark_suite_gidl_files = [
  "//src/tests/benchmarks/fidl/benchmark_suite/byte_array.gidl",
  "//src/tests/benchmarks/fidl/benchmark_suite/byte_vector.gidl",
  "//src/tests/benchmarks/fidl/benchmark_suite/struct_field.gidl",
  "//src/tests/benchmarks/fidl/benchmark_suite/inline.gidl",
  "//src/tests/benchmarks/fidl/benchmark_suite/result.gidl",
]
benchmark_suite_fidl_files = [
  "byte_array.test.fidl",
  "byte_vector.test.fidl",
  "struct_field.test.fidl",
  "inline.test.fidl",
  "result.test.fidl",
]

fidl("benchmark_suite_fidl") {
  testonly = true
  name = "benchmarkfidl"
  sources = benchmark_suite_fidl_files
}

gidl_go_out_dir = "$target_gen_dir/../go/benchmark_suite"

gidl("benchmark_suite_go") {
  testonly = true
  type = "benchmark"
  language = "go"
  inputs = benchmark_suite_gidl_files
  fidl = ":benchmark_suite_fidl"
  output = "$gidl_go_out_dir/benchmark_suite.go"
}

go_library("benchmark_suite_go_lib") {
  testonly = true
  name = "benchmark_suite"
  source_dir = gidl_go_out_dir
  non_go_deps = [ ":benchmark_suite_go" ]
  deps = [ ":benchmark_suite_fidl(${go_toolchain})" ]
}
