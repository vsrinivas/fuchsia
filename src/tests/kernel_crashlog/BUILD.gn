# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/testing/host_test_data.gni")

group("tests") {
  testonly = true

  deps = []

  if (host_os == "linux") {
    deps += [ ":kernel_crashlog_tests($host_toolchain)" ]
  }
}

if (is_linux) {
  go_library("lib") {
    testonly = true

    name = "fuchsia.googlesource.com/tests/kernel_crashlog"

    deps = [ "//src/testing/qemu" ]

    sources = [ "kernel_crashlog_test.go" ]
  }

  host_test_data("main_zbi") {
    sources = [ "$root_build_dir/fuchsia.zbi" ]
  }

  go_test("kernel_crashlog_tests") {
    gopackages = [ "fuchsia.googlesource.com/tests/kernel_crashlog" ]
    deps = [ ":lib" ]
    non_go_deps = [ ":main_zbi" ]
  }
}
