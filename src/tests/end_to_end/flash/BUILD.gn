# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/test.gni")
import("//build/testing/environments.gni")
import("//build/testing/host_test_data.gni")

dart_test("flash_test") {
  sources = [ "flash_test.dart" ]
  deps = [
    "//sdk/testing/sl4f/client",
    "//third_party/dart-pkg/pub/test",
  ]
  non_dart_deps = [ ":test_deps($host_toolchain)" ]
  environments = [
    {
      dimensions = {
        device_type = "Astro"
      }

      # This is only present to force this test to run in its own shard.
      service_account =
          "internal-artifacts-readers@fuchsia-infra.iam.gserviceaccount.com"
    },
  ]
}

if (is_host) {
  host_test_data("test_deps") {
    sources = [
      "$root_out_dir/firmware.img",
      "$root_out_dir/firmware_bl2.img",
      "$root_out_dir/flash.sh",
      "$root_out_dir/fuchsia.vbmeta",
      "$root_out_dir/fuchsia.zbi.signed",
      "$root_out_dir/obj/build/images/fvm.fastboot.blk",
      "$root_out_dir/zedboot.vbmeta",
      "$root_out_dir/zedboot.zbi.signed",
    ]
  }
}

group("tests") {
  testonly = true
  deps = [ ":flash_test" ]
}
