# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/test.gni")
import("//build/testing/environments.gni")

dart_test("screen_is_not_black_test") {
  sources = [ "screen_is_not_black_test.dart" ]

  deps = [
    "//sdk/testing/sl4f/client",
    "//third_party/dart-pkg/pub/image",
    "//third_party/dart-pkg/pub/logging",
    "//third_party/dart-pkg/pub/matcher",
    "//third_party/dart-pkg/pub/test",
  ]

  non_dart_deps = [ ":runtime_deps" ]

  environments = [
    # Runs on "main" builders (try and ci) in NUC environments.
    nuc_env,

    # Also runs on fyi builders in NUC environments, so it doesn't look empty
    # there, and we have a second data set of flakiness information if needed.
    {
      dimensions = {
        device_type = "Intel NUC Kit NUC7i5DNHE"
      }
      tags = [ "e2e-fyi" ]
    },

    # The astro environment has tags as it is still to be run on an FYI builder.
    {
      dimensions = {
        device_type = "Astro"
      }
      tags = [ "e2e-fyi" ]
    },
  ]
}

copy("runtime_deps") {
  _data_dir = "$target_gen_dir/runtime_deps"

  sources = [ "$root_out_dir/catapult_converter" ]

  outputs = [ "$_data_dir/{{source_file_part}}" ]

  metadata = {
    test_runtime_deps = [ "$_data_dir/catapult_converter" ]
  }

  deps = [ "//garnet/bin/catapult_converter:converter_bin" ]
}

group("test") {
  testonly = true

  deps = [ ":screen_is_not_black_test($host_toolchain)" ]
}
