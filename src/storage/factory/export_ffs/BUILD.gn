# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//src/sys/build/fuchsia_component.gni")
import("//src/sys/build/fuchsia_test_package.gni")

rustc_library("export_ffs") {
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//garnet/lib/rust/files_async",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/storage/block_client/rust:remote-block-device",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]
  test_deps = [
    "//src/lib/fuchsia-async",
    "//src/lib/isolated_devmgr/v2_component/rust:isolated-driver-manager",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/storage/vfs/rust:vfs",
    "//third_party/rust_crates:matches",
  ]

  sources = [ "src/lib.rs" ]
}

fuchsia_component("export-ffs-tests-component") {
  component_name = "export-ffs-tests"
  testonly = true
  manifest = "meta/export-ffs-tests.cml"
  deps = [ ":export_ffs_test" ]
}

fuchsia_test_package("export-ffs-tests") {
  test_components = [ ":export-ffs-tests-component" ]
}

group("tests") {
  testonly = true
  deps = [
    ":export-ffs-tests",
    "//src/lib/isolated_devmgr/v2_component",
  ]
}
