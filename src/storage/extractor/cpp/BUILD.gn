# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//src/sys/build/fuchsia_test_package.gni")
import("//src/sys/build/fuchsia_unittest_package.gni")

group("tests") {
  testonly = true
  deps = [ ":disk-extractor-unit-test" ]
}

test("disk_extractor_unit_test") {
  testonly = true
  sources = [
    "extractor_test.cc",
    "minfs_extractor_test.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.fs",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/extractor/c:disk_extractor",
    "//src/storage/extractor/cpp:disk_extractor",
    "//src/storage/fs_test",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zxc",
  ]
}

fuchsia_component("disk-extractor-unit-test-component") {
  testonly = true
  manifest = "meta/cpp_lib_tests.cml"
  deps = [ ":disk_extractor_unit_test" ]
}

fuchsia_test_package("disk-extractor-unit-test") {
  test_components = [ ":disk-extractor-unit-test-component" ]
}

static_library("disk_extractor") {
  public = [ "extractor.h" ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/fs",
    "//zircon/third_party/ulib/safemath:safemath",
  ]

  sources = [
    "extractor.cc",
    "minfs_extractor.cc",
  ]
  public_deps = [
    "//src/storage/extractor/c:disk_extractor",
    "//src/storage/minfs",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zxc",
  ]
  if (is_fuchsia) {
    public_deps += [
      "//zircon/system/ulib/fzl",
      "//zircon/system/ulib/syslog",
    ]
    deps += [
      "//sdk/lib/fdio",
      "//zircon/public/lib/fbl",
      "//zircon/public/lib/zx",
    ]
    include_dirs = [ ".." ]
  }
}
