# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//src/storage/lib/paver/e2e_tests/e2e_tests.gni")

if (is_host) {
  # Add this to go_test()'s environments to disable a test in infra
  disabled_env = {
    tags = [ "system-tests-disabled" ]  # Tells infra not to run this by default
    dimensions = host_env.dimensions
  }

  go_library("lib") {
    testonly = true
    sources = [
      "config.go",
      "pave_test.go",
    ]
    non_go_deps = [ "//src/sys/pkg/tests/system-tests:test_tools" ]
    deps = [
      "//src/testing/host-target-testing",
      "//tools/botanist:constants",
      "//tools/lib/color",
      "//tools/lib/logger",
    ]
  }

  go_test("e2e_tests_pave") {
    library = ":lib"
    environments = [ disabled_env ]
  }
}

if (is_linux || is_mac) {
  e2e_tests_pave("nuc-release-pave") {
    downgrade_release_builder = "fuchsia/global.ci/core.x64-release"

    environments = [
      {
        dimensions = {
          device_type = "Intel NUC Kit NUC7i5DNHE"
        }
        tags = [ "e2e-isolated" ]
      },
    ]
    pave_timeout = "5m"
    cycle_count = "1"
    cycle_timeout = "15m"
  }
}

group("e2e_tests") {
  testonly = true
  deps = [ ":nuc-release-pave($host_toolchain)" ]
}
