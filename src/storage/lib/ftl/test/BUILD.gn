# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":ftl-library-test" ]
}

test("ftl-test") {
  output_name = "ftl-library-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "ftl_test.cc",
    "ndm_driver_test.cc",
    "ndm_test.cc",
  ]
  deps = [
    "//src/devices/block/drivers/ftl/tests:shared",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/lib/ftl",
    "//src/storage/lib/ftl:private_headers",
    "//third_party/googletest:gtest",
  ]
  defines = [ "NDM_DEBUG" ]
}

fuchsia_unittest_package("ftl-library-test") {
  deps = [ ":ftl-test" ]
}

source_set("ftl-workload-fuzzer") {
  sources = [ "ftl_workload_fuzzer.cc" ]
  deps = [
    "//src/devices/block/drivers/ftl/tests:shared",
    "//src/storage/lib/ftl",
    "//src/storage/lib/ftl:private_headers",
  ]
}
