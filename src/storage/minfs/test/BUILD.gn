# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//src/storage/fs_test/suite.gni")
import("//src/storage/testing/isolated_devmgr.gni")

test("minfs_unit") {
  sources = [
    "unit/bcache_test.cc",
    "unit/buffer_view_test.cc",
    "unit/command_handler_test.cc",
    "unit/disk_struct_test.cc",
    "unit/format_test.cc",
    "unit/fsck_test.cc",
    "unit/inspector_test.cc",
    "unit/journal_integration_fixture.cc",
    "unit/journal_test.cc",
    "unit/lazy_buffer_test.cc",
    "unit/lazy_reader_test.cc",
    "unit/loader_test.cc",
    "unit/minfs_inspector_test.cc",
    "unit/mkfs_test.cc",
    "unit/mount_test.cc",
    "unit/parser_test.cc",
    "unit/resizeable_array_buffer_test.cc",
    "unit/resizeable_vmo_buffer_test.cc",
    "unit/rw_test.cc",
    "unit/superblock_test.cc",
    "unit/transaction_limits_test.cc",
    "unit/transaction_test.cc",
    "unit/truncate_test.cc",
    "unit/unlink_test.cc",
    "unit/vnode_mapper_test.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_c",
    "//src/lib/cobalt/cpp/testing:mock_cobalt_logger",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/storage/fs_management",
    "//src/storage/fvm",
    "//src/storage/lib/disk_inspector",
    "//src/storage/minfs",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/safemath",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/block-client",
    "//zircon/system/ulib/block-client:fake-device",
    "//zircon/system/ulib/cobalt-client:in-memory-logger",
    "//zircon/third_party/ulib/cksum",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("minfs-integration") {
  sources = [
    "integration/backup_superblock.cc",
    "integration/mount_test.cc",
    "integration/power_test.cc",
    "integration/runtime_state_test.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_llcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/storage/fs_management",
    "//src/storage/fs_test",
    "//src/storage/fvm",
    "//src/storage/minfs",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fdio-caller",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

test("minfs_dirty_cache_integration") {
  sources = [ "integration/dirty_cache_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.fs",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_llcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/fs_test",
    "//src/storage/minfs",
    "//src/storage/testing:storage_driver_test_support",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/fdio-caller",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

test("minfs-power") {
  sources = [ "integration/power_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/fs_test",
    "//src/storage/memfs",
    "//src/storage/minfs",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

test("minfs_micro_benchmark") {
  sources = [
    "micro-benchmark/minfs-costs.cc",
    "micro-benchmark/minfs-tests.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_c",
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//src/lib/digest",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/storage/fs_management",
    "//src/lib/storage/vfs/cpp",
    "//src/storage/fs_test",
    "//src/storage/fvm",
    "//src/storage/gpt",
    "//src/storage/memfs",
    "//src/storage/memfs:memfs-cpp",
    "//src/storage/minfs",
    "//src/storage/testing:storage_driver_test_support",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/perftest",
    "//zircon/system/ulib/ramdevice-client",
    "//zircon/system/ulib/storage-metrics",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-engine",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

test("fs_tests") {
  sources = [ "integration/fs_tests.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.minfs:fuchsia.minfs_c",
    "//sdk/lib/fdio",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/fs_test",
    "//src/storage/fvm",
    "//src/storage/minfs",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fdio-caller",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

storage_isolated_devmgr_unittest_v2_component("minfs-integration-fs-tests") {
  deps = [ ":fs_tests" ]
}

fuzzer("minfs-fuzzer") {
  sources = [ "unit/fuzz.cc" ]
  deps = [
    "//src/storage/minfs",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/block-client:fake-device",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuzzers_package("minfs-fuzzers") {
  cpp_fuzzers = [ ":minfs-fuzzer" ]
}

storage_isolated_devmgr_unittest_v2_component("minfs-integration-test") {
  deps = [ ":minfs-integration" ]
}

storage_isolated_devmgr_unittest_v2_component(
    "minfs-dirty-cache-integration-test") {
  deps = [ ":minfs_dirty_cache_integration" ]
}

storage_isolated_devmgr_unittest_v2_component("minfs-power-test") {
  deps = [ ":minfs-power" ]
}

fuchsia_unittest_component("minfs-unit-test") {
  deps = [ ":minfs_unit" ]
  manifest = "meta/minfs_unit_test.cml"
}

storage_isolated_devmgr_unittest_v2_component("minfs-micro-benchmark-test") {
  deps = [ ":minfs_micro_benchmark" ]
}

fs_test_config("minfs") {
  timestamp_granularity = 1
  supports_fs_query = false
  supports_hard_links = true
  supports_resize = true
  max_file_size = 4294950912
  use_directory_admin_to_unmount = true
  has_directory_size_limit = true
  supports_fsck_after_every_transaction = true
  supports_watch_event_deleted = false
  options = [
    {
      description = "MinfsWithFvm"
      use_fvm = true
    },
    {
      description = "MinfsWithoutFvm"
      use_fvm = false
    },
  ]
}

fs_test_suite("minfs") {
  deps = [
    ":minfs_fs_test_config",
    ":minfs_validate_fs_test_config",
    "//src/storage/bin/minfs",
  ]
  extra_components = [
    ":minfs-dirty-cache-integration-test",
    ":minfs-integration-fs-tests",
    ":minfs-integration-test",
    ":minfs-micro-benchmark-test",
    ":minfs-power-test",
    ":minfs-unit-test",
  ]
}

slow_fs_test_suite("minfs") {
  deps = [
    ":minfs_fs_test_config",
    ":minfs_validate_fs_test_config",
    "//src/storage/bin/minfs",
  ]
}
