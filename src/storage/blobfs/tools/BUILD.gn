# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/sdk/sdk_host_tool.gni")
import("//build/testing/host_test.gni")

source_set("blobfs_creator") {
  visibility = [ "./*" ]
  sources = [
    "blobfs_creator.cc",
    "blobfs_creator.h",
  ]
  public_deps = [
    "//src/lib/chunked-compression:chunked-compression",
    "//src/lib/digest",
    "//src/storage/blobfs",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fs-host",
  ]
  deps = [
    "//sdk/lib/fit",
    "//zircon/system/ulib/bitmap",
  ]
}

executable("blobfs") {
  sources = [ "main.cc" ]
  deps = [
    ":blobfs_creator",
    "//src/storage/blobfs",
    "//zircon/system/ulib/fbl",
  ]

  if (!is_fuchsia) {
    deps += [ "//zircon/system/ulib/zx-panic-libc" ]

    # fxbug.dev/105094: Optimizing for speed saves a considerable amount of
    # time when constructing blobfs images with a large number of blobs.
    configs -= [ "//build/config:default_optimize" ]
    configs += [ "//build/config:optimize_speed" ]

    exclude_toolchain_tags = [
      # TODO(fxbug.dev/53356): blobfs performs poorly when built with asan
      "asan",

      # TODO(fxbug.dev/59219): blobfs performs poorly when built with profile
      "profile",

      # TODO(fxbug.dev/59219): blobfs performs poorly when built with coverage
      "coverage",
    ]
  }
}

sdk_host_tool("blobfs_sdk") {
  category = "partner"
  output_name = "blobfs"
  sdk_name = "blobfs_do_not_depend"
  deps = [ ":blobfs" ]
}

if (is_host) {
  executable("compressed_export_test_bin") {
    testonly = true
    sources = [ "compressed_export_test.cc" ]
    deps = [
      ":blobfs_creator",
      "//src/lib/chunked-compression",
      "//src/lib/fxl/test:gtest_main",
      "//src/lib/json_parser",
      "//third_party/googletest:gtest",
      "//third_party/rapidjson",
      "//zircon/system/ulib/fbl",
    ]
  }

  host_test("compressed_export_test") {
    binary_path = "$root_out_dir/compressed_export_test_bin"

    deps = [ ":compressed_export_test_bin" ]
  }
}

group("tests") {
  testonly = true
  deps = [ ":compressed_export_test($host_toolchain)" ]
}
