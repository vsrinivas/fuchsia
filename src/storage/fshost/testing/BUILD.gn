# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/component_manifest_resources.gni")
import("//build/test.gni")
import("//src/storage/fshost/fshost.gni")
import("//tools/cmc/build/cmc.gni")

source_set("integration_test_fixture") {
  testonly = true
  sources = [
    "fshost_integration_test.cc",
    "fshost_integration_test.h",
    "mock-block-device.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_cpp_wire",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp_wire",
    "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_cpp_wire",
    "//sdk/fidl/fuchsia.inspect:fuchsia.inspect_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_wire_testing",
    "//sdk/lib/fdio",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/fshost:headers",
  ]
  deps = [
    "//sdk/lib/inspect/service/cpp:cpp",
    "//sdk/lib/sys/component/cpp",
  ]
}

fshost_component_and_config("test-fshost") {
  testonly = true
  options = default_integration_test_options
  options.data_filesystem_format = "minfs"
}

fshost_component_and_config("test-fshost-fxfs") {
  testonly = true
  options = default_integration_test_options
  options.data_filesystem_format = "fxfs"
}

fshost_component_and_config("test-fshost-recovery") {
  testonly = true
  options = default_integration_test_options
  options.data_filesystem_format = "minfs"
  options.fvm_ramdisk = true

  # Intentionally match against non-existent paths so tests can use ramdisks and they'll still be
  # matched as non-ramdisks.
  options.ramdisk_prefix = "/nada/zip/zilch"
}

fshost_component_and_config("test-fshost-fxfs-recovery") {
  testonly = true
  options = default_integration_test_options
  options.data_filesystem_format = "fxfs"
  options.fvm_ramdisk = true

  # Intentionally match against non-existent paths so tests can use ramdisks and they'll still be
  # matched as non-ramdisks.
  options.ramdisk_prefix = "/nada/zip/zilch"
}

fshost_component_and_config("test-fshost-no-watcher") {
  testonly = true
  options = default_integration_test_options
  options.data_filesystem_format = "minfs"
  options.disable_block_watcher = true
}

fshost_component_and_config("test-fshost-fxfs-disk-migration") {
  testonly = true
  options = default_integration_test_options
  options.data_filesystem_format = "fxfs"
  options.use_disk_based_minfs_migration = true

  # FVM doesn't persist a slice_limit. The limit is set in BlockDeviceManager:Add based on
  # this value stored in fshost_config.data_max_bytes. This value has been specifically
  # chosen for our success/fail tests to ensure that a small device (10MiB) won't have
  # enough free space for a side-by-side migration but a larger device will.
  options.data_max_bytes = 9437184  # 9MiB
}
