# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/packaged_shared_library.gni")
import("//build/test.gni")
import("//src/storage/fs_test/suite.gni")

group("tests") {
  testonly = true
  deps = [
    ":memfs-fs-tests",
    ":memfs-inotify-test",
    ":memfs-inotify-test-pkg",
    ":memfs-slow-fs-tests",
    ":memfs-tests",
  ]
}

test("memfs-inotify-test") {
  sources = [ "memfs-inotify-tests.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//sdk/fidl/fuchsia.io2:fuchsia.io2_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/fxl",
    "//src/lib/storage/vfs/cpp",
    "//src/lib/storage/vfs/cpp:inotify_test_base",
    "//src/storage/memfs",
    "//src/storage/memfs:memfs-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

test("memfs-test") {
  sources = [ "memfs-tests.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//src/lib/storage/vfs/cpp",
    "//src/storage/memfs",
    "//src/storage/memfs:memfs-cpp",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_unittest_package("memfs-inotify-test-pkg") {
  package_name = "memfs-inotify-test"
  deps = [ ":memfs-inotify-test" ]
}

fuchsia_unittest_package("memfs-tests") {
  manifest = "memfs-unit-test.cmx"
  deps = [ ":memfs-test" ]
}

shared_library("memfs_fs_test") {
  testonly = true
  sources = [
    "memfs_fs_test.cc",
    "memfs_fs_test.h",
  ]
  deps = [
    "//src/storage/fs_test",
    "//src/storage/memfs",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}

packaged_shared_library("memfs_fs_test_as_module") {
  testonly = true
  library = ":memfs_fs_test"
}

fs_test_config("memfs") {
  data_deps = [ ":memfs_fs_test_as_module" ]
  library = "libmemfs_fs_test.so"
}

fs_test_suite("memfs") {
  deps = [ ":memfs_fs_test_config" ]
}

slow_fs_test_suite("memfs") {
  deps = [ ":memfs_fs_test_config" ]
}
