# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//src/storage/testing/driver_test_realm.gni")

rustc_library("framework") {
  edition = "2021"
  testonly = true
  name = "storage_benchmarks"
  sources = [
    "src/block_device.rs",
    "src/filesystem.rs",
    "src/io_benchmarks.rs",
    "src/lib.rs",
    "src/trace.rs",
  ]
  deps = [
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:prettytable-rs",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:rand_xorshift",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tracing",
  ]
  if (is_fuchsia) {
    deps += [ "//src/lib/trace/rust:trace" ]
  }
  test_deps = [
    "//src/lib/fuchsia",
    "//src/sys/test_runners:tmp_storage",
    "//third_party/rust_crates:futures",
  ]
  with_unit_tests = true
}

fuchsia_unittest_package("storage-benchmarks-framework-tests") {
  deps = [ ":framework_test" ]
  test_specs = {
    parallel = 1
  }
}

group("benchmarks") {
  testonly = true
  deps = [
    "fuchsia:benchmarks",
    "host:benchmarks",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":storage-benchmarks-framework-tests",
    "fuchsia:tests",
  ]
}
