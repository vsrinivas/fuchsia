# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("core") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "block.core"
  deps = [
    ":shared",
    "//src/devices/lib/driver",
  ]
}

group("test") {
  testonly = true
  deps = [ ":core-unittest" ]
}

test("core-unittest") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "blockcore"
  include_dirs = [ "." ]
  sources = [ "test/main.cc" ]
  deps = [
    ":shared",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/io-scheduler",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/banjo/ddk.protocol.block",
  ]
}

source_set("shared") {
  visibility = [ "./*" ]
  defines = [ "_ALL_SOURCE" ]
  sources = [
    "block-device.cc",
    "iobuffer.cc",
    "manager.cc",
    "message.cc",
    "server.cc",
    "txn-group.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.block.partition",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/dev-operation",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl-utils",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/storage-metrics",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.block",
    "//zircon/system/banjo/ddk.protocol.block.volume",
    "//zircon/system/fidl/fuchsia-hardware-block:c",
    "//zircon/system/fidl/fuchsia-hardware-block-partition:c",
    "//zircon/system/fidl/fuchsia-hardware-block-volume:c",
  ]
}

migrated_manifest("core-manifest") {
  deps = [ ":core" ]
}

migrated_manifest("core-unittest-manifest") {
  deps = [ ":core-unittest" ]
}
