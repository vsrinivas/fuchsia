# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//src/sys/build/components.gni")

rustc_library("lib") {
  name = "fvm_stress_test_lib"
  edition = "2018"
  source_root = "lib/mod.rs"

  deps = [
    "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition-rustc",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/storage/block_client/rust:remote-block-device",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/stress-test-utils",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",  # for recording messages
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:rand_core",
  ]

  sources = [
    "lib/fvm.rs",
    "lib/mod.rs",
    "lib/operator.rs",
  ]
}

rustc_binary("stressor-bin") {
  name = "fvm_stressor"
  edition = "2018"
  source_root = "main.rs"

  deps = [
    ":lib",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/stress-test-utils",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:rand_core",
  ]

  sources = [ "main.rs" ]
}

fuchsia_component("crashable-stressor") {
  testonly = true
  manifest = "meta/crashable-stressor.cmx"
  deps = [ ":stressor-bin" ]
}

fuchsia_component("no-crash-stressor") {
  testonly = true
  manifest = "meta/no-crash-stressor.cmx"
  deps = [ ":stressor-bin" ]
}

# This test package is designed to run on infra bots.
# Infra bots have a time limit of 23 hours. The test runs
# for 22 hours, leaving an hour for the build process.
fuchsia_test_package("fvm-stress-test") {
  test_components = [
    ":crashable-stressor",
    ":no-crash-stressor",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          device_type = "QEMU"
        }
        tags = [ "stress-tests" ]
      },
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":fvm-stress-test",
    "//src/lib/isolated_devmgr/v2_component:isolated-devmgr",
    "//src/sys/component_manager:component-manager",
  ]
}
