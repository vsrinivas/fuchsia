# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

rustc_library("fvm-stress-test-lib") {
  edition = "2018"
  source_root = "lib/mod.rs"

  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.device:fuchsia.device-rustc",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block-rustc",
    "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition-rustc",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/storage/block_client/rust:remote-block-device",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/component_manager/testing:test_utils_lib",
    "//src/sys/pkg/lib/fuchsia-hash",
    "//src/sys/pkg/lib/fuchsia-merkle",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",  # for recording messages
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:rand_core",
  ]

  sources = [
    "lib/fvm.rs",
    "lib/mod.rs",
    "lib/state.rs",
    "lib/utils.rs",
  ]

  non_rust_deps = [ "//zircon/system/ulib/fs-management" ]
}

rustc_binary("stressor-bin") {
  name = "stressor"
  edition = "2018"
  source_root = "main.rs"

  deps = [
    ":fvm-stress-test-lib",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:rand_core",
  ]

  sources = [ "main.rs" ]
}

test_package("fvm-stress-test") {
  deps = [
    ":stressor-bin",
    "//src/lib/isolated_devmgr/v2_component:isolated-devmgr",
    "//src/sys/component_manager:component-manager",
  ]

  meta = [
    {
      path = rebase_path("meta/root.cml")
      dest = "root.cm"
    },
  ]

  tests = [
    {
      name = "stressor"
      log_settings = {
        max_severity = "ERROR"
      }
    },
  ]
}
