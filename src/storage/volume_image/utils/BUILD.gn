# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("compression-interfaces") {
  public = [ "compressor.h" ]
  public_deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
  ]
}

source_set("guid") {
  sources = [ "guid.cc" ]
  public = [ "guid.h" ]
  public_deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
  ]
}

source_set("guid-test") {
  testonly = true
  sources = [ "guid_test.cc" ]
  deps = [
    ":guid",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
  ]

  # This double checks that the resulting guids are compatible with our existing guids.
  # Because gpt is fuchsia only, this is required.
  if (is_fuchsia) {
    sources += [ "guid_test_fuchsia.cc" ]
    deps += [ "//zircon/system/ulib/gpt" ]
  }
}

source_set("path") {
  sources = [ "path.cc" ]
  public = [ "path.h" ]
}

source_set("io-interfaces") {
  public = [
    "reader.h",
    "writer.h",
  ]
  public_deps = [ "//zircon/public/lib/fbl" ]
}

source_set("extent") {
  sources = [ "extent.cc" ]
  public = [ "extent.h" ]
  public_deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
  ]
}

source_set("extent-test") {
  testonly = true
  sources = [ "extent_test.cc" ]
  deps = [
    ":extent",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":extent-test",
    ":guid-test",
  ]
}
