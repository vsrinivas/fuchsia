# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/package.gni")
import("//src/camera/debug.gni")

name = "camera-factory"

executable("bin") {
  output_name = name

  defines = [ camera_debug_define ]
  sources = [ "main.cc" ]

  deps = [ ":lib" ]
}

source_set("lib") {
  defines = [ camera_debug_define ]

  sources = [
    "capture.cc",
    "capture.h",
    "factory_server.cc",
    "factory_server.h",
    "streamer.cc",
    "streamer.h",
    "web_ui.cc",
    "web_ui.h",
  ]

  deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp",
    "//src/lib/files:files",
  ]

  public_deps = [
    ":fuchsia.factory.camera",
    "//sdk/fidl/fuchsia.camera3",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fsl",
    "//third_party/libpng",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-utils",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fidl("fuchsia.factory.camera") {
  sdk_category = "internal"

  sources = [ "factory.test.fidl" ]

  public_deps = [ "//sdk/fidl/fuchsia.images" ]
}

package("${name}") {
  deps = [ ":bin" ]

  binaries = [
    {
      name = name
    },
  ]

  meta = [
    {
      path = "${name}.cmx"
      dest = "${name}.cmx"
    },
  ]
}
