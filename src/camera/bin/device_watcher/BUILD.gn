# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/package.gni")

declare_args() {
  camera_debug = false
}

executable("bin") {
  output_name = "device_watcher"

  sources = [
    "device_watcher_impl.cc",
    "device_watcher_impl.h",
    "main.cc",
  ]

  deps = [
    ":fuchsia.camera.test",
    "//sdk/fidl/fuchsia.camera2.hal",
    "//sdk/fidl/fuchsia.camera3",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//src/lib/syslog/cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fit",
    "//zircon/system/fidl/fuchsia-hardware-camera",
  ]

  if (camera_debug) {
    fx_log_debug = "(0)"
  } else {
    fx_log_debug = "(-1)"
  }

  defines = [ "FX_LOG_DEBUG=${fx_log_debug}" ]
}

fidl("fuchsia.camera.test") {
  sdk_category = "internal"

  sources = [ "device_watcher_tester.test.fidl" ]

  public_deps = [ "//sdk/fidl/fuchsia.camera2.hal" ]
}

package("camera_device_watcher") {
  deps = [ ":bin" ]

  binary = "device_watcher"

  meta = [
    {
      path = rebase_path("manifest.cmx")
      dest = "manifest.cmx"
    },
  ]
}

group("device_watcher") {
  public_deps = [ ":camera_device_watcher" ]
}
