# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

group("tests") {
  testonly = true
  deps = [
    ":imx227_bind_test",
    ":imx227_unittest",
  ]
}

driver_bind_rules("imx227_bind") {
  rules = "imx227.bind"
  header_output = "bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.sony.platform" ]
}

source_set("lib") {
  sources = [
    "imx227.cc",
    "imx227_otp.cc",
    "imx227_protocol.cc",
  ]

  deps = [
    ":imx227_bind",
    "//sdk/banjo/fuchsia.hardware.camera:fuchsia.hardware.camera_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/lib/syslog/cpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk:ddk-metadata-headers",
    "//zircon/system/ulib/fzl",
    "//zircon/third_party/ulib/safemath",
  ]

  public_deps = [
    "//sdk/banjo/fuchsia.hardware.camera.sensor:fuchsia.hardware.camera.sensor_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.clock:fuchsia.hardware.clock_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.mipicsi:fuchsia.hardware.mipicsi_banjo_cpp",
    "//sdk/lib/fit-promise",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//zircon/system/ulib/driver-unit-test",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_driver("fuchsia_driver") {
  output_name = "imx227"

  sources = [ "imx227_hardware_test.cc" ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_hlcpp",
    "//sdk/lib/syslog/cpp:backend",
  ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}

fuchsia_driver_component("driver") {
  component_name = "imx227"
  deps = [ ":fuchsia_driver" ]
  info = "driver-info.json"
  colocate = true
}

fuchsia_driver_package("imx227") {
  driver_components = [ ":driver" ]
}

executable("imx227_unittest_bin") {
  testonly = true
  sources = [ "imx227_unittest.cc" ]
  deps = [
    ":lib",
    "//sdk/banjo/fuchsia.hardware.clock:fuchsia.hardware.clock_banjo_cpp_mock",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp_mock",
    "//sdk/banjo/fuchsia.hardware.mipicsi:fuchsia.hardware.mipicsi_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("imx227_unittest") {
  deps = [ ":imx227_unittest_bin" ]
}
