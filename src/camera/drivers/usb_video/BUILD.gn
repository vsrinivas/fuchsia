# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

bind_rules("usb_video_bind") {
  rules = "usb_video.bind"
  output = "usb_video_bind.h"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.usb" ]
}

driver_module("usb_video_driver") {
  output_name = "usb_video"

  sources = [
    "camera_control_impl.cc",
    "camera_control_impl.h",
    "usb_video.cc",
    "usb_video.h",
    "usb_video_stream.cc",
    "usb_video_stream.h",
    "uvc_format.cc",
    "uvc_format.h",
    "video_util.c",
    "video_util.h",
  ]

  deps = [
    ":usb_video_bind",
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/fidl/fuchsia.camera",
    "//sdk/fidl/fuchsia.hardware.camera:fuchsia.hardware.camera_c",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/listnode",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fzl",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

driver_package("usb_video") {
  deps = [ ":usb_video_driver" ]

  drivers = [
    {
      name = "usb_video.so"
    },
  ]
}

test("usb_video_test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "usb_video_test"
  sources = [
    "uvc_format.cc",
    "uvc_format_test.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.i2c",
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/fidl/fuchsia.camera",
    "//sdk/fidl/fuchsia.hardware.camera:fuchsia.hardware.camera_c",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/testing/fake_ddk",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fzl",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

unittest_package("usb_video_test_package") {
  package_name = "usb-video-test"
  deps = [ ":usb_video_test" ]

  tests = [
    {
      name = "usb_video_test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":usb_video_bind_test",
    ":usb_video_test_package",
  ]
}
