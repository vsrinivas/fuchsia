# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cipd.gni")
import("//src/camera/drivers/controller/camera_controller_driver.gni")

group("sherlock") {
  public_deps = [ ":sherlock-camera-controller" ]
}

source_set("config") {
  sources = [
    "common_util.cc",
    "common_util.h",
    "isp_debug_config.cc",
    "isp_debug_config.h",
    "monitoring_config.cc",
    "monitoring_config.h",
    "sherlock_product_config.cc",
    "sherlock_product_config.h",
    "video_conferencing_config.cc",
    "video_conferencing_config.h",
  ]
  public_deps = [
    "//sdk/banjo/ddk.protocol.ge2d",
    "//sdk/banjo/ddk.protocol.sysmem",
    "//sdk/fidl/fuchsia.camera2",
    "//sdk/fidl/fuchsia.camera2.hal",
    "//src/camera/drivers/controller/configs",
    "//src/camera/lib/stream_utils:stream_configs",
    "//zircon/system/ulib/fidl-utils",
  ]
}

camera_controller_driver("sherlock-camera-controller") {
  deps = [
    ":config",
    "//sdk/lib/syslog/cpp:backend_legacy",
  ]

  # The following config files are currently gated behind internal
  # access to the CIPD repository.
  if (internal_access) {
    gdc_config_files = [
      "config_001_2176x2720-to-640x512-RS-YUV420SemiPlanar.bin",
      "config_001_2240x1792-to-640x512-S-YUV420SemiPlanar.bin",
      "config_002_1152x1440-to-1440x1152-DKCR-YUV420SemiPlanar.bin",
      "config_002_2176x2720-to-2240x1792-DKCR-YUV420SemiPlanar.bin",
      "config_003_2176x2720-to-2240x1792-DKCR-YUV420SemiPlanar.bin",
      "config_1152x1440_to_1152x864_Crop_Rotate.bin",
      "config_1152x1440_to_512x384_Crop_Rotate.bin",
      "config_1152x1440_to_720x540_Crop_Rotate.bin",
    ]
    watermark_files = [
      "watermark-360p.rgba",
      "watermark-480p.rgba",
      "watermark-720p.rgba",
    ]
  }
}
