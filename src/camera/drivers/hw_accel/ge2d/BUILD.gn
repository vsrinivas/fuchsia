# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")

bind_rules("ge2d_bind") {
  rules = "ge2d.bind"
  output = "ge2d-bind.h"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.composite",
    "//src/devices/bind/fuchsia.platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ge2d_bind_test" ]
}

source_set("lib") {
  sources = [
    "ge2d.cc",
    "ge2d.h",
    "ge2d_regs.h",
    "ge2d_task.cc",
    "ge2d_task.h",
  ]
  public_deps = [
    "//sdk/banjo/ddk.protocol.amlogiccanvas",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.ge2d",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.sysmem",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
    "//sdk/lib/syslog/cpp",
    "//src/camera/drivers/hw_accel/task:camera-task",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
  ]
  deps = [
    "//src/devices/lib/driver",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/trace:trace-driver",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
}

driver_module("ge2d-driver") {
  output_name = "ge2d"
  sources = [ "bind.cc" ]
  deps = [
    ":ge2d_bind",
    ":lib",
    "test:device_test",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//zircon/system/ulib/driver-unit-test",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
}

driver_package("ge2d") {
  deps = [ ":ge2d-driver" ]

  drivers = [
    {
      name = "ge2d.so"
    },
  ]
}
