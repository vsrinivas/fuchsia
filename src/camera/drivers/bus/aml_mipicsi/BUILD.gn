# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

group("tests") {
  testonly = true
  deps = [ ":aml_mipi_bind_test" ]
}

driver_bind_rules("aml_mipi_bind") {
  rules = "aml_mipi.bind"
  header_output = "bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.amlogic.platform" ]
}

fuchsia_driver("aml_mipicsi_driver-driver") {
  output_name = "aml_mipicsi"
  sources = [
    "aml_mipi.cc",
    "aml_mipi.h",
    "aml_mipi_adap.cc",
    "aml_mipi_debug.cc",
    "aml_mipi_regs.h",
  ]
  deps = [
    ":aml_mipi_bind",
    "//sdk/banjo/fuchsia.hardware.mipicsi:fuchsia.hardware.mipicsi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
}

fuchsia_driver_component("aml_mipicsi_driver") {
  component_name = "aml_mipicsi"
  deps = [ ":aml_mipicsi_driver-driver" ]
  info = "aml_mipicsi_driver-info.json"
  colocate = true
}

fuchsia_driver_package("driver_package") {
  package_name = "aml-mipicsi"
  driver_components = [ ":aml_mipicsi_driver" ]
}

group("aml_mipicsi") {
  deps = [ ":driver_package" ]
}
