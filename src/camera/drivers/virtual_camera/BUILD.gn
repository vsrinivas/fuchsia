# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")

driver_module("virtual_camera_driver") {
  output_name = "virtual_camera_driver"

  deps = [
    "//src/camera/drivers/virtual_camera:virtual_camera_source",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}

source_set("virtual_camera_source") {
  sources = [
    "virtual_camera.cc",
    "virtual_camera2_control.cc",
    "virtual_camera2_control.h",
    "virtual_camera_control.cc",
    "virtual_camera_control.h",
    "virtual_camera_device.cc",
    "virtual_camera_device.h",
  ]

  deps = [
    ":hsv_color_source",
    "//sdk/fidl/fuchsia.camera",
    "//sdk/fidl/fuchsia.camera2",
    "//sdk/fidl/fuchsia.camera2.hal",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/media/cpp",
    "//src/devices:driver",
    "//src/lib/fxl",
    "//src/lib/syslog/cpp",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.usb",
    "//zircon/system/fidl/fuchsia-hardware-camera:fuchsia-hardware-camera_c",
  ]
}

source_set("hsv_color_source") {
  sources = [
    "color_source.cc",
    "hsv_generator.cc",
    "image_format_rgba.cc",
  ]

  public = [
    "color_source.h",
    "hsv_generator.h",
    "image_format_rgba.h",
  ]

  deps = [
    "//sdk/lib/fidl/cpp",
    "//src/lib/syslog/cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/image-format",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-sysmem",
  ]
}

package("virtual_camera") {
  deprecated_system_image = true

  deps = [
    ":virtual_camera_driver",
  ]

  drivers = [
    {
      name = "virtual_camera_driver.so"
    },
  ]
}
