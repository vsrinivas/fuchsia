# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//src/sys/build/components.gni")

rustc_binary("bin") {
  name = "sink-validator"
  edition = "2018"
  with_unit_tests = true
  deps = [
    "fidl:sink-puppet-rustc",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.diagnostics.stream:fuchsia.diagnostics.stream-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/lib/diagnostics/stream/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_unittest_package("sink-validator-tests") {
  deps = [ ":bin_test" ]
  executable_path = "bin/sink_validator_bin_test"
}

### C++ ###

fuchsia_component("sink-validator-for-cpp") {
  manifest = "meta/validator-cpp.cmx"
  deps = [ ":bin" ]
}

fuchsia_test_package("log-sink-validator-cpp") {
  deps = [ "puppet/cpp:component" ]
  test_components = [ ":sink-validator-for-cpp" ]
}

group("tests") {
  testonly = true

  deps = [
    ":log-sink-validator-cpp",
    ":sink-validator-tests",
  ]
}
