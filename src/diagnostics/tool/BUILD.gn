# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_binary("bin") {
  name = "diag_tool"
  version = "0.1.0"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//src/connectivity/overnet/lib/hoist",
    "//src/diagnostics/lib/diagnostics-schema",
    "//src/diagnostics/lib/selectors",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/inspect/rust/fuchsia-inspect-node-hierarchy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:clap",
    "//third_party/rust_crates:difference",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:structopt",
    "//third_party/rust_crates:tempfile",
    "//third_party/rust_crates:termion",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/main.rs" ]
}

install_host_tools("diag_tool_host") {
  deps = [ ":bin($host_toolchain)" ]
  outputs = [ "diag_tool" ]
}

test_package("diag_tool_tests") {
  deps = [ ":bin_test" ]
  tests = [
    {
      name = "diag_tool_bin_test"
      dest = "diag_tool_bin_test"
      environments = basic_envs
    },
  ]
}

group("host_tests") {
  testonly = true
  deps = [ ":diag_tool_tests($host_toolchain)" ]
}
