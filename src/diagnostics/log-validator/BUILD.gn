# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

group("tests") {
  testonly = true

  deps = [
    ":log-validator-tests",
    "puppet/cpp:tests",
    "puppet/dart:tests",
    "puppet/rust:tests",
  ]
}

validator_deps = [
  "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
  "//sdk/fidl/fuchsia.diagnostics.stream:fuchsia.diagnostics.stream-rustc",
  "//sdk/fidl/fuchsia.mem:fuchsia.mem-rustc",
  "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
  "//src/diagnostics/log-validator/fidl:validate-rustc",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fuchsia-async",
  "//src/lib/fuchsia-component",
  "//src/lib/fuchsia-url",
  "//src/lib/syslog/rust:syslog",
  "//src/lib/zircon/rust:fuchsia-zircon",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:argh",
  "//third_party/rust_crates:log",
  "//third_party/rust_crates:pretty_assertions",
]

rustc_binary("log-validator-bin") {
  name = "log-validator"
  edition = "2018"
  deps = validator_deps

  sources = [ "src/main.rs" ]
}

rustc_test("log-validator-bin-test") {
  edition = "2018"
  source_root = "src/main.rs"
  deps = validator_deps + [ "//src/lib/fuchsia-async" ]

  sources = [ "src/main.rs" ]
}

test_package("log-validator-tests") {
  deps = [ ":log-validator-bin-test" ]

  tests = [
    {
      name = "log_validator_bin_test"
    },
  ]
}
