# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//src/sys/build/components.gni")

group("logs-lifecycle") {
  testonly = true
  deps = [ ":package" ]
}

rustc_test("test") {
  name = "logs-lifecycle"

  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "src/lib.rs" ]
}

fuchsia_component("component") {
  testonly = true
  component_name = "test-logs-lifecycle"
  manifest = "meta/test-logs-lifecycle.cml"
  deps = [ ":test" ]
}

rustc_binary("launched") {
  testonly = true
  name = "logs_when_launched"
  source_root = "src/logs_when_launched.rs"

  deps = [
    "//src/lib/syslog/rust",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/logs_when_launched.rs" ]
}

fuchsia_component("logs-when-launched") {
  testonly = true
  manifest = "meta/logs-when-launched.cml"
  deps = [ ":launched" ]
}

fuchsia_test_package("package") {
  package_name = "test-logs-lifecycle"
  test_components = [ ":component" ]
  deps = [
    ":logs-when-launched",
    "//src/diagnostics/archivist:archivist-for-embedding-v2",
  ]
}
