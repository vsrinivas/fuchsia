# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_binary("stub_inspect_component") {
  name = "stub_inspect_component"
  edition = "2018"
  source_root = "components/stub_inspect_component.rs"
  deps = [
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/inspect/rust/fuchsia-inspect",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "components/stub_inspect_component.rs" ]
}

rustc_test("integration_tests_bin") {
  name = "archivist_integration_tests"
  edition = "2018"
  deps = [
    "//src/diagnostics/lib/selectors",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/inspect/rust/fuchsia-inspect",
    "//src/lib/inspect/rust/fuchsia-inspect-contrib",
    "//src/lib/syslog/rust:syslog",
    "//src/sys/lib/stdout-to-debuglog/rust:stdout-to-debuglog",
    "//src/sys/tools/cs:lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:log",
  ]

  sources = [ "src/lib.rs" ]
}

test_package("archivist-integration-tests-v2") {
  deps = [
    ":integration_tests_bin",
    ":stub_inspect_component",
    "//src/diagnostics/archivist:bin",
  ]

  binaries = [
    {
      name = "archivist"
    },
    {
      name = "stub_inspect_component"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/archivist_for_integration.cml")
      dest = "archivist.cm"
    },
    {
      path = rebase_path("meta/stub_inspect_component.cml")
      dest = "stub_inspect_component.cm"
    },
    {
      path = rebase_path("meta/driver.cml")
      dest = "driver.cm"
    },
  ]

  v2_tests = [
    {
      name = "archivist_integration_tests"
      environments = basic_envs
    },
  ]

  resources = [
    {
      path = rebase_path("configs/archivist_config.json")
      dest = "config/archivist_config.json"
    },
  ]
}
