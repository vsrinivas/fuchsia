# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/config.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

rustc_test("unified_reader_test") {
  edition = "2018"
  source_root = "src/main.rs"

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/lib/cstr",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:difference",
    "//third_party/rust_crates:difference",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:glob",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tempfile",
  ]
}

test_package("unified_reader_tests") {
  deps = [
    ":unified_reader_test",
    "//src/diagnostics/archivist:bin",
    "//src/diagnostics/iquery:bin",
    "//src/diagnostics/iquery/test/example_component:iquery_example_component",
  ]

  binaries = [
    {
      name = "iquery_example_component"
    },
    {
      name = "archivist"
    },
  ]

  meta = [
    {
      path = rebase_path(
              "//src/diagnostics/iquery/test/meta/iquery_example_component.cmx")
      dest = "iquery_example_component.cmx"
    },
    {
      path = "meta/unified_reader_test_archivist.cmx"
      dest = "unified_reader_test_archivist.cmx"
    },
  ]

  tests = [
    {
      name = "unified_reader_test"
    },
  ]
}

config_data("reader_golden_json") {
  for_pkg = "unified_reader_tests"
  outputs = [ "golden.json" ]

  sources = [ "configs/golden.json" ]
}

group("unified_reader_integration") {
  testonly = true
  public_deps = [
    ":reader_golden_json",
    ":unified_reader_tests",
  ]
}
