# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

lib_deps = [
  "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
  "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_rust",
  "//src/lib/diagnostics/hierarchy/rust",
  "//src/lib/diagnostics/selectors",
  "//src/lib/injectable-time",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:itertools",
  "//third_party/rust_crates:lazy_static",
  "//third_party/rust_crates:nom",
  "//third_party/rust_crates:num-derive",
  "//third_party/rust_crates:num-traits",
  "//third_party/rust_crates:regex",
  "//third_party/rust_crates:serde",
  "//third_party/rust_crates:serde_derive",
  "//third_party/rust_crates:serde_json",
  "//third_party/rust_crates:serde_json5",
]
if (is_fuchsia) {
  lib_deps += [ "//third_party/rust_crates:tracing" ]
}

library_sources = [
  "src/act.rs",
  "src/act_structured.rs",
  "src/config.rs",
  "src/lib.rs",
  "src/metrics.rs",
  "src/metrics/arithmetic.rs",
  "src/metrics/context.rs",
  "src/metrics/fetch.rs",
  "src/metrics/metric_value.rs",
  "src/metrics/parse.rs",
  "src/metrics/variable.rs",
  "src/plugins.rs",
  "src/plugins/crashes.rs",
  "src/plugins/helpers.rs",
  "src/plugins/memory.rs",
  "src/plugins/routing.rs",
  "src/plugins/sandbox_errors.rs",
  "src/result_format.rs",
  "src/validate.rs",
]

rustc_library("triage") {
  name = "fuchsia-triage"
  edition = "2021"
  version = "0.0.1"

  deps = lib_deps

  sources = library_sources
}

rustc_test("triage-lib-unittests") {
  edition = "2021"
  deps = [
           "//src/lib/fuchsia",
           "//third_party/rust_crates:maplit",
         ] + lib_deps

  sources = library_sources
}

group("tests") {
  testonly = true
  deps = [
    ":triage-lib-unittests($host_toolchain)",
    "wasm:tests",
  ]
}
