# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")

source_set("use_media_decoder_source_set") {
  testonly = true

  sources = [
    "cleartvp.cc",
    "in_stream.cc",
    "in_stream_file.cc",
    "in_stream_http.cc",
    "in_stream_peeker.cc",
    "test/use_video_decoder_test.cc",
    "use_aac_decoder.cc",
    "use_aac_decoder.h",
    "use_video_decoder.cc",
    "use_video_decoder.h",
    "util.cc",
    "util.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.mediacodec",
    "//sdk/fidl/fuchsia.net.oldhttp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//src/media/lib/codec_impl:fourcc",
    "//third_party/boringssl:crypto",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fbl",
  ]

  deps = [
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/ui/base_view",
    "//src/media/audio/lib/wav_writer:wav_writer",
    "//src/media/lib/codec_impl:fourcc",
    "//src/media/lib/raw_video_writer:raw_video_writer",
    "//src/media/lib/test:codec_client",
    "//src/media/lib/test:frame_sink",
    "//src/media/lib/test:one_shot_event",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/tee-client-api",
    "//zircon/system/fidl/fuchsia-hardware-securemem",
  ]
}

executable("use_media_decoder") {
  testonly = true

  output_name = "use_media_decoder"

  sources = [
    "main.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
    "//src/media/lib/test:frame_sink",
    "//src/media/lib/test:one_shot_event",
  ]
}

# This test target uses the AAC decoder the same way as the example program and
# then also verifies the output data (without writing an output file).
executable("use_aac_decoder_test") {
  testonly = true
  output_name = "use_aac_decoder_test"

  sources = [
    "test/use_aac_decoder_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
    "//src/lib/fxl/test:gtest_main",
  ]
}

# This test target uses the h264 decoder the same way as the example program and
# then also verifies the output data (without writing an output file).
test("use_h264_decoder_test") {
  testonly = true

  output_name = "use_h264_decoder_test"

  sources = [
    "test/use_h264_decoder_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # In environments without an h264 HW decoder, this test will fall back to a SW
  # decoder.
  environments = [ astro_env ] + basic_envs
}

test("use_h264_decoder_secure_output_test") {
  testonly = true

  output_name = "use_h264_decoder_secure_output_test"

  sources = [
    "test/use_h264_decoder_secure_output_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # This test can only use HW decode because codec_impl won't map the output
  # VMOs for CPU access.
  environments = [ astro_env ]
}

test("use_h264_decoder_secure_input_output_test") {
  testonly = true

  output_name = "use_h264_decoder_secure_input_output_test"

  sources = [
    "test/use_h264_decoder_secure_input_output_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # This test needs a the clearTVP TA
  environments = [ sherlock_env ]
}

test("use_vp9_decoder_test") {
  testonly = true

  output_name = "use_vp9_decoder_test"

  sources = [
    "test/use_vp9_decoder_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # In contrast to use_h264_decoder_test, there is no SW fallback for VP9 at the
  # moment, so for now only list envs with HW decode.
  environments = [
    astro_env,
    sherlock_env,
    vim2_env,
  ]
}

test("use_vp9_decoder_24_output_frames_test") {
  testonly = true

  output_name = "use_vp9_decoder_24_output_frames_test"

  sources = [
    "test/use_vp9_decoder_24_output_frames_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # In contrast to use_h264_decoder_test, there is no SW fallback for VP9 at the
  # moment, so for now only list envs with HW decode, and which also have
  # sufficient contiguous sysmem heap to handle 24 frames from bear-vp9.ivf.
  #
  # TODO(fxb/41735): Enable on astro in future if/when posible.
  environments = [
    sherlock_env,
    vim2_env,
  ]
}

test("use_vp9_decoder_secure_output_test") {
  testonly = true

  output_name = "use_vp9_decoder_secure_output_test"

  sources = [
    "test/use_vp9_decoder_secure_output_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]
}

test("vp9_decoder_conformance_test") {
  testonly = true

  output_name = "vp9_decoder_conformance_test"

  sources = [
    "test/vp9_decoder_conformance_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # No VP9 SW fallback for now, so the test can only pass running on envs with
  # HW decode.
  environments = [ astro_env ]
}
