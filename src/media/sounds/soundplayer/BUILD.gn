# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

package("soundplayer") {
  deps = [ ":bin" ]

  binaries = [
    {
      name = "soundplayer"
    },
  ]
  meta = [
    {
      path = rebase_path("meta/soundplayer.cmx")
      dest = "soundplayer.cmx"
    },
  ]
}

rustc_binary("bin") {
  name = "soundplayer"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//sdk/fidl/fuchsia.media.sounds:fuchsia.media.sounds-rustc",
    "//sdk/fidl/fuchsia.mem:fuchsia.mem-rustc",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]

  # TODO(56138): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]

  test_deps = [
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//third_party/rust_crates:futures-test",
    "//third_party/rust_crates:matches",
  ]

  sources = [
    "src/main.rs",
    "src/sound_player.rs",
    "src/sound_player/wav_reader.rs",
    "src/test.rs",
  ]
}

config_data("configs") {
  for_pkg = "sysmgr"
  outputs = [ "soundplayer.config" ]
  sources = [ "service.config" ]
}

test_package("soundplayer_tests") {
  deps = [ ":bin_test" ]

  tests = [
    {
      name = "soundplayer_bin_test"
      environments = basic_envs
    },
  ]

  resources = [
    {
      path = rebase_path(
              "//garnet/test_data/media/third_party/chromium_media_test_data/sfx_s16le.wav")
      dest = "sfx.wav"
    },
  ]
}
