# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_macro.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("test") {
  testonly = true
  deps = [
    ":audio_encoder_test",
    ":h264_decoder_test",
    ":stream_processor_lib_test",
    ":video_frame_hasher_test",
  ]
}

rustc_library("stream_processor_test_lib") {
  name = "stream_processor_test"
  edition = "2018"
  testonly = true
  with_unit_tests = true

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/mundane",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//sdk/fidl/fuchsia.mediacodec:fuchsia.mediacodec-rustc",
    "//src/lib/fidl_table_validation",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/media/stream_processors/fuchsia-stream-processors",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hex",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:thiserror",
    "//zircon/system/fidl/fuchsia-sysmem:fuchsia-sysmem-rustc",
  ]
}

test_package("stream_processor_lib_test") {
  deps = [
    ":stream_processor_test_lib_test",
  ]

  tests = [
    {
      name = "stream_processor_test_lib_test"
      environments = basic_envs
    },
  ]
}

rustc_library("stream_processor_decoder_factory_lib") {
  name = "stream_processor_decoder_factory"
  edition = "2018"
  source_root = "stream_processor_factories/decoders.rs"
  testonly = true

  deps = [
    ":stream_processor_test_lib",
    "//garnet/public/lib/fidl/rust/fidl",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//sdk/fidl/fuchsia.mediacodec:fuchsia.mediacodec-rustc",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]
}

rustc_library("stream_processor_encoder_factory_lib") {
  name = "stream_processor_encoder_factory"
  edition = "2018"
  source_root = "stream_processor_factories/encoders.rs"
  testonly = true

  deps = [
    ":stream_processor_test_lib",
    "//garnet/public/lib/fidl/rust/fidl",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//sdk/fidl/fuchsia.mediacodec:fuchsia.mediacodec-rustc",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]
}

rustc_test("audio_encoder_test_bin") {
  name = "audio_encoder_test"
  edition = "2018"
  source_root = "audio_encoder_test/lib.rs"

  deps = [
    ":stream_processor_encoder_factory_lib",
    ":stream_processor_test_lib",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/mundane",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//src/lib/fuchsia-async",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hex",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:thiserror",
    "//zircon/system/fidl/fuchsia-sysmem:fuchsia-sysmem-rustc",
  ]
}

test_package("audio_encoder_test") {
  deps = [
    ":audio_encoder_test_bin",
  ]

  tests = [
    {
      name = "audio_encoder_test"
      environments = basic_envs
    },
  ]
}

rustc_library("video_frame_hasher_lib") {
  name = "video_frame_hasher"
  edition = "2018"
  with_unit_tests = true
  testonly = true
  source_root = "video_frame_hasher/lib.rs"

  deps = [
    ":stream_processor_test_lib",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/mundane",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//src/media/stream_processors/fuchsia-stream-processors",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:hex",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:thiserror",
    "//zircon/system/fidl/fuchsia-sysmem:fuchsia-sysmem-rustc",
  ]
}

test_package("video_frame_hasher_test") {
  deps = [
    ":video_frame_hasher_lib_test",
  ]

  tests = [
    {
      name = "video_frame_hasher_lib_test"
      environments = basic_envs
    },
  ]
}

rustc_test("h264_decoder_test_bin") {
  name = "h264_decoder_test"
  edition = "2018"
  source_root = "h264_decoder_test/lib.rs"

  deps = [
    ":stream_processor_decoder_factory_lib",
    ":stream_processor_test_lib",
    ":video_frame_hasher_lib",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-syslog",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:thiserror",
  ]
}

test_package("h264_decoder_test") {
  deps = [
    ":h264_decoder_test_bin",
  ]

  tests = [
    {
      name = "h264_decoder_test"
      environments = basic_envs
    },
  ]

  resources = [
    {
      path = rebase_path(
              "../../../../garnet/test_data/media/third_party/chromium_media_test_data/bear.h264")
      dest = "bear.h264"
    },
  ]
}
