# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//src/media/audio/audio_core_package.gni")
import("//src/media/audio/hardware.gni")

loadable_module("inversion_filter") {
  sources = [ "inversion_filter.cc" ]

  deps = [
    "//sdk/lib/media/audio/effects",
    "//sdk/lib/syslog/cpp",
    "//third_party/rapidjson",
    "//zircon/system/ulib/fzl",
  ]
}

audio_core_test_package("audio-core-for-pipeline-tests") {
  effects = {
    target = "//src/media/audio/audio_core/test/pipeline:inversion_filter"
    name = "inversion_filter.so"
  }
}

test_package("audio_pipeline_tests") {
  deps = [
    ":audio-core-for-pipeline-tests",
    ":audio_pipeline_tests_bin",
  ]

  resources = [
    {
      path = "audio_core_config_with_inversion_filter.json"
      dest = "audio_core_config_with_inversion_filter/audio_core_config.json"
    },
    {
      path = "ultrasound_audio_core_config.json"
      dest = "ultrasound/audio_core_config.json"
    },
  ]

  tests = [
    {
      name = "audio_pipeline_tests"

      # This (effectively real-time) test must be run on a true hardware-based target.
      environments = hardware_envs
    },
  ]
}

executable("audio_pipeline_tests_bin") {
  testonly = true
  output_name = "audio_pipeline_tests"

  sources = [
    "audio_pipeline_test.cc",
    "ultrasound_test.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.media",
    "//sdk/fidl/fuchsia.ultrasound",
    "//sdk/fidl/fuchsia.virtualaudio",
    "//sdk/lib/media/cpp:no_converters",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/clock:utils",
    "//src/media/audio/lib/clock/testing",
    "//src/media/audio/lib/logging",
    "//src/media/audio/lib/test:hermetic_audio_test",
    "//zircon/system/ulib/fzl",
  ]
}
