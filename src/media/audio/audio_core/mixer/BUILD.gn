# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("mixer") {
  public_configs = [ "..:audio_core_config" ]
  sources = [
    "constants.h",
    "gain.cc",
    "gain.h",
    "linear_sampler.cc",
    "linear_sampler.h",
    "mixer.cc",
    "mixer.h",
    "mixer_utils.h",
    "no_op.cc",
    "no_op.h",
    "output_producer.cc",
    "output_producer.h",
    "point_sampler.cc",
    "point_sampler.h",
    "usage_gain_settings.cc",
    "usage_gain_settings.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.media",
    "//src/media/audio/lib/logging",
    "//zircon/public/lib/fbl",
  ]

  deps = [
    "//sdk/lib/media/cpp:no_converters",
    "//src/lib/fxl",
    "//zircon/public/lib/trace",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":audio_mixer_unittests",
    "test",
  ]
}

test_package("audio_mixer_unittests") {
  deps = [
    ":unittest_bin",
  ]

  tests = [
    {
      name = "audio_mixer_unittests"
      environments = basic_envs
    },
  ]
}

executable("unittest_bin") {
  testonly = true
  output_name = "audio_mixer_unittests"

  sources = [
    "bookkeeping_unittest.cc",
    "output_producer_unittest.cc",
    "usage_gain_settings_unittest.cc",
  ]

  deps = [
    ":mixer",
    "//src/lib/fxl/test:gtest_main",
  ]
}
