# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("effects") {
  testonly = true
  deps = [ ":audio_effects_example" ]
}

group("tests") {
  testonly = true
  deps = [ ":audio_effects_example_tests" ]
}

source_set("headers") {
  testonly = true
  sources = [
    "delay_effect.h",
    "effect_base.h",
    "rechannel_effect.h",
    "swap_effect.h",
  ]
}

loadable_module("audio_effects_example") {
  testonly = true

  sources = [
    "delay_effect.cc",
    "effect_base.cc",
    "lib_audio_effects.cc",
  ]

  deps = [
    ":headers",
    "//sdk/lib/media/audio/effects",
    "//zircon/public/lib/fbl",

    # TODO(57392): Move it back to //third_party once unification completes.
    "//zircon/third_party/rapidjson",
  ]
  configs += [
    # TODO(fxb/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}

executable("test_bin") {
  testonly = true
  output_name = "audio_effects_example_tests"

  sources = [ "test/audio_effects_example_tests.cc" ]

  libs = [ "zircon" ]

  deps = [
    ":headers",
    "//sdk/lib/media/audio/effects",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/effects_loader",
    "//third_party/googletest:gmock",
    "//zircon/public/lib/fbl",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

unittest_package("audio_effects_example_tests") {
  deps = [
    ":audio_effects_example",
    ":test_bin",
  ]

  loadable_modules = [
    {
      name = "audio_effects_example.so"
    },
  ]

  tests = [
    {
      name = "audio_effects_example_tests"
      environments = basic_envs
    },
  ]
}
