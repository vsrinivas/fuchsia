# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/unification/zbi/bootfs_test.gni")

group("audio-driver-ctl") {
  testonly = true
  deps = [
    ":bin",
    ":src",
    ":test",
  ]
}

source_set("src") {
  visibility = [ ":*" ]
  sources = [
    "generated-source.cc",
    "generated-source.h",
    "noise-source.h",
    "sine-source.h",
    "wav-common.cc",
    "wav-common.h",
    "wav-sink.cc",
    "wav-sink.h",
    "wav-source.cc",
    "wav-source.h",
  ]
  public_deps = [
    "//src/lib/fsl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/audio-proto-utils",
    "//zircon/system/ulib/audio-utils",
  ]
  deps = [ "//sdk/lib/fdio" ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "audio-driver-ctl"
  output_dir = "$root_out_dir/zircon-migrated"
  sources = [ "audio.cc" ]
  deps = [ ":src" ]
}

test("test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "audio-test"
  sources = [
    "noise-source-test.cc",
    "sine-source-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":src",
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fzl",
  ]
}

bootfs_test("test-bootfs-test") {
  name = "audio-test"
  deps = [ ":test" ]
}
