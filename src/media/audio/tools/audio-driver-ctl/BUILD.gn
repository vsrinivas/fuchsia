# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("audio-driver-ctl") {
  testonly = true
  deps = [
    ":audio-driver-ctl-test",
    ":bin",
    ":src",
  ]
}

source_set("src") {
  visibility = [ ":*" ]
  sources = [
    "generated-source.cc",
    "generated-source.h",
    "noise-source.h",
    "sine-source.h",
    "wav-common.cc",
    "wav-common.h",
    "wav-sink.cc",
    "wav-sink.h",
    "wav-source.cc",
    "wav-source.h",
  ]
  public_deps = [
    "//src/lib/fsl",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/drivers/lib/audio-utils",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
  ]
  deps = [ "//sdk/lib/fdio" ]
}

executable("bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "audio-driver-ctl"
  output_dir = "$root_out_dir/zircon-migrated"
  sources = [ "audio.cc" ]
  deps = [ ":src" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

test("audio-driver-ctl-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "audio-driver-ctl-test"
  sources = [
    "noise-source-test.cc",
    "sine-source-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":src",
    "//sdk/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
  ]
}

fuchsia_unittest_package("audio-driver-ctl-test-pkg") {
  deps = [ ":audio-driver-ctl-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":audio-driver-ctl-test-pkg" ]
}
