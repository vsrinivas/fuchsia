# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":audio_services_mixer_common_unittests" ]
}

source_set("common") {
  visibility = [
    "./*",
    "//src/media/audio/services/mixer/*",
    "//src/media/audio/services/mixer/fidl/*",
    "//src/media/audio/services/mixer/mix/*",
  ]

  sources = [
    "atomic_optional.h",
    "basic_types.h",
    "global_task_queue.cc",
    "global_task_queue.h",
    "memory_mapped_buffer.cc",
    "memory_mapped_buffer.h",
    "thread_safe_queue.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.audio.mixer:fuchsia.audio.mixer_cpp_wire",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fidl/cpp",
    "//src/media/audio/lib/clock",
    "//src/media/audio/lib/format2",
    "//src/media/audio/lib/timeline",
    "//zircon/system/ulib/fzl",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]
}

executable("common_unittests") {
  output_name = "audio_services_mixer_common_unittests"
  testonly = true

  sources = [
    "atomic_optional_unittest.cc",
    "global_task_queue_unittest.cc",
    "memory_mapped_buffer_unittest.cc",
    "thread_safe_queue_unittest.cc",
  ]

  deps = [
    ":common",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_unittest_package("audio_services_mixer_common_unittests") {
  deps = [ ":common_unittests" ]
}
