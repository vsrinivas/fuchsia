# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

# Clock-related utility functions, for audio tests, tools or drivers

import("//build/test/test_package.gni")

source_set("clock") {
  sources = [
    "pid_control.cc",
    "pid_control.h",
    "utils.cc",
    "utils.h",
  ]

  public_deps = [
    "//sdk/lib/media/cpp:no_converters",
    "//src/lib/syslog/cpp",
    "//zircon/public/lib/affine",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]
}

executable("unittest_bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-clock-lib-unittests"

  sources = [
    "pid_control_unittest.cc",
    "utils_unittest.cc",
  ]

  deps = [
    ":clock",
    "//src/lib/fxl/test:gtest_main_with_syslog",
  ]

  libs = [ "zircon" ]
}

test_package("audio-clock-lib-unittests") {
  deps = [ ":unittest_bin" ]

  tests = [
    {
      name = "audio-clock-lib-unittests"

      dest = "audio_clock_lib_unittests"
      manifest = rebase_path("meta/audio_clock_lib_unittests.cmx")

      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":audio-clock-lib-unittests" ]
}
