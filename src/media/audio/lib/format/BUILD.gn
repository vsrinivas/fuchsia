# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")

source_set("format") {
  sources = [
    "audio_buffer.h",
    "constants.h",
    "driver_format.cc",
    "driver_format.h",
    "format.cc",
    "format.h",
    "frames.h",
    "traits.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.audio",
    "//sdk/fidl/fuchsia.media",
    "//sdk/lib/syslog/cpp",
    "//src/media/audio/lib/timeline",
    "//zircon/public/lib/ffl",
    "//zircon/system/ulib/trace",
  ]

  deps = [
    "//src/lib/fxl",
    "//zircon/system/ulib/audio-proto-utils",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("unittest-bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libformat-unittests"

  sources = [
    "audio_buffer_unittest.cc",
    "format_unittest.cc",
  ]

  deps = [
    ":format",
    "//src/lib/fxl/test:gtest_main",
    "//src/zircon/lib/zircon",
    "//third_party/googletest:gmock",
  ]
}

test_package("audio-libformat-unittests") {
  deps = [ ":unittest-bin" ]

  tests = [
    {
      name = "audio-libformat-unittests"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":audio-libformat-unittests" ]
}
