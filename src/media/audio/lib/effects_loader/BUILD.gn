# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

source_set("effects_loader") {
  sources = [
    "effect_v1.cc",
    "effect_v1.h",
    "effects_loader.h",
    "effects_loader_v1.cc",
    "effects_loader_v1.h",
    "effects_loader_v2.cc",
    "effects_loader_v2.h",
    "effects_module.cc",
    "effects_module.h",
    "effects_processor_v1.cc",
    "effects_processor_v1.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.audio.effects:fuchsia.audio.effects_cpp_wire",
    "//sdk/fidl/fuchsia.audio.effects:fuchsia.audio.effects_hlcpp",
    "//sdk/fidl/fuchsia.media:fuchsia.media_hlcpp",
    "//sdk/lib/media/audio/effects",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/fbl",
  ]

  deps = [
    "//sdk/lib/sys/component/cpp",
    "//src/media/audio/lib/timeline",
    "//zircon/system/ulib/trace",
  ]
}

executable("test") {
  testonly = true
  output_name = "audio_effects_loader_unittests"

  sources = [
    "effect_v1_unittest.cc",
    "effects_loader_v1_unittest.cc",
    "effects_loader_v2_unittest.cc",
    "effects_module_unittest.cc",
    "effects_processor_v1_unittest.cc",
  ]

  deps = [
    ":effects_loader",
    "testing",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/effects/test_effects:test_effects_v2",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fzl",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_package("audio_effects_loader_unittests") {
  deps = [
    ":test",
    "//src/media/audio/effects/test_effects:test_effects_v1",
  ]

  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":audio_effects_loader_unittests" ]
}
