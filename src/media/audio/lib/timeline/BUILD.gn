# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

source_set("timeline") {
  sources = [
    "timeline_function.cc",
    "timeline_function.h",
    "timeline_rate.cc",
    "timeline_rate.h",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]

  public_deps = [ "//zircon/system/ulib/zx" ]
}

executable("unittest_bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio_lib_timeline_unittests"

  sources = [
    "timeline_function_unittest.cc",
    "timeline_rate_unittest.cc",
  ]

  deps = [
    ":timeline",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("audio_lib_timeline_unittests") {
  deps = [ ":unittest_bin" ]

  test_specs = {
    # This test does not require, nor behave differently on, a non-virtualized target
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":audio_lib_timeline_unittests" ]
}
