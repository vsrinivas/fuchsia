# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")

source_set("analysis") {
  sources = [
    "analysis.cc",
    "analysis.h",
    "generators.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.media",
    "//sdk/lib/syslog/cpp",
  ]

  deps = [
    "//src/lib/fxl",
    "//src/media/audio/lib/format",
    "//src/media/audio/lib/wav",
    "//zircon/public/lib/fbl",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("unittest-bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libanalysis-unittests"

  sources = [
    "analysis_unittest.cc",
    "generators_unittest.cc",
  ]

  deps = [
    ":analysis",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/public/lib/fbl",
  ]

  libs = [ "zircon" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test_package("audio-libanalysis-unittests") {
  deps = [ ":unittest-bin" ]

  tests = [
    {
      name = "audio-libanalysis-unittests"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":audio-libanalysis-unittests" ]
}
