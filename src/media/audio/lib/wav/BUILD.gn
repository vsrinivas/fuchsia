# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")

source_set("wav") {
  sources = [
    "wav_internal.h",
    "wav_reader.cc",
    "wav_reader.h",
    "wav_writer.cc",
    "wav_writer.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.media",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//zircon/public/lib/fbl",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("unittest_bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio-libwav-unittests"

  sources = [ "wav_unittest.cc" ]

  deps = [
    ":wav",
    "//src/lib/fxl/test:gtest_main",
  ]
}

test_package("audio-libwav-unittests") {
  deps = [ ":unittest_bin" ]

  tests = [
    {
      name = "audio-libwav-unittests"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":audio-libwav-unittests" ]
}
