# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("usb_audio_bind") {
  rules = "usb_audio.bind"
  header_output = "usb_audio_bind.h"
  bind_output = "usb-audio.bindbc"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.audio",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":usb_audio_bind_test",
    "tests:usb-audio-test-package",
  ]
}

source_set("shared") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [
    "midi.cc",
    "usb-audio-control-interface.cc",
    "usb-audio-descriptors.cc",
    "usb-audio-device.cc",
    "usb-audio-path.cc",
    "usb-audio-stream-interface.cc",
    "usb-audio-stream.cc",
    "usb-audio-units.cc",
    "usb-audio.cc",
    "usb-midi-sink.cc",
    "usb-midi-source.cc",
  ]
  public_deps = [
    ":usb_audio_bind",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.composite:fuchsia.hardware.usb.composite_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.audio:fuchsia.hardware.audio_llcpp",
    "//sdk/fidl/fuchsia.hardware.midi:fuchsia.hardware.midi_llcpp",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/digest",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//third_party/boringssl",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO: delete the below and fix compiler errors.
  configs += [ "//build/config:Wno-unused-but-set-variable" ]
}

fuchsia_driver("usb-audio-driver") {
  output_name = "usb-audio"
  deps = [ ":shared" ]
}

fuchsia_driver_component("usb-audio") {
  component_name = "usb-audio"
  deps = [ ":usb-audio-driver" ]
}
