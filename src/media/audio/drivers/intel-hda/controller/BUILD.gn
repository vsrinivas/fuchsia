# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//build/unification/firmware.gni")

driver_bind_rules("ihda_controller_bind") {
  rules = "intel_hda.bind"
  header_output = "intel_hda-bind.h"
  deps = [ "//src/devices/bind/fuchsia.pci" ]
  tests = "bind_test.json"
}

# All non-test targets.
group("controller") {
  deps = [ ":intel-hda" ]
}

# All test targets.
group("test") {
  testonly = true
  deps = [ ":controller_test" ]
}

source_set("src") {
  visibility = [ ":*" ]
  sources = [
    "binary_decoder.h",
    "codec-cmd-job.cc",
    "codec-cmd-job.h",
    "debug-logging.h",
    "device-ids.h",
    "hda-codec-connection.cc",
    "hda-codec-connection.h",
    "intel-dsp-code-loader.cc",
    "intel-dsp-code-loader.h",
    "intel-dsp-debug.cc",
    "intel-dsp-ipc.cc",
    "intel-dsp-ipc.h",
    "intel-dsp-modules.cc",
    "intel-dsp-modules.h",
    "intel-dsp-stream.cc",
    "intel-dsp-stream.h",
    "intel-dsp-topology.cc",
    "intel-dsp-topology.h",
    "intel-dsp.cc",
    "intel-dsp.h",
    "intel-hda-controller-init.cc",
    "intel-hda-controller.cc",
    "intel-hda-controller.h",
    "intel-hda-debug.cc",
    "intel-hda-irq.cc",
    "intel-hda-stream.cc",
    "intel-hda-stream.h",
    "nhlt.cc",
    "nhlt.h",
    "utils.cc",
    "utils.h",
  ]
  deps = [
    ":ihda_controller_bind",
    "//sdk/banjo/fuchsia.hardware.intelhda.codec:fuchsia.hardware.intelhda.codec_banjo_cpp",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.pci:fuchsia.hardware.pci_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.intel.hda:fuchsia.hardware.intel.hda_c",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//sdk/lib/fit",
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/media/audio/drivers/lib/intel-hda",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/refcount",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

firmware("firmware") {
  sources = [ "intel-adsp-sst/dsp_fw_kbl_v3420.bin" ]
}

driver_module("intel-hda-driver") {
  output_name = "intel-hda"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [ ":src" ]
  data_deps = [ ":firmware" ]
}

fuchsia_driver_component("intel-hda") {
  component_name = "intel-hda"
  deps = [ ":intel-hda-driver" ]
}

test("controller_test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "intel-hda-controller-test"
  sources = [
    "binary_decoder_test.cc",
    "intel-dsp-ipc-test.cc",
    "intel-dsp-modules-test.cc",
    "intel-dsp-topology-test.cc",
    "nhlt_test.cc",
  ]
  deps = [
    ":src",
    "//sdk/lib/fdio",
    "//src/media/audio/drivers/lib/intel-hda",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("intel-hda-controller-test") {
  deps = [ ":controller_test" ]

  test_specs = {
    environments = basic_envs
  }
}

executable("intel-hda-stream-test-bin") {
  testonly = true
  sources = [ "intel-hda-stream-test.cc" ]
  deps = [
    ":src",
    "//src/devices/testing/fake_ddk",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("intel-hda-stream-test") {
  package_name = "intel-hda-stream-test"
  deps = [ ":intel-hda-stream-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

# Fuzz testing for NHLT parsing.
#
# Name needs to end with "-fuzzer" to be included in the Zircon fuzz test package.
fuzzer("nhlt-fuzzer") {
  sources = [ "nhlt_fuzzer.cc" ]
  deps = [
    ":src",
    "//zircon/system/ulib/fbl",
  ]

  # TODO(fxbug.dev/47959): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

fuzzers_package("intel-hda-controller-fuzzers") {
  cpp_fuzzers = [ ":nhlt-fuzzer" ]
}

group("tests") {
  testonly = true
  deps = [
    ":ihda_controller_bind_test",
    ":intel-hda-controller-fuzzers",
    ":intel-hda-controller-test",
    ":intel-hda-stream-test",
  ]
}
