# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/test/test_package.gni")
import("//src/media/audio/hardware.gni")

group("test") {
  testonly = true
  deps = [
    ":audio_driver_tests",
  ]
}

test_package("audio_driver_tests") {
  deps = [
    ":audio_driver_tests_bin",
  ]

  tests = [
    {
      name = "audio_driver_tests"

      # This test should be run on real hardware, although it can run everywhere. virtual_audio is
      # not automatically loaded (or involved in any way), but if manually loaded ahead of time it
      # does successfully load and pass, just like any other audio driver.
      # By specifying 'hardware_envs' instead of 'extended_hardware_envs', we omit testing on the
      # VIM2 platform, where our audio driver for the HDMI-based audio output does not yet generate
      # position notifications, and thus cannot pass certain test cases. At this time, inability to
      # generate position notifications does not prevent audio output streaming from working
      # correctly (although it may in the future, when clocking relies on accurate correlation of
      # audio position with system monotonic_time).
      # TODO(34961): enable position notifications on VIM2 audio output.
      # VIM2 is an older platform; its audio output is HDMI-based; this is unlikely to be addressed.
      environments = hardware_envs
    },
  ]
}

executable("audio_driver_tests_bin") {
  testonly = true
  output_name = "audio_driver_tests"

  sources = [
    "audio_driver_test.cc",
    "audio_driver_test.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.media",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main_with_syslog",
    "//src/lib/fxl/test:test_settings",
    "//src/media/audio/lib/logging",
    "//src/media/audio/lib/test:message_transceiver",
    "//src/media/audio/lib/test:test_fixture",
    "//zircon/public/lib/fzl",
    "//zircon/system/fidl/fuchsia-hardware-audio",
  ]
}
