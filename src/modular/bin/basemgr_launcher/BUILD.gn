# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

executable("basemgr_launcher_bin") {
  output_name = "basemgr_launcher"
  sources = [ "main.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fdio",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/fit-promise",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//src/modular/lib/modular_config",
    "//src/modular/lib/session",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_shell_package("basemgr_launcher") {
  manifest = "meta/basemgr_launcher.cmx"
  deps = [ ":basemgr_launcher_bin" ]
}

executable("basemgr_launcher_test") {
  testonly = true

  sources = [ "basemgr_launcher_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.devicesettings",
    "//sdk/fidl/fuchsia.identity.account",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.testing.runner",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/scenic/lib/scenic:scenic",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/zxc",
  ]
}

fuchsia_unittest_package("basemgr_launcher_tests") {
  manifest = "meta/basemgr_launcher_test.cmx"
  deps = [ ":basemgr_launcher_test" ]
}

group("tests") {
  testonly = true

  # basemgr_launcher_test requires framework and tools packages to be available.
  public_deps = [
    ":basemgr_launcher_tests",
    "//src/modular/bundles:framework",
    "//src/modular/bundles:testing",
    "//src/modular/bundles:tools",
  ]
}
