# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("command_runners") {
  testonly = true
  deps = [
    ":add-mod-command-runner-unittest",
    ":add_mod_command_runner",
    ":command_runner",
    ":remove-mod-command-runner-unittest",
    ":remove_mod_command_runner",
    "operation_calls",
  ]
}

source_set("command_runner") {
  sources = [
    "command_runner.cc",
    "command_runner.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular:fuchsia.modular_hlcpp",
    "//src/modular/bin/sessionmgr/storage",
  ]
}

source_set("add_mod_command_runner") {
  sources = [
    "add_mod_command_runner.cc",
    "add_mod_command_runner.h",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/modular/bin/sessionmgr/puppet_master/command_runners/operation_calls:add_mod_call",
  ]

  public_deps = [
    ":command_runner",
    "//src/modular/lib/async/cpp:operation",
  ]
}

executable("add_mod_command_runner_unittest") {
  testonly = true

  sources = [ "add_mod_command_runner_unittest.cc" ]

  deps = [
    ":add_mod_command_runner",
    "//sdk/lib/fidl/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/modular/lib/testing:test_with_session_storage",
  ]
}

fuchsia_unittest_component("add-mod-command-runner-unittest") {
  manifest = "../meta/add_mod_command_runner_unittest.cml"
  deps = [ ":add_mod_command_runner_unittest" ]
}

source_set("remove_mod_command_runner") {
  sources = [
    "remove_mod_command_runner.cc",
    "remove_mod_command_runner.h",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]

  public_deps = [
    ":command_runner",
    "//src/modular/lib/async/cpp:operation",
  ]
}

executable("remove_mod_command_runner_unittest") {
  testonly = true

  sources = [ "remove_mod_command_runner_unittest.cc" ]

  deps = [
    ":remove_mod_command_runner",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/modular/lib/testing:test_with_session_storage",
  ]
}

fuchsia_unittest_component("remove-mod-command-runner-unittest") {
  manifest = "../meta/remove_mod_command_runner_unittest.cml"
  deps = [ ":remove_mod_command_runner_unittest" ]
}

fuchsia_test_package("command_runners_unittests") {
  test_components = [
    ":add-mod-command-runner-unittest",
    ":remove-mod-command-runner-unittest",
  ]
}
