# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

source_set("agent_runner") {
  sources = [
    "agent_context_impl.cc",
    "agent_context_impl.h",
    "agent_runner.cc",
  ]

  public_deps = [
    ":public",
    "//sdk/fidl/fuchsia.modular:fuchsia.modular_hlcpp",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//src/modular/lib/async/cpp:operation",
  ]

  deps = [
    "//sdk/fidl/fuchsia.intl:fuchsia.intl_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/lib/fdio",
    "//src/lib/fsl",
    "//src/lib/json_parser",
    "//src/modular/bin/sessionmgr:component_context",
    "//src/modular/lib/common:teardown",
    "//src/modular/lib/deprecated_service_provider",
    "//src/modular/lib/fidl:array_to_string",
    "//src/modular/lib/fidl:json_xdr",
  ]
}

executable("agent_runner_unittest") {
  testonly = true

  sources = [ "agent_runner_unittest.cc" ]

  deps = [
    ":agent_runner",
    ":public",
    "//sdk/fidl/fuchsia.modular:fuchsia.modular_hlcpp",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/storage/vfs/cpp",
    "//src/lib/testing/loop_fixture",
    "//src/modular/lib/deprecated_svc",
    "//src/modular/lib/fidl:app_client",
    "//src/modular/lib/fidl:array_to_string",
    "//src/modular/lib/modular_config:modular_config",
    "//src/modular/lib/pseudo_dir",
    "//src/modular/tests:fuchsia.testing.modular_hlcpp",
    "//zircon/system/ulib/trace",
  ]
}

fuchsia_unittest_package("agent_runner_unittests") {
  manifest = "meta/agent_runner_unittest.cml"
  deps = [ ":agent_runner_unittest" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

# Broken out for component to depend on w/o creating circles.
source_set("public") {
  sources = [ "agent_runner.h" ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular:fuchsia.modular_hlcpp",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//src/modular/bin/sessionmgr:agent_services_factory",
    "//src/modular/lib/async/cpp:operation",
    "//src/modular/lib/fidl:app_client",
    "//src/modular/lib/modular_config:modular_config_accessor",
  ]
}
