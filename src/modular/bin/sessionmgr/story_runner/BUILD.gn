# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

source_set("story_runner") {
  sources = [
    "annotation_controller_impl.cc",
    "annotation_controller_impl.h",
    "module_context_impl.cc",
    "module_context_impl.h",
    "module_controller_impl.cc",
    "module_controller_impl.h",
    "ongoing_activity_impl.cc",
    "ongoing_activity_impl.h",
    "story_controller_impl.cc",
    "story_controller_impl.h",
    "story_provider_impl.cc",
    "story_provider_impl.h",
    "story_shell_context_impl.cc",
    "story_shell_context_impl.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.element:fuchsia.element_hlcpp",
    "//sdk/fidl/fuchsia.modular:fuchsia.modular_hlcpp",
    "//sdk/fidl/fuchsia.modular.internal:fuchsia.modular.internal_hlcpp",
    "//sdk/fidl/fuchsia.ui.policy:fuchsia.ui.policy_hlcpp",
    "//sdk/fidl/fuchsia.ui.views:fuchsia.ui.views_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//src/lib/fostr/fidl/fuchsia.modular",
    "//src/modular/bin/basemgr/cobalt",
    "//src/modular/bin/sessionmgr:component_context",
    "//src/modular/bin/sessionmgr/agent_runner",
    "//src/modular/bin/sessionmgr/puppet_master/command_runners/operation_calls:add_mod_call",
    "//src/modular/bin/sessionmgr/storage:encode_module_path",
    "//src/modular/lib/async/cpp:future",
    "//src/modular/lib/async/cpp:operation",
    "//src/modular/lib/common:async_holder",
    "//src/modular/lib/common:teardown",
    "//src/modular/lib/deprecated_service_provider",
    "//src/modular/lib/fidl:app_client",
    "//src/modular/lib/fidl:array_to_string",
    "//src/modular/lib/fidl:environment",
    "//src/modular/lib/fidl:json_xdr",
    "//src/modular/lib/fidl:proxy",
    "//src/modular/lib/string_escape",
  ]

  deps = [
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fsl",
    "//src/lib/uuid",
    "//src/modular/bin/sessionmgr/storage",
    "//src/modular/lib/fidl:clone",
    "//src/modular/lib/modular_config",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("annotation_controller_impl_unittest") {
  testonly = true

  sources = [ "annotation_controller_impl_unittest.cc" ]

  deps = [
    ":story_runner",
    "//src/lib/fxl/test:gtest_main",
    "//src/modular/bin/sessionmgr:annotations",
    "//src/modular/bin/sessionmgr/testing:annotations_matchers",
    "//src/modular/lib/testing:test_with_session_storage",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_component("annotation-controller-impl-unittest") {
  deps = [ ":annotation_controller_impl_unittest" ]
}

executable("story_controller_impl_unittest") {
  testonly = true

  sources = [ "story_controller_impl_unittest.cc" ]

  deps = [
    ":story_runner",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/modular/lib/fidl:clone",
  ]
}

fuchsia_unittest_component("story-controller-impl-unittest") {
  deps = [ ":story_controller_impl_unittest" ]
}

fuchsia_test_package("story_runner_unittests") {
  test_components = [
    ":annotation-controller-impl-unittest",
    ":story-controller-impl-unittest",
  ]
}
