# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/environments.gni")
import("//src/modular/build/tests_package.gni")

# Convenience dep for most Systems.
group("model") {
  public_deps = [
    ":story_mutator",
    ":story_observer",
    "//sdk/fidl/fuchsia.modular.storymodel",
  ]
}

hermetic_tests_package("story_model_unittests") {
  deps = [
    ":apply_mutations_unittest",
    ":ledger_story_model_storage_unittest",
    ":story_model_owner_unittest",
  ]
  environments = basic_envs
}

source_set("story_observer") {
  sources = [ "story_observer.h" ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular.storymodel",
    "//src/lib/fxl",
  ]
}

source_set("story_mutator") {
  sources = [
    "story_mutator.cc",
    "story_mutator.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular.storymodel",
    "//src/lib/fxl",
  ]
}

source_set("story_model_storage") {
  sources = [
    "story_model_storage.cc",
    "story_model_storage.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular.storymodel",
    "//src/lib/fxl",
    "//zircon/public/lib/async-cpp",
  ]
}

source_set("apply_mutations") {
  sources = [
    "apply_mutations.cc",
    "apply_mutations.h",
  ]

  public_deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.modular.storymodel",
    "//sdk/fidl/fuchsia.modular.storymodel",
  ]

  deps = [ "//src/lib/fxl" ]
}

executable("apply_mutations_unittest") {
  testonly = true

  sources = [ "apply_mutations_unittest.cc" ]

  deps = [
    ":apply_mutations",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

source_set("noop_story_model_storage") {
  sources = [
    "noop_story_model_storage.cc",
    "noop_story_model_storage.h",
  ]

  public_deps = [ "//sdk/fidl/fuchsia.modular.storymodel" ]

  deps = [
    ":story_model_storage",
    "//src/lib/fxl",
  ]
}

source_set("ledger_story_model_storage") {
  sources = [
    "ledger_story_model_storage.cc",
    "ledger_story_model_storage.h",
  ]

  public_deps = [ "//sdk/fidl/fuchsia.modular.storymodel" ]

  deps = [
    ":apply_mutations",
    ":story_model_storage",
    "//peridot/lib/ledger_client:page_client",
    "//peridot/lib/ledger_client:promise",
    "//src/ledger/fidl/fuchsia.ledger",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/modular/lib/fidl:array_to_string",
  ]
}

executable("ledger_story_model_storage_unittest") {
  testonly = true

  sources = [ "ledger_story_model_storage_unittest.cc" ]

  deps = [
    ":apply_mutations",
    ":ledger_story_model_storage",
    "testing",
    "//peridot/lib/ledger_client:page_client",
    "//src/lib/fxl/test:gtest_main",
    "//src/modular/lib/testing:test_with_ledger",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async-cpp",
  ]
}

source_set("story_model_owner") {
  sources = [
    "story_model_owner.cc",
    "story_model_owner.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular.storymodel",
    "//src/lib/fxl",
    "//zircon/public/lib/async-cpp",
  ]

  deps = [
    ":apply_mutations",
    ":story_model_storage",
    ":story_mutator",
    ":story_observer",
  ]
}

executable("story_model_owner_unittest") {
  testonly = true

  sources = [ "story_model_owner_unittest.cc" ]

  deps = [
    ":story_model_owner",
    ":story_model_storage",
    "//garnet/public/lib/gtest",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}
