# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/environments.gni")
import("//src/modular/build/executable_package.gni")
import("//src/modular/build/tests_package.gni")

group("clipboard") {
  testonly = true
  deps = [
    ":clipboard_agent",
    ":clipboard_impl_unittest",
    ":clipboard_sources",
    ":clipboard_unittests",
  ]
}

executable_package("clipboard_agent") {
  meta = [
    {
      path = "meta/clipboard_agent.cmx"
      dest = "clipboard_agent.cmx"
    },
  ]

  sources = [
    "clipboard_agent.cc",
  ]

  deps = [
    ":clipboard_sources",
    "//peridot/lib/ledger_client:page_client",
    "//src/modular/lib/app_driver/cpp:agent_driver",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

source_set("clipboard_sources") {
  sources = [
    "clipboard_impl.cc",
    "clipboard_impl.h",
    "clipboard_storage.cc",
    "clipboard_storage.h",
  ]

  deps = [
    "//peridot/lib/ledger_client:page_client",
    "//sdk/fidl/fuchsia.modular",
    "//sdk/lib/fidl/cpp",
    "//src/modular/lib/async/cpp:operation",
    "//src/modular/lib/fidl:array_to_string",
  ]
}

executable("clipboard_impl_unittest") {
  testonly = true

  sources = [
    "clipboard_impl_unittest.cc",
  ]

  deps = [
    ":clipboard_sources",
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.modular",
    "//src/lib/fxl/test:gtest_main",
    "//src/modular/lib/testing:test_with_ledger",
  ]
}

hermetic_tests_package("clipboard_unittests") {
  deps = [
    ":clipboard_impl_unittest",
  ]
  environments = basic_envs
}
