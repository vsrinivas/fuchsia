# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "account_handler"
  edition = "2021"
  with_unit_tests = true

  deps = [
    ":structured-config-lib",
    "//sdk/fidl/fuchsia.identity.account:fuchsia.identity.account_rust",
    "//sdk/fidl/fuchsia.identity.authentication:fuchsia.identity.authentication_rust",
    "//sdk/fidl/fuchsia.identity.internal:fuchsia.identity.internal_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_rust",
    "//src/identity/lib/account_common",
    "//src/identity/lib/identity_common",
    "//src/identity/lib/storage_manager",
    "//src/lib/async-utils",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:bincode",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:scopeguard",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/identity/lib/identity_testutil",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:tempfile",
    "//third_party/rust_crates:typed-builder",
  ]

  sources = [
    "src/account.rs",
    "src/account_handler.rs",
    "src/common.rs",
    "src/inspect.rs",
    "src/interaction.rs",
    "src/lock_request.rs",
    "src/main.rs",
    "src/persona.rs",
    "src/pre_auth.rs",
    "src/stored_account.rs",
    "src/test_util.rs",
  ]
}

fuchsia_component_manifest("manifest") {
  component_name = "account_handler"
  manifest = "meta/account_handler.cml"
}

fuchsia_structured_config_rust_lib("structured-config-lib") {
  name = "account_handler_structured_config"
  cm_label = ":manifest"
}

fuchsia_structured_config_values("ephemeral-config") {
  cm_label = ":manifest"
  values_source = "configs/ephemeral.json5"
}

fuchsia_structured_config_values("persistent-config") {
  cm_label = ":manifest"
  values_source = "configs/persistent.json5"
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_package("account_handler_persistent") {
  deps = [
    ":component",
    ":persistent-config",
    "//src/storage/bin/minfs",
  ]
}

fuchsia_package("account_handler_ephemeral") {
  deps = [
    ":component",
    ":ephemeral-config",
    "//src/storage/bin/minfs",
  ]
}

group("account_handler") {
  deps = [
    ":account_handler_ephemeral",
    ":account_handler_persistent",
  ]
}
