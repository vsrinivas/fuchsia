# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "password_authenticator"
  with_unit_tests = true
  edition = "2021"

  deps = [
    ":password_authenticator_config",
    "//sdk/fidl/fuchsia.identity.account:fuchsia.identity.account_rust",
    "//sdk/fidl/fuchsia.identity.authentication:fuchsia.identity.authentication_rust",
    "//sdk/fidl/fuchsia.identity.authentication:fuchsia.identity.authentication_rust",
    "//sdk/fidl/fuchsia.identity.credential:fuchsia.identity.credential_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_rust",
    "//src/identity/lib/account_common",
    "//src/identity/lib/identity_common",
    "//src/identity/lib/storage_manager",
    "//src/lib/async-utils",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hmac",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:scrypt",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:sha2",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/identity/lib/identity_testutil",
    "//src/lib/fuchsia",
    "//src/lib/storage/vfs/rust:vfs",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:tempfile",
  ]

  sources = [
    "src/account.rs",
    "src/account_manager.rs",
    "src/account_metadata.rs",
    "src/keys.rs",
    "src/main.rs",
    "src/password_interaction.rs",
    "src/pinweaver.rs",
    "src/scrypt.rs",
    "src/state.rs",
    "src/storage_unlock_mechanism.rs",
  ]
}

fuchsia_component_manifest("manifest") {
  component_name = "password-authenticator"
  manifest = "meta/password-authenticator.cml"
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_structured_config_rust_lib("password_authenticator_config") {
  cm_label = ":manifest"
}

fuchsia_package("password_authenticator") {
  deps = [
    ":component",
    "//src/storage/bin/minfs",
  ]
}

fuchsia_structured_config_values("integration_test_config") {
  cm_label = ":manifest"
  values = {
    allow_scrypt = true
    allow_pinweaver = false
  }
}

fuchsia_unittest_package("password-authenticator-tests") {
  manifest = "meta/password-authenticator-test.cml"
  deps = [
    ":bin_test",
    "//src/sys/test_runners:tmp_storage",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fidl("test.identity") {
  sources = [ "block.test.fidl" ]
  public_deps = [
    "//sdk/fidl/fuchsia.device",
    "//sdk/fidl/fuchsia.hardware.block",
    "//sdk/fidl/fuchsia.hardware.block.partition",
    "//sdk/fidl/fuchsia.io",
  ]

  # TODO(https://fxbug.dev/88443): Remove this dependency since this target does
  # not directly depend on these libraries.
  public_deps += [ "//sdk/fidl/fuchsia.unknown" ]
}

group("tests") {
  testonly = true
  public_deps = [ ":password-authenticator-tests" ]
}
