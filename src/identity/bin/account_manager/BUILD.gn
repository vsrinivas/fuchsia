# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")

declare_args() {
  # Flag to switch between the C++ and Rust versions of Google auth provider.
  use_rust_auth_provider = false
}

rustc_binary("bin") {
  name = "account_manager"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-inspect",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.auth:fuchsia.auth-rustc",
    "//sdk/fidl/fuchsia.auth.account:fuchsia.auth.account-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/identity/fidl:account-rustc",
    "//src/identity/lib/account_common",
    "//src/identity/lib/token_manager",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:getopts",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_derive",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tempfile",
  ]
  if (use_rust_auth_provider) {
    features = [ "use_rust_auth_provider" ]
  }
}

package("account_manager") {
  deps = [
    ":bin",
  ]

  binary = "rust_crates/account_manager"

  meta = [
    {
      path = rebase_path("meta/account_manager.cmx")
      dest = "account_manager.cmx"
    },
  ]
}

group("account_manager_auth_providers") {
  if (use_rust_auth_provider) {
    public_deps = [
      "../google_auth_provider_rust",
    ]
  } else {
    public_deps = [
      "../google_auth_provider",
    ]
  }
}
