# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("aml-nna") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = [ "aml-nna.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/driver:driver",
    "//src/devices/lib/mmio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
  ]
}

test("aml-nna-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "aml-nna-test"
  sources = [
    "aml-nna-test.cc",
    "aml-nna.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake_ddk",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/zxtest",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
  ]
}

unittest_package("aml-nna-test-package") {
  package_name = "aml-nna-test"
  deps = [ ":aml-nna-test" ]

  tests = [
    {
      name = "aml-nna-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":aml-nna-test-package" ]
}
