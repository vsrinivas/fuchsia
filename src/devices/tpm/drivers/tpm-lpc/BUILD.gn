# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tpm-lpc") {
  testonly = true
  deps = [
    ":tests",
    ":tpm-lpc-pkg",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":tpm-lpc-tests" ]
}

driver_bind_rules("tpm-lpc_bind") {
  rules = "tpm-lpc.bind"
  header_output = "tpm-lpc-bind.h"
  bind_output = "tpm-lpc.bindbc"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.acpi" ]
}

common_deps = [
  ":tpm-lpc_bind",
  "//sdk/banjo/fuchsia.hardware.tpmimpl:fuchsia.hardware.tpmimpl_banjo_cpp",
  "//sdk/fidl/fuchsia.hardware.tpmimpl:fuchsia.hardware.tpmimpl_cpp_wire",
  "//src/devices/lib/acpi",
  "//src/devices/lib/driver",
  "//src/devices/lib/mmio",
  "//src/lib/ddktl",
  "//zircon/system/ulib/async:async-cpp",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("fuchsia_driver") {
  output_name = "tpm-lpc"
  sources = [ "tpm-lpc.cc" ]
  deps = common_deps
}

fuchsia_driver_component("tpm-lpc-driver") {
  component_name = "tpm-lpc"
  deps = [ ":fuchsia_driver" ]
  info = "tpm-lpc-info.json"
  colocate = true
}

fuchsia_driver_package("tpm-lpc-pkg") {
  package_name = "tpm-lpc"
  driver_components = [ ":tpm-lpc-driver" ]
}

test("tpm-lpc-unit-tests") {
  sources = [
    "tpm-lpc.cc",
    "tpm-lpc.h",
    "unit-tests.cc",
  ]
  deps = common_deps + [
           "//sdk/lib/inspect/testing/cpp:zxtest",
           "//src/devices/lib/acpi/mock:mock-acpi",
           "//src/devices/testing/fake-mmio-reg",
           "//src/devices/testing/mock-ddk",
           "//zircon/system/ulib/async-loop:async-loop-cpp",
           "//zircon/system/ulib/zxtest",
         ]
}

fuchsia_unittest_package("tpm-lpc-tests") {
  deps = [
    ":tpm-lpc-unit-tests",
    ":tpm-lpc_bind_test",
  ]
}
