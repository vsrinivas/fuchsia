# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

bind_rules("mtk-power-bind") {
  rules = "mtk-power.bind"
  output = "mtk-power-bind.h"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.mediatek.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_module("mtk-power") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "mtk-power.cc" ]
  deps = [
    ":mtk-power-bind",
    "//sdk/banjo/ddk.protocol.gpio",
    "//sdk/banjo/ddk.protocol.i2c",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.powerimpl",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mt8167",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
  ]
}

test("mtk-power-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "mtk-power-test"
  sources = [
    "mtk-power-test.cc",
    "mtk-power.cc",
  ]
  deps = [
    ":mtk-power-bind",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.powerimpl",
    "//sdk/lib/fdio",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mt8167",
    "//src/devices/testing/mock-mmio-reg",
    "//src/devices/testing/no_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

unittest_package("mtk-power-test-package") {
  package_name = "mtk-power-test"
  deps = [ ":mtk-power-test" ]

  tests = [
    {
      name = "mtk-power-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":mtk-power-bind_test",
    ":mtk-power-test-package",
  ]
}
