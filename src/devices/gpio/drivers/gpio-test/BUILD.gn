# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("gpio_test_bind") {
  rules = "gpio_test.bind"
  header_output = "gpio_test_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":gpio_test_bind_test" ]
}

fuchsia_driver("gpio-test-driver") {
  output_name = "gpio-test"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  # TODO: testonly = true
  sources = [ "gpio-test.cc" ]
  deps = [
    ":gpio_test_bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("gpio-test") {
  testonly = true
  component_name = "gpio-test"
  deps = [ ":gpio-test-driver" ]
  info = "gpio-test-info.json"
}
