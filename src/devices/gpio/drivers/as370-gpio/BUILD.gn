# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("as370-gpio-bind") {
  rules = "as370-gpio.bind"
  header_output = "as370-gpio-bind.h"
  bind_output = "as370-gpio.bindbc"
  tests = "as370-gpio-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.synaptics.platform" ]
}

fuchsia_driver("as370-gpio-driver") {
  output_name = "as370-gpio"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "as370-gpio.cc" ]
  deps = [
    ":as370-gpio-bind",
    "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/as370",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("as370-gpio") {
  component_name = "as370-gpio"
  deps = [ ":as370-gpio-driver" ]
  info = "as370-gpio-info.json"
  manifest = "meta/as370-gpio.cml"
}

test("as370-gpio-test-bin") {
  output_name = "as370-gpio-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "as370-gpio-test.cc",
    "as370-gpio.cc",
  ]
  deps = [
    ":as370-gpio-bind",
    "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//sdk/lib/fdio",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/as370",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("as370-gpio-test") {
  deps = [ ":as370-gpio-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":as370-gpio-bind_test",
    ":as370-gpio-test",
  ]
}
