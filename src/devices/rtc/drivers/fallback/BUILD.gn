# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

driver_module("fallback") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = [ "rtc-impl.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.rtc:fuchsia.hardware.rtc_c",
    "//src/devices/rtc/lib/rtc",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
  ]
}

test("fallback-rtc") {
  sources = [ "fallback-rtc-test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.rtc:fuchsia.hardware.rtc_c",
    "//sdk/lib/fdio",
    "//src/devices/rtc/lib/rtc",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/driver-integration-test",
  ]
}

isolated_devmgr_unittest_package("fallback-rtc-test") {
  executable_path = "test/fallback-rtc"

  deps = [
    ":fallback",
    ":fallback-rtc",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":fallback-rtc-test" ]
}
