# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")

declare_args() {
  # This is used to temporarily enable building with the C driver
  # and will be removed once cpp driver is stabilized.
  # See fxb/62176.
  rtc_c = false
}

bind_rules("pl031_rtc_bind") {
  rules = "pl031_rtc.bind"
  output = "pl031_rtc_bind.h"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

driver_module("pl031-rtc") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  if (rtc_c) {
    sources = [ "pl031-rtc.c" ]
    deps = [
      "//sdk/banjo/ddk.protocol.platform.device",
      "//sdk/fidl/fuchsia.hardware.rtc:fuchsia.hardware.rtc_c",
      "//src/devices/bus/lib/device-protocol-platform-device",
      "//src/devices/rtc/lib/rtc",
      "//src/lib/ddk",

      # TODO(fxb/38132): Migrate to the new bind rules and delete the below
      "//src/lib/ddk:ddk-deprecated-binding-headers",
    ]
  } else {
    sources = [ "pl031-rtc.cc" ]
    deps = [
      ":pl031_rtc_bind",
      "//sdk/banjo/ddk.protocol.platform.device",
      "//sdk/fidl/fuchsia.hardware.rtc:fuchsia.hardware.rtc_llcpp",
      "//src/devices/bus/lib/device-protocol-platform-device",
      "//src/devices/lib/driver",
      "//src/devices/lib/mmio",
      "//src/devices/rtc/lib/rtc:rtc_llcpp",
      "//src/lib/ddk",
      "//src/lib/ddktl",
      "//zircon/system/ulib/fidl-utils",
    ]
  }
}

group("tests") {
  testonly = true
  deps = [ ":pl031_rtc_bind_test" ]
}
