# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("ltr-578als-bind") {
  rules = "ltr-578als.bind"
  header_output = "ltr-578als-bind.h"
  tests = "ltr-578als-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.i2c",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_module("lite-on-driver") {
  output_name = "lite-on"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "ltr-578als.cc" ]
  deps = [
    ":ltr-578als-bind",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/lib/fit",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/simplehid",
  ]
}

fuchsia_driver_component("lite-on") {
  component_name = "lite-on"
  deps = [ ":lite-on-driver" ]
}

test("ltr-578als-test-bin") {
  configs += [ "//build/config:all_source" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "ltr-578als-test"
  sources = [
    "ltr-578als-test.cc",
    "ltr-578als.cc",
  ]
  deps = [
    ":ltr-578als-bind",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/lib/fit",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/lib/mmio",
    "//src/devices/testing/no_ddk",
    "//src/lib/ddktl",
    "//src/ui/input/testing/mock-hidbus-ifc",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/simplehid",
  ]
}

fuchsia_unittest_package("ltr-578als-test") {
  deps = [ ":ltr-578als-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":ltr-578als-bind_test",
    ":ltr-578als-test",
  ]
}
