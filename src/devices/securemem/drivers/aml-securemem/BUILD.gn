# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

bind_rules("aml-securemem-bind") {
  rules = "aml-securemem.bind"
  output = "aml-securemem-bind.h"
  tests = "aml-securemem-bind-test.json"
  deps = [ "//src/devices/bind/amlogic.platform" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-securemem-bind_test",
    ":aml-securemem-test",
  ]
}

driver_module("aml-securemem") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [
    "device.cc",
    "device.h",
    "secmem-session.cc",
    "sysmem-secure-mem-server.cc",
  ]

  deps = [
    ":aml-securemem-bind",
    "//sdk/banjo/fuchsia.hardware.platform.device",
    "//sdk/banjo/fuchsia.hardware.sysmem",
    "//sdk/banjo/fuchsia.hardware.tee",
    "//sdk/fidl/fuchsia.hardware.securemem:fuchsia.hardware.securemem_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//sdk/fidl/fuchsia.tee",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/security/tee/tee-client-api:tee-client-api-headers",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/safemath",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/closure-queue",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
  ]
}

test("aml-securemem-test-bin") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  output_name = "aml-securemem-test"
  sources = [
    "device.cc",
    "device.h",
    "secmem-session.cc",
    "sysmem-secure-mem-server.cc",
    "test/aml-securemem-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":aml-securemem-bind",
    "//sdk/banjo/fuchsia.hardware.platform.device",
    "//sdk/banjo/fuchsia.hardware.sysmem",
    "//sdk/banjo/fuchsia.hardware.tee",
    "//sdk/fidl/fuchsia.hardware.securemem:fuchsia.hardware.securemem_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//sdk/fidl/fuchsia.tee",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//src/security/tee/tee-client-api:tee-client-api-static",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/safemath",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/closure-queue",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
  ]
}

fuchsia_unittest_package("aml-securemem-test") {
  deps = [ ":aml-securemem-test-bin" ]
}
