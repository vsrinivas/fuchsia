# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("aml-securemem-bind") {
  rules = "aml-securemem.bind"
  header_output = "aml-securemem-bind.h"
  bind_output = "aml-securemem.bindbc"
  tests = "aml-securemem-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.amlogic.platform" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-securemem-bind_test",
    ":aml-securemem-test",
  ]
}

driver_module("aml-securemem-driver") {
  output_name = "aml-securemem"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [
    "device.cc",
    "device.h",
    "secmem-session.cc",
    "sysmem-secure-mem-server.cc",
  ]

  deps = [
    ":aml-securemem-bind",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.tee:fuchsia.hardware.tee_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.securemem:fuchsia.hardware.securemem_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//sdk/fidl/fuchsia.tee",
    "//sdk/lib/fit-promise",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/security/tee/tee-client-api:tee-client-api-headers",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/safemath",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/closure-queue",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_driver_component("aml-securemem") {
  component_name = "aml-securemem"
  deps = [ ":aml-securemem-driver" ]
}

test("aml-securemem-test-bin") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  output_name = "aml-securemem-test"
  sources = [
    "device.cc",
    "device.h",
    "secmem-session.cc",
    "sysmem-secure-mem-server.cc",
    "test/aml-securemem-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":aml-securemem-bind",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.tee:fuchsia.hardware.tee_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.securemem:fuchsia.hardware.securemem_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//sdk/fidl/fuchsia.tee",
    "//sdk/lib/fit-promise",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/testing/fake_ddk",
    "//src/security/tee/tee-client-api:tee-client-api-static",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/safemath",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/closure-queue",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("aml-securemem-test") {
  deps = [ ":aml-securemem-test-bin" ]
}
