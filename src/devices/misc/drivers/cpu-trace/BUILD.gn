# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

source_set("perf-mon") {
  visibility = [ ":*" ]

  sources = [
    "perf-mon.cc",
    "perf-mon.h",
  ]

  if (current_cpu == "arm64") {
    sources += [ "arm64-pm.cc" ]
  } else if (current_cpu == "x64") {
    sources += [ "intel-pm.cc" ]
  }

  public_deps = [
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
  ]

  deps = [
    "//sdk/fidl/fuchsia.perfmon.cpu:fuchsia.perfmon.cpu_llcpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/banjo/ddk.protocol.platform.device",
  ]
}

driver_module("cpu-trace") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "cpu-trace.cc" ]

  if (current_cpu == "x64") {
    sources += [ "intel-pt.cc" ]
  }

  deps = [
    ":perf-mon",
    "//sdk/fidl/fuchsia.hardware.cpu.insntrace:fuchsia.hardware.cpu.insntrace_llcpp",
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.platform.device",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":perf-mon-tests" ]
}

test("perf-mon-tests") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "tests/perf-mon-tests.cc" ]
  deps = [
    ":perf-mon",
    "//sdk/fidl/fuchsia.perfmon.cpu:fuchsia.perfmon.cpu_llcpp",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/fake-bti",
    "//zircon/system/dev/lib/fake_ddk",
  ]
}

migrated_manifest("cpu-trace-manifest") {
  deps = [ ":cpu-trace" ]
}

unittest_package("perf-mon-tests-package") {
  package_name = "perf-mon-tests"
  deps = [ ":perf-mon-tests" ]

  tests = [
    {
      name = "perf-mon-tests"
      dest = "perf-mon-tests-test"
    },
  ]
}
