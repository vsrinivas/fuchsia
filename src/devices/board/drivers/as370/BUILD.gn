# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("as370-bind") {
  rules = "as370.bind"
  header_output = "as370-bind.h"
  bind_output = "as370.bc"
  tests = "as370-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.hardware.platform.bus",
    "//src/devices/bind/fuchsia.synaptics.platform",
  ]
}

driver_bind_rules("as370-wifi-bind") {
  rules = "bind/as370-wifi.bind"
  header_output = "as370-wifi-bind.h"
  bind_output = "as370-wifi-bind.bc"
  tests = "bind-tests/as370-wifi-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.sdmmc" ]
}

fuchsia_driver("as370-driver") {
  output_name = "as370"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "as370-audio.cc",
    "as370-clock.cc",
    "as370-gpio.cc",
    "as370-i2c.cc",
    "as370-light.cc",
    "as370-nand.cc",
    "as370-nna.cc",
    "as370-power.cc",
    "as370-registers.cc",
    "as370-sdio.cc",
    "as370-thermal.cc",
    "as370-touch.cc",
    "as370-usb.cc",
    "as370.cc",
  ]
  deps = [
    ":as370-bind",
    ":as370-bind_header",
    ":as370-nna-bind_header",
    ":as370-wifi-bind_header",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.shareddma:fuchsia.hardware.shareddma_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_cpp_wire",
    "//src/devices/bus/lib/platform-bus-composites",
    "//src/devices/lib/as370",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/fidl-metadata",
    "//src/devices/lib/metadata:llcpp",
    "//src/devices/lib/mmio",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:cdc",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/47406): restore some form of data dependency on as370 and visalia
  # boot shims.
}

fuchsia_driver_component("as370") {
  component_name = "as370"
  deps = [ ":as370-driver" ]
  info = "as370-info.json"
  manifest = "meta/as370.cml"
}

driver_bind_rules("as370-nna-bind") {
  rules = "bind/as370-nna.bind"
  header_output = "as370_nna_bind.h"
  bind_output = "as370_nna_bind.bc"
  tests = "bind-tests/as370-nna-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.registers",
    "//src/devices/bind/fuchsia.synaptics.platform",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":as370-bind_test",
    ":as370-nna-bind_test",
    ":as370-wifi-bind_test",
  ]
}
