# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")

bind_rules("msm8x53_bind") {
  rules = "msm8x53.bind"
  output = "msm8x53_bind.h"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.qualcomm.platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":msm8x53_bind_test" ]
}

driver_module("msm8x53-som") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [
    "msm8x53-clock.cc",
    "msm8x53-gpio.cc",
    "msm8x53-pil.cc",
    "msm8x53-power.cc",
    "msm8x53-sdc1.cc",
    "msm8x53.cc",
  ]
  deps = [
    ":msm8x53_bind",
    "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device",
    "//src/devices/lib/driver",
    "//src/devices/lib/msm8x53",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
  ]

  # TODO(fxbug.dev/47406): restore some form of data dependency on msm8x53-som
  # boot shim.
}
