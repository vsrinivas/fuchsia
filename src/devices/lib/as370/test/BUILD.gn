# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/test/test_package.gni")

group("tests") {
  testonly = true
  deps = [ ":as370-audio-lib-tests-package" ]
}

test("syn-audio-in-test") {
  if (is_fuchsia) {
    configs += [
      "//build/unification/config:zircon-migrated",
      "//build/config:all_source",
    ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "syn-audio-in-test"
  sources = [
    "../syn-audio-in.cc",
    "syn-audio-in-test.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.shareddma",
    "//sdk/banjo/ddk.protocol.shareddma:ddk.protocol.shareddma_mock",
    "//src/devices/lib/as370",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake_ddk",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fzl",
  ]
}

test("audio-dsp-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "audio-dsp-test"
  sources = [
    "../audio-dsp.cc",
    "audio-dsp-test.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//src/devices/lib/as370",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fzl",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

unittest_package("as370-audio-lib-tests-package") {
  package_name = "as370-audio-lib-tests"
  deps = [
    ":audio-dsp-test",
    ":syn-audio-in-test",
  ]
  tests = [
    {
      name = "audio-dsp-test"
    },
    {
      name = "syn-audio-in-test"
    },
  ]
}
