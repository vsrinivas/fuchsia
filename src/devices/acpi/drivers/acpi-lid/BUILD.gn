# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":acpi-lid-tests" ]
}

driver_bind_rules("bind") {
  rules = "acpi_lid.bind"
  header_output = "acpi_lid-bind.h"
  bind_output = "acpi_lid.bindbc"
  deps = [ "//src/devices/bind/fuchsia.acpi" ]
}

common_deps = [
  ":bind",
  "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
  "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_cpp_wire",
  "//sdk/lib/driver/runtime:driver_runtime_cpp",
  "//src/devices/lib/acpi",
  "//src/devices/lib/driver:driver_runtime",
  "//src/lib/ddktl",
  "//zircon/system/ulib/hid",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("driver") {
  output_name = "acpi-lid"
  sources = [ "acpi_lid.cc" ]
  deps = common_deps
}

fuchsia_driver_component("component") {
  component_name = "acpi-lid"
  deps = [ ":driver" ]
  info = "component-info.json"
  colocate = true
  default_dispatcher_opts = [ "allow_sync_calls" ]
}

fuchsia_driver_package("acpi-lid") {
  driver_components = [ ":component" ]
}

test("acpi-lid-unit-tests") {
  sources = [
    "acpi_lid.cc",
    "acpi_lid.h",
    "unit-tests.cc",
  ]
  deps = common_deps + [
           "//sdk/lib/inspect/testing/cpp:zxtest",
           "//src/devices/lib/acpi/mock:mock-acpi",
           "//src/devices/testing/mock-ddk",
           "//src/ui/input/testing/mock-hidbus-ifc",
           "//zircon/system/ulib/async-loop:async-loop-cpp",
           "//zircon/system/ulib/zircon-internal",
           "//zircon/system/ulib/zxtest",
         ]
}

fuchsia_unittest_package("acpi-lid-tests") {
  deps = [ ":acpi-lid-unit-tests" ]
}
