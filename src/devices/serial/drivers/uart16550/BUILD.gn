# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("uart16550") {
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  sources = [ "uart16550.cc" ]
  deps = [ "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_c" ]
  public_deps = [
    "//sdk/banjo/ddk.protocol.acpi",
    "//sdk/banjo/ddk.protocol.serial",
    "//sdk/banjo/ddk.protocol.serialimpl",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/hwreg-mock",
    "//zircon/public/lib/zx",
  ]
}

test("uart16550-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "uart16550-test"
  sources = [ "uart16550-test.cc" ]
  deps = [
    ":common",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

migrated_manifest("uart16550-manifest") {
  deps = [ ":uart16550" ]
}

migrated_manifest("uart16550-test-manifest") {
  deps = [ ":uart16550-test" ]
}
