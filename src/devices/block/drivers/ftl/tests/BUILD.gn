# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/packaged_shared_library.gni")
import("//build/test.gni")

source_set("shared") {
  sources = [
    "ftl-shell.cc",
    "ndm-ram-driver.cc",
  ]
  public_deps = [
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/ftl",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

group("tests") {
  testonly = true
  deps = [
    ":ftl-integration-package",  # Flaky Test, build only.
    ":ftl-test",
  ]
}

test("ftl-test-bin") {
  output_name = "ftl-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "block_device_test.cc",
    "driver-test.cc",
    "ftl-test.cc",
    "metrics_test.cc",
    "nand_driver_test.cc",
    "nand_operation_test.cc",
    "oob_doubler_test.cc",
  ]
  include_dirs = [ "//src/devices/block/drivers/ftl" ]
  deps = [
    ":shared",
    "//src/devices/block/drivers/ftl:common",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("ftl-test") {
  deps = [ ":ftl-test-bin" ]
}

test("ftl-integration") {
  output_name = "ftl-integration-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "ftl_integration_test.cc",
    "ftl_test_observer.cc",
    "integration_main.cc",
    "launch.cc",
  ]
  include_dirs = [ "//src/devices/block/drivers/ftl" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_llcpp",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//sdk/lib/fdio",
    "//src/lib/storage/ramdevice_client/cpp:ramdevice-client-test",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # Other components/binaries we depend on.
  deps += [
    "//src/devices/block/bin/blktest",
    "//src/devices/block/bin/iochk",
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ftl",
    "//src/devices/bus/drivers/platform",
    "//src/devices/misc/drivers/test",
    "//src/devices/nand/drivers/nand",
    "//src/devices/nand/drivers/ram-nand",
    "//src/devices/tests/sysdev",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}

fuchsia_test_component("ftl-integration-test-component") {
  component_name = "ftl-integration-test"
  manifest = "meta/ftl-integration-test.cml"
  deps = [ ":ftl-integration" ]
}

fuchsia_test_package("ftl-integration-package") {
  package_name = "ftl-integration"
  test_specs = {
    # Raising log severity limit to allow for NDM init errors which are truly
    # bad in actual devices, but when init'ing a new device, it is fine.
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":ftl-integration-test-component" ]
}
