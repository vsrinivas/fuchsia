# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

source_set("shared") {
  testonly = true
  sources = [
    "ftl-shell.cc",
    "ndm-ram-driver.cc",
  ]
  public_deps = [
    "//src/devices/block/drivers/ftl:common",
    "//src/devices/testing/fake_ddk",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

group("tests") {
  testonly = true
  deps = [
    ":ftl-fuzzers",
    ":ftl-package",
  ]
}

test("ftl") {
  output_name = "ftl-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "block_device_test.cc",
    "driver-test.cc",
    "ftl-test.cc",
    "nand_driver_test.cc",
    "nand_operation_test.cc",
    "oob_doubler_test.cc",
  ]
  include_dirs = [ "//src/devices/block/drivers/ftl" ]
  deps = [
    ":shared",
    "//zircon/public/lib/zxtest",
  ]
}

test("ftl-integration") {
  output_name = "ftl-integration-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "ftl_fidl_integration_test.cc",
    "ftl_integration_test.cc",
    "ftl_test_observer.cc",
    "integration_main.cc",
    "launch.cc",
  ]
  include_dirs = [ "//src/devices/block/drivers/ftl" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_llcpp",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/ramdevice-client",
  ]
}

fuzzer("ftl-read-write-fuzzer") {
  sources = [ "ftl-fuzzer.cc" ]
  include_dirs = [ "//src/devices/block/drivers/ftl" ]
  deps = [ ":shared" ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuzzers_package("ftl-fuzzers") {
  cpp_fuzzers = [ ":ftl-read-write-fuzzer" ]
}

isolated_devmgr_unittest_package("ftl-integration-package") {
  package_name = "ftl-integration"
  executable_path = "test/ftl-integration-test"
  deps = [
    ":ftl-integration",
    "//src/devices/block/bin/blktest",
    "//src/devices/block/bin/iochk",
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ftl",
    "//src/devices/bus/drivers/platform",
    "//src/devices/misc/drivers/test",
    "//src/devices/nand/drivers/nand",
    "//src/devices/nand/drivers/ram-nand",
    "//src/devices/tests/sysdev",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
  ]
}

unittest_package("ftl-package") {
  package_name = "ftl"
  tests = [
    {
      name = "ftl-test"
    },
  ]

  deps = [ ":ftl" ]
}
