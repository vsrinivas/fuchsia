# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

bind_rules("mtk-sdmmc-bind") {
  rules = "mtk-sdmmc.bind"
  output = "mtk-sdmmc-bind.h"
  tests = "mtk-sdmmc-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.composite",
    "//src/devices/bind/fuchsia.mediatek.platform",
  ]
}

driver_module("mtk-sdmmc") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "mtk-sdmmc.cc" ]
  deps = [
    ":mtk-sdmmc-bind",
    "//sdk/banjo/ddk.protocol.block",
    "//sdk/banjo/ddk.protocol.clock",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.gpio",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.sdmmc",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mt8167",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fzl",
  ]
}

test("mtk-sdmmc-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "mtk-sdmmc-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "mtk-sdmmc-test.cc",
    "mtk-sdmmc.cc",
  ]
  deps = [
    ":mtk-sdmmc-bind",
    "//sdk/banjo/ddk.protocol.block",
    "//sdk/banjo/ddk.protocol.clock",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.gpio",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.sdmmc",
    "//sdk/lib/fdio",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mt8167",
    "//src/devices/testing/mock-mmio-reg",
    "//src/devices/testing/no_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fzl",
  ]
}

fuchsia_unittest_package("mtk-sdmmc-test") {
  executable_path = "test/mtk-sdmmc-test"
  deps = [ ":mtk-sdmmc-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":mtk-sdmmc-bind_test",
    ":mtk-sdmmc-test",
  ]
}
