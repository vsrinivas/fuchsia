# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("core") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "block.core"
  deps = [
    ":shared",
    "//src/devices/lib/driver",
  ]
}

group("test") {
  testonly = true
  deps = [ ":core-unittest" ]
}

test("core-unittest") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "blockcore"
  include_dirs = [ "." ]
  sources = [
    "test/main.cc",
    "test/server.cc",
    "test/stub-block-device.cc",
  ]
  deps = [
    ":shared",
    "//sdk/banjo/ddk.protocol.block",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/io-scheduler",
  ]
}

source_set("shared") {
  visibility = [ "./*" ]
  defines = [ "_ALL_SOURCE" ]
  sources = [
    "block-device.cc",
    "iobuffer.cc",
    "manager.cc",
    "message.cc",
    "server.cc",
    "txn-group.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.block",
    "//sdk/banjo/ddk.protocol.block.partition",
    "//sdk/banjo/ddk.protocol.block.volume",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_c",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_c",
    "//src/devices/lib/dev-operation",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/storage-metrics",
  ]
}

migrated_manifest("core-manifest") {
  deps = [ ":core" ]
}

migrated_manifest("core-unittest-manifest") {
  deps = [ ":core-unittest" ]
}
