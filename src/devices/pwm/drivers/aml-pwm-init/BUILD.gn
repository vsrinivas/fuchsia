# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("aml-pwm-init-bind") {
  rules = "aml-pwm-init.bind"
  header_output = "aml-pwm-init-bind.h"
  bind_output = "aml-pwm-init.bindbc"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/amlogic.platform" ]
}

driver_module("aml-pwm-init-driver") {
  output_name = "aml-pwm-init"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "aml-pwm-init.cc" ]
  deps = [
    ":aml-pwm-init-bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
  ]
}

fuchsia_driver_component("aml-pwm-init") {
  component_name = "aml-pwm-init"
  deps = [ ":aml-pwm-init-driver" ]
}

test("aml-pwm-init-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "aml-pwm-init-test"
  sources = [
    "aml-pwm-init-test.cc",
    "aml-pwm-init.cc",
  ]
  deps = [
    ":aml-pwm-init-bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp_mock",
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp_mock",
    "//src/devices/lib/amlogic",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

fuchsia_unittest_package("aml-pwm-init-test") {
  deps = [ ":aml-pwm-init-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":aml-pwm-init-test" ]
}
