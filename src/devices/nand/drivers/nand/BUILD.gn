# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("nand-bind") {
  rules = "nand.bind"
  header_output = "nand-bind.h"
  bind_output = "nand.bindbc"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.rawnand" ]
}

source_set("common") {
  configs += [ "//build/config:all_source" ]
  public = [ "nand.h" ]
  sources = [
    "nand.cc",
    "read_cache.cc",
  ]
  deps = [
    ":nand-bind",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.nand:fuchsia.hardware.nand_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.rawnand:fuchsia.hardware.rawnand_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//sdk/lib/sys/inspect/cpp",
    "//src/devices/lib/dev-operation",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("nand-driver") {
  output_name = "nand"
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("nand") {
  component_name = "nand"
  deps = [ ":nand-driver" ]
}

group("tests") {
  testonly = true
  deps = [
    ":nand-bind_test",
    "tests",
  ]
}
