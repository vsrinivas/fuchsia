# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/zbi/bootfs_test.gni")

driver_module("ram-nand") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = [
    "bind.cc",
    "ram-nand-ctl.cc",
  ]
  deps = [
    ":common",
    "//sdk/banjo/ddk.protocol.nand",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  sources = [ "ram-nand.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.nand",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
  ]
}

test("ram-nand-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "ram-nand-test"
  sources = [
    "test/ram-nand-ctl.cc",
    "test/ram-nand.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "//sdk/banjo/ddk.protocol.nand",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//sdk/lib/fdio",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/ramdevice-client",
  ]
}

bootfs_test("ram-nand-test-bootfs-test") {
  name = "ram-nand-test"
  deps = [ ":ram-nand-test" ]
}
