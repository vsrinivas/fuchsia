# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("skip-block") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = []
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

source_set("common") {
  sources = [
    "logical-to-physical-map.cc",
    "skip-block.cc",
  ]
  deps = [
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-hardware-nand:c",
  ]
  public_deps = [
    "//sdk/banjo/ddk.protocol.badblock",
    "//sdk/banjo/ddk.protocol.nand",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/dev-operation",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/fidl/fuchsia-hardware-skipblock:llcpp",
  ]
}

test("skip-block-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "skip-block-test"
  sources = [
    "test/logical-to-physical-map-test.cc",
    "test/skip-block-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/fake_ddk",
  ]
}

migrated_manifest("skip-block-manifest") {
  deps = [ ":skip-block" ]
}

unittest_package("skip-block-test-package") {
  package_name = "skip-block-test"
  deps = [ ":skip-block-test" ]

  tests = [
    {
      name = "skip-block-test"
      environments = basic_envs
    },
  ]
}
group("tests") {
  testonly = true
  deps = [ ":skip-block-test-package" ]
}
