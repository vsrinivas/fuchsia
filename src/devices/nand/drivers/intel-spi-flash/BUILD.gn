# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("intel_spi_flash_bind") {
  rules = "intel-spi-flash.bind"
  header_output = "intel_spi_flash_bind.h"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.intel.pci",
    "//src/devices/bind/fuchsia.pci",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":intel-spi-flash-test",
    ":intel_spi_flash_bind_test",
  ]
}

common_deps = [
  ":intel_spi_flash_bind",
  "//sdk/banjo/fuchsia.hardware.nand:fuchsia.hardware.nand_banjo_cpp",
  "//sdk/banjo/fuchsia.hardware.pci:fuchsia.hardware.pci_banjo_cpp",
  "//src/devices/lib/driver",
  "//src/devices/lib/mmio",
  "//src/devices/pci/lib/device-protocol-pci",
  "//src/devices/pci/lib/pci",
  "//src/lib/ddktl",
  "//zircon/system/ulib/hwreg",
  "//zircon/system/ulib/inspect",
  "//zircon/third_party/ulib/safemath",
]

fuchsia_driver("fuchsia_driver") {
  output_name = "intel-spi-flash"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = common_deps
  sources = [ "intel-spi-flash.cc" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("driver") {
  info = "driver-info.json"
  component_name = "intel-spi-flash"
  deps = [ ":fuchsia_driver" ]
  colocate = true
}

fuchsia_driver_package("intel-spi-flash") {
  package_name = "intel-spi-flash"
  driver_components = [ ":driver" ]
}

test("intel-spi-flash-test-bin") {
  output_name = "intel-spi-flash-test"
  sources = [
    "intel-spi-flash-test.cc",
    "intel-spi-flash.cc",
  ]

  deps =
      common_deps + [
        "//sdk/banjo/fuchsia.hardware.pci:fuchsia.hardware.pci_banjo_cpp_mock",
        "//src/devices/testing/mock-ddk",
        "//src/devices/testing/fake-mmio-reg",
        "//zircon/system/ulib/async-loop:async-loop-cpp",
        "//zircon/system/ulib/mock-function",
        "//zircon/system/ulib/zxtest",
      ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("intel-spi-flash-test") {
  deps = [ ":intel-spi-flash-test-bin" ]
}
