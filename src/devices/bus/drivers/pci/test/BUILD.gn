# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//src/lib/isolated_devmgr/devmgr_manifest.gni")

# The output of the test code below is made up of several binaries
# pci-unit-test: A binary to run the unit tests defiend in test/
# pci-driver-test: A binary to run the driver tests
# pci_fake.so: A fake bus driver loaded by pci-driver-test to provide a bind
#              point for pci_test.so
# pci_fake.proxy.so: The corresponding proxy driver that binds to pci_fake.so
#                    and loads pci_test.so
# pci_test.so: A zxtest driver to test the PCI protocol
test("pci-unit") {
  output_name = "pci-unit-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "unit/allocation_tests.cc",
    "unit/config_tests.cc",
    "unit/device_tests.cc",
  ]
  deps = [
    "//src/devices/bus/drivers/pci:user_pci",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zxtest",
  ]
}

test("pci-driver") {
  output_name = "pci-driver-test"

  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  if (is_fuchsia) {
    testonly = false
  }
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "driver/driver_tests.cc" ]
  deps = [
    ":pci_fake",
    ":pci_fake.proxy",
    ":pci_protocol_test_driver",
    "//sdk/fidl/fuchsia.device.test:fuchsia.device.test_c",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/driver-integration-test",
  ]
}

devmgr_manifest("devmgr-manifest") {
}

test_package("pci-driver-package") {
  package_name = "pci-driver"
  deps = [
    ":devmgr-manifest",
    ":pci-driver",
  ]
  extra = [ "$target_gen_dir/devmgr.manifest" ]
  tests = [
    {
      name = "pci-driver-test"
    },
  ]
}

# These drivers are not 'test' drivers because at the moment having other
# drivers in test/ breaks ddkltl integration tests.
driver_module("pci_fake") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "driver/fake_bus_driver.cc" ]
  public_deps = [
    "//src/devices/bus/drivers/pci:user_pci",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/zxtest",
  ]
}

driver_module("pci_fake.proxy") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  deps = [ "//src/devices/bus/drivers/pci:user_pci.proxy" ]
}

driver_module("pci_protocol_test_driver") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "driver/protocol_test_driver.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.device.test:fuchsia.device.test_c",
    "//src/devices/bus/drivers/pci:user_pci",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/zxtest",
  ]
}

unittest_package("pci-unit-package") {
  package_name = "pci-unit"
  deps = [ ":pci-unit" ]

  tests = [
    {
      name = "pci-unit-test"
    },
  ]
}
