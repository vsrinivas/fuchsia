# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("test") {
  testonly = true
  deps = [ ":device-protocol-pdev-test" ]
}

test("device-protocol-pdev-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "device-protocol-pdev-test"
  sources = [ "pdev-test.cc" ]
  public_deps = [
    # <lib/device-protocol/pdev.h> has #include <fuchsia/hardware/platform/device/cpp/banjo.h>.
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",

    # Banjo protocols have #include <ddk/driver.h>.
    "//src/lib/ddk",

    # Banjo protocols have #include <ddktl/device-internal.h>.
    "//src/lib/ddktl",
  ]

  deps = [
    # pdev.cpp has #include <lib/mmio/mmio.h>.
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/mmio",
    "//src/devices/testing/no_ddk",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_unittest_package("device-protocol-pdev-test-pkg") {
  package_name = "device-protocol-pdev-test"
  deps = [ ":device-protocol-pdev-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":device-protocol-pdev-test-pkg" ]
}
