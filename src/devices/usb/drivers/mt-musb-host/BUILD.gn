# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")

bind_rules("mt_musb_host_bind") {
  rules = "mt_musb_host.bind"
  output = "mt_musb_host_bind.h"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.mediatek.platform",
    "//src/devices/bind/fuchsia.mentorgraphics.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_module("mt-musb-host") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

source_set("common") {
  configs += [ "//build/config:all_source" ]
  visibility = [ ":*" ]
  sources = [
    "usb-device.cc",
    "usb-hci.cc",
    "usb-request-queue.cc",
    "usb-root-hub.cc",
    "usb-spew.cc",
    "usb-transaction.cc",
  ]
  public_deps = [
    ":mt_musb_host_bind",
    "//sdk/banjo/fuchsia.hardware.usb.bus",
    "//sdk/banjo/fuchsia.hardware.usb.hci",
    "//sdk/banjo/fuchsia.hardware.usb.hubdescriptor",
    "//sdk/lib/fit",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mt8167",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
  ]
}

test("mt-hci-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "mt-hci-test"
  sources = [ "usb-hci-test.cc" ]
  deps = [
    ":common",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zxtest",
  ]
}

test("mt-hci-transaction-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "usb-transaction-test.cc",
    "usb-transaction.cc",
  ]
  output_name = "mt-hci-transaction-test"
  deps = [
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mt8167",
    "//src/devices/testing/fake_ddk",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

test("mt-hci-request-queue-test-bin") {
  configs += [
    "//build/unification/config:zircon-migrated",
    "//build/config:all_source",
  ]
  sources = [
    "usb-request-queue-test.cc",
    "usb-request-queue.cc",
    "usb-transaction.cc",
  ]
  output_name = "mt-hci-request-queue-test"
  deps = [
    "//sdk/banjo/fuchsia.hardware.usb.hci",
    "//sdk/banjo/fuchsia.hardware.usb.request",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mt8167",
    "//src/devices/testing/fake_ddk",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

fuchsia_unittest_component("mt-hci-test") {
  deps = [ ":mt-hci-test-bin" ]
}

fuchsia_unittest_component("mt-hci-transaction-test") {
  deps = [ ":mt-hci-transaction-test-bin" ]
}

fuchsia_unittest_component("mt-hci-request-queue-test") {
  deps = [ ":mt-hci-request-queue-test-bin" ]
}

fuchsia_test_package("mt-hci-tests") {
  test_components = [
    ":mt-hci-test",
    ":mt-hci-transaction-test",
    ":mt-hci-request-queue-test",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":mt-hci-tests",
    ":mt_musb_host_bind_test",
  ]
}
