# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("dwc3_bind") {
  rules = "dwc3.bind"
  header_output = "dwc3_bind.h"
  bind_output = "dwc3.bindbc"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.designware.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("dwc3-driver") {
  output_name = "dwc3"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "dwc3-commands.cc",
    "dwc3-endpoints.cc",
    "dwc3-ep0.cc",
    "dwc3-events.cc",
    "dwc3.cc",
  ]
  deps = [
    ":dwc3_bind",
    "//sdk/banjo/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_banjo_cpp",
    "//sdk/lib/fit",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_driver_component("dwc3") {
  info = "dwc3-info.json"
  component_name = "dwc3"
  deps = [ ":dwc3-driver" ]
  colocate = true
}

test("dwc3-test-bin") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "dwc3-commands.cc",
    "dwc3-endpoints.cc",
    "dwc3-ep0.cc",
    "dwc3-events.cc",
    "dwc3-test.cc",
    "dwc3.cc",
  ]
  output_name = "dwc3-test"
  deps = [
    ":dwc3_bind",
    "//sdk/banjo/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_banjo_cpp",
    "//sdk/lib/fit",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/mock-ddk",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("dwc3-test") {
  deps = [ ":dwc3-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":dwc3-test",
    ":dwc3_bind_test",
  ]
}
