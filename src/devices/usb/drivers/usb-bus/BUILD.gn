# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")

bind_rules("usb_bus_bind") {
  rules = "usb_bus.bind"
  output = "usb_bus_bind.h"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.usb" ]
}

driver_module("usb-bus") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "usb-bus.cc" ]
  deps = [
    ":common",
    ":usb_bus_bind",
    "//src/devices/lib/driver",
  ]
}

source_set("common") {
  configs += [ "//build/config:all_source" ]
  sources = [ "usb-device.cc" ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.usb.composite",
    "//sdk/banjo/fuchsia.hardware.usb.hub:fuchsia.hardware.usb.hub_banjo_cpp",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/utf_conversion",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.usb",
    "//sdk/banjo/fuchsia.hardware.usb.bus:fuchsia.hardware.usb.bus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.hci:fuchsia.hardware.usb.hci_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request",
    "//sdk/fidl/fuchsia.hardware.usb.device:fuchsia.hardware.usb.device_llcpp",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":usb-device-test-pkg",
    ":usb_bus_bind_test",
  ]
}

test("usb-device") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "tests/usb-device.cc" ]
  deps = [
    ":common",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/utf_conversion",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("usb-device-test-pkg") {
  package_name = "usb-device"
  deps = [ ":usb-device" ]
}
