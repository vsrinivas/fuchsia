# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("usb-peripheral") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = [
    "usb-function.cc",
    "usb-peripheral.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/banjo/ddk.protocol.usb.dci",
    "//sdk/banjo/ddk.protocol.usb.function",
    "//sdk/banjo/ddk.protocol.usb.modeswitch",
    "//sdk/banjo/ddk.protocol.usb.request",
    "//sdk/fidl/fuchsia.hardware.usb.peripheral:fuchsia.hardware.usb.peripheral_llcpp",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
  ]

  # Set default configuration here, rather than relying on usbctl to do it.
  defines = [
    "USB_DEVICE_VID=0x18D1",
    "USB_DEVICE_PID=0xA020",
    "USB_DEVICE_MANUFACTURER=\"Zircon\"",
    "USB_DEVICE_PRODUCT=\"CDC-Ethernet\"",
    "USB_DEVICE_SERIAL=\"0123456789ABCDEF\"",
    "USB_DEVICE_FUNCTIONS=\"cdc\"",
  ]
}

test("usb-peripheral-unittest") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "usb-function.cc",
    "usb-peripheral-test.cc",
    "usb-peripheral.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/banjo/ddk.protocol.usb.dci",
    "//sdk/banjo/ddk.protocol.usb.function",
    "//sdk/banjo/ddk.protocol.usb.modeswitch",
    "//sdk/banjo/ddk.protocol.usb.phy",
    "//sdk/banjo/ddk.protocol.usb.request",
    "//sdk/fidl/fuchsia.hardware.usb.peripheral:fuchsia.hardware.usb.peripheral_llcpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/fake_ddk",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/utf_conversion",
  ]
}

unittest_package("usb-peripheral-unittest-package") {
  package_name = "usb-peripheral-unittest"
  deps = [ ":usb-peripheral-unittest" ]

  tests = [
    {
      name = "usb-peripheral-unittest"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":usb-peripheral-unittest-package" ]
}
