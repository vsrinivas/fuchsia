# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/component_manifest_resources.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test.gni")

rustc_binary("bin") {
  name = "ctap_agent"
  edition = "2021"
  with_unit_tests = true

  sources = [
    "src/ctap_agent.rs",
    "src/ctap_device.rs",
    "src/ctap_hid/connection.rs",
    "src/ctap_hid/device.rs",
    "src/ctap_hid/message.rs",
    "src/ctap_hid/mod.rs",
    "src/main.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.fido.report:fuchsia.fido.report_rust",
    "//sdk/fidl/fuchsia.identity.ctap:fuchsia.identity.ctap_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/storage/fuchsia-vfs-watcher",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:bitfield",
    "//third_party/rust_crates:bytes",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/lib/fuchsia",
    "//src/lib/storage/vfs/rust:vfs",
  ]
}

fuchsia_component("ctap_agent") {
  component_name = "ctap_agent"
  manifest = "meta/ctap_agent.cml"
  deps = [ ":bin" ]
}

fuchsia_package("ctap_agent_service") {
  deps = [ ":ctap_agent" ]
}

fuchsia_unittest_package("ctap_agent_test") {
  deps = [ ":bin_test" ]
}
