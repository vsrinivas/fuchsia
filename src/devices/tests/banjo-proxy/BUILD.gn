# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

test("test") {
  output_name = "banjo-proxy-test"

  sources = [ "banjo-proxy-test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_hlcpp",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_hlcpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/driver_test_realm/realm_builder/cpp",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_unittest_package("banjo-proxy-test") {
  deps = [
    ":child",
    ":parent",
    ":test",
    "//src/devices/internal/drivers/fragment",
    "//src/devices/misc/drivers/test-parent",
  ]
}

driver_bind_rules("parent-driver-bind") {
  rules = "parent-driver.bind"
  header_output = "parent-driver-bind.h"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

fuchsia_driver("parent-driver") {
  output_name = "banjo-proxy-parent"
  sources = [ "parent-driver.cc" ]
  deps = [
    ":parent-driver-bind",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
  ]
}

fuchsia_driver_component("parent") {
  component_name = "parent"
  deps = [ ":parent-driver" ]
  info = "parent-info.json"
  colocate = true
}

driver_bind_rules("child-driver-bind") {
  rules = "child-driver.bind"
  header_output = "child-driver-bind.h"
  deps = [ "//src/devices/bind/fuchsia.pci" ]
}

fuchsia_driver("child-driver") {
  output_name = "banjo-proxy-child"
  sources = [ "child-driver.cc" ]
  deps = [
    ":child-driver-bind",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
  ]
}

fuchsia_driver_component("child") {
  component_name = "child"
  deps = [ ":child-driver" ]
  info = "child-info.json"
  colocate = false
}

group("tests") {
  testonly = true
  deps = [ ":banjo-proxy-test" ]
}

group("drivers") {
  deps = [
    ":child",
    ":parent",
  ]
}
