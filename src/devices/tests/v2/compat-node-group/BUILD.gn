# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":package" ]
}

group("drivers") {
  testonly = true
  deps = [
    "node-group:component",
    "root:component",
  ]
}

fidl("fuchsia.compat.nodegroup.test") {
  testonly = true
  sources = [ "compat.nodegroup.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

rustc_test("compat-node-group-test") {
  edition = "2021"
  testonly = true
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    ":fuchsia.compat.nodegroup.test_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]
}

cmc_merge("realm.cml") {
  sources = [
    "//sdk/lib/driver_test_realm/meta/driver_test_realm.cml",
    "meta/realm.shard.cml",
  ]
}

fuchsia_test_component("realm") {
  manifest = "${target_out_dir}/realm.cml"
  manifest_deps = [ ":realm.cml" ]
}

fuchsia_unittest_package("package") {
  package_name = "compat-node-group-test"
  deps = [
    ":compat-node-group-test",
    ":realm",
    "node-group:component",
    "root:component",
    "//src/devices/misc/drivers/test-parent",
  ]
}
