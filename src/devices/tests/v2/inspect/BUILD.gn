# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/rust/rustc_test.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":package" ]
}

group("drivers") {
  testonly = true
  deps = [ ":root" ]
}

fidl("fuchsia.inspect.test") {
  testonly = true
  sources = [ "inspect.test.fidl" ]
}

driver_bind_rules("bind") {
  rules = "root.bind"
  header_output = "root-bind.h"
  bind_output = "root.bindbc"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

fuchsia_driver("root-driver") {
  testonly = true
  output_name = "inspect-root"
  sources = [ "root-driver.cc" ]
  deps = [
    ":bind",
    "//src/devices/lib/driver",
    "//src/devices/tests/v2/inspect:fuchsia.inspect.test_cpp_wire",
    "//src/devices/tests/v2/inspect/bind:fuchsia.inspect_cpp",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("root") {
  testonly = true
  component_name = "root"
  deps = [ ":root-driver" ]
  info = "root-info.json"
  colocate = false
}

rustc_test("bin") {
  edition = "2021"
  testonly = true
  output_name = "inspect-bin"
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    ":fuchsia.inspect.test_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/device-watcher/rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_unittest_package("package") {
  manifest = "meta/test.cml"
  package_name = "compat-driver-inspect-test"
  deps = [
    ":bin",
    ":root",
    "//src/devices/misc/drivers/test-parent",
  ]
}
