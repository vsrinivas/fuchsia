# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

test("tests") {
  testonly = true

  output_name = "libdriver-integration-test"

  sources = [
    "action-list.cc",
    "basic-lifecycle-test.cc",
    "composite-device-test.cc",
    "device-add-tests.cc",
    "integration-test.cc",
    "mock-device-hooks.cc",
    "mock-device-thread.cc",
    "mock-device.cc",
    "root-mock-device.cc",
  ]

  deps = [
    ":mock-device",
    "//sdk/fidl/fuchsia.device",
    "//sdk/fidl/fuchsia.device.test",
    "//sdk/fidl/fuchsia.io:fuchsia.io",
    "//sdk/lib/fdio",
    "//sdk/lib/fit-promise",
    "//src/devices/testing/devmgr-integration-test-shim",
    "//src/lib/ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspector",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
  ]
}

fuchsia_unittest_package("libdriver-integration-test") {
  deps = [
    ":tests",
    "//src/devices/internal/drivers/fragment",
    "//src/devices/internal/drivers/fragment:fragment.proxy",
    "//src/devices/misc/drivers/test",
    "//src/devices/tests/mock-device",
    "//src/devices/tests/sysdev",
  ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fidl("mock-device") {
  # TODO(fxbug.dev/35879): Remove lint exclusions by fixing known FIDL lint violations in this target
  excluded_checks = [ "event-names-must-start-with-on" ]

  name = "fuchsia.device.mock"

  sources = [ "//src/devices/tests/mock-device/mock-device.test.fidl" ]

  public_deps = [ "//zircon/vdso/zx" ]
}
