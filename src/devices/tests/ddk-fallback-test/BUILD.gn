# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

driver_bind_rules("ddk-fallback-test-bind") {
  rules = "test-driver.bind"
  header_output = "test-driver-bind.h"

  deps = [ "//src/devices/bind/fuchsia.test.platform" ]
}

fuchsia_driver("ddk-fallback-test-driver") {
  output_name = "ddk-fallback-test"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "test-fallback-driver.cc" ]
  deps = [
    ":ddk-fallback-test-bind",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/files",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_driver_component("ddk-fallback-test") {
  component_name = "ddk-fallback-test"
  deps = [ ":ddk-fallback-test-driver" ]
  info = "ddk-fallback-test-info.json"
  colocate = true
  fallback = true
}

fuchsia_driver("ddk-not-fallback-test-driver") {
  output_name = "ddk-not-fallback-test"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "test-not-fallback-driver.cc" ]
  deps = [
    ":ddk-fallback-test-bind",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/files",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_driver_component("ddk-not-fallback-test") {
  component_name = "ddk-not-fallback-test"
  deps = [ ":ddk-not-fallback-test-driver" ]
  info = "ddk-not-fallback-test-info.json"
  colocate = true
}

test("ddk-fallback") {
  sources = [ "test.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//src/devices/testing/driver-integration-test-shim",
    "//src/lib/ddk",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("ddk-fallback-test-package") {
  package_name = "ddk-fallback-test-test"
  deps = [
    ":ddk-fallback",
    ":ddk-fallback-test",
    ":ddk-not-fallback-test",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ddk-fallback-test-package" ]
}
