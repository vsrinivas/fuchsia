# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//build/unification/images/migrated_manifest.gni")
import("//src/lib/isolated_devmgr/devmgr_manifest.gni")

executable("tests") {
  testonly = true

  output_name = "bind-debugger-test"

  sources = [ "bind-debugger-test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.device.test:fuchsia.device.test_llcpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/devmgr-integration-test",
    "//zircon/public/lib/fdio",
    "//zircon/system/fidl/fuchsia-device:llcpp",
    "//zircon/system/fidl/fuchsia-device-manager",
  ]
}

devmgr_manifest("devmgr-manifest") {
}

test_package("bind-debugger-test") {
  deps = [
    ":devmgr-manifest",
    ":tests",
  ]

  extra = [ "$target_gen_dir/devmgr.manifest" ]

  tests = [
    {
      name = "bind-debugger-test"
      environments = basic_envs
    },
  ]
}

bind_rules("bind") {
  rules = "bind-debugger-test.bind"
  output = "bind-debugger-test-bind.h"
  disable_autobind = true
  deps = [ "//src/devices/bind/deprecated.test" ]
}

# TODO: Move this out of bootfs and into test package.
driver_module("bind-debugger-test-driver") {
  output_name = "bind-debugger-test"
  test = true
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "bind-debugger-test-driver.cc" ]
  deps = [
    ":bind",
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddk",
  ]
}

migrated_manifest("bind-debugger-test-driver-manifest") {
  deps = [ ":bind-debugger-test-driver" ]
}
