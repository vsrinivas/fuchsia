# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_test("bin") {
  name = "intl_integration_test"
  edition = "2021"
  source_root = "src/intl_tests.rs"
  sources = [
    "src/common.rs",
    "src/intl_tests.rs",
  ]
  deps = [
    "//sdk/fidl/fuchsia.intl:fuchsia.intl-rustc",
    "//sdk/fidl/fuchsia.settings:fuchsia.settings-rustc",
    "//src/intl:icudtl",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//src/settings/service/integration_tests/utils",
    "//third_party/rust_crates:anyhow",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("intl_integration_test") {
  testonly = true
  manifest = "meta/intl_integration_test.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("intl_integration_test_pkg") {
  test_specs = {
    log_settings = {
      # test_intl_invalid_locale expects to produce an error level log due to
      # calling Set with an invalid locale.
      max_severity = "ERROR"
    }
  }
  package_name = "intl_integration_test"
  test_components = [ ":intl_integration_test" ]
  deps = [
    ":intl_integration_test",
    ":tests_config",
    "//src/settings/service:setui_service_v2",
    "//src/sys/stash:pkg",
  ]
}

config_data("tests_config") {
  for_pkg = "intl_integration_test_pkg"
  sources = [ "testdata/interface_configuration.json" ]
}

group("intl_tests") {
  testonly = true
  public_deps = [ ":intl_integration_test_pkg" ]
}
