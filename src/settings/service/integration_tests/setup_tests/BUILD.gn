# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_test("bin") {
  name = "setup_integration_test"
  edition = "2021"
  source_root = "src/setup_tests.rs"
  sources = [
    "src/common.rs",
    "src/setup_tests.rs",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol-rustc",
    "//sdk/fidl/fuchsia.settings:fuchsia.settings-rustc",
    "//src/intl:icudtl",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/settings/service/integration_tests/utils",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:test-case",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("setup_integration_test") {
  testonly = true
  manifest = "meta/setup_integration_test.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("setup_integration_test_pkg") {
  package_name = "setup_integration_test"
  test_components = [ ":setup_integration_test" ]
  deps = [
    ":setup_integration_test",
    ":tests_config",
    "//src/settings/service:setui_service_v2",
    "//src/sys/stash:pkg",
  ]
}

config_data("tests_config") {
  for_pkg = "setup_integration_test_pkg"
  sources = [ "testdata/interface_configuration.json" ]
}

group("setup_tests") {
  testonly = true
  public_deps = [ ":setup_integration_test_pkg" ]
}
