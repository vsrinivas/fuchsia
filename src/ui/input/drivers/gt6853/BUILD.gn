# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/cipd.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

config_9364_path = "gt6853/goodix_cfg_group_fiti9364.bin"
config_9365_path = "gt6853/goodix_cfg_group_fiti9365.bin"
firmware_path = "gt6853/goodix_firmware.bin"

fuchsia_system_package("gt6853") {
  deps = [ ":gt6853-module" ]

  if (internal_access) {
    deps += [
      ":config-9364",
      ":config-9365",
      ":firmware",
    ]
  }
  allowed_in_extra_deps = true
}

if (internal_access) {
  resource("config-9364") {
    sources = [ "//prebuilt/touch/$config_9364_path" ]
    outputs = [ "lib/firmware/$config_9364_path" ]
  }
  resource("config-9365") {
    sources = [ "//prebuilt/touch/$config_9365_path" ]
    outputs = [ "lib/firmware/$config_9365_path" ]
  }
  resource("firmware") {
    sources = [ "//prebuilt/touch/$firmware_path" ]
    outputs = [ "lib/firmware/$firmware_path" ]
  }
}

group("common") {
  public_deps = [
    ":gt6853-bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/ui/input/lib/input-report-reader",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxc",
  ]
}

driver_bind_rules("gt6853-bind") {
  rules = "gt6853.bind"
  header_output = "gt6853-bind.h"
  tests = "gt6853-bind-test.json"
  deps = [ "//src/devices/bind/goodix.platform" ]
}

fuchsia_driver("gt6853-module-driver") {
  output_name = "gt6853"
  sources = [ "gt6853.cc" ]
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [ ":common" ]
  defines = [
    "GT6853_CONFIG_9364_PATH=\"$config_9364_path\"",
    "GT6853_CONFIG_9365_PATH=\"$config_9365_path\"",
    "GT6853_FIRMWARE_PATH=\"$firmware_path\"",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("gt6853-module") {
  component_name = "gt6853"
  deps = [ ":gt6853-module-driver" ]
}

test("gt6853-test") {
  output_name = "gt6853-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "gt6853-test.cc",
    "gt6853.cc",
  ]
  deps = [
    ":common",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp_mock",
    "//src/devices/i2c/testing/fake-i2c",
    "//src/devices/testing/fake_ddk",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/zxtest",
  ]
  defines = [
    "GT6853_CONFIG_9364_PATH=\"$config_9364_path\"",
    "GT6853_CONFIG_9365_PATH=\"$config_9365_path\"",
    "GT6853_FIRMWARE_PATH=\"$firmware_path\"",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("gt6853-test-package") {
  package_name = "gt6853-test"
  deps = [ ":gt6853-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":gt6853-bind_test",
    ":gt6853-test-package",
  ]
}
