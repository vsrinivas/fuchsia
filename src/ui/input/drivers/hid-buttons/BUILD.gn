# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("hid-buttons") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = []
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

source_set("common") {
  configs += [ "//build/config:all_source" ]
  sources = [ "hid-buttons.cc" ]

  public_deps = [
    "//sdk/banjo/ddk.protocol.buttons",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.gpio",
    "//sdk/banjo/ddk.protocol.hidbus",
    "//sdk/fidl/fuchsia.buttons:fuchsia.buttons_llcpp",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/hid",
  ]
}

test("hid-buttons-test") {
  configs += [
    "//build/config:all_source",
    "//build/unification/config:zircon-migrated",
  ]
  output_name = "hid-buttons-test"
  sources = [ "hid-buttons-test.cc" ]
  deps = [
    ":common",
    "//sdk/banjo/ddk.protocol.gpio:ddk.protocol.gpio_mock",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/zxtest",
  ]
}

unittest_package("hid-buttons-test-package") {
  package_name = "hid-buttons-test"
  deps = [ ":hid-buttons-test" ]

  tests = [
    {
      name = "hid-buttons-test"
      environments = basic_envs
    },
  ]
}
group("tests") {
  testonly = true
  deps = [ ":hid-buttons-test-package" ]
}
