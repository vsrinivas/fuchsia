# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("drivers") {
  deps = [
    ":i2c-hid",
    ":i2c-hid-dfv2",
  ]
}

driver_bind_rules("i2c_hid_bind") {
  rules = "i2c_hid.bind"
  header_output = "i2c_hid_bind.h"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.i2c",
  ]
}

driver_bind_rules("i2c_hid_bind_dfv2") {
  rules = "i2c_hid_dfv2.bind"
  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.i2c",
  ]
}

fuchsia_driver("i2c-hid-driver") {
  output_name = "i2c-hid"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "i2c-hid.cc" ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_cpp_wire",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/trace",
  ]
}

fuchsia_driver_component("i2c-hid") {
  component_name = "i2c-hid"
  deps = [
    ":i2c-hid-driver",
    ":i2c_hid_bind",
  ]
  info = "i2c-hid-info.json"
  colocate = true
  default_dispatcher_opts = [ "allow_sync_calls" ]
}

fuchsia_driver_component("i2c-hid-dfv2") {
  deps = [
    ":i2c-hid-driver",
    ":i2c_hid_bind_dfv2",
  ]
  info = "i2c-hid-info.json"
  colocate = true
  default_dispatcher_opts = [ "allow_sync_calls" ]
}

test("i2c-hid-test") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "i2c-hid-test.cc",
    "i2c-hid.cc",
  ]
  deps = [
    ":i2c_hid_bind",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_cpp_wire",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/fake-i2c",
    "//src/devices/lib/acpi",
    "//src/devices/lib/acpi/mock:mock-acpi",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/ui/input/testing/fake-hidbus-ifc",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("i2c-hid-test-package") {
  package_name = "i2c-hid-test"
  deps = [ ":i2c-hid-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":i2c-hid-test-package",
    ":i2c_hid_bind_test",
  ]
}
