# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("i2c_hid_bind") {
  rules = "i2c_hid.bind"
  header_output = "i2c_hid_bind.h"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.i2c",
  ]
}

driver_module("i2c-hid-driver") {
  output_name = "i2c-hid"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "i2c-hid.cc" ]
  deps = [
    ":i2c_hid_bind",
    "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/trace:trace-driver",
  ]
}

fuchsia_driver_component("i2c-hid") {
  component_name = "i2c-hid"
  deps = [ ":i2c-hid-driver" ]
}

test("i2c-hid-test") {
  configs += [
    "//build/unification/config:zircon-migrated",
    "//build/config:all_source",
  ]
  sources = [
    "i2c-hid-test.cc",
    "i2c-hid.cc",
  ]
  deps = [
    ":i2c_hid_bind",
    "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/fake-i2c",
    "//src/devices/lib/acpi",
    "//src/devices/lib/acpi/mock:mock-acpi",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/ui/input/testing/fake-hidbus-ifc",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/trace:trace-driver",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("i2c-hid-test-package") {
  package_name = "i2c-hid-test"
  deps = [ ":i2c-hid-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":i2c-hid-test-package",
    ":i2c_hid_bind_test",
  ]
}
