# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers/fhcp_test_package.gni")

executable("touchpad-test-bin") {
  testonly = true
  output_name = "touchpad-test"

  sources = [ "touchpad-test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_cpp_wire",
    "//sdk/lib/fdio",
    "//sdk/lib/fhcp/cpp",
    "//sdk/lib/fit",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

fuchsia_component("touchpad-test-component") {
  testonly = true
  deps = [ ":touchpad-test-bin" ]
  component_name = "touchpad-test"
  manifest = "meta/touchpad-test.cml"
}

fhcp_test_package("touchpad-test-pkg") {
  test_components = [ ":touchpad-test-component" ]
  package_name = "touchpad-test"
  test_types = [ "functional" ]
  device_categories = [
    {
      category = "input"
      subcategory = "touchpad"
    },
  ]
  test_specs = {
    environments = [
      {
        dimensions = {
          device_type = "Atlas"
        }
        tags = [ "fhcp-manual" ]
      },
    ]
  }
}

group("tests") {
  testonly = true
  deps = [ ":touchpad-test-pkg" ]
}
