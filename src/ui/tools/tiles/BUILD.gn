# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("tiles_lib") {
  sources = [
    "tiles.cc",
    "tiles.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.developer.tiles",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.gfx",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/ui/base_view",
    "//zircon/public/lib/async-cpp:async-cpp",
    "//zircon/public/lib/zx",
  ]

  deps = [
    "//sdk/fidl/fuchsia.sys",
    "//src/lib/fxl",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("bin") {
  output_name = "tiles"

  sources = [ "main.cc" ]

  deps = [
    ":tiles_lib",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace-provider:trace-provider-with-fdio",
  ]
}

package("tiles") {
  meta = [
    {
      path = "meta/tiles.cmx"
      dest = "tiles.cmx"
    },
  ]

  binaries = [
    {
      name = "tiles"
    },
  ]
  deps = [ ":bin" ]
}

executable("tiles_unittests") {
  testonly = true

  sources = [ "tiles_unittest.cc" ]

  deps = [
    ":tiles_lib",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
  ]
}

test_package("tiles_tests") {
  deps = [ ":tiles_unittests" ]

  tests = [
    {
      name = "tiles_unittests"
      environments = basic_envs
    },
  ]
}
