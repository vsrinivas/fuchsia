# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":focus_dispatcher_unittests",
    ":root_presenter_tests",
  ]
}

fuchsia_unittest_component("focus_dispatcher_unittests") {
  deps = [ ":focus_dispatcher_unittests_bin" ]
}

fuchsia_unittest_component("root_presenter_unittests") {
  deps = [
    ":root_presenter_unittests_bin",
    "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider-cmv2-component",
    "//src/ui/scenic:component_v2_with_supplied_display_provider",
  ]
  manifest = "meta/root_presenter_unittests.cml"
}

fuchsia_test_package("root_presenter_tests") {
  test_components = [
    ":root_presenter_unittests",
    ":focus_dispatcher_unittests",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [ nuc_env ]
  }
}

executable("root_presenter_unittests_bin") {
  testonly = true
  output_name = "root_presenter_unittests"

  sources = [
    "color_transform_handler_unittest.cc",
    "display_configuration_unittest.cc",
    "root_presenter_unittest.cc",
    "safe_presenter_unittest.cc",
    "virtual_keyboard_controller_unittest.cc",
    "virtual_keyboard_coordinator_unittest.cc",
    "virtual_keyboard_fidl_test.cc",
    "virtual_keyboard_manager_unittest.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.accessibility:fuchsia.accessibility_hlcpp",
    "//sdk/fidl/fuchsia.recovery:fuchsia.recovery_hlcpp",
    "//sdk/fidl/fuchsia.recovery.policy:fuchsia.recovery.policy_hlcpp",
    "//sdk/fidl/fuchsia.ui.brightness:fuchsia.ui.brightness_hlcpp",
    "//sdk/fidl/fuchsia.ui.focus:fuchsia.ui.focus_hlcpp",
    "//sdk/fidl/fuchsia.ui.input:fuchsia.ui.input_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointerinjector:fuchsia.ui.pointerinjector_hlcpp",
    "//sdk/fidl/fuchsia.ui.policy:fuchsia.ui.policy_hlcpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/lib/testing/loop_fixture",
    "//src/ui/a11y/lib/view",
    "//src/ui/bin/root_presenter:color_transform",
    "//src/ui/bin/root_presenter:displays",
    "//src/ui/bin/root_presenter:lib",
    "//src/ui/bin/root_presenter:safe_presenter",
    "//src/ui/bin/root_presenter:virtual_keyboard",
    "//src/ui/bin/root_presenter/tests/fakes",
    "//src/ui/input/lib/injector/tests/mocks",
    "//src/ui/lib/key_util",
    "//src/ui/testing:mock_input_device",
    "//third_party/googletest:gmock",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("focus_dispatcher_unittests_bin") {
  testonly = true
  output_name = "focus_dispatcher_unittests"

  sources = [ "focus_dispatcher_unittest.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.ui.input.accessibility:fuchsia.ui.input.accessibility_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/bin/root_presenter:lib",
    "//src/ui/bin/root_presenter/tests/fakes",
  ]
}
