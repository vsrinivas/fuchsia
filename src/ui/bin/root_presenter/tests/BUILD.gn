# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":focus_dispatcher_unittests",
    ":root_presenter_tests",
  ]
}

fuchsia_component("focus_dispatcher_unittests") {
  testonly = true
  deps = [ ":focus_dispatcher_unittests_bin" ]
  manifest = "meta/focus_dispatcher_unittests.cmx"
}

fuchsia_unittest_component("root_presenter_unittests") {
  deps = [ ":root_presenter_unittests_bin" ]
  manifest = "meta/root_presenter_unittests.cmx"
}

fuchsia_test_package("root_presenter_tests") {
  test_components = [
    ":root_presenter_unittests",
    ":focus_dispatcher_unittests",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [ nuc_env ]
  }
}

executable("root_presenter_unittests_bin") {
  testonly = true
  output_name = "root_presenter_unittests"

  sources = [
    "color_transform_handler_unittest.cc",
    "display_configuration_unittest.cc",
    "factory_reset_manager_test.cc",
    "media_buttons_handler_unittest.cc",
    "root_presenter_unittest.cc",
    "safe_presenter_unittest.cc",
    "virtual_keyboard_controller_unittest.cc",
    "virtual_keyboard_coordinator_unittest.cc",
    "virtual_keyboard_fidl_test.cc",
    "virtual_keyboard_manager_unittest.cc",
  ]

  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.accessibility",
    "//sdk/fidl/fuchsia.media.sounds",
    "//sdk/fidl/fuchsia.recovery",
    "//sdk/fidl/fuchsia.recovery.policy",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.ui.brightness",
    "//sdk/fidl/fuchsia.ui.focus",
    "//sdk/fidl/fuchsia.ui.input",
    "//sdk/fidl/fuchsia.ui.pointerinjector",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/a11y/lib/view",
    "//src/ui/bin/root_presenter:color_transform",
    "//src/ui/bin/root_presenter:displays",
    "//src/ui/bin/root_presenter:factory_reset_manager",
    "//src/ui/bin/root_presenter:lib",
    "//src/ui/bin/root_presenter:media_buttons_handler",
    "//src/ui/bin/root_presenter:safe_presenter",
    "//src/ui/bin/root_presenter:virtual_keyboard",
    "//src/ui/bin/root_presenter/tests/fakes",
    "//src/ui/input/lib/injector/tests/mocks",
    "//src/ui/lib/key_util",
    "//src/ui/testing:mock_input_device",
    "//third_party/googletest:gmock",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("focus_dispatcher_unittests_bin") {
  testonly = true
  output_name = "focus_dispatcher_unittests"

  sources = [ "focus_dispatcher_unittest.cc" ]

  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.ui.input.accessibility",
    "//sdk/fidl/fuchsia.ui.policy.accessibility",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/bin/root_presenter:lib",
    "//src/ui/bin/root_presenter/tests/fakes",
  ]
}
