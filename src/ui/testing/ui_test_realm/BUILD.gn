# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")

source_set("ui_test_realm") {
  testonly = true

  sources = [
    "ui_test_realm.cc",
    "ui_test_realm.h",
  ]

  public_deps = [
    ":a11y_deps",
    ":test_realms",
    "//sdk/fidl/fuchsia.accessibility:fuchsia.accessibility_hlcpp",
    "//sdk/fidl/fuchsia.accessibility.scene:fuchsia.accessibility.scene_hlcpp",
    "//sdk/fidl/fuchsia.accessibility.semantics:fuchsia.accessibility.semantics_hlcpp",
    "//sdk/fidl/fuchsia.input.injection:fuchsia.input.injection_hlcpp",
    "//sdk/fidl/fuchsia.input.virtualkeyboard:fuchsia.input.virtualkeyboard_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_hlcpp",
    "//sdk/fidl/fuchsia.session.scene:fuchsia.session.scene_hlcpp",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_hlcpp",
    "//sdk/fidl/fuchsia.ui.accessibility.view:fuchsia.ui.accessibility.view_hlcpp",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.focus:fuchsia.ui.focus_hlcpp",
    "//sdk/fidl/fuchsia.ui.input:fuchsia.ui.input_hlcpp",
    "//sdk/fidl/fuchsia.ui.observation.test:fuchsia.ui.observation.test_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointerinjector:fuchsia.ui.pointerinjector_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointerinjector.configuration:fuchsia.ui.pointerinjector.configuration_hlcpp",
    "//sdk/fidl/fuchsia.ui.policy:fuchsia.ui.policy_hlcpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//sdk/fidl/fuchsia.ui.test.scene:fuchsia.ui.test.scene_hlcpp",
    "//sdk/fidl/fuchsia.ui.views:fuchsia.ui.views_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl",
    "//src/lib/ui/input-synthesis:test.inputsynthesis_hlcpp",
    "//src/ui/a11y/testing/fidl:test.accessibility_hlcpp",
  ]
}

group("test_realms") {
  testonly = true

  deps = [
    ":root_presenter_scene",
    ":root_presenter_scene_with_input",
    ":scene_manager_scene_with_input",
    ":scenic_only",
  ]
}

fuchsia_component("scenic_only") {
  testonly = true

  component_name = "scenic_only"
  manifest = "meta/scenic_only.cml"
  deps = [
    "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider-cmv2-component",
    "//src/ui/scenic:component_v2_with_supplied_display_provider",
  ]
}

group("a11y_deps") {
  testonly = true

  deps = [
    "//src/ui/a11y/bin/a11y_manager:component_v2",
    "//src/ui/a11y/testing:fake-a11y-manager",
  ]
}

fuchsia_component("root_presenter_scene") {
  testonly = true

  component_name = "root_presenter_scene"
  manifest = "meta/root_presenter_scene.cml"
  deps = [
    "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider-cmv2-component",
    "//src/ui/bin/root_presenter:component_v2",
    "//src/ui/scenic:component_v2_with_supplied_display_provider",
    "//src/ui/testing/scene_provider:scene-provider-deps",
  ]
}

fuchsia_component("root_presenter_scene_with_input") {
  testonly = true

  component_name = "root_presenter_scene_with_input"
  manifest = "meta/root_presenter_scene_with_input.cml"
  deps = [
    "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider-cmv2-component",
    "//src/ui/bin/input-pipeline:component-for-test",
    "//src/ui/bin/root_presenter:component_v2",
    "//src/ui/bin/text:text_manager_comp",
    "//src/ui/scenic:component_v2_with_supplied_display_provider",
    "//src/ui/testing/input_helper:input-helper-component",
    "//src/ui/testing/scene_provider:scene-provider-deps",
  ]
}

fuchsia_component("scene_manager_scene_with_input") {
  testonly = true

  component_name = "scene_manager_scene_with_input"
  manifest = "meta/scene_manager_scene_with_input.cml"
  deps = [
    "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    "//src/lib/ui/input-synthesis:input-synthesis-component",
    "//src/ui/a11y/bin/a11y_manager:component_v2",
    "//src/ui/bin/hardware_display_controller_provider:fake-hardware-display-controller-provider-cmv2-component",
    "//src/ui/bin/root_presenter:virtual_keyboard_component_v2",
    "//src/ui/bin/scene_manager:scene_manager_component_for_test",
    "//src/ui/bin/shortcut:shortcut_manager",
    "//src/ui/bin/text:text_manager_comp",
    "//src/ui/scenic:component_v2_with_supplied_display_provider",
    "//src/ui/testing/input_helper:input-helper-component",
    "//src/ui/testing/scene_provider:scene-provider-deps",
  ]
}
