# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true

  deps = [
    ":mediabuttons-integration-tests",
    ":mediabuttons-integration-tests-ip",
  ]
}

executable("mediabuttons_listener_test") {
  testonly = true

  sources = [ "mediabuttons_listener_test.cc" ]

  deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.ui.input",
    "//sdk/fidl/fuchsia.hardware.display",
    "//sdk/fidl/fuchsia.ui.input",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/ui/base_view",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_component("mediabuttons-listener-test") {
  manifest = "meta/mediabuttons_listener_test.cmx"
  deps = [ ":mediabuttons_listener_test" ]
}

fuchsia_test_package("mediabuttons-integration-tests") {
  test_components = [ ":mediabuttons-listener-test" ]

  deps = [
    "//src/ui/bin/root_presenter:component_v1_for_test",
    "//src/ui/scenic:component",
  ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          device_type = "AEMU"
        }
      },
    ]
  }
}

executable("mediabuttons_listener_test_ip") {
  testonly = true
  sources = [ "mediabuttons_listener_test_ip.cc" ]
  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.input.injection",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/input/testing/fake_input_report_device",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_package("mediabuttons-integration-tests-ip") {
  manifest = "meta/mediabuttons_listener_test_ip.cmx"
  deps = [
    ":mediabuttons_listener_test_ip",
    "//src/ui/bin/input-pipeline:component-for-test",
    "//src/ui/bin/root_presenter:component_v1_for_test",
    "//src/ui/scenic:component",
  ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          device_type = "AEMU"
        }
      },
    ]
  }
}
