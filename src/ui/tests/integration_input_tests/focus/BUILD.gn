# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")

fidl("test.focus") {
  sdk_category = "internal"
  sources = [ "response_listener.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

executable("focus-input-test-bin") {
  testonly = true
  sources = [ "focus-input-test.cc" ]
  output_name = "focus-input-test"
  deps = [
    ":test.focus_hlcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_hlcpp",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_hlcpp",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.ui.focus:fuchsia.ui.focus_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/testing/ui_test_manager",
    "//src/ui/testing/util",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_test_component("focus-input-test-component") {
  manifest = "meta/focus-input-test.cml"
  deps = [ ":focus-input-test-bin" ]
}

fuchsia_test_package("focus-input-test") {
  test_components = [ ":focus-input-test-component" ]
  test_specs = {
    environments = [
      {
        dimensions = {
          # Ensure the device has Vulkan.
          device_type = "AEMU"
        }
      },
    ]
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":focus-input-test",

    # The deps below are necessary on minimal product configs.
    "//src/graphics/bin/vulkan_loader",
  ]
}
