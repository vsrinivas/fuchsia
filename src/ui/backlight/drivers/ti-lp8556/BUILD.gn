# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("ti-lp8556-bind") {
  rules = "ti-lp8556.bind"
  header_output = "ti-lp8556-bind.h"
  bind_output = "ti-lp8556.bindbc"
  tests = "ti-lp8556-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.ti.platform" ]
}

fuchsia_driver("ti-lp8556-driver") {
  output_name = "ti-lp8556"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "ti-lp8556.cc" ]
  deps = [
    ":ti-lp8556-bind",
    "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_llcpp",
    "//sdk/fidl/fuchsia.hardware.power.sensor:fuchsia.hardware.power.sensor_llcpp",
    "//sdk/lib/fidl",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/85473): This target uses |fidl::WireTryDispatch| which is deprecated.
  # Please remove ad-hoc composition of FIDL protocols and migrate to |fidl::WireDispatch|.
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-wire-try-dispatch" ]
}

fuchsia_driver_component("ti-lp8556") {
  component_name = "ti-lp8556"
  deps = [ ":ti-lp8556-driver" ]
  info = "ti-lp8556-info.json"
  colocate = true
}

test("ti-lp8556-test") {
  output_name = "ti-lp8556-test"
  sources = [
    "ti-lp8556-test.cc",
    "ti-lp8556.cc",
  ]
  deps = [
    ":ti-lp8556-bind",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_llcpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_llcpp",
    "//sdk/fidl/fuchsia.hardware.power.sensor:fuchsia.hardware.power.sensor_llcpp",
    "//sdk/lib/fidl",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/lib/mmio",
    "//src/devices/testing/mock-ddk",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/85473): This target uses |fidl::WireTryDispatch| which is deprecated.
  # Please remove ad-hoc composition of FIDL protocols and migrate to |fidl::WireDispatch|.
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-wire-try-dispatch" ]
}

fuchsia_unittest_package("ti-lp8556-test-package") {
  package_name = "ti-lp8556-test"
  deps = [ ":ti-lp8556-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":ti-lp8556-bind_test",
    ":ti-lp8556-test-package",
  ]
}
