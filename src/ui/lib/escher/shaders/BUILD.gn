# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/ui/lib/escher/shaders/shaders.gni")

assert(is_linux,
       "shaders need only be copied to the build directory for linux tests")

# Copies shaders needed for escher linux tests to a specific test data
# directory near the test binary.
group_deps = []
test_data = []
foreach(shader, escher_shaders_resources) {
  name = get_path_info(shader.path, "file")
  copy_name = "copy_$name"
  copy(copy_name) {
    sources = [ shader.path ]
    outputs = [ "$root_out_dir/test_data/escher/" + shader.dest ]
  }
  group_deps += [ ":$copy_name" ]
  test_data += get_target_outputs(":$copy_name")
}

foreach(shader, escher_shaders_spirv_resources) {
  name = get_path_info(shader.path, "file")
  copy_name = "copy_$name"
  copy(copy_name) {
    sources = [ shader.path ]
    outputs = [ "$root_out_dir/test_data/escher/" + shader.dest ]
  }
  group_deps += [ ":$copy_name" ]
  test_data += get_target_outputs(":$copy_name")
}

group("linux_shader_data") {
  deps = group_deps
  metadata = {
    test_runtime_deps = test_data
  }
}
