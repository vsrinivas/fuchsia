# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":a11y-tests" ]
}

executable("a11y_manager_apptests") {
  output_name = "a11y_manager_apptests"

  testonly = true

  sources = [ "app_unittest.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.accessibility:fuchsia.accessibility_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/json_parser",
    "//src/lib/testing/loop_fixture",
    "//src/ui/a11y/bin/a11y_manager:lib",
    "//src/ui/a11y/bin/a11y_manager/tests/mocks",
    "//src/ui/a11y/bin/a11y_manager/tests/util",
    "//src/ui/a11y/lib/annotation/tests/mocks",
    "//src/ui/a11y/lib/magnifier/tests/mocks:mock_magnification_handler",
    "//src/ui/a11y/lib/screen_reader/tests/mocks",
    "//src/ui/a11y/lib/semantics/tests/mocks",
    "//src/ui/a11y/lib/testing:input",
    "//src/ui/a11y/lib/util",
    "//src/ui/a11y/lib/util/tests/mocks",
    "//src/ui/a11y/lib/view/tests/mocks",
    "//third_party/glm",
    "//third_party/rapidjson",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

resource("l10n_en_resource") {
  deps = [ "//src/ui/a11y/lib/screen_reader/i18n:l10n_to_json_en" ]
  sources = [ "$target_gen_dir/../../../lib/screen_reader/i18n/en/l10n.json" ]
  outputs = [ "data/assets/locales/en/l10n.json" ]
}

fuchsia_unittest_package("a11y-tests") {
  manifest = "meta/a11y_manager_apptests.cml"
  deps = [
    ":a11y_manager_apptests",
    ":l10n_en_resource",

    # Fuchsia's ICU does not have libicudata.so, and the locale data MUST
    # be loaded from a file instead.
    "//src/intl:icudtl",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
