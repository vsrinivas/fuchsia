# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/intl/intl_strings.gni")
import("//src/lib/icu/third_party_icu_headers.gni")

third_party_icu_headers("icu_headers") {
  headers = [
    "third_party/icu/source/i18n/unicode/msgfmt.h",
    "third_party/icu/source/common/unicode/ucnv.h",
  ]
}

group("resources") {
  public_deps = [
    ":l10n",
    ":l10n_l10n_assets",
  ]
}

source_set("i18n") {
  sources = [
    "message_formatter.cc",
    "message_formatter.h",
    "messages.h",
  ]

  public_deps = [
    ":fuchsia.intl.l10n_hlcpp",
    "${icu_root}:icu",
    "//src/lib/intl/lookup/cpp:lib",
  ]

  deps = [
    ":icu_headers",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

intl_strings("l10n") {
  source = "strings.xml"
  source_locale = "en"
  output_locales = [
    "ar-XB",
    "da",
    "de",
    "de-CH",
    "en",
    "en-GB",
    "en-XA",
    "en-XC",
    "es",
    "es-419",
    "fr",
    "fr-CA",
    "it",
    "ja",
    "ko",
    "nb",
    "nl",
    "no",
    "sv",
  ]
  library = "fuchsia.intl.l10n"
}
