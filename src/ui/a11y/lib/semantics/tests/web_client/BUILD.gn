# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

executable("bin") {
  testonly = true
  output_name = "web_client_bin"
  sources = [ "web_client.cc" ]
  deps = [
    ":web_client_config_lib",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.web:fuchsia.web_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_network",
    "//src/chromium/web_engine/meta/shards:web_engine_view",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/json_parser",
    "//third_party/rapidjson",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

# Required for structured config.
fuchsia_component_manifest("web_client_manifest") {
  testonly = true
  component_name = "semantics-test-web-client"
  manifest = "meta/web_client.cml"
}

fuchsia_structured_config_cpp_elf_lib("web_client_config_lib") {
  testonly = true
  cm_label = ":web_client_manifest"
}

fuchsia_structured_config_values("web_client_default_config") {
  testonly = true
  cm_label = ":web_client_manifest"
  values = {
    html = ""
  }
}

fuchsia_component("component") {
  testonly = true
  cm_label = ":web_client_manifest"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  testonly = true
  package_name = "semantics-test-web-client"
  deps = [ ":component" ]
}
