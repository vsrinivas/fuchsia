# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  public_deps = [ ":magnifier_tests" ]
}

group("integration-tests") {
  testonly = true
  public_deps = [ ":magnification-pixeltest" ]
}

executable("magnifier_tests") {
  testonly = true

  sources = [
    "gfx_magnifier_delegate_unittest.cc",
    "magnifier2_unittest.cc",
    "magnifier_util_test.cc",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/a11y/lib/gesture_manager/arena/tests/mocks",
    "//src/ui/a11y/lib/gesture_manager/tests/mocks",
    "//src/ui/a11y/lib/magnifier",
    "//src/ui/a11y/lib/magnifier/tests/mocks:mock_magnification_handler",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("magnification_pixeltest_bin") {
  testonly = true
  sources = [ "magnification_pixeltest.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.ui.accessibility.view:fuchsia.ui.accessibility.view_hlcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/testing/ui_test_manager",
    "//src/ui/testing/util",
    "//src/ui/testing/views",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_test_component("magnification-pixeltest-component") {
  component_name = "magnification-pixeltest-component"
  manifest = "meta/magnification_pixel_test.cml"
  deps = [ ":magnification_pixeltest_bin" ]
}

fuchsia_test_package("magnification-pixeltest") {
  test_components = [ ":magnification-pixeltest-component" ]
  test_specs = {
    log_settings = {
      # TODO(fxbug.dev/93081): Investigate flakes and remove allowing ERROR severity.
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          # Ensure the device has Vulkan.
          device_type = "AEMU"
        }
      },
    ]
  }
}
