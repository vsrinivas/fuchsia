# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/renamed_binary.gni")

# NOTE: whenever a new example is added, also add it to
# //src/ui/lib/escher/test:force_waterfall_to_build_on_host
group("escher") {
  deps = [
    ":escher_rainfall",
    ":escher_waterfall",
  ]
}

renamed_binary("waterfall_bin") {
  dest = "bin/escher_examples"
  source = "$root_out_dir/waterfall"
  source_deps = [ "waterfall" ]
}

fuchsia_package_with_single_component("escher_waterfall") {
  manifest = "meta/escher_examples.cmx"
  deps = [
    ":waterfall_bin",
    "//src/lib/vulkan:vulkan_validation_layers",
    "//src/lib/vulkan/swapchain:image_pipe_swapchain_fb_layer",
    "//src/ui/examples/escher/common/assets:escher_examples_assets",
    "//src/ui/lib/escher/shaders:escher_shaders_resources",
  ]
}

resource("flat_main.vert") {
  sources = [ "//src/ui/lib/escher/shaders/flatland/flat_main.vert" ]
  outputs = [ "data/shaders/flatland/flat_main.vert" ]
  visibility = [ ":*" ]
}

resource("flat_main.frag") {
  sources = [ "//src/ui/lib/escher/shaders/flatland/flat_main.frag" ]
  outputs = [ "data/shaders/flatland/flat_main.frag" ]
  visibility = [ ":*" ]
}

renamed_binary("rainfall_bin") {
  dest = "bin/escher_examples"
  source = "$root_out_dir/rainfall"
  source_deps = [ "rainfall" ]
}

fuchsia_package_with_single_component("escher_rainfall") {
  manifest = "meta/escher_examples.cmx"
  deps = [
    ":flat_main.frag",
    ":flat_main.vert",
    ":rainfall_bin",
    "//src/lib/vulkan:vulkan_validation_layers",
    "//src/lib/vulkan/swapchain:image_pipe_swapchain_fb_layer",
  ]
}
