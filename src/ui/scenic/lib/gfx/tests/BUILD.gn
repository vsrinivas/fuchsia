# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//src/ui/lib/escher/build_args.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":apptests",
    ":delegating_frame_scheduler_integration_tests",
    ":pixeltests",
    ":swapchain_tests",
    ":unittests",
    ":viewstate_apptests",
    "//garnet/packages/examples:vkcube-on-scenic",
    "//src/ui/bin/hardware_display_controller_provider",
    "//src/ui/examples:bouncing_ball",
  ]
}

source_set("gfx_tests_main") {
  testonly = true
  sources = [ "run_all_unittests.cc" ]
  deps = [ "//src/ui/lib/escher/test/common:gtest_escher" ]
  public_deps = [ "//third_party/googletest:gtest" ]
}

source_set("testing_deps") {
  testonly = true
  sources = [
    "error_reporting_test.cc",
    "error_reporting_test.h",
    "gfx_test.cc",
    "gfx_test.h",
    "session_handler_test.cc",
    "session_handler_test.h",
    "session_test.cc",
    "session_test.h",
    "view_tree_session_test.cc",
    "view_tree_session_test.h",
    "vk_session_handler_test.cc",
    "vk_session_handler_test.h",
    "vk_session_test.cc",
    "vk_session_test.h",
    "vk_util.cc",
    "vk_util.h",
  ]
  public_deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.ui.scenic",
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.ui.gfx",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.scenic.internal",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/ui/scenic/cpp",
    "//src/ui/bin/root_presenter:safe_presenter",
    "//src/ui/lib/escher",
    "//src/ui/lib/escher/test/common:gtest_escher",
    "//src/ui/lib/glm_workaround",
    "//src/ui/scenic/lib/display/tests:mocks",
    "//src/ui/scenic/lib/gfx",
    "//src/ui/scenic/lib/gfx:ids",
    "//src/ui/scenic/lib/gfx/tests/mocks:util",
    "//src/ui/scenic/lib/scenic/tests:testing_deps",
    "//src/ui/scenic/lib/scenic/util",
    "//src/ui/scenic/lib/scheduling/tests/mocks:frame_scheduler_mocks",
    "//src/ui/scenic/lib/utils",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/inspect",
  ]
}

source_set("pixel_test") {
  testonly = true

  public = [ "pixel_test.h" ]

  sources = [ "pixel_test.cc" ]

  public_deps = [
    "//sdk/fidl/fuchsia.ui.annotation",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/sys/cpp/testing:integration",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/ui/base_view",
    "//src/ui/testing/views",
    "//zircon/public/lib/zx",
  ]

  deps = [
    "//sdk/fidl/fuchsia.ui.gfx",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/lib/syslog/cpp",
    "//third_party/googletest:gtest",
  ]
}

source_set("magnification_pixeltests") {
  testonly = true

  sources = [ "magnification_pixeltest.cc" ]
  deps = [
    ":pixel_test",
    "//sdk/fidl/fuchsia.accessibility",
    "//sdk/lib/fidl/cpp",
    "//src/lib/files",
    "//src/ui/a11y/lib/magnifier/tests/mocks:mock_magnifier",
    "//src/ui/testing/views",
    "//third_party/googletest:gtest",
  ]
}

source_set("scenic_pixeltests") {
  testonly = true

  sources = [ "scenic_pixeltest.cc" ]
  include_dirs = [ "//src/ui/lib/escher" ]
  deps = [
    ":gfx_tests_main",
    ":pixel_test",
    ":testing_deps",
    "//sdk/fidl/fuchsia.images",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl",
    "//src/ui/lib/escher",
    "//src/ui/lib/glm_workaround",
    "//src/ui/lib/yuv",
    "//src/ui/testing/views",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
  ]
}

executable("unittests") {
  output_name = "gfx_unittests"

  testonly = true
  sources = [
    "annotation_manager_unittest.cc",
    "camera_unittest.cc",
    "compositor_unittest.cc",
    "dump_visitor_unittest.cc",
    "escher_vulkan_smoke_test.cc",
    "focus_chain_unittest.cc",
    "focus_transfer_unittest.cc",
    "gfx_command_applier_unittest.cc",
    "hardware_layer_assignment_unittest.cc",
    "has_renderable_content_visitor_unittest.cc",
    "hittest_accumulator_unittest.cc",
    "hittest_global_unittest.cc",
    "host_image_unittest.cc",
    "id_unittest.cc",
    "image_pipe2_unittest.cc",
    "image_pipe_render_unittest.cc",
    "image_pipe_unittest.cc",
    "image_pipe_unittest_common.cc",
    "image_pipe_updater_unittest.cc",
    "memory_unittest.cc",
    "node_unittest.cc",
    "object_linker_unittest.cc",
    "pose_buffer_unit_test.cc",
    "protected_memory_visitor_unittest.cc",
    "scene_graph_unittest.cc",
    "scene_unittest.cc",
    "session_manager_unittest.cc",
    "session_unittest.cc",
    "shape_unittest.cc",
    "size_change_hint_unittest.cc",
    "snapshotter_unittest.cc",
    "stereo_camera_unittest.cc",
    "view_clipping_unittest.cc",
    "view_ref_installed_impl_unittest.cc",
    "view_tree_unittest.cc",
    "view_unittest.cc",
  ]
  deps = [
    ":gfx_tests_main",
    ":testing_deps",
    "//src/lib/cobalt/cpp/testing:mock_cobalt_logger",
    "//src/lib/fsl",
    "//src/ui/lib/glm_workaround",
    "//src/ui/scenic/lib/gfx:object_linker",
    "//src/ui/scenic/lib/input",
    "//src/ui/scenic/lib/scheduling:id",
    "//third_party/googletest:gmock",
  ]

  include_dirs = [ "//src/ui/lib/escher" ]
}

executable("swapchain_tests") {
  output_name = "gfx_swapchain_tests"
  testonly = true
  sources = [
    "display_swapchain_mock_test.cc",
    "display_swapchain_test.cc",
  ]
  deps = [
    ":testing_deps",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/lib/display:hdcp_client",
    "//src/ui/lib/display:hdcp_service",
  ]
  include_dirs = [ "//src/ui/lib/escher" ]
}

executable("delegating_frame_scheduler_integration_tests") {
  output_name = "gfx_frame_scheduler_integration_tests"
  testonly = true
  sources = [ "delegating_frame_scheduler_integration_test.cc" ]
  deps = [
    ":testing_deps",
    "//src/lib/fxl/test:gtest_main",
  ]
  include_dirs = [ "//src/ui/lib/escher" ]
}

executable("apptests") {
  output_name = "gfx_apptests"

  testonly = true
  sources = [ "gfx_apptest.cc" ]
  deps = [
    ":gfx_tests_main",
    ":testing_deps",
  ]
}

executable("pixeltests") {
  output_name = "gfx_pixeltests"

  testonly = true
  deps = [
    ":magnification_pixeltests",
    ":scenic_pixeltests",
  ]
}

executable("viewstate_apptests") {
  output_name = "gfx_viewstate_apptests"

  testonly = true
  sources = [ "gfx_viewstate_apptest.cc" ]
  include_dirs = [ "//src/ui/lib/escher" ]
  deps = [
    ":gfx_tests_main",
    ":pixel_test",
    ":testing_deps",
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/sys/cpp/testing:integration",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl",
    "//src/lib/ui/base_view",
    "//src/ui/lib/glm_workaround",
    "//src/ui/testing/views",
  ]
}

executable("mock_pose_buffer_provider_cc") {
  output_name = "mock_pose_buffer_provider"

  sources = [
    "mock_pose_buffer_provider.cc",
    "mock_pose_buffer_provider.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.ui.gfx",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//src/ui/lib/glm_workaround",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

package("mock_pose_buffer_provider") {
  meta = [
    {
      path = "meta/mock_pose_buffer_provider.cmx"
      dest = "mock_pose_buffer_provider.cmx"
    },
  ]

  deps = [ ":mock_pose_buffer_provider_cc" ]

  binaries = [
    {
      name = "mock_pose_buffer_provider"
    },
  ]
}
