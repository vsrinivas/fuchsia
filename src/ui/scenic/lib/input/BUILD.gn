# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("input") {
  public_deps = [
    ":constants",
    ":gesture_contender_interface",
    ":injection",
    ":legacy",
    ":mouse",
    ":system",
    ":touch",
    ":utils",
  ]
}

source_set("system") {
  sources = [
    "input_system.cc",
    "input_system.h",
  ]

  deps = [
    ":injection",
    ":legacy",
    ":mouse",
    ":touch",
    ":utils",
    "//src/ui/scenic/lib/gfx",
  ]

  public_deps = [ "//src/ui/scenic/lib/view_tree:view_tree_snapshot" ]
}

source_set("constants") {
  sources = [
    "constants.cc",
    "constants.h",
  ]

  deps = [ "//src/lib/files:files" ]
}

source_set("utils") {
  sources = [
    "helper.cc",
    "helper.h",
    "hit_tester.cc",
    "hit_tester.h",
    "internal_pointer_event.h",
    "stream_id.cc",
    "stream_id.h",
  ]

  deps = [
    "//src/ui/scenic/lib/utils",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/trace",
  ]

  public_deps = [ "//src/ui/scenic/lib/view_tree:view_tree_snapshot" ]
}

source_set("gesture_contender_interface") {
  sources = [
    "gesture_contender.h",
    "gesture_contender_inspector.cc",
    "gesture_contender_inspector.h",
  ]

  public_deps = [
    ":utils",
    "//src/ui/scenic/lib/view_tree:view_tree_snapshot",
    "//zircon/system/ulib/inspect",
  ]
}

source_set("legacy") {
  sources = [
    "a11y_legacy_contender.cc",
    "a11y_legacy_contender.h",
    "a11y_registry.cc",
    "a11y_registry.h",
    "gfx_legacy_contender.cc",
    "gfx_legacy_contender.h",
  ]

  deps = [
    ":gesture_contender_interface",
    "//src/ui/scenic/lib/gfx",
    "//src/ui/scenic/lib/gfx:ids",
    "//src/ui/scenic/lib/scenic",
    "//src/ui/scenic/lib/scheduling:id",
    "//src/ui/scenic/lib/utils",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/trace",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.ui.policy:fuchsia.ui.policy_hlcpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//src/lib/fostr/fidl/fuchsia.ui.input",
    "//src/lib/fostr/fidl/fuchsia.ui.input.accessibility",
  ]
}

source_set("injection") {
  sources = [
    "injector.cc",
    "injector.h",
    "mouse_injector.cc",
    "mouse_injector.h",
    "pointerinjector_registry.cc",
    "pointerinjector_registry.h",
    "touch_injector.cc",
    "touch_injector.h",
  ]

  deps = [
    ":constants",
    ":utils",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/trace",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.ui.pointerinjector:fuchsia.ui.pointerinjector_hlcpp",
    "//sdk/lib/sys/cpp:cpp",
    "//src/lib/fostr/fidl/fuchsia.ui.pointerinjector",
  ]
}

source_set("mouse") {
  sources = [
    "mouse_source.cc",
    "mouse_source.h",
    "mouse_source_base.cc",
    "mouse_source_base.h",
    "mouse_source_with_global_mouse.cc",
    "mouse_source_with_global_mouse.h",
    "mouse_system.cc",
    "mouse_system.h",
  ]

  deps = [
    ":constants",
    ":gesture_contender_interface",
    ":utils",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/trace",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.ui.pointer:fuchsia.ui.pointer_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointer.augment:fuchsia.ui.pointer.augment_hlcpp",
  ]
}

source_set("touch") {
  sources = [
    "gesture_arena.cc",
    "gesture_arena.h",
    "touch_source.cc",
    "touch_source.h",
    "touch_source_base.cc",
    "touch_source_base.h",
    "touch_source_with_local_hit.cc",
    "touch_source_with_local_hit.h",
    "touch_system.cc",
    "touch_system.h",
  ]

  deps = [
    ":constants",
    ":gesture_contender_interface",
    ":legacy",
    ":utils",
    "//sdk/lib/sys/cpp",
    "//src/ui/scenic/lib/gfx",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/trace",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.ui.pointer:fuchsia.ui.pointer_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointer.augment:fuchsia.ui.pointer.augment_hlcpp",
  ]
}
