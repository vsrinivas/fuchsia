# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/ui/scenic/lib/flatland/engine/build_args.gni")

config("engine_config") {
  defines = []
  if (flatland_visual_debugging) {
    defines += [ "VISUAL_DEBUGGING_ENABLED" ]
  }
  if (flatland_disable_display_composition) {
    defines += [ "FLATLAND_DISABLE_DISPLAY_COMPOSITION" ]
  }
}

config("cpu_accessible_config") {
  defines = [ "CPU_ACCESSIBLE_VMO=1" ]
}

template("engine_library") {
  source_set(target_name) {
    forward_variables_from(invoker, "*")
    sources = [
      "color_converter.cc",
      "color_converter.h",
      "display_compositor.cc",
      "display_compositor.h",
      "engine.cc",
      "engine.h",
      "engine_types.cc",
      "engine_types.h",
      "release_fence_manager.cc",
      "release_fence_manager.h",
    ]

    public_deps = [
      "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_hlcpp",
      "//sdk/fidl/fuchsia.ui.display.color:fuchsia.ui.display.color_hlcpp",
      "//sdk/lib/sys/inspect/cpp",
      "//src/lib/fxl",
      "//src/ui/scenic/lib/display",
      "//src/ui/scenic/lib/display:display",
      "//src/ui/scenic/lib/flatland",
      "//src/ui/scenic/lib/flatland/renderer",
      "//src/ui/scenic/lib/scheduling:frame_scheduler_interface",
      "//src/ui/scenic/lib/view_tree:view_tree_snapshot",
    ]

    deps = [
      "//sdk/lib/fidl/cpp",
      "//sdk/lib/sys/cpp",
    ]

    public_configs = [ ":engine_config" ]
    if (make_cpu_accessible) {
      public_configs += [ ":cpu_accessible_config" ]
    }

    # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
    configs += [ "//build/config:Wno-conversion" ]
  }
}

# The default engine will not have render buffers be host-readable
# for performance.
engine_library("engine") {
  make_cpu_accessible = false
}

# For tests where we need the framebuffers to be host-readable so
# that we can inspect the pixels, use this build configuration.
engine_library("engine_host_accessible_framebuffers") {
  make_cpu_accessible = true
}
