# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/ui/scenic/lib/utils/build_args.gni")

config("logging_config") {
  defines = []
  if (flatland_verbose_logging) {
    defines += [ "USE_FLATLAND_VERBOSE_LOGGING" ]
  }
}

source_set("utils") {
  sources = [
    "dispatcher_holder.h",
    "helpers.cc",
    "helpers.h",
    "logging.h",
    "math.cc",
    "math.h",
    "metrics.h",
    "metrics_impl.cc",
    "metrics_impl.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.metrics:fuchsia.metrics_cpp",
    "//sdk/fidl/fuchsia.sysmem",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/fidl/cpp/contrib/connection:service_hub_connector",
    "//src/ui/scenic/lib/scheduling:frame_metrics_registry_cc",
    "//third_party/glm",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]

  deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/fidl/cpp:cpp_base",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//zircon/system/ulib/zx",
  ]

  public_configs = [ ":logging_config" ]
}
