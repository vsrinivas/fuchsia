# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//garnet/lib/magma/gnbuild/magma.gni")
import("//src/lib/vulkan/layers.gni")
import("//src/ui/lib/escher/build_args.gni")

group("scenic") {
  public_deps = [
    ":scenic_pkg",
  ]
}

group("tests") {
  testonly = true

  public_deps = [
    ":scenic_tests",
    "lib/gfx/tests:mock_pose_buffer_provider",
    "lib/gfx/tests/rotated_square_test",
  ]
}

test_package("scenic_tests") {
  deps = [
    "lib/display/tests",
    "lib/flatland/tests",
    "lib/gfx/tests",
    "lib/gfx/util/tests",
    "lib/input/tests",
    "lib/scenic/tests",
    "lib/scheduling/tests",
  ]

  tests = [
    {
      name = "display_unittests"
      environments = basic_envs
    },
    {
      name = "flatland_unittests"
      environments = basic_envs
    },
    {
      name = "gfx_apptests"
      environments = basic_envs
    },
    {
      name = "gfx_pixeltests"
      environments = magma_libvulkan_hardware_envs
    },
    {
      name = "gfx_unittests"
      environments = basic_envs + magma_libvulkan_hardware_envs
    },
    {
      name = "gfx_util_unittests"
      environments = basic_envs
    },
    {
      name = "gfx_viewstate_apptests"
      environments = magma_libvulkan_hardware_envs
    },
    {
      name = "input_unittests"
      environments = basic_envs
    },
    {
      name = "scenic_unittests"
      environments = basic_envs
    },
    {
      name = "scheduling_unittests"
      environments = basic_envs
    },
  ]

  public_deps = vulkan_validation_layers.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules
  resources = vulkan_validation_layers.resources
}

declare_args() {
  # Include the vulkan validation layers in scenic.
  scenic_enable_vulkan_validation = is_debug
}

# For migration purposes only.  To be moved to src/ui/scenic/bin:scenic
package("scenic_pkg") {
  package_name = "scenic"

  deps = [
    "bin",
  ]

  binaries = [
    {
      name = "scenic"
    },
  ]

  meta = [
    {
      path = rebase_path("bin/meta/scenic.cmx")
      dest = "scenic.cmx"
    },
  ]

  resources = []

  if (escher_use_runtime_glsl) {
    resources += [
      {
        path =
            "//src/ui/lib/escher/shaders/model_renderer/default_position.vert"
        dest = "shaders/model_renderer/default_position.vert"
      },
      {
        path = "//src/ui/lib/escher/shaders/model_renderer/main.frag"
        dest = "shaders/model_renderer/main.frag"
      },
      {
        path = "//src/ui/lib/escher/shaders/model_renderer/main.vert"
        dest = "shaders/model_renderer/main.vert"
      },
      {
        path = "//src/ui/lib/escher/shaders/model_renderer/shadow_map_generation.frag"
        dest = "shaders/model_renderer/shadow_map_generation.frag"
      },
      {
        path = "//src/ui/lib/escher/shaders/model_renderer/shadow_map_lighting.frag"
        dest = "shaders/model_renderer/shadow_map_lighting.frag"
      },
      {
        path = "//src/ui/lib/escher/shaders/model_renderer/wobble_position.vert"
        dest = "shaders/model_renderer/wobble_position.vert"
      },
      {
        path = "//src/ui/lib/escher/shaders/paper/common/use.glsl"
        dest = "shaders/paper/common/use.glsl"
      },
      {
        path = "//src/ui/lib/escher/shaders/paper/frag/main_ambient_light.frag"
        dest = "shaders/paper/frag/main_ambient_light.frag"
      },
      {
        path = "//src/ui/lib/escher/shaders/paper/frag/main_point_light.frag"
        dest = "shaders/paper/frag/main_point_light.frag"
      },
      {
        path = "//src/ui/lib/escher/shaders/paper/vert/compute_model_space_position.vert"
        dest = "shaders/paper/vert/compute_model_space_position.vert"
      },
      {
        path = "//src/ui/lib/escher/shaders/paper/vert/compute_world_space_position.vert"
        dest = "shaders/paper/vert/compute_world_space_position.vert"
      },
      {
        path = "//src/ui/lib/escher/shaders/paper/vert/main_shadow_volume_extrude.vert"
        dest = "shaders/paper/vert/main_shadow_volume_extrude.vert"
      },
      {
        path = "//src/ui/lib/escher/shaders/paper/vert/vertex_attributes.vert"
        dest = "shaders/paper/vert/vertex_attributes.vert"
      },
    ]
  } else {
    resources += [
      # Spirv shaders
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_model_renderer_main_frag17553292397499926694.spirv"
        dest =
            "shaders/shaders_model_renderer_main_frag17553292397499926694.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_model_renderer_main_frag8280587512758179706.spirv"
        dest =
            "shaders/shaders_model_renderer_main_frag8280587512758179706.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_model_renderer_main_vert11112688489391456647.spirv"
        dest =
            "shaders/shaders_model_renderer_main_vert11112688489391456647.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_model_renderer_main_vert17553292397499926694.spirv"
        dest =
            "shaders/shaders_model_renderer_main_vert17553292397499926694.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_model_renderer_main_vert4295183060635058569.spirv"
        dest =
            "shaders/shaders_model_renderer_main_vert4295183060635058569.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_model_renderer_main_vert8280587512758179706.spirv"
        dest =
            "shaders/shaders_model_renderer_main_vert8280587512758179706.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_paper_frag_main_ambient_light_frag17553292397499926694.spirv"
        dest = "shaders/shaders_paper_frag_main_ambient_light_frag17553292397499926694.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_paper_frag_main_point_light_frag11112688489391456647.spirv"
        dest = "shaders/shaders_paper_frag_main_point_light_frag11112688489391456647.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/shaders_paper_frag_main_point_light_frag4295183060635058569.spirv"
        dest = "shaders/shaders_paper_frag_main_point_light_frag4295183060635058569.spirv"
      },
      {
        path = "//src/ui/lib/escher/shaders/spirv/pose_buffer_latching_comp14695981039346656037.spirv"
        dest = "shaders/pose_buffer_latching_comp14695981039346656037.spirv"
      },
    ]
  }

  # If configured, include the vulkan validation layers.
  if (scenic_enable_vulkan_validation) {
    loadable_modules = vulkan_validation_layers.loadable_modules
    public_deps = vulkan_validation_layers.public_deps
    resources += vulkan_validation_layers.resources
  }
}
