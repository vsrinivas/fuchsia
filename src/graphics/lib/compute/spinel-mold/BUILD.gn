# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_staticlib.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")
import("../gn/build_rules.gni")

config("spinel-mold_config") {
  # Clients should include <mold/mold.h>, which includes "spinel_types.h".
  # Clients should also be able to include "spinel/spinel.h".
  include_dirs = [
    "include",
    "../spinel/include",
  ]
}

rustc_library("spinel-mold") {
  name = "spinel-mold"
  with_unit_tests = true
  version = "0.1.0"
  edition = "2018"
  features = [ "lib" ]
  deps = [ "//src/graphics/lib/compute/mold" ]
}

rustc_staticlib("spinel-mold-static") {
  name = "spinel-mold-static"
  with_unit_tests = true
  version = "0.1.0"
  edition = "2018"
  deps = [ "//src/graphics/lib/compute/mold" ]
}

group("spinel-mold-c") {
  deps = [ ":spinel-mold-static" ]
  public_configs = [ ":spinel-mold_config" ]
  if (is_fuchsia) {
    public_deps = [ "//zircon/public/lib/trace-engine" ]
  }
}

unittest_package("spinel-mold-tests") {
  deps = [ ":spinel-mold_test" ]
  tests = [
    {
      name = "spinel_mold_lib_test"
      environments = basic_envs
    },
  ]
}

unittest_package("spinel-mold-static-tests") {
  deps = [ ":spinel-mold-static_test" ]
  tests = [
    {
      name = "spinel_mold_static_staticlib_test"
      environments = basic_envs
    },
  ]
}

graphics_compute_unittests("mold_c_unittests") {
  sources = [ "tests/mold_c_unittests.cc" ]
  deps = [
    ":spinel-mold-c",
    "//src/graphics/lib/compute/spinel:spinel_headers",
  ]
}
