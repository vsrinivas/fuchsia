# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/board.gni")
import("//build/package.gni")
import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")

rustc_library("mold-next") {
  name = "mold-next"
  with_unit_tests = true
  version = "0.1.0"
  edition = "2018"
  deps = [
    "//src/graphics/lib/compute/surpass",
    "//third_party/rust_crates:rustc-hash",
  ]
  configs += [ ":mold-next-performance" ]
}

config("mold-next-performance") {
  rustflags = [ "-Copt-level=3" ]

  if (board_name == "chromebook-x64") {
    rustflags += [ "-Ctarget-cpu=skylake" ]
  }

  if (board_name == "astro" || board_name == "sherlock") {
    rustflags += [ "-Ctarget-cpu=cortex-a53" ]
  }
}

unittest_package("mold-next-tests") {
  deps = [ ":mold-next_test" ]
  tests = [
    {
      name = "mold_next_lib_test"
      environments = basic_envs
    },
  ]
}
