# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# These rules are only used by the Fuchsia platform build.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//third_party/vulkan_loader_and_validation_layers/layers/layers.gni")
import("build_settings.gni")

# A target providing access to Vulkan at compile time when added to deps.
# TODO(SPN-14): Change this to a more recent upstream.
graphics_compute_vulkan_loader_target =
    "//third_party/vulkan_loader_and_validation_layers:vulkan"

# Generate an executable for the graphics compute project.
# This also generates a Fuchsia package for it, using a default .cmx file
# that can be overriden by providing a "meta" argument.
#
# Expect all variables supported by the Fuchsia package() template.
#
template("graphics_compute_executable") {
  _binary_name = "bin_${target_name}"

  executable(_binary_name) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "loadable_modules",
                             "meta",
                             "resources",
                             "target_name",
                           ])
    output_name = invoker.target_name
  }

  package(target_name) {
    forward_variables_from(invoker, "*")
    if (!defined(deps)) {
      deps = []
    }
    deps += [ ":${_binary_name}" ]

    binary = target_name

    if (!defined(meta)) {
      meta = [
        {
          path =
              rebase_path("${graphics_compute_dir}/gn/meta/default_package.cmx")
          dest = "$target_name.cmx"
        },
      ]
    }
  }
}

# Generate a Vulkan-based executable for the graphics compute project.
# Compared to graphics_compute_executable(), this adds Vulkan dependencies
# automatically to the executable and its Fuchsia package.
#
template("graphics_compute_vulkan_executable") {
  graphics_compute_executable(target_name) {
    forward_variables_from(invoker, "*")
    if (!defined(deps)) {
      deps = []
    }
    deps += [ graphics_compute_vulkan_loader_target ]

    if (!defined(public_deps)) {
      public_deps = []
    }
    public_deps += vulkan_validation_layers.public_deps

    if (!defined(loadable_modules)) {
      loadable_modules = []
    }
    loadable_modules += vulkan_validation_layers.loadable_modules

    if (!defined(resources)) {
      resources = []
    }
    resources = vulkan_validation_layers.resources

    if (!defined(meta)) {
      meta = [
        {
          path =
              rebase_path("${graphics_compute_dir}/gn/meta/vulkan_package.cmx")
          dest = "$target_name.cmx"
        },
      ]
    }
  }
}

# NOTE: DO NOT CALL DIRECTLY, use graphics_compute_unittest_package() instead.
#
# Accepts the following variables:
#
#
template("graphics_compute_unittest_package_rule") {
  # Each dependency listed in |tests| is a source_set, so create an executable
  # target that links them to gtest_main, then a package for it.
  _binary_target = "bin_${target_name}"
  _test_target_name = target_name

  executable(_binary_target) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "deps",
                             "meta",
                             "target_name",
                             "tests",
                           ])
    if (!defined(deps)) {
      deps = []
    }
    deps += invoker.tests
    deps += [
      # This version sets up logging appropriately for Fuchsia on startup.
      "//src/lib/fxl/test:gtest_main",
    ]
    output_name = _test_target_name
    testonly = true
  }

  # Generate a manifest
  _test_manifest = "${target_gen_dir}/meta/${_test_target_name}.cmx"
  _test_manifest_target = "${_test_target_name}__manifest"
  generated_file(_test_manifest_target) {
    outputs = [
      _test_manifest,
    ]
    contents = {
      program = {
        binary = "test/${_test_target_name}"
      }
      sandbox = {
        features = [ "system-temp" ]
        services = [ "fuchsia.logger.LogSink" ]
      }
    }
    output_conversion = "json"
  }

  # Create a package now.
  test_package(target_name) {
    forward_variables_from(invoker,
                           [
                             "public_deps",
                             "package_name",
                             "meta",
                           ])
    deps = [
      ":${_binary_target}",
      ":${_test_manifest_target}",
    ]
    tests = [
      {
        name = _test_target_name
        manifest = _test_manifest
      },
    ]
  }
}
