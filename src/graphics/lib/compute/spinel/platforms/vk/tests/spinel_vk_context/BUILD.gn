# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//third_party/vulkan_loader_and_validation_layers/layers/layers.gni")

#
# spinel_vk_context: create and destroy a Spinel context
#
package_name = "spinel_vk_context"

executable("bin") {
  output_name = package_name
  sources = [
    "main.c",
  ]
  include_dirs = [
    "$root_gen_dir/src/graphics/lib/compute/spinel/platforms/vk",
  ]
  deps = [
    ":targets",
    "//src/graphics/lib/compute/common",
    "//src/graphics/lib/compute/common/vk",
    "//src/graphics/lib/compute/hotsort/platforms/vk",
    "//src/graphics/lib/compute/spinel/platforms/vk",
    "//third_party/vulkan_loader_and_validation_layers:vulkan",
  ]
}

#
# generate and build Spinel targets
#
group("targets") {
  public_deps = [
    "//src/graphics/lib/compute/spinel/platforms/vk/targets/vendors/amd/gcn3:generate",
    "//src/graphics/lib/compute/spinel/platforms/vk/targets/vendors/intel/gen8:generate",
    "//src/graphics/lib/compute/spinel/platforms/vk/targets/vendors/nvidia/sm50:generate",

    "//src/graphics/lib/compute/spinel/platforms/vk/targets/vendors/amd/gcn3/hotsort",
    "//src/graphics/lib/compute/spinel/platforms/vk/targets/vendors/intel/gen8/hotsort",
    "//src/graphics/lib/compute/spinel/platforms/vk/targets/vendors/nvidia/sm50/hotsort",
  ]
}

#
# define the package
#
package(package_name) {
  deps = [
    ":bin",
    "//third_party/vulkan_loader_and_validation_layers/layers",
  ]

  binary = package_name

  meta = [
    {
      path = rebase_path("meta/$package_name.cmx")
      dest = "$package_name.cmx"
    },
  ]

  public_deps = vulkan_validation_layers.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules
  resources = vulkan_validation_layers.resources
}
