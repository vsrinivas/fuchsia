# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("integration") {
  testonly = true

  public_deps = [
    ":linux_x64",
    ":magma_conformance_tests",
    ":magma_conformance_tests_hermetic",
  ]
}

group("linux_x64") {
  testonly = true
  public_deps = [
    ":virtmagma_conformance_tests(//build/toolchain:linux_x64)",
    ":virtmagma_no_cpp_test(//build/toolchain:linux_x64)",
  ]
}

executable("magma_conformance_tests") {
  testonly = true

  sources = [
    "main.cc",
    "test_magma.cc",
    "test_magma_c.c",
    "test_magma_fidl.cc",
  ]

  deps = [
    "$magma_build_root/include:magma",
    "$magma_build_root/src/libmagma",
    "$magma_build_root/tests/helper:magma_map_cpu",
    "//src/graphics/drivers/msd-arm-mali/include",
    "//src/graphics/drivers/msd-intel-gen/include",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
      "//sdk/fidl/fuchsia.logger:fuchsia.logger_cpp_wire",
      "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
      "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_cpp_wire",
      "//sdk/lib/fdio",
      "//src/graphics/lib/magma/src/magma_util/platform:logger",
      "//src/graphics/lib/magma/src/magma_util/platform:trace_provider",
      "//zircon/system/ulib/async-loop:async-loop-cpp",
    ]
  }
}

executable("magma_conformance_tests_hermetic") {
  testonly = true

  defines = [ "MAGMA_HERMETIC=1" ]
  sources = [
    "main.cc",
    "test_magma.cc",
    "test_magma_c.c",
    "test_magma_fidl.cc",
  ]

  deps = [
    "$magma_build_root/include:magma",
    "$magma_build_root/src/libmagma:magma_hermetic",
    "$magma_build_root/tests/helper:magma_map_cpu",
    "//src/graphics/drivers/msd-arm-mali/include",
    "//src/graphics/drivers/msd-intel-gen/include",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
      "//sdk/fidl/fuchsia.logger:fuchsia.logger_cpp_wire",
      "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
      "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_cpp_wire",
      "//sdk/lib/fdio",
      "//src/graphics/lib/magma/src/magma_util/platform:logger",
      "//src/graphics/lib/magma/src/magma_util/platform:trace_provider",
      "//zircon/system/ulib/async-loop:async-loop-cpp",
    ]
  }
}

executable("virtmagma_conformance_tests") {
  testonly = true

  defines = [ "VIRTMAGMA=1" ]

  sources = [
    "main.cc",
    "test_magma.cc",
    "test_magma_c.c",
  ]

  deps = [
    "$magma_build_root/include:magma",
    "$magma_build_root/src/libmagma_linux",
    "$magma_build_root/tests/helper:magma_map_cpu",
    "//src/graphics/drivers/msd-arm-mali/include",
    "//src/graphics/drivers/msd-intel-gen/include",
    "//src/lib/fxl",
    "//third_party/googletest:gtest",
  ]
}

executable("virtmagma_no_cpp_test") {
  testonly = true

  # TODO(fxbug.dev/89257) - use configs to set -no-stdlib++ in a way that doesn't
  # conflict with -static-libstdc++.
  ldflags = [
    "-nostdlib++",
    "-Wno-unused-command-line-argument",
  ]

  sources = [
    "main.c",
    "test_magma_c.c",
  ]

  deps = [
    "$magma_build_root/include:magma",
    "$magma_build_root/src/libmagma_linux",
  ]

  disable_syslog_backend = true

  # Builds fail
  exclude_toolchain_tags = [ "asan" ]
}
