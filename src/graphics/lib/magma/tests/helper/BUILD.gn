# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

source_set("platform_device_helper") {
  public_configs = [ "$magma_build_root:magma_tests_include_config" ]

  public_deps = [
    "$magma_build_root/src/magma_util:macros",
    "$magma_build_root/src/magma_util/platform:device",
  ]

  sources = [
    "platform_device_helper.cc",
    "platform_device_helper.h",
  ]
}

source_set("command_buffer_helper") {
  public_configs = [ "$magma_build_root:magma_tests_include_config" ]

  public_deps = [ "$magma_build_root/src/magma_util/platform:device" ]

  sources = [ "command_buffer_helper.h" ]

  deps = [ "$magma_build_root/src/sys_driver" ]
}

source_set("test_device_helper") {
  testonly = true
  public_configs = [ "//src/graphics/lib/magma:magma_tests_include_config" ]

  public_deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp_wire",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/component/cpp",
    "//src/graphics/lib/magma/include:magma",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/zx",
  ]

  sources = [ "test_device_helper.h" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

source_set("logger_init_helper") {
  testonly = true
  sources = [ "logger_init_helper.cc" ]
  deps = [
    "$magma_build_root/src/magma_util/platform:logger_impl",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/zx",
  ]
}

source_set("inflight_list") {
  testonly = true

  public_configs = [ "//src/graphics/lib/magma:magma_tests_include_config" ]

  sources = [ "inflight_list.h" ]

  public_deps = [
    "$magma_build_root/include:magma",
    "$magma_build_root/src/magma_util:macros",
    "$magma_build_root/src/magma_util:status",
  ]
}

source_set("magma_map_cpu") {
  testonly = true

  public_configs = [ "//src/graphics/lib/magma:magma_tests_include_config" ]

  sources = [
    "magma_map_cpu.cc",
    "magma_map_cpu.h",
  ]

  public_deps = [ "$magma_build_root/include:magma" ]

  if (is_fuchsia) {
    deps = [ "//zircon/system/ulib/zx" ]
  }
}
