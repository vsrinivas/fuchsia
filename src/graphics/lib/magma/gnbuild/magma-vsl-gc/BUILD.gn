# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/driver_package.gni")
import("//build/test/test_package.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("magma-vulkan") {
  if (!magma_enable_developer_build) {
    public_deps = [ ":magma-vsl-gc" ]
    if (build_libvulkan_vsl_gc != []) {
      public_deps += [ ":magma-vsl-gc-config" ]
    }
  }
}

if (magma_enable_developer_build) {
  group("magma-dev") {
    testonly = true
    public_deps = [ ":magma-vsl-gc" ]
    if (build_libvulkan_vsl_gc != []) {
      public_deps += [ ":magma-vsl-gc-config" ]
    }
    deps = [ "//src/graphics/lib/magma/tests/unit_tests:testing_firmware_file" ]
  }
}

driver_package("magma-vsl-gc") {
  if (magma_enable_developer_build) {
    testonly = true
  }

  deps = []

  if (magma_enable_developer_build) {
    deps += [ "//src/graphics/drivers/msd-vsl-gc:msd_vsl_test" ]
    drivers = [
      {
        name = "libmsd_vsl_test.so"
      },
    ]
  } else {
    deps += [ "//src/graphics/drivers/msd-vsl-gc:msd_vsl" ]
    drivers = [
      {
        name = "libmsd_vsl.so"
      },
    ]
  }

  resources = []
  libraries = []

  if (build_libvulkan_vsl_gc != []) {
    foreach(entry, build_libvulkan_vsl_gc) {
      icd_name = get_label_info(entry, "name")
      deps += [ entry ]

      libraries += [
        {
          name = "$icd_name.so"
        },
      ]
    }
  }
}

if (build_libvulkan_vsl_gc != []) {
  icd_config_data("magma-vsl-gc-config") {
    icds = []
    foreach(entry, build_libvulkan_vsl_gc) {
      icd_name = get_label_info(entry, "name")
      filename = "$target_gen_dir/vulkan/icd.d/$icd_name.json"
      icds += [
        {
          lib = "${icd_name}.so"
          manifest = filename
        },
      ]
    }
  }
}

test_package("msd_vsl_gc_nonhardware_tests") {
  deps = [ "//src/graphics/drivers/msd-vsl-gc:tests" ]

  tests = [
    {
      name = "msd_vsl_gc_nonhardware_tests"
    },
  ]
}
