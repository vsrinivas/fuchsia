# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cipd.gni")
import("//build/config.gni")
import("//build/dist/resource.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")
import("//src/sys/build/components.gni")

# Only including packages built inside this file.
have_libvulkan_img =
    build_libvulkan_img_rgx != [] || have_prebuilt_libvulkan_img

group("magma-vulkan") {
  if (!magma_enable_developer_build) {
    public_deps = [
      ":magma-img-rgx-mt8167",
      ":magma-img-rgx-mt8167-firmware",
    ]
    if (have_libvulkan_img) {
      public_deps += [ ":magma-img-rgx-mt8167-config" ]
    }
  }
}

if (magma_enable_developer_build) {
  group("magma-dev") {
    testonly = true
    public_deps = [
      ":magma-img-rgx-mt8167",
      ":magma-img-rgx-mt8167-firmware",
    ]
    if (have_libvulkan_img) {
      public_deps += [ ":magma-img-rgx-mt8167-config" ]
    }
    deps = [ "//src/graphics/lib/magma/tests/unit_tests:testing_firmware_file" ]
  }
}

group("magma-vulkan-no-hardware") {
  testonly = true

  # Without internal access (and the firmware) the driver will fail to load.
  if (internal_access) {
    public_deps = [
      ":magma-img-rgx-mt8167-firmware",
      ":magma-img-rgx-no-hardware",
    ]
  }
}

fuchsia_package("magma-img-rgx-mt8167-firmware") {
  # internal access is needed to get the firmware.
  if (internal_access) {
    deps = [ ":firmware" ]
  }
}

if (have_prebuilt_libvulkan_img && build_libvulkan_img_rgx == []) {
  resource("prebuilt_libvulkan_img_rgx") {
    sources = [ prebuilt_libvulkan_img_path ]
    outputs = [ "lib/libvulkan_img_rgx_mt8167.so" ]
  }
}

fuchsia_package("magma-img-rgx-mt8167") {
  deps = [ "//src/graphics/drivers/msd-img-rgx/mtk:msd_img_rgx_mt8167" ]

  if (magma_enable_developer_build) {
    testonly = true
  }

  if (build_libvulkan_img_rgx != []) {
    deps += build_libvulkan_img_rgx
  } else if (have_prebuilt_libvulkan_img) {
    deps += [ ":prebuilt_libvulkan_img_rgx" ]
  }
}

fuchsia_package("magma-img-rgx-no-hardware") {
  deps = [
    "//src/graphics/drivers/msd-img-rgx/no_hardware:msd_img_rgx_no_hardware",
  ]
}

if (have_libvulkan_img) {
  icd_config_data("magma-img-rgx-mt8167-config") {
    icds = [
      {
        lib = "libvulkan_img_rgx_mt8167.so"
        manifest = "$target_gen_dir/vulkan/icd.d/libvulkan_img_rgx_mt8167.json"
      },
    ]
  }
}

resource("firmware") {
  sources =
      [ "//src/graphics/drivers/msd-img-rgx/mtk/firmware/rgx.fw.22.40.54.30" ]
  outputs = [ "lib/firmware/rgx.fw.22.40.54.30" ]
}
