# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/driver_package.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("magma-vulkan") {
  if (!magma_enable_developer_build) {
    public_deps = [ ":magma-qcom-adreno" ]
    if (build_libvulkan_qcom_adreno != []) {
      public_deps += [ ":magma-qcom-adreno-config" ]
    }
  }
}

if (magma_enable_developer_build) {
  group("magma-dev") {
    testonly = true
    public_deps = [ ":magma-qcom-adreno" ]
    if (build_libvulkan_qcom_adreno != []) {
      public_deps += [ ":magma-qcom-adreno-config" ]
    }
    deps = [ "//src/graphics/lib/magma/tests/unit_tests:testing_firmware_file" ]
  }
}

driver_package("magma-qcom-adreno") {
  if (magma_enable_developer_build) {
    testonly = true
  }

  deps = []

  if (build_libvulkan_qcom_adreno != []) {
    deps += build_libvulkan_qcom_adreno
    libraries = [
      {
        name = "libvulkan_adreno.so"
      },
    ]
  }

  if (magma_enable_developer_build) {
    deps += [ "//src/graphics/drivers/msd-qcom-adreno:msd_qcom_test" ]
    drivers = [
      {
        name = "libmsd_qcom_test.so"
      },
    ]
  } else {
    deps += [ "//src/graphics/drivers/msd-qcom-adreno:msd_qcom" ]
    drivers = [
      {
        name = "libmsd_qcom.so"
      },
    ]
  }
}

if (build_libvulkan_qcom_adreno != []) {
  icd_config_data("magma-qcom-adreno-config") {
    icds = [
      {
        lib = "libvulkan_adreno.so"
        manifest = "$target_gen_dir/vulkan/icd.d/libvulkan_adreno.json"
      },
    ]
  }
}
