# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("tools") {
  testonly = true
  deps = [
    ":magma-info",
    "mali-performance-counters",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":magma-info-test",
    "mali-performance-counters:mali-performance-counters-test",
  ]
}

group("tools-only") {
  deps = [ ":magma-info" ]
}

fuchsia_component("magma-info-cmp") {
  component_name = "magma-info"
  deps = [ ":magma-info-bin" ]
  manifest = "meta/magma-info.cml"
}

fuchsia_shell_package("magma-info") {
  deps = [ ":magma-info-bin" ]
}

fuchsia_test_package("magma-info-test") {
  test_components = [ ":magma-info-cmp" ]
  test_specs = {
    environments = magma_hardware_envs
  }
}

executable("magma-info-bin") {
  sources = [ "magma_info.cc" ]
  output_name = "magma-info"

  deps = [
    "$magma_build_root/include:magma",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
    "//sdk/lib/fdio",
    "//src/lib/fxl",
    "//src/zircon/lib/zircon",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}
