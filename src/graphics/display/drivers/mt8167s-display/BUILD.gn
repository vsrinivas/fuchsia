# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("mt8167s-display") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "aal.cc",
    "ccorr.cc",
    "color.cc",
    "disp-rdma.cc",
    "dither.cc",
    "gamma.cc",
    "lcd.cc",
    "mt-dsi-host.cc",
    "mt-sysconfig.cc",
    "mt8167s-display.cc",
    "ovl.cc",
  ]
  deps = [ ":common" ]
}

source_set("common") {
  visibility = [ ":*" ]
  sources = []
  public_deps = [
    "//sdk/banjo/ddk.protocol.composite",
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/device-protocol-pdev",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/image-format",
    "//zircon/public/lib/image-format-llcpp",
    "//zircon/public/lib/mipi-dsi",
    "//zircon/public/lib/mmio",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.display.controller",
    "//zircon/system/banjo/ddk.protocol.dsiimpl",
    "//zircon/system/banjo/ddk.protocol.gpio",
    "//zircon/system/banjo/ddk.protocol.platform.device",
    "//zircon/system/banjo/ddk.protocol.power",
    "//zircon/system/banjo/ddk.protocol.sysmem",
    "//zircon/system/dev/lib/device-protocol-platform-device",
    "//zircon/system/fidl/fuchsia-sysmem:llcpp",
  ]
}

test("dsi-host") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "aal.cc",
    "ccorr.cc",
    "color.cc",
    "disp-rdma.cc",
    "dither.cc",
    "gamma.cc",
    "lcd.cc",
    "mt-dsi-host.cc",
    "mt-sysconfig.cc",
    "mt8167s-display-test.cc",
    "mt8167s-display.cc",
    "ovl.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fake-bti",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/image-format-llcpp",
    "//zircon/public/lib/mock-mmio-reg",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/mock-sysmem",
    "//zircon/system/fidl/fuchsia-sysmem:llcpp",
  ]
}

migrated_manifest("mt8167s-display-manifest") {
  deps = [ ":mt8167s-display" ]
}

migrated_manifest("dsi-host-manifest") {
  deps = [ ":dsi-host" ]
}
