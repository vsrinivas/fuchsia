# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

source_set("common") {
  public_deps = [
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.i2cimpl",
    "//zircon/public/lib/amlogic",
    "//zircon/public/lib/boringssl",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/digest",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/image-format",
    "//zircon/public/lib/mmio",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.amlogiccanvas",
    "//zircon/system/banjo/ddk.protocol.display.controller",
    "//zircon/system/banjo/ddk.protocol.gpio",
    "//zircon/system/banjo/ddk.protocol.platform.device",
    "//zircon/system/banjo/ddk.protocol.sysmem",
    "//zircon/system/dev/audio/lib/simple-audio-stream",
    "//zircon/system/dev/lib/device-protocol-platform-device",
    "//zircon/system/fidl/fuchsia-sysmem:c",
    "//zircon/system/ulib/audio-driver-proto",
    "//zircon/system/ulib/audio-proto-utils",
    "//zircon/system/ulib/dispatcher-pool",
  ]
}

driver_module("vim-display") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "edid.cc",
    "hdmitx.cc",
    "hdmitx_clk.cc",
    "registers.cc",
    "vim-audio-utils.cc",
    "vim-audio.cc",
    "vim-display.cc",
    "vim-spdif-audio-stream.cc",
    "vpp.cc",
  ]

  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

test("vim-display-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "edid.cc",
    "hdmitx.cc",
    "hdmitx_clk.cc",
    "registers.cc",
    "vim-audio-utils.cc",
    "vim-audio.cc",
    "vim-display-test.cc",
    "vim-display.cc",
    "vim-spdif-audio-stream.cc",
    "vpp.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/mock-sysmem",
    "//zircon/system/fidl/fuchsia-sysmem:llcpp",
  ]
}

migrated_manifest("vim-display-manifest") {
  deps = [ ":vim-display" ]
}

migrated_manifest("vim-display-test-manifest") {
  deps = [ ":vim-display-test" ]
}
