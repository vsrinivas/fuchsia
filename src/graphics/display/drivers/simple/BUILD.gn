# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")
import("//src/graphics/display/drivers/args.gni")

source_set("shared") {
  visibility = [ ":*" ]
  sources = [ "simple-display.cc" ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_cpp_wire",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/pci/lib/pci",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

# Creates a Fuchsia driver assuming the driver's files start with the same
# naming scheme: "simple-${target_name}" where `target_name` is the name of the
# driver.
#
# This template assumes that the following files exist for the driver:
# * simple-${target_name}.bind - the driver's bind rules
# * ${target_name}_bind_tests.json - the driver's bind tests
# * simple-${target_name}.cc - the driver's C++ source code
# * simple.${target_name}-info.json - the driver's info
#
# Parameters
#
#   is_fallback (optional)
#     Whether or not the driver is a fallback driver
#     Type: boolean
#     Default: false
template("simple_fuchsia_driver") {
  driver_name = target_name
  driver_bind_rules("simple-${driver_name}-bind") {
    # TODO(fxbug.dev/104963): Remove the Intel-specific logic once the Tiger
    # Lake display driver is stable enough.
    if (driver_name == "intel") {
      if (enable_tiger_lake_display_driver) {
        rules = "simple-intel.bind"
        tests = "intel_bind_tests.json"
      } else {
        rules = "simple-intel-tgl-optout.bind"
        tests = "intel_bind_tgl_optout_tests.json"
      }
    } else {
      rules = "simple-${driver_name}.bind"
      tests = "${driver_name}_bind_tests.json"
    }

    header_output = "simple-${driver_name}-bind.h"
    deps = [
      "//src/devices/bind/fuchsia.hardware.sysmem",
      "//src/devices/bind/fuchsia.pci",
      "//src/devices/bind/fuchsia.sysmem",
    ]
  }

  fuchsia_driver("simple.${driver_name}-driver") {
    output_name = "simple.${driver_name}"
    configs += [
      "//build/config:all_source",
      "//build/config/fuchsia:enable_zircon_asserts",
    ]
    sources = [ "simple-${driver_name}.cc" ]
    deps = [
      ":shared",
      ":simple-${driver_name}-bind",
      "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
      "//src/devices/pci/lib/device-protocol-pci",
      "//src/devices/pci/lib/fuchsia.hardware.pci:fuchsia.hardware.pci_banjo_cpp",
      "//src/devices/pci/lib/pci",
      "//src/lib/ddk",
    ]
  }

  is_fallback = false
  if (defined(invoker.is_fallback)) {
    is_fallback = invoker.is_fallback
  }
  fuchsia_driver_component("simple.${driver_name}") {
    component_name = "simple.${driver_name}"
    deps = [ ":simple.${driver_name}-driver" ]
    info = "simple.${driver_name}-info.json"
    root_resource = true
    fallback = is_fallback
    colocate = false

    if (driver_name == "intel") {
      # TODO(fxbug.dev/93333): remove this once DFv2 is stabilised.
      default_dispatcher_opts = [ "allow_sync_calls" ]
    }
  }
}

simple_fuchsia_driver("amd-kaveri") {
  is_fallback = false
}
simple_fuchsia_driver("bochs") {
  is_fallback = false
}
simple_fuchsia_driver("gga") {
  is_fallback = false
}
simple_fuchsia_driver("intel") {
  is_fallback = true
}
simple_fuchsia_driver("nv") {
  is_fallback = false
}
simple_fuchsia_driver("vmware") {
  is_fallback = false
}

group("tests") {
  testonly = true
  deps = [
    ":simple-amd-kaveri-bind_test",
    ":simple-bochs-bind_test",
    ":simple-gga-bind_test",
    ":simple-intel-bind_test",
    ":simple-vmware-bind_test",
  ]
}
