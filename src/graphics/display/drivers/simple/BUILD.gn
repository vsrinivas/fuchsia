# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")

source_set("shared") {
  visibility = [ ":*" ]
  sources = [ "simple-display.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.display.controller",
    "//sdk/banjo/ddk.protocol.pci",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddk",
    "//src/lib/ddktl",
  ]
}

simple_drivers = [
  "bochs",
  "amd-kaveri",
  "nv",
  "intel",
  "vmware",
]

foreach(driver, simple_drivers) {
  driver_module("simple.$driver") {
    configs += [
      "//build/config:all_source",
      "//build/config/fuchsia:enable_zircon_asserts",
    ]
    configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
    configs += [
      "//build/config/fuchsia:static_cpp_standard_library",
      "//build/unification/config:zircon-migrated",
    ]
    sources = [ "simple-$driver.c" ]
    deps = [
      ":shared",
      "//sdk/banjo/ddk.protocol.display.controller",
      "//sdk/banjo/ddk.protocol.pci",
      "//src/devices/pci/lib/device-protocol-pci",
      "//src/lib/ddk",

      # TODO(fxb/38132): Migrate to the new bind rules and delete the below
      "//src/lib/ddk:ddk-deprecated-binding-headers",
    ]

    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    configs += [ "//build/config:Wno-conversion" ]
  }
}
