# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("goldfish-display") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "display.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  defines = [ "_ALL_SOURCE" ]
  sources = []
  public_deps = [
    "//sdk/banjo/ddk.protocol.goldfish.control",
    "//sdk/banjo/ddk.protocol.goldfish.pipe",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/trace-driver",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.display.controller",
    "//zircon/system/banjo/ddk.protocol.sysmem",
    "//zircon/system/fidl/fuchsia-sysmem:c",
  ]
}

test("goldfish-unittests") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "display-test.cc",
    "display.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/device-protocol-pdev",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zxtest",
    "//zircon/system/banjo/ddk.protocol.platform.device",
    "//zircon/system/dev/lib/device-protocol-platform-device",
  ]
}

migrated_manifest("goldfish-display-manifest") {
  deps = [ ":goldfish-display" ]
}

migrated_manifest("goldfish-unittests-manifest") {
  deps = [ ":goldfish-unittests" ]
}
