# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("goldfish-display") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "display.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  defines = [ "_ALL_SOURCE" ]
  sources = []
  public_deps = [
    "//sdk/banjo/ddk.protocol.display.controller",
    "//sdk/banjo/ddk.protocol.goldfish.control",
    "//sdk/banjo/ddk.protocol.goldfish.pipe",
    "//sdk/banjo/ddk.protocol.sysmem",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/trace-driver",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
  ]
}

test("goldfish-unittests") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "display-test.cc",
    "display.cc",
  ]
  deps = [
    ":common",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zxtest",
  ]
}

migrated_manifest("goldfish-display-manifest") {
  deps = [ ":goldfish-display" ]
}

group("tests") {
  testonly = true
  deps = [ ":goldfish-unittests-package" ]
}

unittest_package("goldfish-unittests-package") {
  package_name = "goldfish-unittests"
  deps = [ ":goldfish-unittests" ]

  tests = [
    {
      name = "goldfish-unittests"
      environments = basic_envs
    },
  ]
}
