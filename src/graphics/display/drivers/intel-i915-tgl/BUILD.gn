# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")
import("//src/graphics/display/drivers/args.gni")

driver_bind_rules("intel-i915-tgl-bind") {
  # TODO(fxbug.dev/104963): Remove the check and the else branch once the Tiger
  # Lake display driver is stable enough.
  if (enable_tiger_lake_display_driver) {
    rules = "intel-i915-tgl.bind"
    tests = "bind_tests.json"
  } else {
    rules = "intel-i915-tgl-optout.bind"
    tests = "bind_optout_tests.json"
  }

  header_output = "intel-i915-tgl-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.pci",
  ]
}

source_set("common_srcs") {
  configs += [ "//build/config:all_source" ]
  public_configs = [ "//build/config/fuchsia:enable_zircon_asserts" ]

  sources = [
    "acpi-memory-region.cc",
    "clock/cdclk.cc",
    "ddi-aux-channel.cc",
    "ddi-physical-layer-manager.cc",
    "ddi-physical-layer.cc",
    "ddi.cc",
    "display-device.cc",
    "dp-display.cc",
    "dpll.cc",
    "fuse-config.cc",
    "gtt.cc",
    "hdmi-display.cc",
    "igd.cc",
    "intel-i915-tgl.cc",
    "interrupts.cc",
    "pch-engine.cc",
    "pipe-manager.cc",
    "pipe.cc",
    "power-controller.cc",
    "power.cc",
  ]
  public_deps = [
    ":intel-i915-tgl-bind",
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.intelgpucore:fuchsia.hardware.intelgpucore_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire",
    "//sdk/lib/stdcompat",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/pci/lib/pci",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/edid",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/region-alloc",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}

fuchsia_driver("intel-i915-tgl-driver") {
  output_name = "intel-i915-tgl"
  deps = [ ":common_srcs" ]
}

fuchsia_driver_component("intel-i915-tgl") {
  component_name = "intel-i915-tgl"
  deps = [ ":intel-i915-tgl-driver" ]
  info = "intel-i915-tgl-info.json"
  root_resource = true
  uses_profiles = true
  uses_sysmem = true
}

test("intel-i915-tgl-test") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "acpi-memory-region-unittest.cc",
    "acpi-memory-region-util-unittest.cc",
    "ddi-aux-channel-unittest.cc",
    "ddi-physical-layer-manager-unittest.cc",
    "ddi-physical-layer-unittest.cc",
    "dp-capabilities-unittest.cc",
    "dp-display-unittest.cc",
    "fake-dpcd-channel.cc",
    "fake-dpcd-channel.h",
    "fuse-config-unittest.cc",
    "gtt-unittest.cc",
    "hardware-common-unittest.cc",
    "hdmi-display-unittest.cc",
    "intel-i915-tgl-test.cc",
    "interrupts-unittest.cc",
    "mock-mmio-range-unittest.cc",
    "pch-engine-unittest.cc",
    "pipe-manager-unittest.cc",
    "pipe-unittest.cc",
    "poll-until-unittest.cc",
    "power-controller-unittest.cc",
    "power-unittest.cc",
    "registers-ddi-unittest.cc",
    "registers-dpll-unittest.cc",
    "registers-pipe-unittest.cc",
    "registers-transcoder-unittest.cc",
    "registers-typec-unittest.cc",
    "registers-unittest.cc",
  ]
  deps = [
    ":common_srcs",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire_testing",
    "//src/devices/pci/testing:pci-protocol-fake",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/mock-ddk",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

group("tests") {
  testonly = true
  deps = [
    ":intel-i915-tgl-bind_test",
    ":intel-i915-tgl-test-package",
  ]
}

fuchsia_unittest_package("intel-i915-tgl-test-package") {
  package_name = "intel-i915-tgl-test"
  deps = [ ":intel-i915-tgl-test" ]
  manifest = "meta/intel-i915-tgl-test.cml"
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
