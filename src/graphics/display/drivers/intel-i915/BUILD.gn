# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("intel-i915") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = [
    "display-device.cc",
    "dp-display.cc",
    "gtt.cc",
    "hdmi-display.cc",
    "igd.cc",
    "intel-i915.cc",
    "interrupts.cc",
    "pipe.cc",
    "power.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.display.controller",
    "//sdk/banjo/ddk.protocol.i2cimpl",
    "//sdk/banjo/ddk.protocol.intelgpucore",
    "//sdk/banjo/ddk.protocol.pci",
    "//sdk/banjo/ddk.protocol.sysmem",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/edid",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/region-alloc",
  ]
}

test("intel-i915-test") {
  configs += [
    "//build/unification/config:zircon-migrated",
    "//build/config:all_source",
  ]
  sources = [
    "display-device.cc",
    "dp-display.cc",
    "gtt.cc",
    "hdmi-display.cc",
    "igd.cc",
    "intel-i915-test.cc",
    "intel-i915.cc",
    "interrupts.cc",
    "pipe.cc",
    "power.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.display.controller",
    "//sdk/banjo/ddk.protocol.i2cimpl",
    "//sdk/banjo/ddk.protocol.intelgpucore",
    "//sdk/banjo/ddk.protocol.pci",
    "//sdk/banjo/ddk.protocol.sysmem",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/sysmem/testing/mock-sysmem",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/edid",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
    "//zircon/system/ulib/region-alloc",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":intel-i915-test-package" ]
}

unittest_package("intel-i915-test-package") {
  package_name = "intel-i915-test"
  deps = [ ":intel-i915-test" ]

  tests = [
    {
      name = "intel-i915-test"
      environments = basic_envs
    },
  ]
}
