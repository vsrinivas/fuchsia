# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/test/test_package.gni")

test("display-core-unittests") {
  configs += [
    "//build/unification/config:zircon-migrated",
    "//build/config:all_source",
  ]
  sources = [
    "display-test.cc",
    "fence-test.cc",
    "image-test.cc",
    "integration-test.cc",
    "layer-test.cc",
  ]
  include_dirs = [ ".." ]
  deps = [
    ":base",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.sysmem",
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/async-testing",
  ]

  # TODO(fxb/58491): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

static_library("base") {
  configs += [ "//build/config:all_source" ]
  testonly = true
  sources = [
    "base.cc",
    "fidl_client.cc",
    "fidl_client.h",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.sysmem",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_llcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_llcpp",
    "//src/devices/sysmem/drivers/sysmem:sysmem_common_srcs",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//src/graphics/display/drivers/display:shared",
    "//src/graphics/display/drivers/fake:fake-lib",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-testing",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":display-core-unittests-package" ]
}

unittest_package("display-core-unittests-package") {
  package_name = "display-core-unittests"
  deps = [ ":display-core-unittests" ]

  tests = [
    {
      name = "display-core-unittests"
      environments = basic_envs
    },
  ]
}
