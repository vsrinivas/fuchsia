# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

source_set("gpu_lib") {
  defines = [ "_ALL_SOURCE" ]
  sources = [ "gpu.cc" ]
  public_deps = [
    "//src/devices/bus/lib/virtio",
    "//zircon/public/lib/ddktl",
    "//zircon/system/banjo/ddk.protocol.display.controller",
    "//zircon/system/banjo/ddk.protocol.sysmem",
    "//zircon/system/fidl/fuchsia-sysmem:llcpp",
    "//zircon/system/ulib/image-format",
  ]
}

driver_module("virtio_gpu") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "gpu_driver.cc" ]
  deps = [
    ":gpu_lib",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddktl",
  ]
}

test("virtio-gpu-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "gpu_test.cc" ]
  deps = [
    ":gpu_lib",
    "//src/devices/bus/lib/virtio",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/unittest",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/fake-bti",
    "//zircon/system/dev/lib/fake_ddk",
  ]
}

migrated_manifest("virtio_gpu-manifest") {
  deps = [ ":virtio_gpu" ]
}

unittest_package("virtio-gpu-test-package") {
  package_name = "virtio-gpu-test"
  deps = [ ":virtio-gpu-test" ]

  tests = [
    {
      name = "virtio-gpu-test"
      dest = "virtio-gpu-test-test"
    },
  ]
}
