# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/test/test_package.gni")

test("test_magma_vsi_exec_bin") {
  sources = [
    "//third_party/etnaviv_gpu_tests/src/etnaviv_cl_test_gc7000.c",
    "test_magma_vsi.h",
    "test_magma_vsi_execute_command.cc",
  ]

  include_dirs = [ "//third_party/etnaviv_gpu_tests/src/" ]

  deps = [
    "//sdk/lib/fdio",
    "//src/graphics/lib/magma/src/libmagma",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/zx",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test_package("test-magma-vsi-exec") {
  deps = [ ":test_magma_vsi_exec_bin" ]

  tests = [
    {
      name = "test_magma_vsi_exec_bin"
      manifest = "meta/test_magma_vsi_exec.cmx"
      environments = [ sherlock_env ]
      log_settings = {
        max_severity = "ERROR"
      }
    },
  ]
}

test("msd_vsi_vip_integration_tests") {
  sources = [
    "run_unit_tests.cc",
    "test_vsi_query.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_llcpp",
    "//src/graphics/drivers/msd-vsi-vip/include",
    "//src/graphics/lib/magma/src/libmagma",
    "//src/graphics/lib/magma/src/magma_util/platform:buffer",
    "//src/graphics/lib/magma/tests/helper:test_device_helper",
    "//src/lib/fxl/test:gtest_main",
  ]
}

test_package("msd-vsi-vip-integration-tests") {
  deps = [ ":msd_vsi_vip_integration_tests" ]

  tests = [
    {
      name = "msd_vsi_vip_integration_tests"
      environments = [ sherlock_env ]
      log_settings = {
        max_severity = "ERROR"
      }
    },
  ]
}

group("integration") {
  testonly = true
  deps = [
    ":msd-vsi-vip-integration-tests",
    ":test-magma-vsi-exec",
  ]
}
