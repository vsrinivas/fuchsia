# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

declare_args() {
  msd_intel_gen_enable_hardware_unit_tests = false
}

group("integration") {
  testonly = true

  public_deps = [ ":msd_intel_gen_integration_tests" ]
}

executable("msd_intel_gen_integration_tests") {
  testonly = true

  defines =
      [ "ENABLE_HARDWARE_UNIT_TESTS=$msd_intel_gen_enable_hardware_unit_tests" ]

  sources = [
    "test_hang_recovery.cc",
    "test_hardware_unit_tests.cc",
    "test_icd_list.cc",
    "test_shutdown.cc",
    "test_workarounds.cc",
  ]

  deps = [
    "$magma_build_root/src/libmagma",
    "$magma_build_root/tests/helper:inflight_list",
    "$magma_build_root/tests/helper:magma_map_cpu",
    "$magma_build_root/tests/helper:test_device_helper",
    "../../include",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_llcpp",
    "//src/graphics/lib/magma/tests/helper:logger_init_helper",
    "//src/lib/fxl/test:gtest_main",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}
