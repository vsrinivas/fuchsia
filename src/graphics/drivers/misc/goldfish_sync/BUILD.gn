# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("goldfish-sync-bind") {
  rules = "goldfish_sync.bind"
  header_output = "goldfish_sync-bind.h"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.acpi" ]
}

source_set("common_deps") {
  public_deps = [
    ":goldfish-sync-bind",
    "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.goldfish.sync:fuchsia.hardware.goldfish.sync_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_llcpp",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fidl-llcpp",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace:trace-driver",
  ]
}

driver_module("goldfish_sync_driver-driver") {
  output_name = "goldfish_sync"

  sources = [
    "sync_common_defs.h",
    "sync_device.cc",
    "sync_device.h",
  ]

  deps = [ ":common_deps" ]

  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  configs += [ "//build/config:all_source" ]
}

fuchsia_driver_component("goldfish_sync_driver") {
  component_name = "goldfish_sync"
  deps = [ ":goldfish_sync_driver-driver" ]
}

test("goldfish-sync-device-test") {
  output_name = "goldfish-sync-device-test"
  sources = [
    "sync_common_defs.h",
    "sync_device.cc",
    "sync_device.h",
    "sync_device_test.cc",
  ]
  deps = [
    ":common_deps",
    "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp_mock",
    "//src/devices/lib/acpi/mock:mock-acpi",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
  ]

  configs += [ "//build/config:all_source" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("goldfish-sync-device-test-package") {
  package_name = "goldfish-sync-device-test"
  deps = [ ":goldfish-sync-device-test" ]
}

fuchsia_system_package("goldfish_sync") {
  deps = [ ":goldfish_sync_driver" ]
  allowed_in_extra_deps = true
}

group("tests") {
  testonly = true
  deps = [ ":goldfish-sync-bind_test" ]

  # TODO(67872): goldfish-sync-device-test are flaky on arm64 qemu. We should
  # re-enable it once we fixed the flakiness.
  if (target_cpu == "x64") {
    deps += [ ":goldfish-sync-device-test-package" ]
  }
}
