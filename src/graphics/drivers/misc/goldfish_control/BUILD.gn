# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")

bind_rules("goldfish_control_composite-bind") {
  rules = "goldfish_control_composite.bind"
  output = "goldfish_control_composite-bind.h"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.composite",
    "//src/devices/bind/fuchsia.platform",
  ]
}

declare_args() {
  goldfish_control_use_composite_device = true
}

driver_module("control_driver") {
  output_name = "goldfish_control"

  sources = [
    "control_device.cc",
    "control_device.h",
    "device_local_heap.cc",
    "device_local_heap.h",
    "heap.cc",
    "heap.h",
    "host_visible_heap.cc",
    "host_visible_heap.h",
  ]

  deps = [
    ":goldfish_control_composite-bind",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.goldfish.addressspace",
    "//sdk/banjo/ddk.protocol.goldfish.control",
    "//sdk/banjo/ddk.protocol.goldfish.pipe",
    "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_llcpp",
    "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_llcpp",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/fsl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl-llcpp",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace:trace-driver",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:enable_zircon_asserts",
    "//build/config/fuchsia:static_cpp_standard_library",
  ]

  configs += [ "//build/config:all_source" ]
}

driver_package("goldfish_control") {
  deps = [ ":control_driver" ]

  drivers = [
    {
      name = "goldfish_control.so"
    },
  ]
}
