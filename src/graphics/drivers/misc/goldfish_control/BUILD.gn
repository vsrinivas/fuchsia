# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("goldfish_control_composite-bind") {
  rules = "goldfish_control_composite.bind"
  header_output = "goldfish_control_composite-bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.google.platform" ]
}

common_deps = [
  ":goldfish_control_composite-bind",
  "//sdk/banjo/fuchsia.hardware.goldfish.control:fuchsia.hardware.goldfish.control_banjo_cpp",
  "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_cpp_wire",
  "//sdk/fidl/fuchsia.hardware.goldfish.pipe:fuchsia.hardware.goldfish.pipe_cpp_wire",
  "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_cpp_wire",
  "//sdk/lib/fit-promise",
  "//sdk/lib/syslog/cpp:backend",
  "//src/devices/lib/driver",
  "//src/devices/lib/goldfish/pipe_headers",
  "//src/lib/ddk",
  "//src/lib/ddktl",
  "//src/lib/fsl",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/async-loop:async-loop-default",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/zircon-internal",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("control_driver-driver") {
  output_name = "goldfish_control"

  sources = [
    "control_device.cc",
    "control_device.h",
    "device_local_heap.cc",
    "device_local_heap.h",
    "heap.cc",
    "heap.h",
    "host_visible_heap.cc",
    "host_visible_heap.h",
  ]

  deps = common_deps

  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  configs += [ "//build/config:all_source" ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_driver_component("control_driver") {
  component_name = "goldfish_control"
  deps = [ ":control_driver-driver" ]
  info = "control_driver-info.json"
  colocate = true
}

fuchsia_driver_package("goldfish_control") {
  driver_components = [ ":control_driver" ]
}

test("goldfish-control-device-test") {
  output_name = "goldfish-control-device-test"
  sources = [
    "control_device.cc",
    "control_device.h",
    "device_local_heap.cc",
    "device_local_heap.h",
    "heap.cc",
    "heap.h",
    "host_visible_heap.cc",
    "host_visible_heap.h",
    "tests/control_device_test.cc",
  ]
  deps = common_deps + [
           "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_cpp_wire",
           "//src/devices/testing/fake-bti",
           "//src/devices/testing/fake-object",
           "//src/devices/testing/mock-ddk",
           "//src/lib/fxl/test:gtest_main",
           "//third_party/googletest:gtest",
           "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
           "//zircon/system/ulib/sync",
           "//zircon/system/ulib/zircon-internal",
           "//zircon/system/ulib/zx",
         ]

  configs += [ "//build/config:all_source" ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_package("goldfish-control-device-test-package") {
  package_name = "goldfish-control-device-test"
  deps = [ ":goldfish-control-device-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":goldfish_control_composite-bind_test" ]

  if (target_cpu == "x64") {
    deps += [ ":goldfish-control-device-test-package" ]
  }
}
