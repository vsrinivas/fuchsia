# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":vulkan_loader_tests" ]
}

executable("test_bin") {
  testonly = true
  output_name = "vulkan_loader_test"
  sources = [ "test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//sdk/lib/fdio",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fzl",
  ]
}

executable("unittest_bin") {
  testonly = true
  output_name = "vulkan_loader_unittest"
  sources = [ "unittest.cc" ]
  deps = [
    "..:app",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//sdk/lib/fdio",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/storage/vfs/cpp",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("pkg-server-bin") {
  testonly = true
  output_name = "pkg-server"

  sources = [ "pkg-server-main.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_hlcpp",
    "//sdk/lib/fdio",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//src/lib/storage/vfs/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("vulkan_loader_test_driver") {
  testonly = true
  manifest = "meta/vulkan_loader_test_driver.cml"
  deps = [ ":test_bin" ]
}

fuchsia_component("vulkan_loader_test_lib_provider") {
  testonly = true
  manifest = "meta/vulkan_loader_test_lib_provider.cml"
  deps = [ ":pkg-server-bin" ]
}

fuchsia_component("vulkan_loader_test") {
  testonly = true
  manifest = "meta/vulkan_loader_test.cml"
}

fuchsia_component("vulkan_loader_unittest_driver") {
  testonly = true
  manifest = "meta/vulkan_loader_unittest_driver.cml"
  deps = [ ":unittest_bin" ]
}

fuchsia_component("vulkan_loader_unittest") {
  testonly = true
  manifest = "meta/vulkan_loader_unittest.cml"
}

resource("metadata") {
  sources = [ "metadata.json" ]
  outputs = [ "data/test_metadata/metadata.json" ]
}

resource("manifest-json") {
  sources = [ "libvulkan_fake.json" ]
  outputs = [ "data/libvulkan_fake.json" ]
}

fuchsia_component("test_vulkan_driver") {
  testonly = true
  manifest = "meta/test_vulkan_driver.cml"
  deps = [ ":metadata" ]
}

fuchsia_test_package("vulkan_loader_tests") {
  test_components = [
    ":vulkan_loader_test",
    ":vulkan_loader_unittest",
  ]
  deps = [
    ":manifest-json",
    ":test_vulkan_driver",
    ":vulkan_loader_test_driver",
    ":vulkan_loader_test_lib_provider",
    ":vulkan_loader_unittest_driver",
    "..:vulkan_loader_cmp",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
