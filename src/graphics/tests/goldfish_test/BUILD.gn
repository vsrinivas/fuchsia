# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

executable("goldfish_test_bin") {
  testonly = true
  output_name = "goldfish_test"

  sources = [ "goldfish_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire",
    "//sdk/lib/fdio",
    "//sdk/lib/fidl",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_component("goldfish_test_cmp") {
  testonly = true
  deps = [ ":goldfish_test_bin" ]
  component_name = "goldfish_test"
  manifest = "meta/goldfish_test.cml"
}

fuchsia_test_package("goldfish-test-pkg") {
  test_components = [ ":goldfish_test_cmp" ]
  package_name = "goldfish_test"
  test_specs = {
    environments = [
      {
        dimensions = {
          device_type = "AEMU"
        }
      },
    ]
  }
}

group("goldfish_test") {
  testonly = true
  deps = [ ":goldfish-test-pkg" ]
}
