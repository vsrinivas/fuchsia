# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

executable("vkvalidation_bin") {
  testonly = true
  output_name = "vkvalidation"
  sources = [ "vkvalidation.cc" ]
  deps = [
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/vulkan",
  ]
}
resource("override") {
  sources = [ "VkLayer_override.json" ]
  outputs = [ "data/test-xdg/vulkan/implicit_layer.d/VkLayer_override.json" ]
}

fuchsia_component("vkvalidation_cmp") {
  testonly = true
  component_name = "vkvalidation"
  deps = [
    ":override",
    ":vkvalidation_bin",
    "//src/lib/vulkan/validation_layers",
  ]
  manifest = "meta/vkvalidation.cmx"
}

fuchsia_test_package("vkvalidation-pkg") {
  package_name = "vkvalidation"
  test_components = [ ":vkvalidation_cmp" ]
  test_specs = {
    environments = magma_libvulkan_hardware_envs
  }
}

fuchsia_component("vkvalidation_external_cmp") {
  testonly = true
  component_name = "vkvalidation-external"

  # Package must not include validation layers
  deps = [
    ":override",
    ":vkvalidation_bin",
    "//src/lib/vulkan/validation-layer-package:validation-client",
  ]
  manifest = "meta/vkvalidation-external.cml"
}

fuchsia_test_package("vkvalidation-external-pkg") {
  package_name = "vkvalidation-external"
  test_components = [ ":vkvalidation_external_cmp" ]
  test_specs = {
    environments = magma_libvulkan_hardware_envs
  }
}

group("vkvalidation") {
  testonly = true
  deps = [
    ":vkvalidation-external-pkg",
    ":vkvalidation-pkg",
    "//src/lib/vulkan/validation-layer-package",
  ]
}
