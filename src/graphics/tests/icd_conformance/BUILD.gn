# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/resource.gni")
import("//src/lib/vulkan/vulkan.gni")

source_set("src") {
  testonly = true

  sources = [ "icd_conformance.cc" ]
  public_deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_wire",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_cpp_wire",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/elflib",
    "//src/lib/files",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/fxl/test:test_settings",
    "//src/lib/json_parser",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fzl",
  ]
  public_deps += [ "//src/lib/vulkan" ]
}

resource("magma_allowlist") {
  sources = [ "//src/graphics/lib/magma/gnbuild/imported_symbols.allowlist" ]
  outputs = [ "data/imported_symbols.allowlist" ]
}

executable("icd_conformance_bin") {
  testonly = true
  output_name = "icd_conformance"

  deps = [ ":src" ]
}

fuchsia_component("icd_conformance_cmp") {
  testonly = true
  component_name = "icd_conformance"
  deps = [
    ":icd_conformance_bin",
    ":magma_allowlist",
  ]

  manifest = "meta/icd_conformance.cml"
}

fuchsia_test_package("icd-conformance") {
  package_name = "icd_conformance"
  test_components = [ ":icd_conformance_cmp" ]
  test_specs = {
    environments = vulkan_envs
  }
}

group("icd_conformance") {
  testonly = true
  deps = [ ":icd-conformance" ]
}
