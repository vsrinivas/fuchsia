# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/environments.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

# Used by other Vulkan tests.
source_set("helper") {
  testonly = true

  sources = [
    "vkreadback.cc",
    "vkreadback.h",
  ]

  deps = [
    "//src/graphics/tests/common",
    "//zircon/system/ulib/zx",
  ]

  public_deps = [
    "//src/lib/vulkan",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/zx",
  ]
}

# Built both on Fuchsia and on the host.
test("vkreadback_test") {
  sources = [ "main.cc" ]

  deps = [
    ":helper",
    "//src/lib/fxl/test:gtest_main",
  ]

  # TODO(fxbug.dev/102268): Add macOS support to VulkanContext and remove this.
  if (is_host) {
    environments = [ linux_env ]
  }
}

fuchsia_test_component("vkreadback_test_component") {
  component_name = "vkreadback_test"
  deps = [
    ":vkreadback_test",
    "//src/lib/vulkan/validation_layers",
  ]

  manifest = "meta/vkreadback.cml"
}

fuchsia_test_package("vkreadback_test_package") {
  package_name = "vkreadback_test"
  test_components = [ ":vkreadback_test_component" ]
  test_specs = {
    # TODO(fxbug.dev/47373): Restore astro_env when fxbug.dev/44570 is fixed.
    environments = [ aemu_env ] + magma_libvulkan_hardware_envs +
                   [ astro_env ] - [ astro_env ]
  }
}

group("vkreadback") {
  testonly = true
  deps = [
    ":vkreadback_test($host_toolchain)",
    ":vkreadback_test_package",
  ]
}
