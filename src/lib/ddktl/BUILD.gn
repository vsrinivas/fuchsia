# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/zircon/zx_library.gni")

group("tests") {
  testonly = true
  deps = [ "tests" ]
}

config("include") {
  include_dirs = [ "include" ]
}

source_set("ddktl") {
  sources = [
    "include/ddktl/device-group.h",
    "include/ddktl/device-internal.h",
    "include/ddktl/device.h",
    "include/ddktl/fidl.h",
    "include/ddktl/init-txn.h",
    "include/ddktl/metadata.h",
    "include/ddktl/resume-txn.h",
    "include/ddktl/suspend-txn.h",
    "include/ddktl/unbind-txn.h",
  ]
  public_configs = [ ":include" ]
  public_deps = [
    # <ddktl/fidl.h> has #include <lib/stdcompat/span.h>
    "//sdk/lib/stdcompat",

    # <ddktl/fidl.h> has #include <lib/fidl/cpp/wire/transaction.h>.
    "//sdk/lib/fidl/cpp/wire",

    # <ddktl/device.h> has #include <lib/zx/channel.h>.
    "//zircon/system/ulib/zx",

    # <ddktl/device.h> has #include <lib/zx/result.h>.
    "//zircon/system/ulib/zxc",

    # <ddktl/fidl.h> has #include <ddk/device.h>
    "//src/lib/ddk",
  ]

  if (is_fuchsia) {
    public_deps += [
      # <ddktl/device.h> has #include <lib/sys/component/cpp/service_client.h>.
      "//sdk/lib/sys/component/cpp",

      # <ddktl/device.h> has #include <lib/fidl_driver/cpp/transport.h>
      "//sdk/lib/fidl_driver:fidl_driver_transport",
    ]
  }

  deps = [
    "//sdk/lib/driver_runtime:driver_runtime_cpp",
    "//src/lib/ddk",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
}

config("deprecated-read-write") {
  cflags = [ "-DDDKTL_ALLOW_READ_WRITE" ]

  # Please do not add to this list.
  # TODO(fxbug.dev/33820): Remove all usages and delete this functionality.
  visibility = [
    "//src/devices/block/drivers/core:core-unittest-bin",
    "//src/devices/block/drivers/core:shared",
    "//src/devices/tests/mock-device:mock-device-driver",
    "//src/lib/ddktl/tests:ddktl-test",
  ]
}
