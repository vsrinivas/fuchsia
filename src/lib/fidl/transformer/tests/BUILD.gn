# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//src/tests/fidl/conformance_suite/gidl-conformance-suite.gni")
import("//tools/fidl/gidl/gidl.gni")

gidl_deps = []
foreach(gidl_file, conformance_suite_gidl_files) {
  gidl_name = get_path_info(gidl_file, "name")
  target = "conformance_suite__${gidl_name}"
  gidl_deps += [ ":" + target ]
  gidl_transformer_conformance(target) {
    testonly = true
    inputs = [ gidl_file ]
    fidl = conformance_suite_fidl_target
    deps = [
      "//zircon/public/lib/fidl_base",
      "//zircon/public/lib/zxtest",
    ]
  }
}

executable("transformer_conformance_tests_bin") {
  output_name = "transformer-conformance-tests"
  testonly = true
  sources = []
  deps = gidl_deps
}

fuchsia_unittest_package("fidl-transformer-conformance-tests") {
  deps = [ ":transformer_conformance_tests_bin" ]
}

if (!is_fuchsia) {
  test("fidl-transformer-conformance-host-tests") {
    sources = []
    deps = gidl_deps
  }
}
