# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

group("message_container") {
  testonly = true
  deps = [
    ":fidl_llcpp_message_container_tests",
    ":fidl_llcpp_message_storage_tests",
    ":fidl_llcpp_status_types_tests",
    ":fidl_llcpp_status_types_tests_bin($host_toolchain)",
  ]
}

fidl("fidl.llcpp.linearized.test") {
  sources = [ "linearized.test.fidl" ]
}

fidl("fidl.llcpp.empty.test") {
  sources = [ "empty.test.fidl" ]
}

fidl("fidl.llcpp.buffersize.test") {
  sources = [ "buffersize.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

test("fidl_llcpp_message_container_tests_bin") {
  output_name = "fidl_llcpp_message_container_tests"
  sources = [
    "decoded_value_test.cc",
    "encoded_test.cc",
    "incoming_message_test.cc",
    "outgoing_message_test.cc",
    "outgoing_to_incoming_message_test.cc",
    "wire_format_metadata_test.cc",
  ]
  deps = [
    ":fidl.llcpp.empty.test_cpp_wire",
    ":fidl.llcpp.linearized.test_cpp_wire",
    "//sdk/testing/fidl:fidl_test_cpp_wire",
    "//sdk/testing/fidl/types_tests:test.types_cpp_wire",
    "//src/lib/fidl/llcpp/tests:types_test_utils",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("fidl_llcpp_message_container_tests") {
  # TODO(fxbug.dev/74490): Required for death-tests. We could remove the
  # manifests by switching to zxtest.
  manifest = "meta/fidl_llcpp_message_container_tests.cml"
  deps = [ ":fidl_llcpp_message_container_tests_bin" ]
}

test("fidl_llcpp_status_types_tests_bin") {
  output_name = "fidl_llcpp_status_types_tests"
  sources = [ "status_description_test.cc" ]
  deps = [
    "//sdk/lib/fidl/cpp/wire",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("fidl_llcpp_status_types_tests") {
  deps = [ ":fidl_llcpp_status_types_tests_bin" ]
}

test("fidl_llcpp_message_storage_tests_bin") {
  output_name = "fidl_llcpp_message_storage_tests"
  sources = [
    "any_allocator_test.cc",
    "message_buffer_size_test.cc",
  ]
  deps = [
    ":fidl.llcpp.buffersize.test_cpp_wire",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("fidl_llcpp_message_storage_tests") {
  deps = [ ":fidl_llcpp_message_storage_tests_bin" ]
}
