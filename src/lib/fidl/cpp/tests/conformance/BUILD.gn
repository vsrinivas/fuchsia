# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//src/tests/fidl/conformance_suite/gidl-conformance-suite.gni")
import("//tools/fidl/gidl/gidl.gni")

group("conformance") {
  testonly = true
  deps = [ ":fidl_cpp_conformance_tests" ]
}

gidl_output_prefix = "conformance_suite__"

gidl_deps = []
foreach(gidl_file, conformance_suite_gidl_files) {
  gidl_name = get_path_info(gidl_file, "name")
  target = "${gidl_output_prefix}${gidl_name}"
  gidl_deps += [ ":" + target ]
  gidl_cpp_conformance(target) {
    testonly = true
    inputs = [ gidl_file ]
    fidl = conformance_suite_fidl_target
    deps = [
      ":conformance_utils",
      "//zircon/system/ulib/zxtest",
    ]
  }
}

test("fidl_cpp_conformance_tests_bin") {
  output_name = "fidl_cpp_conformance_tests"
  deps =
      [ "//src/tests/fidl/conformance_suite:conformance_fidl_cpp" ] + gidl_deps
}

fuchsia_unittest_package("fidl_cpp_conformance_tests") {
  deps = [ ":fidl_cpp_conformance_tests_bin" ]
}

source_set("conformance_utils") {
  testonly = true
  public = [ "conformance_utils.h" ]
  public_deps = [
    "//zircon/system/public",
    "//zircon/system/ulib/fidl:fidl-llcpp",
    "//zircon/system/ulib/fidl:fidl_base",
    "//zircon/system/ulib/zx",
  ]
}
