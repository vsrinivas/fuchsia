# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("conversion") {
  testonly = true
  deps = [
    ":fidl_cpp_conversion_tests",
    ":fidl_cpp_conversion_tests_bin($host_toolchain)",
  ]
}

test("fidl_cpp_conversion_tests_bin") {
  output_name = "fidl_cpp_conversion_tests"
  sources = [ "conversion_test.cc" ]
  deps = [
    "//src/lib/fidl/llcpp/tests:arena_checker",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
  if (is_fuchsia) {
    deps += [ "//sdk/testing/fidl/types_tests:test.types_cpp" ]
  } else {
    deps += [ "//sdk/testing/fidl/types_tests:test.types_cpp_type_conversions" ]
  }
}

fuchsia_unittest_package("fidl_cpp_conversion_tests") {
  deps = [ ":fidl_cpp_conversion_tests_bin" ]
}
