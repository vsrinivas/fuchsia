# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# The base library that supports domain objects.
#
# It works on host and Fuchsia.
source_set("cpp_base") {
  public = [
    "include/lib/fidl/cpp/natural_coding_traits.h",
    "include/lib/fidl/cpp/natural_decoder.h",
    "include/lib/fidl/cpp/natural_encoder.h",
    "include/lib/fidl/cpp/natural_types.h",
    "include/lib/fidl/cpp/wire_natural_conversions.h",
  ]
  sources = [
    "natural_decoder.cc",
    "natural_encoder.cc",
  ]
  public_deps = [
    "//sdk/lib/stdcompat",
    "//zircon/system/ulib/fidl:fidl-llcpp",
    "//zircon/system/ulib/zxc",
  ]
  deps = [ "//zircon/system/ulib/fidl:fidl_base" ]
  public_configs = [ ":cpp_base.headers" ]
}

config("cpp_base.headers") {
  include_dirs = [ "include" ]
}

# The unified C++ library that adds messaging APIs on top of domain objects.
#
# It is Fuchsia-only.
source_set("cpp") {
  public = [
    "include/lib/fidl/cpp/any_error_in.h",
    "include/lib/fidl/cpp/channel.h",
    "include/lib/fidl/cpp/client.h",
    "include/lib/fidl/cpp/internal/client_details.h",
    "include/lib/fidl/cpp/internal/make_response_context.h",
    "include/lib/fidl/cpp/internal/natural_client_base.h",
    "include/lib/fidl/cpp/internal/natural_message_encoder.h",
    "include/lib/fidl/cpp/internal/thenable.h",
    "include/lib/fidl/cpp/unified_messaging.h",
    "include/lib/fidl/cpp/unified_messaging_declarations.h",
  ]
  sources = [
    "any_error_in.cc",
    "internal/natural_message_encoder.cc",
  ]
  public_deps = [
    "//src/lib/fidl/cpp:cpp_base",
    "//zircon/system/ulib/fidl:fidl_base",
  ]
}

# The support header for HLCPP / Natural conversion
source_set("hlcpp_conversion") {
  public = [ "include/lib/fidl/cpp/hlcpp_conversion.h" ]
  public_configs = [ ":cpp_base.headers" ]
  public_deps = [ "//sdk/lib/fidl/cpp:cpp_base" ]
}
