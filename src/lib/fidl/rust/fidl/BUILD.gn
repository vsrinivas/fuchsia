# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/args.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

fidl_rust_deps = [
  "//src/lib/fuchsia-async",
  "//src/lib/zerocopy",
  "//src/lib/zircon/rust:fuchsia-zircon-status",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:bitflags",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:log",
  "//third_party/rust_crates:parking_lot",
  "//third_party/rust_crates:slab",
  "//third_party/rust_crates:static_assertions",
  "//third_party/rust_crates:thiserror",
]

if (is_fuchsia) {
  fidl_rust_deps += [ "//src/lib/zircon/rust:fuchsia-zircon" ]

  if (fidl_trace_level != 0) {
    fidl_rust_deps += [
      "//src/lib/trace/rust:trace",
      "//src/lib/trace/rust:trace-provider",
    ]
  }
}

rustc_library("fidl") {
  edition = "2018"
  deps = fidl_rust_deps

  if (is_fuchsia && fidl_trace_level != 0) {
    features = [ "fidl_trace" ]
  }

  sources = [
    "src/client.rs",
    "src/encoding.rs",
    "src/endpoints.rs",
    "src/epitaph.rs",
    "src/error.rs",
    "src/handle.rs",
    "src/lib.rs",
    "src/server.rs",
  ]
}

rustc_test("fidl_rust_lib_tests") {
  edition = "2018"
  deps = fidl_rust_deps + [
           "//third_party/rust_crates:futures-test",
           "//third_party/rust_crates:matches",
         ]

  sources = [
    "src/client.rs",
    "src/encoding.rs",
    "src/endpoints.rs",
    "src/epitaph.rs",
    "src/error.rs",
    "src/handle.rs",
    "src/lib.rs",
    "src/server.rs",
  ]
}
