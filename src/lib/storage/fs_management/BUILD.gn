# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")

rustc_library("fs_management") {
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//src/lib/cstr",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-runtime",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:thiserror",
    "//zircon/system/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

generate_manifest("fs_management.manifest") {
  visibility = [ ":*" ]
  args = [
    "--binary=bin/blobfs",
    "--binary=bin/minfs",
  ]
}

manifest_outputs = get_target_outputs(":fs_management.manifest")
fs_management_manifests = [ manifest_outputs[0] ]

test_package("fs_management_tests") {
  extra = fs_management_manifests
  deps = [
    ":fs_management.manifest",
    ":fs_management_test",
  ]
  tests = [
    {
      name = "fs_management_lib_test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":fs_management_tests" ]
}
