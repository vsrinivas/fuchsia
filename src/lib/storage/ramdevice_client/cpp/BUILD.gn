# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("headers_config") {
  include_dirs = [ "include" ]
}

group("cpp") {
  public_deps = [ ":ramdevice-client" ]
}

shared_library("ramdevice-client") {
  public_configs = [ ":headers_config" ]
  public = [
    "include/ramdevice-client/ramdisk.h",
    "include/ramdevice-client/ramnand.h",
  ]
  sources = [
    "ramdisk.cc",
    "ramnand.cc",
  ]
  public_deps = [
    # <ramdevice-client/ramnand.h> has #include <fuchsia/hardware/nand/c/fidl.h>
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",

    # <ramdevice-client/ramnand.h> has #include <fidl/fuchsia.hardware.nand/cpp/wire.h>
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_cpp_wire",
  ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_c",
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//sdk/fidl/fuchsia.hardware.ramdisk:fuchsia.hardware.ramdisk_c",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
  ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}

# We need to use this group because the public dependencies of
# ramdevice-client-test-lib are not propagating correctly.
group("ramdevice-client-test") {
  testonly = true
  public_deps = [
    ":ramdevice-client-test-lib",
    "//src/devices/testing/driver-integration-test-shim",
  ]
}

shared_library("ramdevice-client-test-lib") {
  testonly = true
  output_name = "ramdevice-client-test"
  visibility = [ ":*" ]
  public_configs = [ ":headers_config" ]
  public = [ "include/ramdevice-client-test/ramnandctl.h" ]
  sources = [ "ramnandctl.cc" ]
  public_deps = [
    # <ramdevice-client-test/ramnandctl.h> has #include <lib/driver-integration-test/fixture.h>
    ":ramdevice-client",

    # <ramdevice-client/ramnand.h> has #include <fuchsia/hardware/nand/c/fidl.h>
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//src/devices/testing/driver-integration-test-shim",
  ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_c",
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",
    "//sdk/fidl/fuchsia.hardware.ramdisk:fuchsia.hardware.ramdisk_c",
    "//sdk/lib/fdio",
    "//src/devices/testing/driver-integration-test-shim",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
  ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}

static_library("ramdevice-client-static") {
  public_configs = [ ":headers_config" ]
  public = [
    "include/ramdevice-client/ramdisk.h",
    "include/ramdevice-client/ramnand.h",
  ]
  sources = [
    "ramdisk.cc",
    "ramnand.cc",
  ]
  public_deps = [
    # <ramdevice-client/ramnand.h> has #include <fuchsia/hardware/nand/c/fidl.h>
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_c",

    # <ramdevice-client/ramnand.h> has #include <fidl/fuchsia.hardware.nand/cpp/wire.h>
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_cpp_wire",
  ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_c",
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_c",
    "//sdk/fidl/fuchsia.hardware.ramdisk:fuchsia.hardware.ramdisk_c",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
  ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  # TODO(fxbug.dev/95833): This target uses the deprecated C bindings.
  # Consider switching to the C++ bindings. See linked bug for details.
  configs += [ "//build/c:fidl-deprecated-c-bindings" ]
}
