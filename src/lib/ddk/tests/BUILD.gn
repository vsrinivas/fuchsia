# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":ddk-unittest-package" ]
}

test("ddk-unittest") {
  output_name = "ddk-unittest-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "binding-test.cc",
    "mmio-buffer-test.cc",
    "phys-iter-test.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_unittest_package("ddk-unittest-package") {
  package_name = "ddk-unittest"
  deps = [ ":ddk-unittest" ]
}
