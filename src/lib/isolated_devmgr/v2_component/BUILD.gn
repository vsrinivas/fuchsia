# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/fuchsia_component.gni")
import("//src/sys/build/fuchsia_package.gni")

executable("support") {
  testonly = true
  sources = [ "support.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.boot",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_llcpp",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_c",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_llcpp",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/mock-boot-arguments",
  ]
}

fuchsia_component("isolated-devmgr-component") {
  testonly = true
  component_name = "isolated-devmgr"
  manifest = "meta/isolated_devmgr.cml"
  deps = [ ":support" ]
}

fuchsia_component("driver-manager-test") {
  testonly = true
  manifest = "meta/driver_manager_test.cml"
  deps = [
    "//src/devices/bin/driver_host:driver_host",
    "//src/devices/bin/driver_manager",
  ]
}

group("storage_drivers") {
  testonly = true
  deps = [
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ftl",
    "//src/devices/block/drivers/fvm",
    "//src/devices/block/drivers/gpt",
    "//src/devices/block/drivers/ramdisk:ramdisk",
    "//src/devices/nand/drivers/ram-nand",
  ]
}

fuchsia_package("isolated-devmgr") {
  testonly = true
  deps = [
    ":driver-manager-test",
    ":isolated-devmgr-component",
    ":storage_drivers",
  ]
}

source_set("client") {
  testonly = true
  sources = [
    "bind_devfs_to_namespace.cc",
    "bind_devfs_to_namespace.h",
    "fvm.cc",
    "fvm.h",
    "ram_disk.cc",
    "ram_disk.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.device",
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.sys2",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fs-management",
  ]
  public_deps = [
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/ramdevice-client",
  ]
}

group("v2_component") {
  testonly = true
  deps = [ ":isolated-devmgr" ]
  public_deps = [ ":client" ]
}
