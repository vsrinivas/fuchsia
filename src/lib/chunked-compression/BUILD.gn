# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

source_set("chunked-compression") {
  sources = [
    "chunked-archive.cc",
    "chunked-archive.h",
    "chunked-compressor.cc",
    "chunked-compressor.h",
    "chunked-decompressor.cc",
    "chunked-decompressor.h",
    "compression-params.cc",
    "compression-params.h",
    "status.h",
    "streaming-chunked-compressor.cc",
    "streaming-chunked-compressor.h",
  ]
  public_deps = [ "//zircon/public/lib/fbl" ]
  deps = [
    "//src/lib/fxl",
    "//third_party/zstd",
    "//zircon/public/lib/cksum",
    "//zircon/public/lib/fbl",
  ]

  if (is_fuchsia) {
    deps += [ "//zircon/public/lib/syslog" ]
    public_deps += [ "//zircon/public/lib/zx" ]
  }
}

executable("chunked-compress") {
  sources = [ "main.cc" ]
  deps = [
    ":chunked-compression",
    "//zircon/public/lib/fbl",
  ]
}

install_host_tools("chunked-compress-host-tool") {
  deps = [ ":chunked-compress" ]
  outputs = [ "chunked-compress" ]
}

test("chunked-compression-unittests") {
  testonly = true
  output_name = "chunked-compression-unittests"
  sources = [
    "chunked-compressor-test.cc",
    "chunked-decompressor-test.cc",
    "header-reader-test.cc",
    "header-writer-test.cc",
    "seek-table-test.cc",
    "streaming-chunked-compressor-test.cc",
    "test-utils.cc",
    "test-utils.h",
  ]
  deps = [
    ":chunked-compression",
    "//zircon/public/lib/cksum",
    "//zircon/public/lib/zxtest",
  ]
}

unittest_package("chunked-compression-unittest-package") {
  package_name = "chunked-compression-unittests"
  deps = [ ":chunked-compression-unittests" ]

  tests = [
    {
      name = "chunked-compression-unittests"
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":chunked-compression-unittest-package" ]
}
