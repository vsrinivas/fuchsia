# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("cpp") {
  sources = [
    "connect.h",
    "outgoing.cc",
    "outgoing.h",
    "startup_context.cc",
    "startup_context.h",
  ]

  deps = [
    "//garnet/public/lib/syslog/cpp",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/fdio",
  ]

  public_deps = [
    ":deprecated_service_provider",
    "//garnet/public/lib/svc/cpp",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fidl/cpp",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-inspect-deprecated",
  ]

  public_configs = [ "//garnet/public:config" ]

  # This library is deprecated.
  #
  # Use //sdk/lib/sys/cpp instead.
  #
  # DO NOT ADD MORE ENTRIES TO THIS LIST.
  visibility = [
    "//garnet/public/lib/test_runner/cpp:cpp",
    "//src/lib/component/cpp:environment_services",
    "//src/virtualization/bin/guest_manager:lib",
    "//src/virtualization/bin/guest_runner:lib",
  ]
}

source_set("deprecated_service_provider") {
  sources = [
    "service_provider_impl.cc",
    "service_provider_impl.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.sys",
  ]

  public_configs = [ "//garnet/public:config" ]

  # This library is deprecated.
  #
  # Use a directory(sys::ServiceDirectory) instead.
  #
  # DO NOT ADD MORE ENTRIES TO THIS LIST.
  visibility = [
    "//garnet/public/lib/component/cpp:deprecated_service_provider",
    "//src/ledger/bin/p2p_provider/impl",
    "//src/modular/bin/sessionmgr:sessionmgr_lib",
    "//src/modular/bin/sessionmgr/agent_runner",
    "//src/modular/bin/sessionmgr/story_runner",
    ":cpp",
  ]
}

source_set("environment_services") {
  sources = [
    "environment_services_helper.cc",
    "environment_services_helper.h",
  ]

  public_deps = [
    ":cpp",
  ]

  # This library is deprecated.
  #
  # Use //sdk/lib/sys/cpp instead.
  #
  # DO NOT ADD MORE ENTRIES TO THIS LIST.
  visibility = [
    "testing",
  ]
}
