# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "echo_client_sc"
  edition = "2021"
  deps = [
    ":config_lib",
    "//examples/components/routing/fidl:echo_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "echo_client_sc"
  manifest = "meta/echo_client_sc.cml"
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_structured_config_values("config_values") {
  values_source = "config/default.json5"
  cm_label = ":manifest"
}

fuchsia_structured_config_rust_lib("config_lib") {
  cm_label = ":manifest"
}

group("echo_client_sc") {
  deps = [
    ":component",
    ":config_values",
  ]
}
