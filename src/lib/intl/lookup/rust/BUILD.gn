# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/intl/intl_strings.gni")
import("//build/rust/rustc_staticlib.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("rust") {
  testonly = true
  public_deps = [
    ":lib",
    ":tests",
  ]
}

group("tests") {
  testonly = true
  public_deps = [ ":intl-lookup-rust-tests" ]
}

rustc_staticlib("lib") {
  with_unit_tests = true
  name = "intl_lookup"
  edition = "2018"
  public = [ "lookup.h" ]
  deps = [
    "//src/lib/icu_data/rust/icu_data",
    "//src/lib/intl/model:lib",
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:rust_icu_common",
    "//third_party/rust_crates:rust_icu_sys",
    "//third_party/rust_crates:rust_icu_uloc",
  ]
  non_rust_deps = [
    "//third_party/icu",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/syslog",
  ]
  test_deps = [
    ":fuchsia.intl.test-rustc",
    ":l10n",
    "//sdk/lib/syslog/cpp:backend_legacy",
  ]

  sources = [ "src/lib.rs" ]
}

test_package("intl-lookup-rust-tests") {
  deps = [
    ":l10n",
    ":lib_test",
  ]

  tests = [
    {
      name = "intl_lookup_staticlib_test"
      dest = "intl_lookup_rust_tests"
    },
  ]

  resources = [
    {
      path = rebase_path("$target_gen_dir/en/l10n.json")
      dest = "assets/locales/en/l10n.json"
    },
    {
      path = rebase_path("$target_gen_dir/fr/l10n.json")
      dest = "assets/locales/fr/l10n.json"
    },
    {
      path = rebase_path("$target_gen_dir/es/l10n.json")
      dest = "assets/locales/es/l10n.json"
    },
    {
      path = rebase_path("//third_party/icu/common/icudtl.dat")
      dest = "icudtl.dat"
    },
  ]
}

intl_strings("l10n") {
  source = "strings.xml"
  source_locale = "en"
  output_locales = [
    "en",
    "fr",
    "es",
  ]
  library = "fuchsia.intl.test"

  # Allow keeping files like strings_fr.xml in the same directory as
  # strings.xml.  This is for testing only, we recommend not using this
  # option and using a separate directory per locale instead.
  same_dir_locales = true
}
