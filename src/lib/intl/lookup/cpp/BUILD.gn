# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/components.gni")

group("cpp") {
  testonly = true
  public_deps = [
    ":intl-lookup-cpp",
    ":tests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":intl-lookup-cpp-tests" ]
}

source_set("lib") {
  sources = [
    "lookup.cc",
    "lookup.h",
  ]

  public_deps = [
    "//src/lib/intl/lookup/rust:lib",
    "//third_party/googletest:gtest_prod",
    "//zircon/public/lib/fit",
  ]
}

executable("bin") {
  output_name = "intl-lookup-cpp"
  testonly = true
  sources = [ "main.cc" ]
  deps = [ ":lib" ]
}

executable("intl_lookup_cpp_unittests") {
  testonly = true
  sources = [ "lookup_test.cc" ]
  deps = [
    ":lib",
    "//garnet/public/lib/gtest",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_package_with_single_component("intl-lookup-cpp") {
  manifest = "meta/intl-lookup-cpp.cmx"
  deps = [ ":bin" ]
  testonly = true
}

fuchsia_unittest_package("intl-lookup-cpp-tests") {
  deps = [ ":intl_lookup_cpp_unittests" ]
}
