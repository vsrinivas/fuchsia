# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/test.gni")

group("tests") {
  testonly = true

  # TODO(71013): Empty for now, since this library is due to be updated.
  deps = []
}

########################################
# Common elements
# TODO(71013): Update and replace.

fidl("fuchsia.fuzzer") {
  sources = [ "fuzzer.fidl" ]
  public_deps = [ "//sdk/fidl/fuchsia.mem" ]
}

source_set("core_lib") {
  sources = [
    "shared-memory.cc",
    "test-input.cc",
  ]
  public_deps = [
    ":fuchsia.fuzzer",
    "//src/lib/fxl",
  ]
  public_configs = [ ":config" ]
}

config("config") {
  include_dirs = [
    ".",
    "//src",
  ]
}

########################################
# Fuzzing engine
# TODO(71013): Update and replace.

source_set("engine_lib") {
  sources = [
    "coverage.cc",
    "data-provider.cc",
    "engine.cc",
  ]
  public_deps = [
    ":core_lib",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
  deps = [ "//zircon/system/ulib/async-loop:async-loop-default" ]
}

########################################
# Fuzz target function/client for code under test
# TODO(71013): Update and replace.

source_set("client_lib") {
  sources = [ "llvm-fuzzer.cc" ]
  public_deps = [
    ":core_lib",
    "//sdk/lib/fidl/cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}

source_set("client_main") {
  sources = [ "llvm-fuzzer-main.cc" ]
  deps = [
    ":client_lib",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

########################################
# Instrumentation proxy for code under test
# TODO(71013): Update and replace.

source_set("proxy_lib") {
  sources = [ "sanitizer-cov-proxy.cc" ]
  public_deps = [ ":proxy_lib_public_deps" ]
  deps = [
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
  public_configs = [ ":config" ]
}

group("proxy_lib_public_deps") {
  public_deps = [
    ":core_lib",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}
