# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/renamed_binary.gni")

executable("bin") {
  output_name = "guest"

  sources = [
    "balloon.cc",
    "balloon.h",
    "launch.cc",
    "launch.h",
    "list.cc",
    "list.h",
    "main.cc",
    "serial.cc",
    "serial.h",
    "services.cc",
    "services.h",
    "socat.cc",
    "socat.h",
    "vshc.cc",
    "vshc.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.virtualization",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//src/virtualization/lib/guest_config",
    "//src/virtualization/lib/vsh:util",
    "//src/virtualization/third_party/vm_tools:vsh_cc_proto",
    "//third_party/protobuf:protobuf_lite",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/virtio",
  ]

  public_deps = [ "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_llcpp" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

renamed_binary("vsh-bin") {
  dest = "bin/vsh"
  source = "$root_out_dir/guest"
  source_deps = [ ":bin" ]
  keep_original = true
}

fuchsia_shell_package("guest") {
  deps = [
    ":bin",
    ":vsh-bin",
  ]
}
