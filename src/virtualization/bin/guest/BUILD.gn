# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("guest") {
  deps = [
    ":guest_package",

    # TODO(fxbug.dev/89427): Remove this once vsh is migrated to rust.
    "//src/virtualization/bin/vsh",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":guest_tests" ]
}

rustc_binary("bin") {
  edition = "2021"
  output_name = "guest"
  deps = [
    "//sdk/fidl/fuchsia.virtualization:fuchsia.virtualization_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/lib/zircon/rust:fuchsia-zircon-status",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:prettytable-rs",
  ]

  test_deps = [
    "//src/lib/async-utils",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:pretty_assertions",
  ]

  with_unit_tests = true

  sources = [
    "src/arguments.rs",
    "src/balloon.rs",
    "src/launch.rs",
    "src/list.rs",
    "src/main.rs",
    "src/serial.rs",
    "src/services.rs",
    "src/socat.rs",
    "src/stop.rs",
    "src/vsockperf.rs",
    "src/wipe.rs",
  ]
}

fuchsia_unittest_package("guest_tests") {
  deps = [ ":bin_test" ]
}

fuchsia_shell_package("guest_package") {
  package_name = "guest"
  deps = [ ":bin" ]
}
