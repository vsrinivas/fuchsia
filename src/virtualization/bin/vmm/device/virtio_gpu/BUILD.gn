# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [ ":virtio_gpu_unittests" ]
}

_common_source_root = "src/main.rs"

_common_sources = [
  "src/gpu_command.rs",
  "src/gpu_device.rs",
  "src/resource.rs",
  "src/scanout.rs",
  "src/wire.rs",
  _common_source_root,
]

_common_deps = [
  "//sdk/fidl/fuchsia.element:fuchsia.element_rust",
  "//sdk/fidl/fuchsia.math:fuchsia.math_rust",
  "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_rust",
  "//sdk/fidl/fuchsia.ui.input3:fuchsia.ui.input3_rust",
  "//sdk/fidl/fuchsia.ui.views:fuchsia.ui.views_rust",
  "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware_rust",
  "//src/lib/async-utils",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fuchsia",
  "//src/lib/fuchsia-async",
  "//src/lib/fuchsia-component",
  "//src/lib/mapped-vmo",
  "//src/lib/ui/fuchsia-scenic",
  "//src/lib/zircon/rust:fuchsia-zircon",
  "//src/virtualization/lib/machina-virtio-device",
  "//src/virtualization/lib/virtio-device",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:tracing",
  "//third_party/rust_crates:zerocopy",
]

rustc_binary("virtio_gpu_bin") {
  edition = "2021"

  source_root = _common_source_root

  sources = _common_sources

  deps = _common_deps + [
           "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_rust",
           "//src/lib/ui/fuchsia-framebuffer",
         ]
}

rustc_test("virtio_gpu_bin_test") {
  edition = "2021"

  source_root = _common_source_root

  sources = _common_sources

  deps = _common_deps
}

fuchsia_unittest_package("virtio_gpu_unittests") {
  deps = [ ":virtio_gpu_bin_test" ]
}

fuchsia_package("virtio_gpu") {
  package_name = "virtio_gpu"
  deps = [ ":virtio_gpu_component" ]
}

fuchsia_component("virtio_gpu_component") {
  manifest = "meta/virtio_gpu.cml"
  deps = [ ":virtio_gpu_bin" ]
  component_name = "virtio_gpu"
}
