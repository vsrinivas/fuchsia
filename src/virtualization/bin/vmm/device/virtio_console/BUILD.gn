# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true
  deps = [ ":virtio_console_unittests" ]
}

rustc_binary("virtio_console_bin") {
  name = "virtio_console_bin"
  edition = "2021"
  with_unit_tests = true

  sources = [
    "src/device.rs",
    "src/main.rs",
    "src/wire.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.virtualization.hardware:fuchsia.virtualization.hardware_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/virtualization/lib/machina-virtio-device",
    "//src/virtualization/lib/virtio-device",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/lib/async-utils",
    "//third_party/rust_crates:rand",
  ]
}

fuchsia_unittest_package("virtio_console_unittests") {
  deps = [ ":virtio_console_bin_test" ]
}

fuchsia_package("virtio_console") {
  package_name = "virtio_console"
  deps = [ ":virtio_console_component" ]
}

fuchsia_component("virtio_console_component") {
  manifest = "meta/virtio_console.cml"
  deps = [ ":virtio_console_bin" ]
  component_name = "virtio_console"
}
