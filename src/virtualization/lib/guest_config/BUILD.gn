# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":guest-config-tests" ]
}

source_set("guest_config") {
  sources = [
    "guest_config.cc",
    "guest_config.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//third_party/rapidjson",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.virtualization:fuchsia.virtualization_hlcpp",
    "//zircon/system/ulib/zxc",
  ]
}

executable("unittests") {
  testonly = true
  output_name = "guest_config_unittest"

  sources = [ "guest_config_unittest.cc" ]

  deps = [
    ":guest_config",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_unittest_package("guest-config-tests") {
  deps = [ ":unittests" ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
