# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")
import("//src/virtualization/buildtools/linux_paths.gni")
import("//src/virtualization/buildtools/romfs_image.gni")
import("//third_party/Vulkan-Loader/arg.gni")

# Packages required to run the guest integration tests.
guest_packages = [
  "//src/virtualization/bin",
  "//src/virtualization/packages",
]

# TODO(fxbug.dev/50820): Fix on ASan and switch back to [ nuc_env ].
guest_environments = nuc_env_fails_on_asan + [
                       sherlock_env,
                       vim3_env,
                     ]

linux_tests_label = "//src/virtualization/packages/debian_guest:tests"
resource("debian_tests_img") {
  sources = [ get_label_info(linux_tests_label, "target_out_dir") +
              "/debian/tests.img" ]
  outputs = [ "data/linux_tests.img" ]
}

group("tests") {
  testonly = true
  deps = [
    ":guest_unit_tests",
    ":virtualization_block_tests_pkg",
    ":virtualization_core_tests_pkg",
    ":virtualization_net_tests_pkg",
    ":virtualization_vsock_tests_pkg",
    "hypervisor",
  ]

  # TODO(fxbug.dev/12616): These tests currently do not work on arm64.
  if (current_cpu == "x64") {
    deps += [
      # TODO(fxbug.dev/64348): These tests needs to be reworked so that they
      # interact correctly with Scenic.
      # ":virtualization_gpu_tests_pkg",
      # ":virtualization_input_tests_pkg",
    ]
  }

  # Ensure guest packages are available in the manifest.
  data_deps = guest_packages
}

source_set("logger") {
  visibility = [ ":*" ]
  sources = [
    "logger.cc",
    "logger.h",
  ]
  configs += [
    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}

source_set("periodic_logger") {
  visibility = [ ":*" ]
  sources = [
    "periodic_logger.cc",
    "periodic_logger.h",
  ]
  deps = [ "//src/lib/fxl" ]
  public_deps = [ "//zircon/public/lib/zx" ]
}

source_set("socket_logger") {
  visibility = [ ":*" ]
  sources = [
    "socket_logger.cc",
    "socket_logger.h",
  ]
  public_deps = [
    ":logger",
    "//src/lib/fsl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}

source_set("guest_console") {
  visibility = [
    "//src/connectivity/network/testing/netemul/*",
    "//src/virtualization/*",
  ]
  sources = [
    "guest_console.cc",
    "guest_console.h",
    "socket.cc",
    "socket.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.virtualization",
    "//src/lib/fxl",
  ]
  public_deps = [
    ":logger",
    "//zircon/public/lib/zx",
  ]
}

source_set("fake_netstack") {
  visibility = [
    "//src/connectivity/network/testing/netemul/*",
    "//src/virtualization/*",
  ]
  sources = [
    "fake_netstack.cc",
    "fake_netstack.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.net.interfaces",
    "//sdk/fidl/fuchsia.netstack",
    "//src/lib/inet",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.netstack",
    "//sdk/lib/fit-promise",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

source_set("fake_scenic") {
  visibility = [ ":*" ]
  testonly = true
  sources = [
    "fake_scenic.cc",
    "fake_scenic.h",
  ]

  deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.ui.gfx",
    "//garnet/public/lib/fostr/fidl/fuchsia.ui.scenic",
    "//src/lib/fxl",
  ]
  public_deps = [ "//sdk/fidl/fuchsia.ui.scenic" ]
}

executable("unit_tests") {
  testonly = true
  output_name = "guest_unit_tests"
  sources = [
    "guest_console_test.cc",
    "periodic_logger_test.cc",
  ]
  deps = [
    ":guest_console",
    ":periodic_logger",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

source_set("test_main") {
  testonly = true
  visibility = [ ":*" ]
  sources = [ "test_main.cc" ]
  deps = [
    ":logger",
    "//src/lib/fxl/test:test_settings",
    "//third_party/googletest:gtest",
  ]
}

source_set("lib") {
  visibility = [ ":*" ]
  testonly = true
  sources = [
    "enclosed_guest.cc",
    "enclosed_guest.h",
    "guest_test.h",
  ]

  deps = [
    ":guest_console",
    ":periodic_logger",
    ":socket_logger",
    "//sdk/fidl/fuchsia.kernel",
    "//sdk/fidl/fuchsia.sysinfo",
    "//sdk/lib/fit-promise",
    "//zircon/system/ulib/zxc",
  ]
  public_deps = [
    ":fake_netstack",
    ":fake_scenic",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/virtualization/lib/grpc",
    "//src/virtualization/lib/vsh:client",
    "//src/virtualization/third_party/vm_tools:vm_cc_proto",
    "//zircon/public/lib/async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_unittest_package("guest_unit_tests") {
  deps = [ ":unit_tests" ]
}

executable("virtualization_core_tests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "core_tests.cc" ]

  deps = [
    ":guest_console",
    ":lib",
    ":test_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_test_component("virtualization_core_tests_cmp") {
  component_name = "virtualization_core_tests"
  deps = [
           ":debian_tests_img",
           ":extras",
           ":linux_extras_img",
           ":virtualization_core_tests",
           linux_tests_label,
         ] + guest_packages
  manifest = "meta/virtualization_core_tests.cmx"
}

fuchsia_test_package("virtualization_core_tests_pkg") {
  package_name = "virtualization-core-tests"
  test_components = [ ":virtualization_core_tests_cmp" ]
  test_specs = {
    environments = guest_environments
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("virtualization_block_tests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "virtio_block_tests.cc" ]

  deps = [
    ":guest_console",
    ":lib",
    ":test_main",
    "//src/virtualization/bin/vmm/device:qcow_test_data",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_test_component("virtualization_block_tests_cmp") {
  component_name = "virtualization_block_tests"
  deps = [
           ":debian_tests_img",
           ":virtualization_block_tests",
           linux_tests_label,
         ] + guest_packages
  manifest = "meta/virtualization_block_tests.cmx"
}

fuchsia_test_package("virtualization_block_tests_pkg") {
  package_name = "virtualization-block-tests"
  test_components = [ ":virtualization_block_tests_cmp" ]
  test_specs = {
    environments = guest_environments
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("virtualization_gpu_tests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "virtio_gpu_tests.cc" ]

  deps = [
    ":guest_console",
    ":lib",
    ":periodic_logger",
    ":test_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_test_component("virtualization_gpu_tests_cmp") {
  component_name = "virtualization_gpu_tests"
  deps = [
           ":debian_tests_img",
           ":virtualization_gpu_tests",
           linux_tests_label,
         ] + guest_packages
  manifest = "meta/virtualization_gpu_tests.cmx"
}

fuchsia_test_package("virtualization_gpu_tests_pkg") {
  package_name = "virtualization-gpu-tests"
  test_components = [ ":virtualization_gpu_tests_cmp" ]
  test_specs = {
    environments = guest_environments
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("virtualization_input_tests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "virtio_input_tests.cc" ]

  deps = [
    ":guest_console",
    ":lib",
    ":test_main",
  ]
}

fuchsia_test_component("virtualization_input_tests_cmp") {
  component_name = "virtualization_input_tests"
  deps = [
           ":debian_tests_img",
           ":virtualization_input_tests",
           linux_tests_label,
         ] + guest_packages
  manifest = "meta/virtualization_input_tests.cmx"
}

fuchsia_test_package("virtualization_input_tests_pkg") {
  package_name = "virtualization-input-tests"
  test_components = [ ":virtualization_input_tests_cmp" ]
  test_specs = {
    environments = guest_environments
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("virtualization_net_tests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "virtio_net_tests.cc" ]

  deps = [
    ":guest_console",
    ":lib",
    ":test_main",
    "//src/lib/inet",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_test_component("virtualization_net_tests_cmp") {
  component_name = "virtualization_net_tests"
  deps = [
           ":debian_tests_img",
           ":virtualization_net_tests",
           linux_tests_label,
         ] + guest_packages
  manifest = "meta/virtualization_net_tests.cmx"
}

fuchsia_test_package("virtualization_net_tests_pkg") {
  package_name = "virtualization-net-tests"
  test_components = [ ":virtualization_net_tests_cmp" ]
  test_specs = {
    environments = guest_environments
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("virtualization_vsock_tests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "virtio_vsock_tests.cc" ]

  deps = [
    ":guest_console",
    ":lib",
    ":test_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_test_component("virtualization_vsock_tests_cmp") {
  component_name = "virtualization_vsock_tests"
  deps = [
           ":debian_tests_img",
           ":virtualization_vsock_tests",
           linux_tests_label,
         ] + guest_packages
  manifest = "meta/virtualization_vsock_tests.cmx"
}

fuchsia_test_package("virtualization_vsock_tests_pkg") {
  package_name = "virtualization-vsock-tests"
  test_components = [ ":virtualization_vsock_tests_cmp" ]
  test_specs = {
    environments = guest_environments
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_component("virtio_block_test_util_cmp") {
  component_name = "virtio_block_test_util"
  deps = [ "virtio_block_test_util" ]
  manifest = "virtio_block_test_util/meta/virtio_block_test_util.cmx"
}

fuchsia_component("virtio_rng_test_util_cmp") {
  component_name = "virtio_rng_test_util"
  deps = [ "virtio_rng_test_util" ]
  manifest = "virtio_rng_test_util/meta/virtio_rng_test_util.cmx"
}

fuchsia_component("virtio_net_test_util_cmp") {
  component_name = "virtio_net_test_util"
  deps = [ "virtio_net_test_util" ]
  manifest = "virtio_net_test_util/meta/virtio_net_test_util.cmx"
}

fuchsia_component("virtio_vsock_test_util_cmp") {
  component_name = "virtio_vsock_test_util"
  deps = [ "virtio_vsock_test_util" ]
  manifest = "virtio_vsock_test_util/meta/virtio_vsock_test_util.cmx"
}

fuchsia_package("virtualization_test_utils_pkg") {
  package_name = "virtualization-test-utils"
  deps = [
    ":virtio_block_test_util_cmp",
    ":virtio_net_test_util_cmp",
    ":virtio_rng_test_util_cmp",
    ":virtio_vsock_test_util_cmp",
  ]
}

icd_filename = "libvulkan_intel.so"
icd_manifest = "${target_gen_dir}/magma.json"

action("icd_manifest") {
  visibility = [ ":*" ]
  script = "//src/graphics/lib/magma/scripts/gen_icd_manifest.sh"
  args = [
    "${icd_filename}",
    rebase_path(icd_manifest, root_build_dir),
  ]
  outputs = [ icd_manifest ]
}

romfs_image("extras") {
  testonly = true

  deps = [
    ":icd_manifest",
    "//src/graphics/lib/magma/tests/integration:virtmagma_abi_conformance_tests(${linux_exe_toolchain})",
    "//src/graphics/lib/magma/tests_linux/unit_tests:virtmagma_unit_tests_linux(${linux_exe_toolchain})",
    "//src/graphics/lib/magma/tests_linux/unit_tests:virtmagma_vulkan_unit_tests(${linux_exe_toolchain})",
    "//src/lib/vulkan:vulkan_headers_and_loader(${linux_lib_toolchain})",
  ]

  inputs = [
    "${icd_manifest}",
    "${linux_exe_path}/virtmagma_unit_tests_linux",
    "${linux_exe_path}/virtmagma_vulkan_unit_tests",
    "${linux_exe_path}/virtmagma_abi_conformance_tests",
  ]
  if (output_libvulkan_so_1) {
    inputs += [
      "${linux_lib_path}/libvulkan.so.1",  # Vulkan Loader
    ]
  } else {
    inputs += [
      "${linux_lib_path}/libvulkan.so",  # Vulkan Loader
    ]
  }

  if (current_cpu == "x64") {
    deps += [ "//third_party/mesa/src/intel/vulkan:vulkan_intel(${linux_lib_toolchain})" ]

    inputs += [ "${linux_lib_path}/${icd_filename}" ]
  }

  output = "$target_out_dir/linux/extras.img"
}

# This should be merged into |guest_integration_tests| once the termina guest is stable in CQ.
executable("termina_integration_tests_bin") {
  visibility = [ ":*" ]
  output_name = "termina_integration_tests"
  testonly = true

  sources = [
    "virtio_magma_tests.cc",
    "virtio_wl_tests.cc",
  ]

  deps = [
    ":lib",
    ":test_main",
    "//third_party/googletest:gtest",
  ]
}

group("termina") {
  testonly = true
  deps = guest_packages
  deps += [ ":termina_integration_tests" ]
}

resource("linux_extras_img") {
  sources = [ "$target_out_dir/linux/extras.img" ]
  outputs = [ "data/extras.img" ]
}

fuchsia_test_component("termina_integration_tests_cmp") {
  component_name = "termina_integration_tests"
  deps = [
    ":debian_tests_img",
    ":extras",
    ":linux_extras_img",
    ":termina_integration_tests_bin",
    linux_tests_label,
  ]
  manifest = "meta/termina_integration_tests.cmx"
}

fuchsia_test_package("termina_integration_tests") {
  test_components = [ ":termina_integration_tests_cmp" ]
  test_specs = {
    environments = [ nuc_env ]
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
