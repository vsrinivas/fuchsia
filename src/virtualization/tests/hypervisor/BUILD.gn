# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/virtualization/buildtools/linux_paths.gni")
import("//src/virtualization/buildtools/romfs_image.gni")

# List of environments that support the hypervisor.
hypervisor_environments = [
  astro_env,
  nelson_env,
  nuc_env,
  sherlock_env,
  vim3_env,
]

executable("hypervisor_tests") {
  testonly = true

  sources = [
    # Common code.
    "common_tests.cc",
    "constants.h",
    "main.cc",

    # Arch-specific code.
    "arch/${target_cpu}/arch.cc",
    "arch/${target_cpu}/arch_tests.cc",
    "arch/${target_cpu}/guest_code.S",
  ]

  deps = [
    "//sdk/fidl/fuchsia.kernel",
    "//sdk/fidl/fuchsia.sysinfo",
    "//sdk/lib/fdio",
    "//src/lib/fxl/test:test_settings",
    "//src/virtualization/bin/vmm:bits",
    "//third_party/googletest:gtest",
    "//zircon/kernel/phys/lib/page-table",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxc",
  ]
}

fuchsia_test_component("hypervisor_tests_cmp") {
  component_name = "hypervisor_tests"
  deps = [ ":hypervisor_tests" ]
  manifest = "meta/hypervisor_tests.cmx"
}

fuchsia_test_package("hypervisor_tests_pkg") {
  package_name = "hypervisor-tests-pkg"
  test_components = [ ":hypervisor_tests_cmp" ]
  test_specs = {
    environments = hypervisor_environments
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("hypervisor") {
  testonly = true

  deps = [ ":hypervisor_tests_pkg" ]
}
