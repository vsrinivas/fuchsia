# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//third_party/cobalt_config/metrics_registry.gni")

group("testapp") {
  testonly = true
  deps = [
    ":cobalt_testapp",
    ":prober_metrics_registry",
    ":testapp_metrics_registry",
  ]
}

metrics_registry("testapp_metrics_registry") {
  # This must match the name of our Cobalt project as specified in:
  # https://cobalt-analytics.googlesource.com/config/+/refs/heads/master/projects.yaml
  project_name = "test_app2"
  namespace = "cobalt_registry"
  generate_cc = true
  generate_binarypb = false
}

metrics_registry("prober_metrics_registry") {
  # This must match the name of our Cobalt project as specified in:
  # https://cobalt-analytics.googlesource.com/config/+/refs/heads/master/projects.yaml
  project_name = "prober"
  namespace = "cobalt_prober_registry"
  generate_cc = true
  generate_binarypb = false
}

executable("cobalt_testapp") {
  output_name = "cobalt_testapp"
  sources = [
    "cobalt_testapp.cc",
    "cobalt_testapp.h",
    "cobalt_testapp_logger.cc",
    "cobalt_testapp_logger.h",
    "cobalt_testapp_main.cc",
    "tests.cc",
    "tests.h",
  ]

  deps = [
    ":prober_metrics_registry_cc",
    ":testapp_metrics_registry_cc",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/svc/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/cobalt/cpp:cobalt_event_builder",
    "//src/lib/fxl",
    "//third_party/cobalt/config:cobalt_registry_proto",
    "//third_party/cobalt/config:id",
    "//third_party/cobalt/util:clock",
    "//third_party/cobalt/util:datetime_util",
    "//third_party/cobalt/util/crypto_util",
    "//zircon/public/fidl/fuchsia-cobalt",
    "//zircon/public/lib/async-loop-cpp",
  ]

  configs += [ "//third_party/cobalt:cobalt_config" ]
}
