# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//third_party/cobalt/metrics_registry.gni")
import("//third_party/protobuf/proto_library.gni")

metrics_registry("metrics_registry") {
  project_name = "fuchsia_system_metrics"
  namespace = "fuchsia_system_metrics"
  generate_cc = true
  generate_binarypb = false
}

source_set("system_metrics_daemon_lib") {
  sources = [
    "activity_listener.cc",
    "activity_listener.h",
    "cpu_stats_fetcher.h",
    "cpu_stats_fetcher_impl.cc",
    "cpu_stats_fetcher_impl.h",
    "system_metrics_daemon.cc",
    "system_metrics_daemon.h",
    "temperature_fetcher.h",
    "temperature_fetcher_impl.cc",
    "temperature_fetcher_impl.h",
  ]

  public_deps = [
    ":metrics_registry_cc",
    "//garnet/public/lib/syslog/cpp",
    "//sdk/fidl/fuchsia.ui.activity",
    "//sdk/lib/sys/cpp",
    "//src/cobalt/bin/utils:clock",
    "//src/lib/cobalt/cpp:cobalt_event_builder",
    "//src/lib/fsl",
    "//src/lib/syslog/cpp",
    "//third_party/cobalt/src/registry:buckets_config",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/trace",
    "//zircon/public/lib/trace-provider-with-fdio",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-cobalt",
    "//zircon/system/fidl/fuchsia-hardware-thermal:fuchsia-hardware-thermal_c",
    "//zircon/system/fidl/fuchsia-kernel:llcpp",
  ]
}

executable("system-metrics") {
  output_name = "cobalt_system_metrics_bin"

  sources = [
    "system_metrics_main.cc",
  ]

  deps = [
    ":system_metrics_daemon_lib",
    "//sdk/lib/sys/cpp",
  ]
}

package("cobalt_system_metrics") {
  deps = [
    ":system-metrics",
  ]

  meta = [
    {
      path = rebase_path("meta/cobalt_system_metrics.cmx")
      dest = "cobalt_system_metrics.cmx"
    },
  ]

  binary = "cobalt_system_metrics_bin"
}

executable("cobalt_system_metrics_unittests") {
  testonly = true

  sources = [
    "system_metrics_daemon_test.cc",
  ]

  deps = [
    ":system_metrics_daemon_lib",
    "testing:fake_fetchers",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/cobalt/bin/testing:fake_clock_lib",
    "//src/cobalt/bin/testing:fake_logger_lib",
    "//src/cobalt/bin/utils:clock",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/fidl/fuchsia-cobalt",
  ]
}
