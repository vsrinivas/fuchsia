# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")
import("//build/test/test_package.gni")

source_set("lib") {
  sources = [
    "app.cc",
    "app.h",
    "fidl/stack_impl.cc",
    "fidl/stack_impl.h",
    "fidl/stack_utils.cc",
    "fidl/stack_utils.h",
  ]

  public_deps = [
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/connectivity/weave/adaptation",
    "//src/lib/fsl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("bin") {
  output_name = "weavestack"

  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//sdk/lib/syslog/cpp:backend_legacy",
  ]
}

group("weavestack") {
  public_deps = [ ":pkg" ]
}

package("pkg") {
  package_name = "weavestack"
  deps = [ ":bin" ]

  binaries = [
    {
      name = "weavestack"
    },
  ]

  meta = [
    {
      path = rebase_path("../meta/weavestack.cmx")
      dest = "weavestack.cmx"
    },
  ]

  resources = [
    {
      path = rebase_path("data/default_environment_schema.json")
      dest = "default_environment_schema.json"
    },
  ]
}

config_data("config") {
  for_pkg = "weavestack"
  sources = [
    "config/default_environment.json",
    "config/device_info.json",
  ]
}

config_data("sysmgr_config") {
  for_pkg = "sysmgr"
  sources = [ "config/weavestack_sysmgr.config" ]
  outputs = [ "weavestack.config" ]
}
