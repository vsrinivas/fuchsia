# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_binary("bin") {
  name = "bt_le_central"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth-rustc",
    "//sdk/fidl/fuchsia.bluetooth.gatt:fuchsia.bluetooth.gatt-rustc",
    "//sdk/fidl/fuchsia.bluetooth.le:fuchsia.bluetooth.le-rustc",
    "//src/connectivity/bluetooth/lib/bt-fidl-mocks",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:getopts",
    "//third_party/rust_crates:num",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates/transitional/rustyline",
  ]

  sources = [
    "src/central.rs",
    "src/gatt.rs",
    "src/gatt/commands.rs",
    "src/gatt/repl.rs",
    "src/gatt/types.rs",
    "src/main.rs",
  ]
}

package("bt-le-central") {
  name = "bt_le_central"

  deps = [ ":bin" ]

  binaries = [
    {
      name = "bt_le_central"
      dest = "bt-le-central"
      shell = true
    },
  ]
}

test_package("tests") {
  package_name = "bt-le-central-tests"

  deps = [ ":bin_test" ]

  tests = [
    {
      name = "bt_le_central_bin_test"
      dest = "bt-le-central-unittests"
      environments = basic_envs
    },
  ]
}
