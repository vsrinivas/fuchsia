# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("bin") {
  output_name = "bt_avrcp_integration_tests"
  edition = "2018"
  source_root = "src/main.rs"

  deps = [
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth-rustc",
    "//sdk/fidl/fuchsia.bluetooth.avrcp:fuchsia.bluetooth.avrcp-rustc",
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr-rustc",
    "//src/connectivity/bluetooth/lib/bt-avctp",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/connectivity/bluetooth/testing/mock-piconet-server:lib",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/testing/fixture",
    "//src/lib/trace/rust:trace",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "src/main.rs" ]

  configs -= [ "//build/config/rust:allow_unused_results" ]
}

fuchsia_test_component("component") {
  manifest = "meta/bt-avrcp-integration-tests.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("bt-avrcp-integration-tests") {
  test_components = [ ":component" ]

  test_specs = {
    # The profile integration tests tend to flake if tests are run in parallel.
    parallel = 1
  }

  deps = [
    "//src/connectivity/bluetooth/profiles/bt-avrcp:component",
    "//src/connectivity/bluetooth/testing/mock-piconet-server",
    "//src/lib/fuchsia-component-test/framework_intermediary:fuchsia_component_test_framework_intermediary_component",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":bt-avrcp-integration-tests" ]
}
