# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_binary("bin") {
  name = "bt_a2dp_source"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//src/lib/fdio/rust:fdio",
    "//garnet/public/rust/fuchsia-syslog",
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth-rustc",
    "//sdk/fidl/fuchsia.bluetooth.avdtp:fuchsia.bluetooth.avdtp-rustc",
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr-rustc",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//sdk/fidl/fuchsia.mediacodec:fuchsia.mediacodec-rustc",
    "//src/connectivity/bluetooth/lib/bt-a2dp",
    "//src/connectivity/bluetooth/lib/bt-avdtp",
    "//src/connectivity/bluetooth/lib/fuchsia-audio-device-output",
    "//src/connectivity/bluetooth/lib/fuchsia-audio-encoder",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/lib/argh",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:bitfield",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:thiserror",
  ]

  test_deps = [
    "//src/lib/mundane",
    "//third_party/rust_crates:hex",
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth-rustc",
  ]
}

package("bt-a2dp-source") {
  deps = [
    ":bin",
  ]

  binary = "bt_a2dp_source"

  meta = [
    {
      path = rebase_path("meta/bt-a2dp-source.cmx")
      dest = "bt-a2dp-source.cmx"
    },
  ]
}

test_package("bt-a2dp-source-tests") {
  deps = [
    ":bin_test",
  ]

  tests = [
    {
      name = "bt_a2dp_source_bin_test"
      dest = "bt-a2dp-source-unittests"
      environments = basic_envs
    },
  ]
}
