# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")

driver_bind_rules("bt_hog_bind") {
  rules = "bt_hog.bind"
  header_output = "bt_hog_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.bluetooth" ]
}

driver_module("driver_module") {
  output_name = "bt-hog"

  sources = [
    "boot_descriptors.h",
    "hog.c",
    "hog.h",
  ]

  deps = [
    ":bt_hog_bind",
    "//sdk/banjo/fuchsia.hardware.bt.gattsvc:fuchsia.hardware.bt.gattsvc_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//src/connectivity/bluetooth/lib/device-protocol-bt-gatt-svc",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/public/lib/zx",
  ]
}

fuchsia_driver_component("driver") {
  component_name = "bt-hog"
  deps = [ ":driver_module" ]
}

fuchsia_system_package("bt-hog") {
  deps = [ ":driver" ]

  # This package is reachable from //garnet/packages/prod:all
  allowed_in_extra_deps = true
}

group("tests") {
  testonly = true
  deps = [ ":bt_hog_bind_test" ]
}
