# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  edition = "2021"
  output_name = "bt_avrcp_target"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.bluetooth.avrcp:fuchsia.bluetooth.avrcp_rust",
    "//sdk/fidl/fuchsia.media:fuchsia.media_rust",
    "//sdk/fidl/fuchsia.media.sessions2:fuchsia.media.sessions2_rust",
    "//src/connectivity/bluetooth/fidl:component_rust",
    "//src/connectivity/bluetooth/lib/async-helpers",
    "//src/connectivity/bluetooth/lib/battery-client",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fidl_table_validation",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [ "//third_party/rust_crates:assert_matches" ]

  sources = [
    "src/avrcp_handler.rs",
    "src/battery_client.rs",
    "src/main.rs",
    "src/media/media_sessions.rs",
    "src/media/media_state.rs",
    "src/media/media_types.rs",
    "src/media/mod.rs",
    "src/tests/mod.rs",
    "src/types/bounded_queue.rs",
    "src/types/mod.rs",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("component") {
  component_name = "bt-avrcp-target"
  manifest = "meta/bt-avrcp-target.cml"
  deps = [ ":bin" ]
}

fuchsia_package("bt-avrcp-target") {
  deps = [ ":component" ]
}

fuchsia_unittest_package("bt-avrcp-target-tests") {
  deps = [ ":bin_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":bt-avrcp-target-tests" ]
}
