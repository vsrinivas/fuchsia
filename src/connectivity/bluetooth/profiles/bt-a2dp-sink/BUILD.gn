# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//third_party/cobalt/metrics_registry.gni")
import("//third_party/protobuf/proto_library.gni")

metrics_registry("bt_a2dp_sink_metrics") {
  project_name = "bluetooth"
  generate_rust = true
  generate_binarypb = false
}

rustc_binary("bin") {
  name = "bt_a2dp_sink"
  edition = "2018"
  with_unit_tests = true

  deps = [
    ":bt_a2dp_sink_metrics_rustlib",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth-rustc",
    "//sdk/fidl/fuchsia.bluetooth.avdtp:fuchsia.bluetooth.avdtp-rustc",
    "//sdk/fidl/fuchsia.bluetooth.avrcp:fuchsia.bluetooth.avrcp-rustc",
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr-rustc",
    "//sdk/fidl/fuchsia.media:fuchsia.media-rustc",
    "//sdk/fidl/fuchsia.media.playback:fuchsia.media.playback-rustc",
    "//sdk/fidl/fuchsia.media.sessions2:fuchsia.media.sessions2-rustc",
    "//src/connectivity/bluetooth/lib/bt-a2dp",
    "//src/connectivity/bluetooth/lib/bt-avdtp",
    "//src/connectivity/bluetooth/lib/bt-avdtp",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/lib/cobalt/rust:fuchsia-cobalt",
    "//src/lib/fidl_table_validation",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/inspect/rust/fuchsia-inspect",
    "//src/lib/inspect/rust/fuchsia-inspect-contrib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:bitfield",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:serde_derive",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:slab",
    "//third_party/rust_crates:thiserror",
    "//zircon/system/fidl/fuchsia-cobalt:fuchsia-cobalt-rustc",
    "//zircon/system/fidl/fuchsia-mem:fuchsia-mem-rustc",
  ]
}

package("bt-a2dp-sink") {
  deps = [
    ":bin",
    ":bt_a2dp_sink_metrics",
  ]

  binary = "bt_a2dp_sink"

  meta = [
    {
      path = rebase_path("meta/bt-a2dp-sink.cmx")
      dest = "bt-a2dp-sink.cmx"
    },
  ]
}

config_data("startup_config") {
  for_pkg = "sysmgr"
  outputs = [
    "bt-a2dp-startup.config",
  ]
  sources = [
    "startup.config",
  ]
}

test_package("tests") {
  package_name = "bt-a2dp-sink-tests"

  deps = [
    ":bin_test",
  ]

  tests = [
    {
      name = "bt_a2dp_sink_bin_test"
      dest = "bt-a2dp-sink-unittests"
      environments = basic_envs
    },
  ]
}
