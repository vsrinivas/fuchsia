# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  output_name = "bt_init"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth-rustc",
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr-rustc",
    "//sdk/fidl/fuchsia.bluetooth.gatt:fuchsia.bluetooth.gatt-rustc",
    "//sdk/fidl/fuchsia.bluetooth.le:fuchsia.bluetooth.le-rustc",
    "//sdk/fidl/fuchsia.bluetooth.rfcomm.test:fuchsia.bluetooth.rfcomm.test-rustc",
    "//sdk/fidl/fuchsia.bluetooth.snoop:fuchsia.bluetooth.snoop-rustc",
    "//sdk/fidl/fuchsia.bluetooth.sys:fuchsia.bluetooth.sys-rustc",
    "//sdk/fidl/fuchsia.cobalt:fuchsia.cobalt-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "src/config.rs",
    "src/main.rs",
  ]

  configs -= [ "//build/config/rust:allow_unused_results" ]
}

rustc_binary("bin_v2") {
  name = "bt_init_v2"
  edition = "2018"
  source_root = "src/main_v2.rs"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr-rustc",
    "//sdk/fidl/fuchsia.bluetooth.snoop:fuchsia.bluetooth.snoop-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/lib/fuchsia",
    "//src/lib/zircon/rust:fuchsia-zircon",
  ]

  sources = [
    "src/config.rs",
    "src/main_v2.rs",
  ]

  configs -= [ "//build/config/rust:allow_unused_results" ]
}

resource("default-config") {
  sources = [ "config/default.json" ]
  outputs = [ "data/bt-init-default.json" ]
}

fuchsia_package_with_single_component("bt-init") {
  deps = [
    ":bin",
    ":default-config",
  ]
  manifest = "meta/bt-init.cmx"
}

fuchsia_package_with_single_component("bt-init-v2") {
  deps = [
    ":bin_v2",
    ":default-config",
  ]
  manifest = "meta/bt-init.cml"
}

config_data("bootstrap_config") {
  for_pkg = "sysmgr"
  outputs = [ "bt-init-bootstrap.config" ]
  sources = [ "bootstrap.config" ]
}

fuchsia_unittest_package("tests-v1") {
  component_name = "bt-init-unittests"
  package_name = "bt-init-unittests"

  deps = [
    ":bin_test",
    ":default-config",
  ]
}

fuchsia_unittest_package("tests-v2") {
  component_name = "bt-init-unittests-v2"
  package_name = "bt-init-unittests-v2"

  deps = [
    ":bin_v2_test",
    ":default-config",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":tests-v1",
    ":tests-v2",
  ]
}
