# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("bt_bonding_integration_tests") {
  edition = "2021"
  source_root = "src/lib.rs"
  deps = [
    "//sdk/fidl/fuchsia.bluetooth.sys:fuchsia.bluetooth.sys_rust",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/connectivity/bluetooth/testing/bt-test-harness",
    "//src/connectivity/bluetooth/testing/test-harness",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "src/lib.rs" ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_test_component("bt-bonding-integration-tests-component") {
  manifest = "meta/bt-bonding-integration-tests.cml"
  deps = [ ":bt_bonding_integration_tests" ]
}

fuchsia_test_package("bt-bonding-integration-tests") {
  # These tests intentionally provide malformed bonding data to bt-host. Because this data is only
  # expected to come from trusted sources in prod, bt-host logs this failure at an ERROR level.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":bt-bonding-integration-tests-component" ]
  deps = [ "//src/connectivity/bluetooth/hci/virtual" ]
}
