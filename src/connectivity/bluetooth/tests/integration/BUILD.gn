# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("integration_tests_bin") {
  source_root = "src/main.rs"
  deps = [
    "//sdk/fidl/fuchsia.bluetooth:fuchsia.bluetooth-rustc",
    "//sdk/fidl/fuchsia.bluetooth.gatt:fuchsia.bluetooth.gatt-rustc",
    "//sdk/fidl/fuchsia.bluetooth.le:fuchsia.bluetooth.le-rustc",
    "//sdk/fidl/fuchsia.bluetooth.sys:fuchsia.bluetooth.sys-rustc",
    "//sdk/fidl/fuchsia.bluetooth.test:fuchsia.bluetooth.test-rustc",
    "//src/connectivity/bluetooth/fidl:host-rustc",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/connectivity/bluetooth/testing/bt-test-harness",
    "//src/connectivity/bluetooth/testing/hci-emulator-client",
    "//src/connectivity/bluetooth/testing/test-harness",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/storage/fuchsia-vfs-watcher",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:pin-utils",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:slab",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [
    "src/expect.rs",
    "src/main.rs",
    "src/tests/access.rs",
    "src/tests/bootstrap.rs",
    "src/tests/inspect.rs",
    "src/tests/low_energy_central.rs",
    "src/tests/low_energy_peripheral.rs",
    "src/tests/mod.rs",
  ]

  configs -= [ "//build/config/rust:allow_unused_results" ]
}

fuchsia_test_component("bt-integration-tests-component") {
  manifest = "meta/bt-integration-tests.cmx"
  deps = [ ":integration_tests_bin" ]
}

fuchsia_test_package("bt-integration-tests") {
  test_specs = {
    environments = [
      {
        dimensions = {
          device_type = "QEMU"
        }
        tags = [ "flaky" ]  # fxbug.dev/49570
      },
    ]
  }
  test_components = [ ":bt-integration-tests-component" ]
}
