# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")

package("serial-ppp") {
  deprecated_system_image = true
  testonly = false

  deps = [
    ":serial-ppp_driver",
  ]

  drivers = [
    {
      name = "serial-ppp.so"
    },
  ]
}

source_set("driver") {
  sources = [
    "serial-ppp.cc",
    "serial-ppp.h",
  ]

  include_dirs = [ "//src/connectivity/ppp" ]

  public_deps = [
    "//sdk/fidl/fuchsia.net.ppp:fuchsia.net.ppp_llcpp",
    "//src/connectivity/ppp/lib/common",
    "//src/connectivity/ppp/lib/hdlc:frame",
    "//src/devices:driver",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/fidl-llcpp",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.serial",
  ]
}

source_set("driver-test") {
  testonly = true

  sources = [
    "serial-ppp-test.cc",
  ]

  include_dirs = [ "//src/connectivity/ppp" ]

  deps = [
    ":driver",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

driver_module("serial-ppp_driver") {
  output_name = "serial-ppp"
  testonly = false

  include_dirs = [ "//src/connectivity/ppp" ]

  deps = [
    ":driver",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}
