# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("usb_qmi_function_bind") {
  rules = "usb_qmi_function.bind"
  header_output = "usb_qmi_function_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.usb" ]
}

group("usb-qmi-function") {
  testonly = true
  deps = [ ":tests" ]
}

group("tests") {
  testonly = true
  deps = [ ":usb_qmi_function_bind_test" ]
}

fuchsia_driver("qmi_function-driver") {
  output_name = "qmi-function"
  testonly = true

  sources = [
    "usb-qmi-function.cc",
    "usb-qmi-function.h",
  ]
  deps = [
    ":usb_qmi_function_bind",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/lib/fdio",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:cdc",
    "//src/devices/usb/lib/usb:peripheral",
    "//src/lib/ddk",
    "//zircon/system/ulib/zx",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("qmi_function") {
  testonly = true
  component_name = "qmi-function"
  deps = [ ":qmi_function-driver" ]
  info = "qmi_function-info.json"
  colocate = true
}
