# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")

fuchsia_system_package("at-fake-transport") {
  testonly = true

  deps = [ ":fake_at_driver" ]
}

driver_bind_rules("at_fake_bind") {
  rules = "at_fake.bind"
  header_output = "at_fake_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/test.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":at_fake_bind_test" ]
}

driver_module("fake_at_driver-driver") {
  output_name = "at-fake-transport"
  testonly = true

  sources = [
    "driver.cc",
    "fake-device.cc",
    "fake-device.h",
  ]

  deps = [
    ":at_fake_bind",
    "//sdk/banjo/fuchsia.hardware.test:fuchsia.hardware.test_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.telephony.transport:fuchsia.hardware.telephony.transport_llcpp",
    "//sdk/fidl/fuchsia.telephony.snoop:fuchsia.telephony.snoop_llcpp",
    "//sdk/lib/fdio",
    "//src/connectivity/telephony/tests/fake-drivers/fake-transport-base",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("fake_at_driver") {
  testonly = true
  component_name = "at-fake-transport"
  deps = [ ":fake_at_driver-driver" ]
}
