# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

fuchsia_driver_package("qmi-usb-transport") {
  driver_components = [ ":qmi_driver" ]
  # This package is reachable from //bundles/packages/prod:all
}

driver_bind_rules("qmi_usb_bind") {
  rules = "qmi_usb.bind"
  header_output = "qmi_usb_bind.h"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.sierra.usb",
    "//src/devices/bind/fuchsia.usb",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":qmi_usb_bind_test" ]
}

fuchsia_driver("qmi_driver-driver") {
  output_name = "qmi-transport"

  deps = [ ":qmi_driver_src" ]

  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
}

fuchsia_driver_component("qmi_driver") {
  component_name = "qmi-transport"
  deps = [ ":qmi_driver-driver" ]
  info = "qmi_driver-info.json"
  colocate = true
}

source_set("qmi_driver_src") {
  sources = [
    "qmi-usb-transport.cc",
    "qmi-usb-transport.h",
  ]

  public_deps = [
    ":qmi_usb_bind",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.telephony.transport:fuchsia.hardware.telephony.transport_cpp_wire",
    "//sdk/fidl/fuchsia.telephony.snoop:fuchsia.telephony.snoop_cpp_wire",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:cdc",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}
