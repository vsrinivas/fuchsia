# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")

fuchsia_system_package("qmi-fake-transport") {
  testonly = true

  deps = [ ":fake_qmi_driver" ]

  # This package is reachable from //garnet/packages/prod:all
  allowed_in_extra_deps = true
}

driver_bind_rules("qmi_fake_bind") {
  rules = "qmi_fake.bind"
  header_output = "qmi_fake_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/test.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":qmi_fake_bind_test" ]
}

driver_module("fake_qmi_driver-driver") {
  output_name = "qmi-fake-transport"
  testonly = true

  sources = [
    "driver.cc",
    "fake-device.cc",
    "fake-device.h",
  ]

  deps = [
    ":qmi_fake_bind",
    "//sdk/banjo/fuchsia.hardware.test:fuchsia.hardware.test_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.telephony.transport:fuchsia.hardware.telephony.transport_llcpp",
    "//sdk/fidl/fuchsia.telephony.snoop:fuchsia.telephony.snoop_llcpp",
    "//sdk/lib/fdio",
    "//src/connectivity/telephony/tests/fake-drivers/fake-transport-base",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/fbl",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("fake_qmi_driver") {
  testonly = true
  component_name = "qmi-fake-transport"
  deps = [ ":fake_qmi_driver-driver" ]
}
