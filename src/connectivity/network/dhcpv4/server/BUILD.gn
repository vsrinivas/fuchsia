# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "dhcpv4_server"
  with_unit_tests = true
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.net.dhcp:fuchsia.net.dhcp_rust",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/dhcpv4/lib",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia_nix",
    "//src/lib/network/packet",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:nonzero_ext",
    "//third_party/rust_crates:socket2",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//src/connectivity/lib/net-declare",
    "//third_party/rust_crates:assert_matches",
  ]

  sources = [ "src/main.rs" ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("dhcpv4_server_bin_test") {
  testonly = true
  manifest = "meta/dhcpv4_server_bin_test.cml"
  deps = [ ":bin_test" ]
}

fuchsia_test_package("dhcpv4-server-tests") {
  test_components = [ ":dhcpv4_server_bin_test" ]
  deps = [ "//src/sys/stash:stash_secure_v2" ]
}

fuchsia_component("component") {
  component_name = "dhcpv4_server"
  manifest = "meta/dhcpv4_server.cml"
  deps = [ ":bin" ]
}

group("tests") {
  testonly = true
  deps = [ ":dhcpv4-server-tests" ]
}
