# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/components.gni")

names = [
  "selecttest",
  "sockopttest",
  "gethostbyname_test",
]

foreach(name, names) {
  executable(name) {
    testonly = true
    sources = [ "$name.c" ]
    deps = [
      "//sdk/lib/fdio",
      "//src/zircon/lib/zircon",
    ]
  }
}

fuchsia_shell_package("netstack-manual-tests") {
  testonly = true

  deps = []
  foreach(name, names) {
    deps += [ ":$name" ]
  }
}

group("manual") {
  testonly = true
  deps = [ ":netstack-manual-tests" ]
}
