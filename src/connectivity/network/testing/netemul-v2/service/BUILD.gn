# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//src/lib/isolated_devmgr/v2_component/isolated_devmgr.gni")

rustc_binary("bin") {
  testonly = true
  with_unit_tests = true
  name = "netemul_sandbox_v2"
  edition = "2018"
  deps = [
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.io2:fuchsia.io2-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.net.tun:fuchsia.net.tun-rustc",
    "//sdk/fidl/fuchsia.process:fuchsia.process-rustc",
    "//src/connectivity/network/testing/netemul-v2/fidl:fuchsia.netemul-rustc",
    "//src/connectivity/network/testing/netemul/lib/fidl:network-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/cm_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:pin-utils",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
  ]
  test_deps = [
    "//garnet/lib/rust/files_async",
    "//sdk/fidl/fuchsia.device:fuchsia.device-rustc",
    "//src/connectivity/network/testing/netemul-v2/test-support:fidl-rustc",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/diagnostics/hierarchy/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fuchsia",
    "//src/lib/storage/fuchsia-vfs-watcher",
    "//src/lib/testing/fixture",
    "//third_party/rust_crates:matches",
  ]
  sources = [ "src/main.rs" ]
  configs -= [ "//build/config/rust:allow_unused_results" ]
}

fuchsia_component("netemul-sandbox-component") {
  testonly = true
  component_name = "netemul-sandbox-v2"
  manifest = "meta/netemul-sandbox-v2.cml"
  deps = [ ":bin" ]
}

group("tests") {
  testonly = true
  deps = [ ":netemul-v2-tests" ]
}

# TODO(https://fxbug.dev/71088): combine the test driver and the test root once
# `use from child` is supported.
fuchsia_component("netemul-sandbox-v2-test-root") {
  testonly = true
  manifest = "meta/netemul-sandbox-v2-test-root.cml"
}

fuchsia_component("netemul-sandbox-v2-test-driver") {
  testonly = true
  manifest = "meta/netemul-sandbox-v2-test-driver.cml"
  deps = [ ":bin_test" ]
}

group("network-drivers") {
  testonly = true
  deps = [
    "//src/connectivity/ethernet/drivers/ethernet",
    "//src/connectivity/ethernet/drivers/ethertap",
  ]
}

isolated_devmgr_v2_component("netemul-devmgr") {
  package_name = "netemul-v2-tests"
  deps = [ ":network-drivers" ]
}

fuchsia_test_package("netemul-v2-tests") {
  test_components = [ ":netemul-sandbox-v2-test-root" ]
  deps = [
    ":netemul-devmgr",
    ":netemul-sandbox-v2-test-driver",
    "//src/connectivity/network/testing/netemul-v2/network-context",
    "//src/connectivity/network/testing/netemul-v2/test-support:counter",
    "//src/connectivity/network/tun/network-tun:component-v2",
  ]
}
