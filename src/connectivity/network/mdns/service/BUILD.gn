# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/fuzzing/fuzzer.gni")
import("//build/package.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

declare_args() {
  # Enables the tracing feature of mdns, which can be turned on using
  # "mdns-util verbose".
  enable_mdns_trace = false
}

group("service") {
  testonly = true
  deps = [
    ":mdns",
    ":mdns_config",
    ":source_set",
  ]
}

executable("bin") {
  output_name = "mdns"

  sources = [ "main.cc" ]

  deps = [
    ":source_set",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

source_set("source_set") {
  sources = [
    "address_prober.cc",
    "address_prober.h",
    "address_responder.cc",
    "address_responder.h",
    "config.cc",
    "config.h",
    "dns_formatting.cc",
    "dns_formatting.h",
    "dns_message.cc",
    "dns_message.h",
    "dns_reading.cc",
    "dns_reading.h",
    "dns_writing.cc",
    "dns_writing.h",
    "host_name_resolver.cc",
    "host_name_resolver.h",
    "instance_prober.cc",
    "instance_prober.h",
    "instance_requestor.cc",
    "instance_requestor.h",
    "instance_responder.cc",
    "instance_responder.h",
    "mdns.cc",
    "mdns.h",
    "mdns_addresses.cc",
    "mdns_addresses.h",
    "mdns_agent.h",
    "mdns_fidl_util.cc",
    "mdns_fidl_util.h",
    "mdns_interface_transceiver.cc",
    "mdns_interface_transceiver.h",
    "mdns_interface_transceiver_v4.cc",
    "mdns_interface_transceiver_v4.h",
    "mdns_interface_transceiver_v6.cc",
    "mdns_interface_transceiver_v6.h",
    "mdns_names.cc",
    "mdns_names.h",
    "mdns_service_impl.cc",
    "mdns_service_impl.h",
    "mdns_transceiver.cc",
    "mdns_transceiver.h",
    "packet_reader.cc",
    "packet_reader.h",
    "packet_writer.cc",
    "packet_writer.h",
    "prober.cc",
    "prober.h",
    "reply_address.cc",
    "reply_address.h",
    "resource_renewer.cc",
    "resource_renewer.h",
  ]

  public_deps = [
    "//garnet/public/lib/fostr",
    "//garnet/public/lib/json",
    "//sdk/fidl/fuchsia.device",
    "//sdk/fidl/fuchsia.net",
    "//sdk/fidl/fuchsia.net.mdns",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fsl",
    "//src/lib/inet",
    "//src/lib/json_parser",
    "//zircon/public/lib/fit",

    # TODO(fxbug.dev/57392): Move it back to //third_party once unification completes.
    "//zircon/third_party/rapidjson",
  ]

  if (enable_mdns_trace) {
    defines = [ "MDNS_TRACE" ]
  }
}

test("mdns_test") {
  sources = [
    "test/address_responder_test.cc",
    "test/agent_test.cc",
    "test/agent_test.h",
    "test/config_test.cc",
    "test/dns_names_test.cc",
    "test/dns_reading_test.cc",
    "test/instance_responder_test.cc",
    "test/interface_transceiver_test.cc",
    "test/mdns_unit_tests.cc",
  ]

  deps = [
    ":source_set",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  if (enable_mdns_trace) {
    defines = [ "MDNS_TRACE" ]
  }
}

fuchsia_component("component") {
  component_name = "mdns"
  deps = [ ":bin" ]
  manifest = "meta/mdns.cmx"
}

fuchsia_package("mdns") {
  deps = [ ":component" ]
}

config_data("mdns_config") {
  for_pkg = "sysmgr"
  outputs = [ "mdns.config" ]
  sources = [ rebase_path("config/service.config") ]
}

config_data("mdns_fuchsia_udp_config") {
  for_pkg = "mdns"
  sources = [ rebase_path("config/fuchsia_udp.config") ]

  # Ensure exclusion from production builds.
  deps = [ "//build/validate:non_production_tag" ]
}

group("tests") {
  testonly = true
  deps = [
    ":mdns-fuzzers",
    ":mdns-netemul-tests",
    ":mdns-tests",
    "//src/connectivity/network/testing/netemul/runner/runner:netemul-runner",
  ]
}

fuchsia_unittest_package("mdns-tests") {
  deps = [ ":mdns_test" ]
  manifest = "meta/mdns_test.cmx"
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuzzer("packet_reader_fuzzer") {
  sources = [
    "dns_message.cc",
    "dns_message.h",
    "dns_reading.cc",
    "dns_reading.h",
    "packet_reader.cc",
    "packet_reader.h",
    "packet_reader_fuzzer.cc",
  ]
  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/inet",
  ]
}

fuzzers_package("mdns-fuzzers") {
  fuzzers = [ ":packet_reader_fuzzer" ]
}

executable("mdns_netemul_test_agent") {
  testonly = true

  sources = [ "test/mdns_netemul_test_agent.cc" ]

  deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.net",
    "//garnet/public/lib/fostr/fidl/fuchsia.net.mdns",
    "//sdk/fidl/fuchsia.net.mdns",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fsl",
    "//zircon/public/lib/async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("mdns_netemul_test_agent_component") {
  component_name = "mdns_netemul_test_agent"
  testonly = true
  deps = [ ":mdns_netemul_test_agent" ]
  manifest = "meta/mdns_netemul_test_agent.cmx"
}

fuchsia_component("mdns_netemul_test") {
  testonly = true
  manifest = "meta/mdns_netemul_test.cmx"
}

fuchsia_component("mdns_netemul_v6_test") {
  testonly = true
  manifest = "meta/mdns_netemul_v6_test.cmx"
}

executable("mdns_netemul_multi_network_test_agent") {
  testonly = true

  sources = [ "test/mdns_netemul_multi_network_test_agent.cc" ]

  deps = [
    ":source_set",
    "//sdk/fidl/fuchsia.net",
    "//sdk/lib/sys/cpp",
    "//src/connectivity/network/testing/netemul/lib/fidl:sync",
    "//zircon/public/lib/async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("mdns_netemul_multi_network_test_agent_component") {
  component_name = "mdns_netemul_multi_network_test_agent"
  testonly = true
  deps = [ ":mdns_netemul_multi_network_test_agent" ]
  manifest = "meta/mdns_netemul_multi_network_test_agent.cmx"
}

fuchsia_component("mdns_netemul_multi_network_test") {
  testonly = true
  manifest = "meta/mdns_netemul_multi_network_test.cmx"
}

fuchsia_test_package("mdns-netemul-tests") {
  test_components = [
    ":mdns_netemul_test",
    ":mdns_netemul_v6_test",
    ":mdns_netemul_multi_network_test",
  ]
  deps = [
    ":component",
    ":mdns_netemul_multi_network_test_agent_component",
    ":mdns_netemul_test_agent_component",
    "//src/connectivity/network/netstack:component-debug",
    "//src/connectivity/network/testing/netemul/runner:netstack-cfg",
    "//src/connectivity/network/tests:device_name_provider",
  ]
}
