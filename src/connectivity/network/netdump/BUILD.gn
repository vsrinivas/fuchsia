# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

executable("bin") {
  output_name = "netdump"
  sources = [
    "filter.cc",
    "netdump.cc",
    "parser.cc",
    "tokens.cc",
  ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_c_client",
    "//zircon/public/lib/pretty",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  public_deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
  ]
}

test("unit_tests") {
  output_name = "netdump_unit_tests"
  sources = [
    "filter.cc",
    "parser.cc",
    "test/filter_builder_test.cc",
    "test/filter_test.cc",
    "test/parser_test.cc",
    "test/tokens_test.cc",
    "tokens.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/zxtest",
  ]
}

group("tests") {
  testonly = true

  deps = [
    ":netdump",
    ":netdump-unit-tests",
    "integration:tests",
  ]
}

fuchsia_component("component") {
  component_name = "netdump"
  deps = [ ":bin" ]
  manifest = "meta/netdump.cmx"
}

package("netdump") {
  deps = [ ":bin" ]

  binaries = [
    {
      name = "netdump"
      shell = true
    },
  ]
}

fuchsia_unittest_package("netdump-unit-tests") {
  deps = [ ":unit_tests" ]
  manifest = "meta/netdump_unit_tests.cmx"
}
