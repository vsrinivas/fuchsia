# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

fuchsia_test_component("device_name_provider") {
  manifest = "meta/device_name_provider.cmx"
  deps = [ "//src/bringup/bin/device-name-provider" ]
}

test("name_provider_service_not_present_test") {
  sources = [ "name_provider_service_not_present_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.device",
    "//src/lib/fxl/test:gtest_main",
  ]
}

test("netstack_bsdsocket_c_api_test") {
  sources = [
    "bsdsocket_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_llcpp",
      "//src/lib/testing/predicates",
    ]
  }
}

test("netstack_fuchsia_c_api_test") {
  sources = [
    "fdio_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
  ]
}

test("netstack_if_nameindex_test") {
  sources = [ "if_nameindex_test.cc" ]

  deps = [ "//src/lib/fxl/test:gtest_main" ]
}

test("netstack_no_network_test") {
  sources = [ "no_network_test.cc" ]

  deps = [ "//src/lib/fxl/test:gtest_main" ]
}

test("netstack_packetsocket_c_api_test") {
  sources = [ "packetsocket_test.cc" ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]
}

test("netstack_rawsocket_c_api_test") {
  sources = [ "rawsocket_test.cc" ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/fbl",
  ]
}

tests = [
  "name_provider_service_not_present",
  "netstack_bsdsocket_c_api",
  "netstack_fuchsia_c_api",
  "netstack_if_nameindex",
  "netstack_no_network",
  "netstack_packetsocket_c_api",
  "netstack_rawsocket_c_api",
]

foreach(test, tests) {
  name = "${test}_test"
  fuchsia_unittest_component("${name}_component") {
    component_name = name
    deps = [ ":${name}" ]
    manifest = "meta/${name}.cmx"
  }
}

_universe_deps = [
  # cat is used to test FD passing in :netstack_fuchsia_c_api_test.
  "//third_party/sbase:cat",
]

fuchsia_test_package("netstack-c-api-tests") {
  package_deps_for_infra_only = _universe_deps

  test_components = []
  foreach(test, tests) {
    test_components += [ ":${test}_test_component" ]
  }

  deps = [
    # dns_resolver is under test.
    "//src/connectivity/network/dns:component-legacy",

    # netstack is under test.
    "//src/connectivity/network/netstack:component-debug-legacy",
  ]
}

group("tests") {
  testonly = true
  public_deps = _universe_deps + [
                  ":netstack-c-api-tests",
                  "benchmarks:tests",
                  "connect:tests",
                  "dhcp_interop:dhcp_integration_tests",
                  "external_network:tests",
                  "fidl:tests",
                  "getifaddrs:tests",
                  "integration:tests",
                ]
  if (host_os == "linux") {
    public_deps += [
      ":netstack_bsdsocket_c_api_test($host_toolchain)",
      ":netstack_if_nameindex_test($host_toolchain)",
      ":netstack_packetsocket_c_api_test($host_toolchain)",
      ":netstack_rawsocket_c_api_test($host_toolchain)",
    ]
  }
}
