# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test_package.gni")
import("//build/components/fuchsia_unittest_component.gni")
import("//build/components/fuchsia_unittest_package.gni")
import("//build/test.gni")

group("benchmarks") {
  testonly = true
  deps = [
    ":socket-benchmarks-tests",
    ":udp-serde-benchmarks",
  ]
}

test("udp_serde_benchmarks") {
  sources = [ "udp_serde_benchmarks.cc" ]

  deps = [
    "//sdk/lib/fidl/cpp/wire",
    "//sdk/lib/syslog/cpp",
    "//src/connectivity/network/netstack/udp_serde",
    "//src/connectivity/network/netstack/udp_serde:udp_serde_test_util",
    "//src/lib/fxl",
    "//zircon/system/ulib/perftest",
  ]
}

test("socket_benchmarks") {
  sources = [
    "../os.h",
    "socket_benchmarks.cc",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/perftest",
  ]
}

fuchsia_unittest_component("socket-benchmarks") {
  manifest = "meta/socket-benchmarks.cml"
  deps = [
    ":socket_benchmarks",
    "//src/connectivity/network/netstack:component",
  ]
}

fuchsia_unittest_component("socket-benchmarks-with-fast-udp") {
  manifest = "meta/socket-benchmarks-with-fast-udp.cml"
  deps = [
    ":socket_benchmarks",
    "//src/connectivity/network/netstack:component-with-fast-udp",
  ]
}

fuchsia_unittest_component("socket-benchmarks-with-fake-netstack") {
  manifest = "meta/socket-benchmarks-with-fake-netstack.cml"
  deps = [
    ":socket_benchmarks",
    "fake-netstack",
  ]
}

fuchsia_unittest_component("socket-benchmarks-with-netstack3") {
  manifest = "meta/socket-benchmarks-with-netstack3.cml"
  deps = [
    ":socket_benchmarks",
    "//src/connectivity/network/netstack3:component",
  ]
}

fuchsia_test_package("socket-benchmarks-tests") {
  test_components = [
    ":socket-benchmarks",
    ":socket-benchmarks-with-fake-netstack",
    ":socket-benchmarks-with-fast-udp",
    ":socket-benchmarks-with-netstack3",
  ]
}

fuchsia_unittest_package("udp-serde-benchmarks") {
  manifest = "meta/udp-serde-benchmarks.cml"
  deps = [ ":udp_serde_benchmarks" ]
}

group("tests") {
  testonly = true
  deps = [
    ":socket-benchmarks-tests",
    ":udp-serde-benchmarks",
  ]
  if (host_os == "linux") {
    deps += [ ":socket_benchmarks($host_toolchain)" ]
  }
}
