# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  output_name = "connect"
  edition = "2018"

  deps = [
    "//src/connectivity/network/testing/netemul/lib/fidl:network-rustc",
    "//src/connectivity/network/testing/netemul/lib/fidl:sync-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/tcp-stream-ext",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:socket2",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/main.rs" ]

  configs -= [ "//build/config/rust:allow_unused_results" ]
}

fuchsia_test_component("connect") {
  manifest = "meta/connect.cmx"
  deps = [ ":bin" ]
}

fuchsia_test_component("connect_test") {
  manifest = "meta/connect_test.cmx"
  deps = [ ":connect" ]
}

_universe_deps = [
  # netemul-runner is used to create hermetic test environments.
  "//src/connectivity/network/testing/netemul/runner:netemul-sandbox",
]

fuchsia_test_package("netstack-connect-tests") {
  package_deps_for_infra_only = _universe_deps

  test_components = [ ":connect_test" ]

  deps = [
    # netstack is under test.
    "//src/connectivity/network/netstack:component-debug-legacy",

    # netstack-cfg is used to configure netstack in test environments.
    "//src/connectivity/network/testing/netemul/runner:netstack-cfg",
  ]
}

group("tests") {
  testonly = true
  public_deps = _universe_deps + [ ":netstack-connect-tests" ]
}
