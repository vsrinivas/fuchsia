# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")

group("tests") {
  testonly = true
  deps = [
    ":compile",
    ":netstack-integration-tests",
    ":netstack-integration-tests-v2",
    "ip_forward:netemul-ip-forward-tests",
  ]
}

group("compile") {
  testonly = true
  deps = [
    "common:netstack_testing_common",
    "common-v1:netstack_testing_common",
  ]
}

tests = [
  "dhcp",
  "dns",
  "ipv6",
  "management",
]

foreach(test, tests) {
  name = "netstack_${test}_integration_test"
  fuchsia_test_component(name) {
    manifest = "meta/${name}.cmx"
    deps = [ test ]
  }
}

fuchsia_test_package("netstack-integration-tests") {
  package_name = "netstack-integration-tests"

  test_components = []
  foreach(test, tests) {
    test_components += [ ":netstack_${test}_integration_test" ]
  }
  deps = [
    # device_name_provider provides fuchsia.device.NameProvider.
    "..:device_name_provider",

    # stash_secure is used by dhcpd and netstack.
    "//src/sys/stash:stash_secure",

    # dhcpd is used to test DHCP address acquisition.
    "//src/connectivity/network/dhcp:component-v1",

    # DHCPv6 client is under test.
    "//src/connectivity/network/dhcpv6/client:component",

    # dns_resolver integration with netstack is tested.
    "//src/connectivity/network/dns:component",

    # netcfg integration with netstack is tested.
    ":netcfg-netemul-config",
    "//src/connectivity/network/netcfg:component-netemul",

    # netstack3 is under test.
    "//src/connectivity/network/netstack3:component",

    # netstack is under test.
    "//src/connectivity/network/netstack:component-debug",

    # netemul-sandbox is used to create hermetic environments.
    "//src/connectivity/network/testing/netemul/runner:netemul-sandbox-component",
  ]
}

tests_v2 = [
  "fidl",
  "filter",
  "inspect",
  "ipv4",
  "neighbor",
  "reachability",
  "routes",
  "socket",
  "sys",
]

foreach(test, tests_v2) {
  name = "netstack-${test}-integration-test"
  fuchsia_test_component(name) {
    manifest = "meta/${name}.cml"
    deps = [ test ]
  }
}

fuchsia_test_package("netstack-integration-tests-v2") {
  test_components = []
  foreach(test, tests_v2) {
    test_components += [ ":netstack-${test}-integration-test" ]
  }

  deps = [
    # netemul-sandbox-v2 is used to create hermetic environments.
    "//src/connectivity/network/testing/netemul-v2/service:netemul-sandbox",

    # netstack is under test.
    "//src/connectivity/network/netstack:component-debug-v2",

    # netstack3 is under test.
    "//src/connectivity/network/netstack3:component-v2",

    # reachability monitor is under test.
    "//src/connectivity/management/reachability:component-v2",

    # sampler config is tested as part of inspect integration tests.
    ":sampler-config",
  ]
}

config_data("netcfg-netemul-config") {
  testonly = true
  for_pkg = "netstack-integration-tests"
  sources = [ "//src/connectivity/network/netcfg/config/empty.json" ]
  outputs = [ "netcfg/empty.json" ]
}

resource("sampler-config") {
  sources = [ "//src/diagnostics/sampler/config/netstack.json" ]
  outputs = [ "data/sampler-config/netstack.json" ]
}
