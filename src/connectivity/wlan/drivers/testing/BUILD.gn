# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")

group("testing") {
  testonly = true
  public_deps = [
    ":wlanphy-testdev",
    "util",
  ]
}

driver_bind_rules("wlanphy_test_bind") {
  disable_autobind = true
  rules = "wlanphy_test.bind"
  header_output = "wlanphy_test_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

group("tests") {
  testonly = true
  deps = [ ":wlanphy_test_bind_test" ]
}

driver_module("wlanphy-testdev-driver") {
  output_name = "wlanphy-testdev"
  sources = [
    "driver.cc",
    "iface-device.cc",
    "iface-device.h",
    "phy-device.cc",
    "phy-device.h",
  ]

  deps = [
    ":wlanphy_test_bind",
    "//garnet/lib/wlan/protocol",
    "//sdk/banjo/ddk.hw.wlan.wlaninfo:ddk.hw.wlan.wlaninfo_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdio:fuchsia.hardware.sdio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.test:fuchsia.hardware.test_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.wlanphy:fuchsia.hardware.wlanphy_banjo_cpp",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_banjo_c",
    "//sdk/fidl/fuchsia.wlan.device",
    "//sdk/fidl/fuchsia.wlan.device:fuchsia.wlan.device_llcpp",
    "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_c",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
  ]

  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_driver_component("wlanphy-testdev") {
  component_name = "wlanphy-testdev"
  deps = [ ":wlanphy-testdev-driver" ]
}
