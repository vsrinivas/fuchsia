# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")

all_hdrs = [
  "convert.h",
  "device.h",
  "driver.h",
]

all_src = [
  "convert.cpp",
  "device.cpp",
  "driver.cpp"
]

all_deps = [
  "//garnet/lib/wlan/fidl",
  "//garnet/lib/wlan/protocol",
  "//src/connectivity/wlan/lib/common/cpp:common",
  "//zircon/public/lib/async-cpp",
  "//zircon/public/lib/async-loop-cpp",
  "//zircon/public/lib/ddk",
  "//zircon/public/lib/driver",
]

source_set("lib_source") {
  public = all_hdrs
  sources = all_src
  public_deps = all_deps
}

source_set("test_source") {
  defines = [ "WLANIF_TEST=1" ]
  public = all_hdrs
  sources = all_src
  public_deps = all_deps
}

driver_module("wlanif_driver") {
  output_name = "wlanif"

  deps = [
    ":lib_source",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:enable_zircon_asserts",
    "//build/config/fuchsia:static_cpp_standard_library",
  ]
}

package("wlanif") {
  deprecated_system_image = true

  deps = [
    ":wlanif_driver",
  ]

  drivers = [
    {
      name = "wlanif.so"
    },
  ]
}
