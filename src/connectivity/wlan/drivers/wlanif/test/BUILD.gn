# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

bind_test("wlanif_bind_test") {
  rules = "//src/connectivity/wlan/drivers/wlanif/wlanif.bind"
  tests = "wlanif_bind_test.json"
  deps = [ "//src/devices/bind/fuchsia.wlan" ]
}

test("convert_unittest") {
  output_name = "wlanif_convert_unittest"

  sources = [ "convert_unittest.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.wlan.common",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_banjo_c",
    "//sdk/fidl/fuchsia.wlan.ieee80211",
    "//sdk/fidl/fuchsia.wlan.internal",
    "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_c",
    "//sdk/fidl/fuchsia.wlan.mlme",
    "//sdk/fidl/fuchsia.wlan.stats",
    "//src/connectivity/wlan/drivers/wlanif:lib_source",
    "//src/connectivity/wlan/lib/mlme/cpp:mlme",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("device_unittest") {
  output_name = "wlanif_device_unittest"

  sources = [ "device_unittest.cc" ]

  deps = [
    "//sdk/banjo/ddk.hw.wlan.ieee80211:ddk.hw.wlan.ieee80211_banjo_cpp",
    "//sdk/fidl/fuchsia.wlan.internal",
    "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_c",
    "//sdk/fidl/fuchsia.wlan.mlme",
    "//src/connectivity/wlan/drivers/wlanif:lib_source",
    "//src/connectivity/wlan/lib/mlme/cpp:mlme",
    "//src/devices/lib/driver",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("impl_unittest") {
  output_name = "wlanif_impl_unittest"

  sources = [ "impl_unittest.cc" ]

  deps = [
    "//garnet/lib/wlan/protocol",
    "//src/lib/fxl/test:gtest_main",
  ]
}

tests = [
  "convert",
  "device",
  "impl",
]

foreach(test, tests) {
  name = "${test}_test"
  fuchsia_unittest_component("${name}_component") {
    component_name = name
    deps = [ ":${test}_unittest" ]
  }
}

fuchsia_test_package("wlanif_unittest") {
  test_components = []
  foreach(test, tests) {
    test_components += [ ":${test}_test_component" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":wlanif_bind_test",
    ":wlanif_unittest",
  ]
}
