# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")
import("//build/test/test_package.gni")

group("test") {
  testonly = true
  deps = [
    ":wlanif-test",
    ":wlanif-test-driver",
  ]
}

driver_module("wlanif-test-driver") {
  output_name = "wlanif-test"

  sources = [
    "binding.c",
  ]

  deps = [
    "//garnet/lib/wlan/protocol",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//zircon/public/lib/driver",
    "//zircon/public/lib/zx",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:enable_zircon_asserts",
    "//build/config/fuchsia:static_cpp_standard_library",
  ]
}

package("wlanif-test") {
  deprecated_system_image = true

  deps = [
    ":wlanif-test-driver",
  ]

  drivers = [
    {
      name = "wlanif-test.so"
    },
  ]
}

executable("convert_unittest") {
  output_name = "wlanif_convert_unittest"

  testonly = true

  sources = [
    "convert_unittest.cpp",
  ]

  deps = [
    "//src/connectivity/wlan/drivers/wlanif:source",
    "//src/connectivity/wlan/lib/mlme/cpp:mlme",
    "//third_party/googletest:gtest_main",
  ]
}

unittest_package("wlanif_unittest") {
  deps = [
    ":convert_unittest",
  ]

  tests = [
    {
      name = "wlanif_convert_unittest"
      environments = basic_envs
    },
  ]
}
