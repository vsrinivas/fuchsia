# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found
# in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")

bind_rules("rtl88xx_bind") {
  rules = "rtl88xx.bind"
  output = "rtl88xx-bind.h"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.usb",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":rtl88xx_bind_test" ]
}

driver_module("rtl88xx_driver") {
  output_name = "rtl88xx"

  sources = [
    "bus.cc",
    "bus.h",
    "bus_util.h",
    "device.cc",
    "device.h",
    "register.h",
    "rtl8821c_device.cc",
    "rtl8821c_device.h",
    "rtl88xx_registers.h",
    "usb_bus.cc",
    "usb_bus.h",
    "wlan_mac.cc",
    "wlan_mac.h",
    "wlan_phy.cc",
    "wlan_phy.h",
  ]

  deps = [
    ":rtl88xx_bind",
    "//garnet/lib/wlan/protocol",
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/banjo/ddk.protocol.wlanphyimpl",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/public",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}

driver_package("rtl88xx") {
  # Fuchsia does not yet support loading drivers out of packages.

  deps = [ ":rtl88xx_driver" ]

  drivers = [
    {
      name = "rtl88xx.so"
    },
  ]
}
