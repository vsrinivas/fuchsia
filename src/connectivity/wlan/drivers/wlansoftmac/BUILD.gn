# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

config("wlansoftmac_config") {
  include_dirs = [ "." ]
}

driver_bind_rules("wlansoftmac_bind") {
  rules = "wlansoftmac.bind"
  header_output = "wlansoftmac_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.wlan" ]
}

wlansoftmac_convert_sources = [
  "convert.cc",
  "convert.h",
]

wlansoftmac_convert_deps = [
  "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
  "//sdk/banjo/fuchsia.hardware.wlan.softmac:fuchsia.hardware.wlan.softmac_banjo_c",
  "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_banjo_c",
  "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_c",
  "//sdk/fidl/fuchsia.wlan.softmac:fuchsia.wlan.softmac_cpp_driver",
  "//src/connectivity/wlan/lib/mlme/cpp:mlme",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/zx",
]

source_set("wlansoftmac_convert") {
  sources = wlansoftmac_convert_sources
  public_deps = wlansoftmac_convert_deps
}

fuchsia_driver("wlansoftmac_driver-driver") {
  output_name = "wlansoftmac"
  sources = [
    "device.cc",
    "device.h",
    "driver.cc",
    "probe_sequence.cc",
    "probe_sequence.h",
  ]
  deps = [
    ":wlansoftmac_bind",
    ":wlansoftmac_convert",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/connectivity/wlan/lib/mlme/cpp:mlme",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("wlansoftmac_driver") {
  component_name = "wlansoftmac"
  deps = [ ":wlansoftmac_driver-driver" ]
  info = "wlansoftmac_driver-info.json"
  colocate = true
}

fuchsia_driver_package("wlansoftmac") {
  package_name = "wlansoftmac"
  driver_components = [ ":wlansoftmac_driver" ]
}

test("wlansoftmac_convert_unittest") {
  testonly = true
  output_name = "wlansoftmac_convert_unittest"
  sources = wlansoftmac_convert_sources + [ "tests/convert_unittest.cc" ]

  deps = wlansoftmac_convert_deps + [
           "//src/lib/fxl/test:gtest_main",
           "//third_party/googletest:gtest",
         ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_component("wlansoftmac_convert_unittest_component") {
  deps = [ ":wlansoftmac_convert_unittest" ]
}

fuchsia_test_package("wlansoftmac_unittests") {
  test_components = [ ":wlansoftmac_convert_unittest_component" ]
}

group("tests") {
  testonly = true
  deps = [
    ":wlansoftmac_bind_test",
    ":wlansoftmac_unittests",
  ]
}
