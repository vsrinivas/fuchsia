# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

wlanphy_sources = [
  "device.cc",
  "device.h",
  "driver.cc",
  "driver.h",
]

wlanphy_dfv2_sources = [
  "device_dfv2.cc",
  "device_dfv2.h",
  "driver.h",
  "driver_dfv2.cc",
]

wlanphy_deps = [
  ":wlanphy_bind",
  "//sdk/banjo/fuchsia.hardware.wlanphyimpl:fuchsia.hardware.wlanphyimpl_banjo_cpp",
  "//sdk/fidl/fuchsia.wlan.device:fuchsia.wlan.device_cpp_wire",
  "//sdk/fidl/fuchsia.wlan.wlanphyimpl:fuchsia.wlan.wlanphyimpl_cpp_driver",
  "//sdk/fidl/fuchsia.wlan.wlanphyimpl:fuchsia.wlan.wlanphyimpl_cpp_wire",
  "//sdk/lib/driver/runtime:driver_runtime_cpp",
  "//src/connectivity/wlan/drivers/lib/log/cpp",
  "//src/connectivity/wlan/lib/common/cpp:common",
  "//src/devices/lib/driver",
  "//src/devices/lib/driver:driver_runtime",
  "//src/lib/ddk",
  "//src/lib/ddktl",
  "//zircon/system/ulib/async:async-cpp",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/async-loop:async-loop-default",
  "//zircon/system/ulib/sync:sync-cpp",
]

driver_bind_rules("wlanphy_bind") {
  rules = "wlanphy.bind"
  header_output = "wlanphy-bind.h"
  bind_output = "wlanphy.bindbc"
  tests = "wlanphy_bind_test.json"
  deps = [ "//src/devices/bind/fuchsia.wlan" ]
}

fuchsia_driver("wlanphy_driver-driver") {
  output_name = "wlanphy"
  sources = wlanphy_sources

  deps = wlanphy_deps

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

# TODO(https://fxbug.dev/100036): Remove it when all the drivers that wlanphy driver binds to gets
# migrated to DFv2.
fuchsia_driver("wlanphy_driver_dfv2-driver") {
  output_name = "wlanphy_dfv2"
  sources = wlanphy_dfv2_sources

  deps = wlanphy_deps

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("wlanphy_driver") {
  component_name = "wlanphy"
  deps = [ ":wlanphy_driver-driver" ]
  info = "wlanphy_driver-info.json"
  colocate = true
}

# TODO(https://fxbug.dev/100036): Remove it when all the drivers that wlanphy driver binds to gets
# migrated to DFv2.
fuchsia_driver_component("wlanphy_driver_dfv2") {
  component_name = "wlanphy_dfv2"
  deps = [ ":wlanphy_driver_dfv2-driver" ]
  info = "wlanphy_driver-info.json"
  colocate = true
}

fuchsia_driver_package("wlanphy") {
  driver_components = [ ":wlanphy_driver" ]
}

# TODO(https://fxbug.dev/100036): Remove it when all the drivers that wlanphy driver binds to gets
# migrated to DFv2.
fuchsia_driver_package("wlanphy_dfv2") {
  driver_components = [ ":wlanphy_driver_dfv2" ]
}

# unit tests start here

# TODO(https://fxbug.dev/100036): Remove it when all the drivers that wlanphy driver binds to gets
# migrated to DFv2.
executable("wlanphy_device_test") {
  testonly = true
  output_name = "wlanphy_device_test"
  sources = wlanphy_sources + [ "tests/wlanphy_device_test.cc" ]

  deps = wlanphy_deps + [
           "//src/lib/fxl/test:gtest_main",
           "//src/lib/testing/loop_fixture",
           "//third_party/googletest:gmock",
           "//third_party/googletest:gtest",
         ]

  configs +=
      [ "//src/connectivity/wlan/lib/mlme/cpp/tests:wlan_mlme_tests_config" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("wlanphy_device_test_dfv2") {
  testonly = true
  output_name = "wlanphy_device_test_dfv2"
  sources = wlanphy_dfv2_sources + [ "tests/wlanphy_device_test_dfv2.cc" ]

  deps = wlanphy_deps + [
           "//src/devices/testing/driver-runtime-main:zxtest",
           "//src/devices/testing/mock-ddk",
           "//zircon/system/ulib/sync:sync-cpp",
           "//zircon/system/ulib/zxtest",
         ]

  configs +=
      [ "//src/connectivity/wlan/lib/mlme/cpp/tests:wlan_mlme_tests_config" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_component("wlanphy_device_test_component") {
  deps = [ ":wlanphy_device_test" ]
}

fuchsia_unittest_component("wlanphy_device_test_dfv2_component") {
  deps = [ ":wlanphy_device_test_dfv2" ]
}

fuchsia_test_package("wlanphy-unittests") {
  test_components = [
    ":wlanphy_device_test_dfv2_component",
    ":wlanphy_device_test_component",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":wlanphy-unittests",
    ":wlanphy_bind_test",
  ]
}
