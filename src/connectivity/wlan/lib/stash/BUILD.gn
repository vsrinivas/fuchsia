# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

rustc_library("wlan-stash") {
  name = "wlan_stash"
  version = "0.1.0"
  with_unit_tests = true
  edition = "2021"

  deps = [
    ":wlan-stash-constants",
    "//sdk/fidl/fuchsia.stash:fuchsia.stash_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:serde_json",
  ]
  test_deps = [
    "//src/connectivity/wlan/lib/ieee80211",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:rand",
  ]

  sources = [
    "src/lib.rs",
    "src/policy.rs",
  ]
}

rustc_library("wlan-stash-constants") {
  name = "wlan_stash_constants"
  version = "0.1.0"
  edition = "2021"
  source_root = "src/constants.rs"

  deps = [ "//third_party/rust_crates:serde" ]

  sources = [ "src/constants.rs" ]
}

fuchsia_component("wlan_stash_lib_test") {
  testonly = true
  manifest = "meta/wlan_stash_lib_test.cml"
  deps = [ ":wlan-stash_test" ]
}

fuchsia_test_package("wlan-stash-tests") {
  test_components = [ ":wlan_stash_lib_test" ]
  deps = [
    "//src/sys/stash:stash_secure_v2",
    "//src/sys/stash:stash_v2",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
