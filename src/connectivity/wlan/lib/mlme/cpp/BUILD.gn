# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/args.gni")

declare_args() {
  # Selects the SoftMAC client implementation to use. Choices:
  #   false (default) - C++ Client MLME implementation
  #   true - Rust Client MLME implementation
  # This argument is temporary until Rust MLME is ready to be used.
  experimental_wlan_client_mlme = false
}

config("wlan_mlme_config") {
  include_dirs = [ "include" ]
}

source_set("mlme") {
  if (experimental_wlan_client_mlme) {
    defines = [ "USE_RUST_CLIENT_MLME" ]
  }

  public = [
    "include/wlan/mlme/ap/ap_mlme.h",
    "include/wlan/mlme/ap/tim.h",
    "include/wlan/mlme/client/client_mlme.h",
    "include/wlan/mlme/debug.h",
    "include/wlan/mlme/device_interface.h",
    "include/wlan/mlme/eapol.h",
    "include/wlan/mlme/frame_validation.h",
    "include/wlan/mlme/mac_frame.h",
    "include/wlan/mlme/macaddr_map.h",
    "include/wlan/mlme/mlme.h",
    "include/wlan/mlme/packet.h",
    "include/wlan/mlme/rust_utils.h",
    "include/wlan/mlme/wlan.h",
  ]

  sources = [
    "ap/ap_mlme.cc",
    "ap/tim.cc",
    "beacon.cc",
    "client/client_mlme.cc",
    "debug.cc",
    "device_interface.cc",
    "ht.cc",
    "key.cc",
    "mac_frame.cc",
    "mac_header_writer.cc",
    "packet.cc",
    "rate_limiter.cc",
    "rates_elements.cc",
    "rust_utils.cc",
    "validate_frame.cc",
  ]

  public_deps = [
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.wlan.softmac:fuchsia.hardware.wlan.softmac_banjo_cpp",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_banjo_c",
    "//sdk/fidl/fuchsia.wlan.mlme:fuchsia.wlan.mlme_hlcpp",
    "//sdk/fidl/fuchsia.wlan.stats:fuchsia.wlan.stats_hlcpp",
    "//sdk/lib/fdio",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//src/connectivity/wlan/lib/mlme/rust/c-binding:wlan-mlme-c",
    "//src/devices/lib/dev-operation",

    # TODO(fxbug.dev/38599): The mundane dependency here is purely due to
    # a problem with transitive Rust static library dependencies when linking
    # C++ programs
    "//src/lib/mundane",
    "//src/lib/timekeeper",
    "//src/lib/timekeeper:system",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_c",
    "//sdk/fidl/fuchsia.wlan.minstrel:fuchsia.wlan.minstrel_hlcpp",
  ]

  if (fidl_trace_level != 0) {
    public_deps += [
      "//src/lib/trace/rust:trace-provider",
      "//zircon/system/ulib/trace-engine",
    ]
  }

  public_configs = [ ":wlan_mlme_config" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}
