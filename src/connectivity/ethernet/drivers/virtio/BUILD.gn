# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

source_set("virtio_ethernet_lib") {
  defines = [ "_ALL_SOURCE" ]
  sources = [
    "ethernet.cc",
    "ethernet_driver.cc",
  ]
  public_deps = [
    "//src/devices/bus/lib/virtio",
    "//zircon/public/lib/ddktl",
    "//zircon/system/banjo/ddk.protocol.ethernet",
    "//zircon/system/dev/lib/dev-operation",
  ]
}

driver_module("virtio_ethernet") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  deps = [
    ":virtio_ethernet_lib",
    "//src/devices/lib/driver",
  ]
}

test("virtio-ethernet-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    testonly = true
  }
  sources = [ "ethernet_test.cc" ]
  deps = [
    ":virtio_ethernet_lib",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fidl-async-cpp",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/fake-bti",
    "//zircon/system/dev/lib/fake_ddk",
  ]
}

migrated_manifest("virtio_ethernet-manifest") {
  deps = [ ":virtio_ethernet" ]
}

unittest_package("virtio-ethernet-test-package") {
  package_name = "virtio-ethernet-test"
  deps = [ ":virtio-ethernet-test" ]

  tests = [
    {
      name = "virtio-ethernet-test"
      dest = "virtio-ethernet-test"
    },
  ]
}
