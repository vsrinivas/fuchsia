# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("virtio_ethernet_bind") {
  rules = "virtio_ethernet.bind"
  header_output = "virtio_ethernet-bind.h"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.pci",
  ]
}

source_set("virtio_ethernet_lib") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "ethernet.cc",
    "ethernet_driver.cc",
  ]
  public_deps = [
    ":virtio_ethernet_bind_header",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/dev-operation",
    "//src/lib/ddktl",
  ]
}

fuchsia_driver("virtio_ethernet-driver") {
  output_name = "virtio_ethernet"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":virtio_ethernet_lib",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("virtio_ethernet") {
  component_name = "virtio_ethernet"
  deps = [
    ":virtio_ethernet-driver",
    ":virtio_ethernet_bind",
  ]

  info = "virtio_ethernet-info.json"
  colocate = false
}

test("virtio-ethernet-test") {
  if (is_fuchsia) {
    testonly = true
  }
  sources = [ "ethernet_test.cc" ]
  deps = [
    ":virtio_ethernet_lib",
    "//src/devices/bus/lib/virtio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("virtio-ethernet-test-package") {
  package_name = "virtio-ethernet-test"
  deps = [ ":virtio-ethernet-test" ]
}

test("virtio-netdevice-test") {
  sources = [ "netdevice_test.cc" ]
  deps = [
    ":virtio_netdevice_lib",
    "//src/devices/bus/lib/virtio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/mock-ddk",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("virtio-netdevice-test-package") {
  package_name = "virtio-netdevice-test"
  deps = [ ":virtio-netdevice-test" ]
}

source_set("virtio_netdevice_lib") {
  sources = [
    "netdevice.cc",
    "netdevice.h",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.network.device:fuchsia.hardware.network.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.network.mac:fuchsia.hardware.network.mac_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_cpp_wire",
    "//src/connectivity/network/drivers/network-device/device:shared_lock",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/dev-operation",
    "//src/lib/ddktl",
    "//src/lib/vmo_store",
  ]
}

fuchsia_driver("virtio_netdevice-driver") {
  output_name = "virtio_netdevice"
  sources = [ "netdevice_driver.cc" ]
  deps = [
    ":virtio_ethernet_bind",
    ":virtio_netdevice_lib",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("virtio_netdevice") {
  component_name = "virtio_netdevice"
  deps = [ ":virtio_netdevice-driver" ]
  info = "virtio_ethernet-info.json"
  colocate = true
}

group("tests") {
  testonly = true
  deps = [
    ":virtio-ethernet-test-package",
    ":virtio-netdevice-test-package",
    ":virtio_ethernet_bind_test",
  ]
}
