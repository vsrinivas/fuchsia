# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":bigendian-tests",
    ":circular_queue-tests",
    ":gvnic-tests",
    ":gvnic_bind_test",
    ":pagelist-tests",
  ]
}

driver_bind_rules("gvnic_bind") {
  rules = "gvnic.bind"
  header_output = "gvnic-bind.h"
  bind_output = "gvnic.bindbc"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.pci" ]
}

fuchsia_driver("gvnic-driver") {
  output_name = "gvnic"
  sources = [
    "abi.h",
    "bigendian.h",
    "circular_queue.h",
    "gvnic.cc",
    "gvnic.h",
    "pagelist.cc",
    "pagelist.h",
  ]
  deps = [
    ":gvnic_bind",
    "//sdk/banjo/fuchsia.hardware.network.device:fuchsia.hardware.network.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.network.mac:fuchsia.hardware.network.mac_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_cpp_wire",
    "//src/connectivity/network/drivers/network-device/device:shared_lock",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/driver",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddktl",
    "//src/zircon/lib/zircon",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
  ]
}

fuchsia_driver_component("gvnic") {
  component_name = "gvnic"
  deps = [ ":gvnic-driver" ]
  info = "gvnic-info.json"
  colocate = true
}

fuchsia_driver_package("gvnic-package") {
  package_name = "gvnic"
  driver_components = [ ":gvnic" ]
}

test("gvnic-unit-tests") {
  sources = [
    "abi.h",
    "bigendian.h",
    "circular_queue.h",
    "gvnic.cc",
    "gvnic.h",
    "pagelist.cc",
    "pagelist.h",
    "unit-tests.cc",
  ]
  deps = [
    ":gvnic_bind",
    "//sdk/banjo/fuchsia.hardware.network.device:fuchsia.hardware.network.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.network.mac:fuchsia.hardware.network.mac_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_cpp_wire",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/connectivity/network/drivers/network-device/device:shared_lock",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/driver",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("gvnic-tests") {
  deps = [ ":gvnic-unit-tests" ]
}

test("bigendian_test") {
  sources = [
    "bigendian.h",
    "bigendian_test.cc",
  ]
  deps = [ "//zircon/system/ulib/zxtest" ]
}

fuchsia_unittest_package("bigendian-tests") {
  deps = [ ":bigendian_test" ]
}

test("circular_queue_test") {
  sources = [
    "circular_queue.h",
    "circular_queue_test.cc",
  ]
  deps = [ "//zircon/system/ulib/zxtest" ]
}

fuchsia_unittest_package("circular_queue-tests") {
  deps = [ ":circular_queue_test" ]
}

test("pagelist_test") {
  sources = [
    "bigendian.h",
    "pagelist.cc",
    "pagelist.h",
    "pagelist_test.cc",
  ]
  deps = [
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake-dma-buffer",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("pagelist-tests") {
  deps = [ ":pagelist_test" ]
}
