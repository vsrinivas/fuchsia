# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("rndis_function_bind") {
  rules = "rndis_function.bind"
  header_output = "rndis_function_bind.h"
  bind_output = "rndis-function.bindbc"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.misc",
  ]
}

source_set("rndis-function-lib") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "rndis_function.cc",
    "rndis_function.h",
  ]
  public_deps = [
    ":rndis_function_bind",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.composite:fuchsia.hardware.usb.composite_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//src/connectivity/ethernet/lib/rndis",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:cdc",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver("rndis-function-driver") {
  # visibility = [ ":*" ]
  output_name = "rndis-function"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":rndis-function-lib",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("rndis-function") {
  component_name = "rndis-function"
  deps = [ ":rndis-function-driver" ]
  info = "rndis-function-info.json"
  colocate = true
}

test("rndis-function-test") {
  if (is_fuchsia) {
    testonly = true
  }
  sources = [ "rndis_function_test.cc" ]
  deps = [
    ":rndis-function-lib",
    "//src/devices/testing/fake_ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("rndis-function-test-package") {
  package_name = "rndis-function-test"
  deps = [ ":rndis-function-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":rndis-function-test-package",
    ":rndis_function_bind_test",
  ]
}
