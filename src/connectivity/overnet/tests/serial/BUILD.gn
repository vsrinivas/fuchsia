# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/testing/host_test_data.gni")

group("tests") {
  testonly = true

  deps = []

  if (host_os == "linux") {
    deps += [ ":overnet_serial_tests($host_toolchain)" ]
  }
}

if (is_linux) {
  go_library("lib") {
    testonly = true

    deps = [ "//tools/emulator/emulatortest" ]

    sources = [ "serial_test.go" ]
  }

  host_test_data("ascendd") {
    sources = [ "$root_build_dir/host_x64/ascendd" ]
    deps = [ "//src/connectivity/overnet/tools/ascendd:bin($host_toolchain)" ]
  }

  go_test("overnet_serial_tests") {
    library = ":lib"
    non_go_deps = [
      ":ascendd",
      "//build/images/overnet:test_data($host_toolchain)",
      "//build/images/overnet($target_toolchain)",
    ]
  }
}
