# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/testing/environments.gni")

copy("copy_test_commands") {
  sources = [
    get_label_info(
            "//src/connectivity/overnet/examples/echo:bin($host_toolchain)",
            "root_out_dir") + "/overnet_echo",
    get_label_info(
            "//src/connectivity/overnet/examples/interface_passing:bin($host_toolchain)",
            "root_out_dir") + "/overnet_interface_passing",
    get_label_info(
            "//src/connectivity/overnet/tools/ascendd:bin($host_toolchain)",
            "root_out_dir") + "/ascendd",
    get_label_info("//src/connectivity/overnet/tools/onet:bin($host_toolchain)",
                   "root_out_dir") + "/onet",
  ]

  outputs = [
    "$root_out_dir/overnet_host_examples_test_commands/{{source_file_part}}",
  ]

  metadata = {
    test_runtime_deps = [
      "$root_out_dir/overnet_host_examples_test_commands/overnet_echo",
      "$root_out_dir/overnet_host_examples_test_commands/overnet_interface_passing",
      "$root_out_dir/overnet_host_examples_test_commands/ascendd",
      "$root_out_dir/overnet_host_examples_test_commands/onet",
    ]
  }

  deps = [
    "//src/connectivity/overnet/examples/echo:bin($host_toolchain)",
    "//src/connectivity/overnet/examples/interface_passing:bin($host_toolchain)",
    "//src/connectivity/overnet/tools/ascendd:bin($host_toolchain)",
    "//src/connectivity/overnet/tools/onet:bin($host_toolchain)",
  ]
}

rustc_binary("bin") {
  name = "overnet_host_examples"
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//src/connectivity/overnet/lib/hoist",
    "//src/lib/argh",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:rand",
  ]
  non_rust_deps = [ ":copy_test_commands" ]
  test_environments = [
    linux_env,
    mac_env,
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":bin_test($host_toolchain)",
    ":bin($host_toolchain)"
  ]
}
