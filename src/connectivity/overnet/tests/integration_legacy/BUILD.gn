# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_library.gni")

rustc_test("integration_legacy") {
  edition = "2021"
  output_name = "overnet_integration_legacy_tests"
  deps = [
    ":triangle_rust",
    "//sdk/fidl/fuchsia.overnet:fuchsia.overnet_rust",
    "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol_rust",
    "//src/connectivity/overnet/lib/core",
    "//src/connectivity/overnet/lib/stream_link",
    "//src/connectivity/overnet/lib/udp_link",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/zircon/rust:fuchsia-zircon-status",
    "//src/testing/fidl:placeholders_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:tracing",
  ]
  if (is_host) {
    deps += [ "//src/connectivity/overnet/lib/core:test_certs" ]
  }

  sources = [
    "src/drop.rs",
    "src/echo.rs",
    "src/lib.rs",
    "src/triangle.rs",
  ]
  inputs = [
    "//third_party/rust_crates/mirrors/quiche/quiche/examples/cert.crt",
    "//third_party/rust_crates/mirrors/quiche/quiche/examples/cert.key",
    "//third_party/rust_crates/mirrors/quiche/quiche/examples/rootca.crt",
  ]
}

fidl("triangle") {
  name = "fuchsia.overnet.triangletests"
  sources = [ "src/triangle.test.fidl" ]
  public_deps = [ "//src/testing/fidl:placeholders" ]
}

fuchsia_unittest_package("overnet-integration-legacy-tests") {
  deps = [
    ":integration_legacy",
    "//src/connectivity/overnet/overnetstack:certificates",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":integration_legacy($host_toolchain)",
    ":overnet-integration-legacy-tests",
  ]
}
