# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//src/connectivity/overnet/enable_circuit.gni")

rustc_library("hoist") {
  with_unit_tests = true
  edition = "2021"
  deps = [
    "//sdk/fidl/fuchsia.overnet:fuchsia.overnet_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:once_cell",
  ]
  non_rust_deps = [ "//third_party/boringssl" ]

  sources = [
    "src/fuchsia.rs",
    "src/lib.rs",
    "src/not_fuchsia.rs",
  ]

  inputs = [
    "//third_party/rust_crates/mirrors/quiche/quiche/examples/cert.crt",
    "//third_party/rust_crates/mirrors/quiche/quiche/examples/cert.key",
    "//third_party/rust_crates/mirrors/quiche/quiche/examples/rootca.crt",
  ]

  test_deps = [ "//src/lib/fuchsia" ]

  if (is_fuchsia) {
    deps += [
      "//src/lib/diagnostics/log/rust",
      "//src/lib/fuchsia-component",
    ]
    test_deps += [ "//third_party/rust_crates:futures" ]
  } else {
    deps += [
      "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol_rust",
      "//src/connectivity/overnet/lib/core",
      "//src/connectivity/overnet/lib/stream_link",
      "//third_party/rust_crates:async-net",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:tempfile",
      "//third_party/rust_crates:tracing",
    ]
    test_deps += [ "//third_party/rust_crates:scopeguard" ]

    if (enable_circuit) {
      deps += [ "//src/connectivity/overnet/lib/circuit" ]
    }
  }

  if (enable_circuit) {
    features = [ "circuit" ]
  }
}

fuchsia_unittest_package("overnet-hoist-tests") {
  manifest = "meta/hoist_lib_test.cml"
  deps = [
    ":hoist_test",
    "//src/connectivity/overnet/overnetstack:component",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":hoist_test($host_toolchain)",
    ":overnet-hoist-tests",
  ]
}
