# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//build/testing/host_test_data.gni")

CORE_DEPS = [
  "//sdk/fidl/fuchsia.overnet:fuchsia.overnet-rustc",
  "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol-rustc",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fuchsia-async",
  "//src/lib/zircon/rust:fuchsia-zircon-status",
  "//third_party/boringssl/rust/boringssl-sys",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:base64",
  "//third_party/rust_crates:byteorder",
  "//third_party/rust_crates:crc",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:lazy_static",
  "//third_party/rust_crates:libc",
  "//third_party/rust_crates:log",
  "//third_party/rust_crates:matches",
  "//third_party/rust_crates:parking_lot",
  "//third_party/rust_crates:rand",
  "//third_party/rust_crates:rental",
  "//third_party/rust_crates:thiserror",
  "//third_party/rust-mirrors/quiche",
  "//third_party/boringssl",
]

if (is_fuchsia) {
  CORE_DEPS += [ "//src/lib/zircon/rust:fuchsia-zircon" ]
} else {
  CORE_DEPS += [
    "//third_party/rust_crates:smol",
    "//third_party/rust_crates:tempfile",
  ]
}

CORE_SOURCES = [
  "src/async_quic.rs",
  "src/coding.rs",
  "src/diagnostics_service.rs",
  "src/fidl_tests/mod.rs",
  "src/framed_stream.rs",
  "src/future_help.rs",
  "src/handle_info.rs",
  "src/integration_tests/mod.rs",
  "src/labels.rs",
  "src/lib.rs",
  "src/link.rs",
  "src/link_frame_label.rs",
  "src/link_status_updater.rs",
  "src/peer.rs",
  "src/ping_tracker.rs",
  "src/proxy.rs",
  "src/proxy/main.rs",
  "src/proxy/spawn.rs",
  "src/proxy/xfer.rs",
  "src/proxy_stream.rs",
  "src/proxyable_handle.rs",
  "src/proxyable_handle/channel.rs",
  "src/proxyable_handle/socket.rs",
  "src/quic_link.rs",
  "src/route_planner.rs",
  "src/router.rs",
  "src/security_context.rs",
  "src/service_map.rs",
  "src/socket_link.rs",
  "src/stat_counter.rs",
  "src/stream_framer.rs",
]

rustc_library("core") {
  name = "overnet_core"
  edition = "2018"
  deps = CORE_DEPS

  sources = CORE_SOURCES
}

rustc_test("core_test") {
  name = "overnet_core_test"
  edition = "2018"

  deps = CORE_DEPS + [
           ":triangle-rustc",
           "//garnet/examples/fidl/services:echo-rustc",
           "//src/lib/fidl/rust/handle_tests:fidl_handle_tests",
           "//third_party/rust_crates:arbitrary",
           "//third_party/rust_crates:async-trait",
         ]

  # TODO(56138): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]

  if (is_host) {
    deps += [ ":test_certs" ]
    args = [ "--test-threads=1" ]
  }
  sources = CORE_SOURCES + [
              "src/fidl_tests/channel.rs",
              "src/fidl_tests/socket.rs",
              "src/integration_tests/drop.rs",
              "src/integration_tests/echo.rs",
              "src/integration_tests/mod.rs",
              "src/integration_tests/triangle.rs",
              "src/test_util.rs",
            ]
}

if (is_host) {
  host_test_data("test_certs") {
    sources = [
      "//third_party/rust-mirrors/quiche/examples/cert.crt",
      "//third_party/rust-mirrors/quiche/examples/cert.key",
      "//third_party/rust-mirrors/quiche/examples/rootca.crt",
    ]

    outputs = [ "$root_out_dir/overnet_test_certs/{{source_file_part}}" ]
  }
}

unittest_package("overnet-core-tests") {
  deps = [ ":core_test" ]

  tests = [
    {
      name = "overnet_core_test"
      environments = [ emu_env ]
    },
  ]

  # Use quiche example certs for unit tests.
  resources = [
    {
      path = rebase_path("//third_party/rust-mirrors/quiche/examples/cert.crt")
      dest = "cert.crt"
    },
    {
      path = rebase_path("//third_party/rust-mirrors/quiche/examples/cert.key")
      dest = "cert.key"
    },
    {
      path =
          rebase_path("//third_party/rust-mirrors/quiche/examples/rootca.crt")
      dest = "rootca.crt"
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":core_test($host_toolchain)",
    ":overnet-core-tests",
  ]
}

fidl("triangle") {
  # TODO(fxb/35879): Remove lint exclusions by fixing known FIDL lint violations in this target
  excluded_checks = [ "too-many-nested-libraries" ]

  name = "fuchsia.overnet.triangletests"

  sources = [ "src/integration_tests/triangle.test.fidl" ]

  public_deps = [ "//garnet/examples/fidl/services:echo" ]
}
