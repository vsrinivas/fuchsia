# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")

if (!is_fuchsia) {
  import("//build/rust/rustc_binary.gni")
  import("//build/rust/rustc_library.gni")

  rustc_library("lib") {
    name = "ascendd-lib"
    edition = "2018"

    deps = [
      "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol-rustc",
      "//src/connectivity/overnet/lib/core",
      "//src/connectivity/overnet/lib/hoist",
      "//src/connectivity/overnet/lib/serial_link",
      "//src/lib/fidl/rust/fidl",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:async-std",
      "//third_party/rust_crates:clap",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:log",
      "//third_party/rust_crates:rand",
      "//third_party/rust_crates:termios",
      "//third_party/rust_crates:thiserror",
    ]

    non_rust_deps = [ "//third_party/boringssl" ]

    sources = [
      "src/lib.rs",
      "src/serial.rs",
    ]
  }

  rustc_binary("bin") {
    name = "ascendd"
    edition = "2018"

    deps = [
      ":lib",
      "//src/connectivity/overnet/lib/hoist",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:async-std",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:log",
    ]

    non_rust_deps = [ "//third_party/boringssl" ]

    sources = [ "src/main.rs" ]
  }
}

install_host_tools("host") {
  deps = [ ":bin($host_toolchain)" ]

  outputs = [ "ascendd" ]
}
