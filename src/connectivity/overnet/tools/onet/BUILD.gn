# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/package.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "onet"
  edition = "2018"

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//sdk/fidl/fuchsia.overnet:fuchsia.overnet-rustc",
    "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol-rustc",
    "//src/connectivity/overnet/lib/core",
    "//src/connectivity/overnet/lib/hoist",
    "//third_party/rust_crates:clap",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:structopt",
  ]

  non_rust_deps = []
  if (!is_fuchsia) {
    non_rust_deps += [ "//third_party/boringssl" ]
  }
}

package("onet") {
  meta = [
    {
      path = "meta/onet.cmx"
      dest = "onet.cmx"
    },
  ]

  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "onet"
      shell = true
    },
  ]
}

group("tests") {
  testonly = true
  deps = []
}

install_host_tools("host") {
  deps = [
    ":bin",
  ]
  outputs = [
    "onet",
  ]
}
