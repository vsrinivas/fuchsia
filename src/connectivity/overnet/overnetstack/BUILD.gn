# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("overnet_fuchsia_compatibility") {
  sources = [
    "fuchsia_port.h",
  ]

  deps = [
    "//src/connectivity/overnet/lib",
  ]
}

executable("overnetstack_unittests") {
  testonly = true

  sources = [
    "run_all_tests.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.overnet",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

executable("bin") {
  output_name = "overnetstack"

  sources = [
    "bound_channel.cc",
    "bound_channel.h",
    "bound_socket.cc",
    "bound_socket.h",
    "main.cc",
    "mdns.cc",
    "mdns.h",
    "omdp_nub.cc",
    "omdp_nub.h",
    "overnet_app.cc",
    "overnet_app.h",
    "service.cc",
    "service.h",
    "udp_nub.h",
  ]

  deps = [
    ":overnet_fuchsia_compatibility",
    "//garnet/public/lib/fostr/fidl/fuchsia.net.mdns",
    "//garnet/public/lib/fostr/fidl/fuchsia.overnet.omdp",
    "//garnet/public/lib/fsl",
    "//sdk/fidl/fuchsia.net.mdns",
    "//sdk/fidl/fuchsia.overnet",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//src/connectivity/overnet/lib",
    "//src/connectivity/overnet/lib/omdp",
    "//src/connectivity/overnet/lib/protocol:coding",
    "//src/connectivity/overnet/lib/vocabulary:ip_addr",
    "//src/connectivity/overnet/lib/vocabulary:socket",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/fbl",
  ]
}

package("overnetstack") {
  deps = [
    ":bin",
  ]

  binary = "overnetstack"

  meta = [
    {
      path = rebase_path("meta/overnetstack.cmx")
      dest = "overnetstack.cmx"
    },
  ]
}

package("overnetstack_tests") {
  testonly = true

  deps = [
    ":overnetstack_unittests",
  ]

  tests = [
    {
      name = "overnetstack_unittests"
      environments = basic_envs
    },
  ]
}
