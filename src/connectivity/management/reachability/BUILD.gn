# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "reachability"
  version = "0.1.0"
  edition = "2018"

  deps = [
    "core:lib",
    "//sdk/fidl/fuchsia.net:fuchsia.net-rustc",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces-rustc",
    "//sdk/fidl/fuchsia.netstack:fuchsia.netstack-rustc",
    "//src/lib/diagnostics/inspect/contrib/rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/network/fidl_fuchsia_net_interfaces_ext",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [
    "src/eventloop.rs",
    "src/main.rs",
  ]
}

fuchsia_component("component-v2") {
  component_name = "reachability"
  manifest = "meta/reachability.cml"
  deps = [ ":bin" ]
}

fuchsia_component("component") {
  component_name = "reachability"
  manifest = "meta/reachability.cmx"
  deps = [ ":bin" ]
}

fuchsia_package("reachability") {
  deps = [ ":component" ]
}

group("tests") {
  testonly = true
  deps = [ "core:tests" ]
}

config_data("reachability_sysmgr_config") {
  for_pkg = "sysmgr"
  sources = [ rebase_path("config/reachability.sysmgr_config") ]
}
