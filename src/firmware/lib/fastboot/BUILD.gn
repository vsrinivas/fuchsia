# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("fastboot") {
  public_deps = [ ":lib" ]
}

config("include") {
  include_dirs = [ "include" ]
}

source_set("fastboot-base") {
  sources = [ "fastboot_base.cc" ]
  public_configs = [ ":include" ]
  public_deps = [ "//zircon/system/ulib/zxc" ]

  if (is_kernel) {
    public_deps += [ "//zircon/kernel/lib/libc" ]
  }
}

source_set("lib") {
  sources = [
    "fastboot.cc",
    "payload-streamer.cc",
    "sparse_format.h",
  ]
  public_configs = [ ":include" ]
  public_deps = [
    ":fastboot-base",
    "//sdk/fidl/fuchsia.buildinfo:fuchsia.buildinfo_cpp_wire",
    "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_cpp_wire",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_cpp_wire",
    "//src/developer/sshd-host:sshd_constants",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zxc",
  ]
  deps = [
    "//sdk/lib/sys/component/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
    "//src/storage/lib/paver",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zx",
  ]
}
