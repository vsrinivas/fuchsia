# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("cros-nvtool") {
  with_unit_tests = true

  configs += [ "//build/config/rust:bootfs" ]

  # we can't dynamically link std when we're in the zbi
  configs -= [ "//build/config/fuchsia:dynamic_rust_standard_library" ]

  configs -= [ "//build/config/rust:allow_unused_results" ]

  # TODO(fxbug.dev/80514): Remove the below
  if (is_debug) {
    configs += [ "//build/config/lto:thinlto" ]
    configs -= [ "//build/config/lto:thinlto" ]
    configs += [ "//build/config/lto:thinlto" ]
  }

  deps = [
    "//sdk/fidl/fuchsia.vboot:fuchsia.vboot-rustc",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
  ]

  sources = [
    "src/main.rs",
    "src/param.rs",
  ]
}

fuchsia_unittest_package("cros-nvtool-unittests") {
  deps = [ ":cros-nvtool_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":cros-nvtool-unittests" ]
}
