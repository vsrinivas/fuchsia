# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/python/python_host_test.gni")
import("//src/proc/tests/starnix_test_package.gni")

group("tests") {
  testonly = true
  if (target_cpu == "x64" && !is_asan) {
    deps = [
      ":android",
      ":android_with_error_logs",
      ":android_with_init",
      "benchmarks",
      "gvisor",
      "wayland",
    ]
    # TODO(fxbug.dev/110446): Re-enable once flake is sorted out.
    # public_deps = [ ":adb_art_test($host_toolchain)" ]
  }
}

resource("binder_smoke_test_bin") {
  sources = [
    "//prebuilt/starnix/tests/syscalls/linux-amd64/android/binder_smoke_test",
  ]
  outputs = [ "data/tests/binder_smoke_test" ]
}

fuchsia_test_component("binder_smoke_test") {
  manifest = "meta/binder_smoke_test.cml"
  deps = [
    ":binder_smoke_test_bin",
    "//src/proc/tests:starnix_test",
  ]
}

resource("binder_test_script") {
  sources = [ "binderLibTestLauncher.sh" ]
  outputs = [ "data/tests/binderLibTestLauncher.sh" ]
}

fuchsia_test_component("binder_test") {
  manifest = "meta/binder_test.cml"
  deps = [
    ":binder_test_script",
    "//src/proc/tests:starnix_test",
  ]
}

fuchsia_test_component("adbd_test") {
  manifest = "meta/adbd_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_test_component("libutils_test") {
  manifest = "meta/libutils_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

resource("starnix_testcases_image") {
  sources = [ "//prebuilt/starnix/android-image-amd64/starnix_testcases.img" ]
  outputs = [ "data/testcases.img" ]
}

fuchsia_test_component("init_test") {
  manifest = "meta/init_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_component("sh_test") {
  testonly = true
  check_references = false
  manifest = "meta/sh_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_component("boringssl_self_test64") {
  testonly = true
  check_references = false
  manifest = "meta/boringssl_self_test64.cml"
}

starbionic_test_package("android") {
  test_components = [
    ":adbd_test",
    ":boringssl_self_test64",
    ":sh_test",
    ":binder_smoke_test",
    ":libutils_test",
  ]
}

starnix_test_package("android_with_init") {
  test_components = [ ":init_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  galaxy = "//src/proc/bin/galaxies/starbionic"
}

starbionic_test_package("android_with_error_logs") {
  test_components = [ ":binder_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

if (is_host) {
  python_host_test("adb_art_test") {
    main_source = "adb/adb_art_test.py"
    environments = [ emu_env ]
    deps = [ ":adb_test_deps" ]
  }
  host_test_data("adb_test_deps") {
    sources = [
      "$root_out_dir/ffx",
      "//prebuilt/starnix/internal/android-image-amd64/adb",
      "//prebuilt/starnix/internal/android-image-amd64/art-run-test-target-data.zip",
    ]
  }
}
