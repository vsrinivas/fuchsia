# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/proc/tests/chromiumos/build/starnix_linux_test_component.gni")
import("//src/proc/tests/starnix_test_package.gni")

group("tests") {
  testonly = true
  deps = []

  if (target_cpu == "x64" && !is_asan) {
    deps += [ ":test_chromiumos_syscalls" ]
  }
}

test("syscalls_test_bin") {
  sources = [
    "bpf_test.cc",
    "custom_artifact_test.cc",
    "fcntl_lock_test.cc",
    "fs_test.cc",
    "mmap_test.cc",
    "mount_test.cc",
    "pipe_test.cc",
    "poll_test.cc",
    "prctl_test.cc",
    "restart_read_test.cc",
    "socket_test.cc",
    "terminal_test.cc",
    "test_helper.cc",
    "test_helper.h",
    "test_helper_test.cc",
    "trivial_test.cc",
  ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
  include_dirs = [
    "//third_party/android/platform/bionic/libc/kernel/uapi",
    "//third_party/android/platform/bionic/libc/kernel/android/uapi",
  ]
  if (target_cpu == "x64") {
    include_dirs +=
        [ "//third_party/android/platform/bionic/libc/kernel/uapi/asm-x86" ]
  }
}

starnix_linux_test_component("syscalls_test") {
  test_label = ":syscalls_test_bin"
  test_binary = "syscalls_test_bin"
}

starmium_test_package("test_chromiumos_syscalls") {
  test_components = [ ":syscalls_test" ]
}
