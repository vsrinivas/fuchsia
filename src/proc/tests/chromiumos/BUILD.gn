# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")
import("build/starnix_host_test_component.gni")
import("build/starnix_linux_test_component.gni")

group("tests") {
  testonly = true
  deps = [ "syscalls:tests" ]

  if (target_cpu == "x64") {
    deps += [ ":test_chromiumos_distro" ]

    # TODO(lindkvist): Remove !is_asan check when Starnix can run binaries built for
    # ASAN.
    if (!is_asan) {
      deps += [ ":test_starnix_magma" ]
    }
  }
}

# Tests that use starmium_test_runner should depend on this.
expect_includes("starmium_test") {
  includes = [ "starmium_test.shard.cml" ]
}

fuchsia_component("sh_test") {
  testonly = true
  check_references = false
  manifest = "meta/sh_test.cml"
}

if (host_os == "linux" && host_cpu == "x64") {
  starnix_host_test_component("fdio-test") {
    test_label = "//sdk/lib/fdio/tests:fdio-test"
  }
}

fuchsia_test_package("test_chromiumos_distro") {
  test_components = [ ":sh_test" ]

  # TODO(abarth): Remove !is_asan check when Starnix can run binaries built for
  # ASAN.
  if (host_os == "linux" && host_cpu == "x64" && !is_asan) {
    test_components += [ ":fdio-test" ]
  }

  # This package intentionally contains many non-Fuchia ELF binaries that
  # are not under /data/, and some of them are even unstripped, so disable
  # theses checks to build successfully.
  disable_elf_binaries_checks = true
}

starnix_linux_test_component("virtmagma_conformance_tests") {
  test_label =
      "//src/graphics/lib/magma/tests/integration:virtmagma_conformance_tests"
}

fuchsia_test_package("test_starnix_magma") {
  test_components = [ ":virtmagma_conformance_tests" ]
  test_specs = {
    environments = [
      atlas_env,
      nuc_env,
    ]
  }
}
