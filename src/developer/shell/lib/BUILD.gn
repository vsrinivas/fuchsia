# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/quickjs/compiled_js.gni")

static_library("lib") {
  sources = [
    "fdio.cc",
    "fdio.h",
    "fidl.cc",
    "fidl.h",
    "qjs_util.h",
    "runtime.cc",
    "runtime.h",
    "zx.cc",
    "zx.h",
  ]

  public_deps = [
    ":fidl",
    ":zx",
    "//src/lib/fidl_codec",
    "//src/lib/fxl",
    "//src/lib/line_input",
    "//third_party/quickjs:qjs-lib",
    "//third_party/rapidjson",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fidl_base",
    "//zircon/public/lib/zx",
  ]
}

compiled_js("fidl") {
  source = "fidl.js"
  module = true
  extra_args = [
    "-M",
    "fidl_internal",
  ]
}

compiled_js("zx") {
  source = "zx.js"
  module = true
  extra_args = [
    "-M",
    "zx_internal",
  ]
}

action("js_libs") {
  script = "generate_js_libs.sh"
  sources = [
    "ns.js",
    "pp.js",
    "task.js",
    "util.js",
  ]
  generated_output = "$target_gen_dir/libs.manifest"
  outputs = [
    "$generated_output",
  ]
  args = [ rebase_path("$generated_output", root_build_dir) ] +
         rebase_path(sources, root_build_dir)
}

compiled_js("repl_init") {
  source = "repl_init.js"
  module = true
  extra_args = [
    "-M",
    "li_internal",
  ]
}

executable("spawn_child_test_util") {
  testonly = true

  sources = [
    "spawn_child_test_util.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.sys",
  ]
}

source_set("tests") {
  testonly = true
  sources = [
    "fidl_test.cc",
    "js_testing_utils.h",
    "ns_test.cc",
    "task_test.cc",
    "zx_test.cc",
  ]
  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fidl/cpp:test_support",
    "//sdk/lib/fidl/cpp/test",
    "//sdk/lib/sys/cpp",
    "//src/developer/shell/console",
    "//src/lib/fidl_codec:json_for_test",
    "//src/lib/fxl",
    "//src/sys/component_index/fidl:index",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/memfs",
    "//zircon/public/lib/task-utils",
    "//zircon/public/lib/zx",
  ]
}
