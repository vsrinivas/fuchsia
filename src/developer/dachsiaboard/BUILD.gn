# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/packages/prebuilt_package.gni")

prebuilt_package("dachsiaboard") {
  archive = "//prebuilt/infra/dachsiaboard/dachsiaboard.far"
}

config_data("system_config") {
  for_pkg = "sysmgr"
  sources = [ "config/present_view_dachsiaboard.config" ]
}

# The complete set of packages needed to build a system image which runs
# Dachsiaboard at system startup. This is intentionally not tested on CQ
# and not included in any existing product configuration. Images are built
# manually by the infra team on top of the core product during initial
# device setup. It depends on low-level packages that support many other
# users and use-cases, so should be fairly stable, but ultimately infra is
# on the hook for monitoring and maintaining this target.
#
# To build, use:
#   fx set core.x64 --release --with-base=//src/developer/dachsiaboard:bundle
group("bundle") {
  testonly = true
  public_deps = [
    ":dachsiaboard",
    ":system_config",
    "//garnet/bin/sysmem_connector",
    "//garnet/bin/vulkan_loader",
    "//src/connectivity/wlan:tools",
    "//src/flutter",
    "//src/fonts",
    "//src/ui/a11y",
    "//src/ui/bin/ime:ime_service",
    "//src/ui/bin/root_presenter",
    "//src/ui/bin/shortcut",
    "//src/ui/scenic",
    "//src/ui/tools:present_view",
    "//topaz/runtime/dart_runner:dart_jit_product_runner",
    "//topaz/runtime/dart_runner:dart_jit_runner",
  ]
}
