# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_unittest_component.gni")

visibility = [ ":*" ]

###############################################################################
# EXECUTABLES
###############################################################################
executable("config_unittest_exe") {
  testonly = true

  # "config_unittest" is rather generic for the default_output_dir that is set
  # to root_out_dir so instead we write the binary to target_out_dir.
  output_dir = target_out_dir

  sources = [ "config_unittest.cc" ]

  deps = [
    "//src/developer/forensics/feedback_data:config",
    "//src/developer/forensics/testing:gtest_with_syslog_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/zx",
  ]
}

executable("data_provider_unittest_exe") {
  testonly = true

  sources = [ "data_provider_unittest.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_hlcpp",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_hlcpp",
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_hlcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/fidl/fuchsia.math:fuchsia.math_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//src/developer/forensics/feedback/attachments",
    "//src/developer/forensics/feedback_data:constants",
    "//src/developer/forensics/feedback_data:data_provider",
    "//src/developer/forensics/feedback_data:metadata",
    "//src/developer/forensics/testing:gmatchers",
    "//src/developer/forensics/testing:gpretty_printers",
    "//src/developer/forensics/testing:gtest_with_syslog_main",
    "//src/developer/forensics/testing:unit_test_fixture",
    "//src/developer/forensics/testing/stubs:cobalt",
    "//src/developer/forensics/testing/stubs:scenic",
    "//src/developer/forensics/utils:archive",
    "//src/lib/fostr/fidl/fuchsia.math",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/timekeeper:testing",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//third_party/rapidjson",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("data_provider_controller_unittest_exe") {
  testonly = true

  sources = [ "data_provider_controller_unittest.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_hlcpp",
    "//src/developer/forensics/feedback_data:constants",
    "//src/developer/forensics/feedback_data:data_provider_controller",
    "//src/developer/forensics/testing:gtest_with_syslog_main",
    "//src/developer/forensics/testing:unit_test_fixture",
    "//src/lib/files",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

executable("inspect_data_budget_unittest_exe") {
  testonly = true

  sources = [ "inspect_data_budget_unittest.cc" ]

  deps = [
    "//src/developer/forensics/feedback_data:constants",
    "//src/developer/forensics/feedback_data:inspect_budget",
    "//src/developer/forensics/testing:gtest_with_syslog_main",
    "//src/developer/forensics/testing:unit_test_fixture",
    "//src/lib/files",
    "//src/lib/timekeeper:testing",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("log_source_unittest_exe") {
  testonly = true

  sources = [ "log_source_unittest.cc" ]

  deps = [
    "//sdk/lib/sys/cpp/testing:cpp",
    "//src/developer/forensics/feedback_data:log_source",
    "//src/developer/forensics/testing:gpretty_printers",
    "//src/developer/forensics/testing:gtest_with_syslog_main",
    "//src/developer/forensics/testing:log_message",
    "//src/developer/forensics/testing:unit_test_fixture",
    "//src/developer/forensics/testing/stubs:diagnostics",
    "//src/developer/forensics/utils:logs",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

executable("metadata_unittest_exe") {
  testonly = true

  sources = [ "metadata_unittest.cc" ]

  deps = [
    "//sdk/lib/fit-promise",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback/attachments",
    "//src/developer/forensics/feedback_data:constants",
    "//src/developer/forensics/feedback_data:metadata",
    "//src/developer/forensics/testing:unit_test_fixture",
    "//src/developer/forensics/utils:errors",
    "//src/developer/forensics/utils/redact",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/timekeeper:testing",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//third_party/rapidjson",
  ]
}

executable("screenshot_unittest_exe") {
  testonly = true

  sources = [ "screenshot_unittest.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//src/developer/forensics/feedback_data:screenshot",
    "//src/developer/forensics/testing:gtest_with_syslog_main",
    "//src/developer/forensics/testing:unit_test_fixture",
    "//src/developer/forensics/testing/stubs:scenic",
    "//src/lib/fxl",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/zx",
  ]
}

###############################################################################
# RESOURCES
###############################################################################
resource("test_configs") {
  sources = [
    "configs/bad_schema_duplicated_attachment_key.json",
    "configs/bad_schema_missing_required_field.json",
    "configs/bad_schema_spurious_field.json",
    "configs/valid.json",
    "configs/valid_empty_list.json",
  ]

  outputs = [ "data/configs/{{source_file_part}}" ]
}

resource("test_image") {
  sources =
      [ "//src/developer/forensics/testing/resources/checkerboard_100.png" ]

  outputs = [ "data/checkerboard_100.png" ]
}

###############################################################################
# COMPONENTS
###############################################################################
fuchsia_unittest_component("config_unittest") {
  deps = [
    ":config_unittest_exe",
    ":test_configs",
  ]
}

fuchsia_component("data_provider_unittest") {
  testonly = true

  manifest = "meta/data_provider_unittest.cml"

  deps = [
    ":data_provider_unittest_exe",
    ":test_image",
  ]
}

fuchsia_unittest_component("data_provider_controller_unittest") {
  deps = [
    ":data_provider_controller_unittest_exe",
    "//src/sys/test_runners:tmp_storage",
  ]
}

fuchsia_unittest_component("inspect_data_budget_unittest") {
  deps = [
    ":inspect_data_budget_unittest_exe",
    "//src/sys/test_runners:tmp_storage",
  ]
}

fuchsia_unittest_component("log_source_unittest") {
  deps = [ ":log_source_unittest_exe" ]
}

fuchsia_component("metadata_unittest") {
  testonly = true

  manifest = "meta/metadata_unittest.cml"

  deps = [ ":metadata_unittest_exe" ]
}

fuchsia_component("screenshot_unittest") {
  testonly = true

  manifest = "meta/screenshot_unittest.cml"

  deps = [ ":screenshot_unittest_exe" ]
}

###############################################################################
# PACKAGES
###############################################################################
fuchsia_test_package("feedback-data-tests") {
  test_components = [
    ":config_unittest",
    ":data_provider_unittest",
    ":data_provider_controller_unittest",
    ":inspect_data_budget_unittest",
    ":log_source_unittest",
    ":metadata_unittest",
    ":screenshot_unittest",
  ]

  test_specs = {
    log_settings = {
      max_severity = "FATAL"
    }
  }
}

###############################################################################
# GROUPS
###############################################################################
group("tests") {
  testonly = true

  deps = [ ":feedback-data-tests" ]

  visibility += [ "//src/developer/forensics:tests" ]
}
