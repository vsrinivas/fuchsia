# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("ffx_plugin_suite.gni")

# Defines FFX
#
template("ffx") {
  ffx_deps = []
  if (defined(invoker.deps)) {
    ffx_deps += invoker.deps
  }
  if (defined(invoker.plugin_deps)) {
    ffx_deps += invoker.plugin_deps
  }

  ffx_name = target_name

  if (defined(invoker.name)) {
    ffx_name = invoker.name
  }

  suite_name = ffx_name + "_lib"

  ffx_plugin_suite(suite_name) {
    forward_variables_from(invoker, "*", [])
    args_deps = [ "core:lib" ]

    # These are needed only for the default FFX
    extra_deps = [
      "//src/developer/ffx/plugins/daemon:ffx_daemon_suite_args",
      "//src/developer/ffx/plugins/daemon:ffx_daemon_suite_sub_command",
      "//src/developer/ffx/plugins/daemon/start:ffx_daemon_start_args",
    ]
  }

  rustc_binary(ffx_name + "_bin") {
    output_name = ffx_name
    deps = ffx_deps + [
             ":" + suite_name + "_args",
             ":" + suite_name + "_sub_command",
             ":" + suite_name,
           ]
    non_rust_deps = [ "//third_party/boringssl" ]
    forward_variables_from(invoker,
                           "*",
                           [
                             "name",
                             "deps",
                             "non_rust_deps",
                           ])

    configs += [ "//build/config:rust_panic_abort" ]
  }

  group(ffx_name + "_tests") {
    testonly = true

    deps = [
      ":" + ffx_name + "_bin_test($host_toolchain)",
      ":" + suite_name + "_tests($host_toolchain)",
      "config:tests($host_toolchain)",
      "core:tests($host_toolchain)",
    ]
  }
}
