# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/python/python.gni")

template("services") {
  output_name = target_name
  if (defined(invoker.output_name)) {
    output_name = invoker.output_name
  }
  services_deps = []
  if (defined(invoker.deps)) {
    services_deps += invoker.deps
  }

  generated_file = "$target_gen_dir/" + output_name

  libraries = []
  foreach(dep, services_deps) {
    libraries += [ get_label_info(dep, "name") ]
  }

  action(target_name) {
    script = "//src/developer/ffx/build/gn_generate_services_macro.py"
    args = [
      "--deps",
      string_join(",", libraries),
      "--deps_full",
      string_join(",", services_deps),
      "--out",
      rebase_path(generated_file, root_build_dir),
    ]

    inputs = [ "//src/developer/ffx/build/templates/services_macro.md" ] +
             third_party_jinja2_srcs + third_party_markupsafe_srcs
    outputs = [ generated_file ]
  }
}
