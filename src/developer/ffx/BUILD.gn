# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("./build/ffx.gni")

group("ffx") {
  testonly = true

  public_deps = [
    ":ffx_tool",
    ":tests",
  ]
}

if (host_toolchain == current_toolchain) {
  ffx("ffx") {
    version = "0.1.0"
    edition = "2018"
    with_unit_tests = true

    plugin_deps = [
      "//src/developer/ffx/doctor:ffx_doctor",
      "//src/developer/ffx/packaging:ffx_packaging",
      "//src/developer/ffx/plugins/component:ffx_component",
      "//src/developer/ffx/plugins/config:ffx_config_plugin",
      "//src/developer/ffx/plugins/daemon:ffx_daemon_plugin",
      "//src/developer/ffx/plugins/target:ffx_target",
      "//src/developer/ffx/plugins/zxdb:ffx_zxdb_plugin",
    ]

    sources = [
      "src/args.rs",
      "src/constants.rs",
      "src/logger.rs",
      "src/main.rs",
    ]
  }
}

install_host_tools("host") {
  deps = [ ":ffx_bin($host_toolchain)" ]
  outputs = [ "ffx" ]
}

group("ffx_tool") {
  deps = [
    ":ffx_bin($host_toolchain)",
    ":host",
    "daemon:lib($host_toolchain)",
  ]
}

group("runtime") {
  deps = [
    # The tool
    ":host",

    # Target-side dependencies
    "//src/developer/remote-control:runtime",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":ffx_tests($host_toolchain)",
    "core:tests($host_toolchain)",
    "daemon:tests($host_toolchain)",
    "plugins:tests($host_toolchain)",
  ]
}
