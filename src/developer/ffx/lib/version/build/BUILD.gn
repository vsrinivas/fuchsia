# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

executable("truncate") {
  sources = [ "truncate.cc" ]
}

if (current_toolchain == default_toolchain) {
  import("//build/compiled_action.gni")
  import("//build/info/info.gni")

  action("gen_version_info") {
    script = "gn_write_version_info.sh"

    # Ensure this action is invoked if integration HEAD moves
    inputs = [ "//integration/.git/HEAD" ]
    outputs = [ "$target_out_dir/ffx_version_info.txt" ]

    args = [
      rebase_path(outputs[0], root_build_dir),
      rebase_path("//integration/.git", root_build_dir),
    ]
  }

  compiled_action("version-info.bin") {
    tool = ":truncate"

    sources = get_target_outputs(":gen_version_info")
    outputs = [ "$target_out_dir/$target_name" ]

    args = [
      rebase_path(sources[0], root_build_dir),
      rebase_path(outputs[0], root_build_dir),
      "64",
    ]

    deps = [ ":gen_version_info" ]
  }

  compiled_action("build-version.bin") {
    tool = ":truncate"

    sources = [ build_info_files.version ]
    outputs = [ "$target_out_dir/$target_name" ]

    args = [
      rebase_path(sources[0], root_build_dir),
      rebase_path(outputs[0], root_build_dir),
      "64",
    ]

    deps = [ "//build/info:latest-commit-date" ]
  }
}
