# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/zircon/c_utils.gni")

build_version_bin = get_label_info(
                        "//src/developer/ffx/lib/version/build:build-version.bin($default_toolchain)",
                        "target_out_dir") + "/build-version.bin"
version_info_bin = get_label_info(
                       "//src/developer/ffx/lib/version/build:version-info.bin($default_toolchain)",
                       "target_out_dir") + "/version-info.bin"

if (is_mac) {
  build_info_section = ".FFX_BUILD,.ffx_build"
  version_info_section = ".FFX_VERSION,.ffx_version"
} else {
  build_info_section = ".ffx_build"
  version_info_section = ".ffx_version"
}

# Apply FFX version information after ffx has been linked.
#
# This template solves the problem that ffx, being the largest binary in the
# build, takes a long time to link. It is desirable to minimize causes of cache
# invalidations for builds of this binary, so as to save build time. One of the
# causes of invalidations is changes in repository version information, that
# will occur for every patch in CI/CQ, even if no source files for ffx or its
# dependencies change. Applying the version post-link avoids these costs, as it
# is cheap compared to re-linking the binary.
template("ffx_apply_version") {
  main_target = target_name
  rspfile_target = "_code_patches.rsp.${target_name}"
  rspfile = "${target_gen_dir}/${target_name}.code_patches.rsp"

  link_output_rspfile(rspfile_target) {
    visibility = [ ":$main_target" ]
    outputs = [ rspfile ]
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                           ])
  }

  rsp_outputs = get_target_outputs(":$rspfile_target")
  assert(
      [ rsp_outputs[0] ] == rsp_outputs,
      "invariant assumption failed, expecting link_output_rspfile to only have a single output")

  action(main_target) {
    forward_variables_from(invoker,
                           [
                             "output_name",
                             "output_extension",
                             "deps",
                           ])

    if (defined(output_name)) {
      output_file = output_name
    } else {
      output_file = main_target
    }

    if (defined(output_extension) && output_extension != "") {
      output_file += ".${output_extension}"
    }
    outputs = [ "${root_out_dir}/${output_file}" ]
    inputs = [
               "${clang_prefix}/llvm-objcopy",
               version_info_bin,
               build_version_bin,
             ] + get_target_outputs(":$rspfile_target")

    depfile = outputs[0] + ".d"

    script = "//src/developer/ffx/lib/version/build/rsp2deps.sh"

    # create a depsfile for the responsefile contents:
    args =
        [
          rebase_path(depfile, root_build_dir),
          rebase_path(outputs[0], root_build_dir),
        ] + rebase_path(get_target_outputs(":$rspfile_target"), root_build_dir)

    # do the real work
    args += [
              rebase_path("${clang_prefix}/llvm-objcopy", root_build_dir),

              # -x applies a "strip" on the output. Strip is currently performed after the link
              # step in the normal toolchain rules, and uses this same flag on most platforms (see
              # clang_toolchain.gni). Note on macOS this is a "different strip", as at time of
              # writing, binaries on macOS are normally stripped with xcrun strip -Sx.
              # The outcome produces the same size binary as the stripped binary on macOS.
              "-x",

              "--update-section=${version_info_section}=" +
                  rebase_path(version_info_bin, root_build_dir),
              "--update-section=${build_info_section}=" +
                  rebase_path(build_version_bin, root_build_dir),
              "@" + rebase_path(rspfile, root_build_dir),
            ] + rebase_path(outputs, root_build_dir)

    deps += [
      ":$rspfile_target",
      "//src/developer/ffx/lib/version/build:build-version.bin($default_toolchain)",
      "//src/developer/ffx/lib/version/build:version-info.bin($default_toolchain)",
    ]

    # Note: binaries metadata is not produced, as the build-id is not updated, and will match with the unversioned build.
    metadata = {
      if (defined(invoker.metadata)) {
        forward_variables_from(invoker.metadata, "*")
      }

      tool_paths = [
        {
          cpu = current_cpu
          label = get_label_info(":$target_name", "label_with_toolchain")
          name = output_name
          os = current_os
          path = rebase_path(outputs[0], root_build_dir)
        },
      ]
    }
  }
}
