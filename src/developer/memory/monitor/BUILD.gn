# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//third_party/cobalt_config/metrics_registry.gni")

source_set("lib") {
  sources = [
    "high_water.cc",
    "high_water.h",
    "metrics.cc",
    "metrics.h",
    "monitor.cc",
    "monitor.h",
  ]
  public_deps = [
    "//garnet/public/lib/inspect_deprecated",
    "//sdk/fidl/fuchsia.memory",
    "//sdk/lib/sys/cpp",
    "//src/developer/memory/metrics",
    "//zircon/public/fidl/fuchsia-cobalt",
  ]
  deps = [
    ":memory_metrics_registry",
    "//garnet/public/lib/fsl",
    "//src/lib/cobalt/cpp:cobalt_event_builder",
    "//src/lib/fxl",
    "//zircon/public/fidl/fuchsia-boot:fuchsia-boot_c",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/trace-provider-with-fdio",
  ]
}

metrics_registry("memory_metrics_registry") {
  # This must match the name of our Cobalt project as specified in:
  # https://cobalt-analytics.googlesource.com/config/+/refs/heads/master/projects.yaml
  project_name = "memory"
  namespace = "cobalt_registry"
  generate_cc = true
  generate_binarypb = false
}

executable("bin") {
  output_name = "memory_monitor"
  sources = [
    "main.cc",
  ]
  deps = [
    ":lib",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//zircon/public/fidl/fuchsia-scheduler",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/trace-provider-with-fdio",
  ]
}

package("monitor") {
  rootresource_svc = "//build"
  rootjob_svc = "//build"
  package_name = "memory_monitor"
  deps = [
    ":bin",
    ":config",
  ]
  binary = "memory_monitor"
  meta = [
    {
      path = rebase_path("meta/memory_monitor.cmx")
      dest = "memory_monitor.cmx"
    },
  ]
}

config_data("config") {
  for_pkg = "sysmgr"
  sources = [
    "memory_monitor.config",
  ]
}
