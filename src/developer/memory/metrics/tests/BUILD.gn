# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":memory_metrics_tests" ]
}

source_set("utils") {
  testonly = true
  sources = [
    "test_utils.cc",
    "test_utils.h",
  ]
  deps = [
    "//garnet/public/lib/gtest",
    "//src/developer/memory/metrics",
    "//src/lib/fxl",
    "//zircon/system/ulib/zx",
  ]
}

executable("memory_metrics_unittests") {
  testonly = true
  output_name = "memory_metrics_unittests"
  sources = [
    "bucket_match_unittest.cc",
    "capture_unittest.cc",
    "digest_unittest.cc",
    "printer_unittest.cc",
    "summary_unittest.cc",
    "watcher_unittest.cc",
  ]

  deps = [
    ":utils",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/developer/memory/metrics",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/rapidjson",
    "//zircon/system/public:public",
  ]

  # TODO(fxbug.dev/58160): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-reorder-init-list" ]
}

fuchsia_unittest_component("memory_metrics_unittests_component") {
  component_name = "memory_metrics_unittests"
  deps = [ ":memory_metrics_unittests" ]
}

fuchsia_test_component("memory_metrics_systemtests_component") {
  component_name = "memory_metrics_systemtests"
  manifest = "meta/memory_metrics_systemtests.cmx"
  deps = [ ":memory_metrics_systemtests" ]
}

fuchsia_test_package("memory_metrics_tests") {
  test_components = [
    ":memory_metrics_systemtests_component",
    ":memory_metrics_unittests_component",
  ]
}

executable("memory_metrics_systemtests") {
  testonly = true
  output_name = "memory_metrics_systemtests"
  sources = [ "capture_systemtest.cc" ]

  deps = [
    ":utils",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/developer/memory/metrics",
    "//src/lib/fxl/test:gtest_main",
  ]
}
