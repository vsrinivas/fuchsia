# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")

###############################################################################
# PACKAGES
###############################################################################
package("boot_log_checker") {
  deps = [ ":main" ]

  binaries = [
    {
      name = "boot_log_checker"
    },
  ]

  meta = [
    {
      path = "meta/boot_log_checker.cmx"
      dest = "boot_log_checker.cmx"
    },
  ]
}

###############################################################################
# EXECUTABLES
###############################################################################
executable("main") {
  output_name = "boot_log_checker"

  sources = [ "main.cc" ]

  deps = [
    ":src",
    "//sdk/lib/sys/cpp",
    "//src/lib/syslog/cpp",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fit",
  ]
}

###############################################################################
# SOURCES
###############################################################################
source_set("src") {
  sources = [
    "reboot_log_handler.cc",
    "reboot_log_handler.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//src/developer/feedback/utils:cobalt",
    "//zircon/public/lib/fit",
    "//zircon/system/fidl/fuchsia-cobalt",
  ]

  deps = [
    "//src/developer/feedback/utils:promise",
    "//src/lib/files",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/syslog/cpp",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
  ]
}

###############################################################################
# CONFIGS
###############################################################################
config_data("config") {
  for_pkg = "sysmgr"

  sources = [ "config/config.json" ]

  outputs = [ "boot_log_checker.config" ]
}
