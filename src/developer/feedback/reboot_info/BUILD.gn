# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")

###############################################################################
# PACKAGES
###############################################################################
package("reboot-info") {
  deps = [ ":main" ]

  binaries = [
    {
      name = "reboot_info"
    },
  ]

  meta = [
    {
      path = "meta/reboot_info.cmx"
      dest = "reboot_info.cmx"
    },
  ]
}

###############################################################################
# EXECUTABLES
###############################################################################
executable("main") {
  output_name = "reboot_info"

  sources = [ "main.cc" ]

  deps = [
    ":main_service",
    ":reboot_log",
    ":reporter",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fit",
  ]
}

###############################################################################
# SOURCES
###############################################################################
source_set("last_reboot_info_provider") {
  sources = [
    "last_reboot_info_provider.cc",
    "last_reboot_info_provider.h",
  ]

  public_deps = [
    ":reboot_log",
    "//sdk/fidl/fuchsia.feedback",
  ]

  deps = [
    ":reboot_reason",
    "//sdk/lib/syslog/cpp",
  ]
}

source_set("main_service") {
  sources = [
    "main_service.cc",
    "main_service.h",
  ]

  public_deps = [
    ":last_reboot_info_provider",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/fidl/cpp",
  ]
}

source_set("reboot_log") {
  sources = [
    "reboot_log.cc",
    "reboot_log.h",
  ]

  public_deps = [ ":reboot_reason" ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/files",
  ]
}

source_set("reboot_reason") {
  sources = [
    "reboot_reason.cc",
    "reboot_reason.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//src/developer/feedback/utils/cobalt",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]

  visibility = [ "//src/developer/feedback/reboot_info/*" ]
}

source_set("reporter") {
  sources = [
    "reporter.cc",
    "reporter.h",
  ]

  public_deps = [
    ":reboot_log",
    ":reboot_reason",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//src/developer/feedback/utils:errors",
    "//src/developer/feedback/utils/cobalt",
    "//src/developer/feedback/utils/fidl:oneshot_ptr",
    "//zircon/public/lib/fit",
    "//zircon/system/fidl/fuchsia-cobalt",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/files",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
  ]
}
