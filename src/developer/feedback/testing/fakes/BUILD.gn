# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

visibility = [ "//src/developer/feedback/*" ]

###############################################################################
# PACKAGES
###############################################################################
package("fake-crash-reporter") {
  binary = "fake_crash_reporter"

  meta = [
    {
      path = "meta/fake_crash_reporter.cmx"
      dest = "fake_crash_reporter.cmx"
    },
  ]

  deps = [ ":crash_reporter_main" ]

  visibility += [ "//src/developer/exception_broker/tests" ]
}

package("fake-data-provider") {
  binary = "fake_data_provider"

  meta = [
    {
      path = "meta/fake_data_provider.cmx"
      dest = "fake_data_provider.cmx"
    },
  ]

  resources = [
    {
      path = "//src/developer/feedback/testing/resources/checkerboard_100.png"
      dest = "checkboard_100.png"
    },
  ]

  deps = [ ":data_provider_main" ]
}

package("fake-device-id-provider") {
  binary = "fake_device_id_provider"

  meta = [
    {
      path = "meta/fake_device_id_provider.cmx"
      dest = "fake_device_id_provider.cmx"
    },
  ]

  deps = [ ":device_id_provider_main" ]
}

package("fake-last-reboot-info-provider") {
  binaries = [
    {
      name = "fake_last_reboot_info_provider"
    },
  ]

  meta = [
    {
      path = "meta/fake_last_reboot_info_provider.cmx"
      dest = "fake_last_reboot_info_provider.cmx"
    },
  ]

  deps = [ ":last_reboot_info_provider_main" ]
}

###############################################################################
# EXECUTABLES
###############################################################################
executable("crash_reporter_main") {
  output_name = "fake_crash_reporter"

  sources = [
    "crash_reporter.cc",
    "crash_reporter.h",
    "crash_reporter_main.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("data_provider_main") {
  output_name = "fake_data_provider"

  sources = [
    "data_provider.cc",
    "data_provider.h",
    "data_provider_main.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/developer/feedback/utils:archive",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//zircon/system/fidl/fuchsia-mem",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("device_id_provider_main") {
  output_name = "fake_device_id_provider"

  sources = [
    "device_id_provider.cc",
    "device_id_provider.h",
    "device_id_provider_main.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/uuid",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("last_reboot_info_provider_main") {
  output_name = "fake_last_reboot_info_provider"

  sources = [
    "last_reboot_info_provider.cc",
    "last_reboot_info_provider.h",
    "last_reboot_info_provider_main.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

###############################################################################
# SOURCES
###############################################################################
source_set("cobalt") {
  testonly = true

  sources = [ "cobalt.h" ]

  public_deps = [
    "//sdk/fidl/fuchsia.cobalt",
    "//sdk/fidl/fuchsia.cobalt.test",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/developer/feedback/utils/cobalt",
    "//src/lib/fxl",
  ]
}

source_set("privacy_settings") {
  testonly = true

  sources = [
    "privacy_settings.cc",
    "privacy_settings.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.settings",
    "//sdk/lib/fidl/cpp",
    "//src/lib/fxl",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/fit",
  ]
}
