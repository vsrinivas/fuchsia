# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("usb_adb_bind") {
  rules = "usb_adb.bind"
  header_output = "usb_adb-bind.h"
  bind_output = "usb-adb-function.bindbc"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.google.usb",
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.comm",
  ]
}

source_set("usb-adb-common") {
  sources = [
    "adb-function.cc",
    "adb-function.h",
  ]
  public_deps = [
    ":usb_adb_bind",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.adb:fuchsia.hardware.adb_cpp_wire",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:peripheral",
    "//src/lib/ddk",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace:trace-driver",
  ]
}

fuchsia_driver("usb-adb-function-driver") {
  output_name = "usb-adb-function"
  deps = [ ":usb-adb-common" ]
}

fuchsia_driver_component("usb-adb-function") {
  component_name = "usb-adb-function"
  deps = [ ":usb-adb-function-driver" ]
  info = "usb-adb-function-info.json"
  colocate = true
}

test("usb-adb-test") {
  sources = [ "adb-function-test.cc" ]
  deps = [
    ":usb-adb-common",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.adb:fuchsia.hardware.adb_cpp",
    "//src/devices/testing/mock-ddk",
    "//src/devices/usb/lib/usb",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("usb-adb-unittest") {
  deps = [ ":usb-adb-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":usb-adb-unittest",
    ":usb_adb_bind_test",
  ]
}
