# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  deps = [
    ":feedback_agent_tests",
    ":integration_test",
    ":unittest",
  ]
}

test_package("feedback_agent_tests") {
  tests = [
    {
      name = "feedback_agent_unittest"
    },
    {
      name = "feedback_agent_integration_test"

      # We need to also test on real devices as we talk to a real Scenic service.
      environments = basic_envs
    },
  ]

  resources = [
    {
      path = "resources/checkerboard_100.png"
      dest = "checkerboard_100.png"
    },
  ]

  deps = [
    ":integration_test",
    ":unittest",
  ]
}

executable("unittest") {
  testonly = true

  output_name = "feedback_agent_unittest"

  sources = [
    "feedback_agent_unittest.cc",
  ]

  deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.math",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/gtest",
    "//garnet/public/lib/syslog/cpp",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/fidl/fuchsia.images",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/developer/feedback_agent:src",
    "//src/lib/files",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
  ]
}

executable("integration_test") {
  testonly = true

  output_name = "feedback_agent_integration_test"

  sources = [
    "feedback_agent_integration_test.cc",
  ]

  deps = [
    "//garnet/public/lib/component/cpp/testing",
    "//garnet/public/lib/escher/test:gtest_escher",
    "//garnet/public/lib/fostr/fidl/fuchsia.feedback",
    "//garnet/public/lib/fxl/test:gtest_main",
    "//sdk/fidl/fuchsia.feedback",
    "//src/developer/feedback_agent:src",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
  ]
}
