# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_staticlib.gni")
import("//build/test/test_package.gni")

rustc_binary("bin") {
  name = "cloud_provider_memory_diff"
  edition = "2018"
  deps = [
    ":lib",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.ledger.cloud:fuchsia.ledger.cloud-rustc",
    "//src/ledger/bin/fidl:cloud-test-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:rand",
  ]
}

rustc_library("lib") {
  name = "cloud_provider_memory_diff_lib"
  edition = "2018"
  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.ledger.cloud:fuchsia.ledger.cloud-rustc",
    "//src/ledger/bin/fidl:cloud-test-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:pin-utils",
    "//third_party/rust_crates:rand",
    "//zircon/system/fidl/fuchsia-mem:fuchsia-mem-rustc",
  ]
  with_unit_tests = true
}

package("cloud_provider_memory_diff") {
  deps = [
    ":bin",
  ]

  binary = "cloud_provider_memory_diff"

  meta = [
    {
      path = rebase_path("meta/cloud_provider_memory_diff.cmx")
      dest = "cloud_provider_memory_diff.cmx"
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":cloud_provider_memory_diff",
    ":cloud_provider_memory_diff_tests",
  ]
}

test_package("cloud_provider_memory_diff_tests") {
  deps = [
    ":lib_test",
    "cpp:cloud_provider_memory_diff_staticlib_test",
    "validation",
  ]

  tests = [
    {
      name = "launch_validation_tests_memory_diff"
      environments = basic_envs
    },
    {
      name = "cloud_provider_memory_diff_lib_lib_test"
      environments = basic_envs
    },
    {
      name = "cloud_provider_memory_diff_staticlib_test"
      environments = basic_envs
    },
  ]
}
