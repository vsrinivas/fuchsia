# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

visibility = [ "//src/ledger/*" ]

executable("integration") {
  output_name = "ledger_integration_tests"
  testonly = true
  sources = [
    "sharding_all_except_compatibility.cc",
  ]
  deps = [
    ":integration_sharded",
  ]
  configs += [ "//src/ledger:ledger_config" ]
}

executable("integration_diff_compatibility") {
  output_name = "ledger_integration_tests_diff_compatibility"
  testonly = true
  sources = [
    "sharding_compatibility.cc",
  ]
  deps = [
    ":integration_sharded",
  ]
  configs += [ "//src/ledger:ledger_config" ]
}

source_set("integration_sharded") {
  testonly = true

  sources = [
    "inspect_tests.cc",
    "integration_test_tests.cc",
    "ledger_app_instance_factory_integration.cc",
    "merging_tests.cc",
    "page_mutation_tests.cc",
    "page_snapshot_tests.cc",
    "page_tests.cc",
    "page_watcher_tests.cc",
    "sharding.h",
  ]

  deps = [
    ":integration_lib",
    "//sdk/fidl/fuchsia.ledger",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/ledger/bin/app:constants",
    "//src/ledger/bin/app:lib",
    "//src/ledger/bin/environment:test_loop_notification",
    "//src/ledger/bin/fidl",
    "//src/ledger/bin/fidl:syncable",
    "//src/ledger/bin/p2p_provider/fake",
    "//src/ledger/bin/p2p_provider/impl",
    "//src/ledger/bin/p2p_provider/public",
    "//src/ledger/bin/p2p_sync/impl",
    "//src/ledger/bin/p2p_sync/public",
    "//src/ledger/bin/storage/fake:lib",
    "//src/ledger/bin/storage/public",
    "//src/ledger/bin/testing:gtest_main",
    "//src/ledger/bin/testing:ledger_instance_factory",
    "//src/ledger/bin/testing:lib",
    "//src/ledger/bin/tests/integration/sync",
    "//src/ledger/cloud_provider_in_memory/lib",
    "//src/ledger/lib/backoff",
    "//src/ledger/lib/callback",
    "//src/ledger/lib/convert",
    "//src/ledger/lib/logging",
    "//src/ledger/lib/socket",
    "//src/ledger/lib/vmo",
    "//src/lib/timekeeper:testing",
    "//third_party/abseil-cpp",
  ]

  configs += [ "//src/ledger:ledger_config" ]
}

source_set("integration_lib") {
  testonly = true

  sources = [
    "integration_test.cc",
    "integration_test.h",
    "test_page_watcher.cc",
    "test_page_watcher.h",
    "test_utils.cc",
    "test_utils.h",
  ]

  deps = [
    "//src/ledger/lib/convert",
    "//src/ledger/lib/logging",
    "//src/ledger/lib/socket",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.ledger",
    "//src/ledger/bin/fidl",
    "//src/ledger/bin/testing:ledger_instance_factory",
    "//third_party/abseil-cpp",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/async-testing",
    "//zircon/public/lib/trace-provider-with-fdio",
  ]

  configs += [ "//src/ledger:ledger_config" ]
}
