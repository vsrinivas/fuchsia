# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  deps = [
    ":smart-door-memory-server-unittest",
    "//src/security/codelab/smart_door:smart-door",
    "//src/security/codelab/smart_door_memory:smart-door-memory",
  ]
}

executable("bin") {
  output_name = "smart-door-memory"
  sources = [ "src/smart_door_memory_server.cc" ]
  deps = [
    ":lib",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

source_set("lib") {
  sources = [
    "src/smart_door_memory_server_app.cc",
    "src/smart_door_memory_server_app.h",
  ]

  public_deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/security/codelab/services:codelab_hlcpp",
  ]
}

fuchsia_component("smart-door-memory-component") {
  component_name = "smart-door-memory"
  manifest = "meta/smart-door-memory.cml"

  deps = [ ":bin" ]
}

fuchsia_package("smart-door-memory") {
  deps = [
    ":smart-door-memory-component",
    "//build/validate:non_production_tag",
  ]
}

fuchsia_unittest_package("smart-door-memory-server-unittest") {
  manifest = "meta/smart-door-memory-server-unittest.cml"
  deps = [ ":smart-door-memory-server-unittest-bin" ]
}

executable("smart-door-memory-server-unittest-bin") {
  testonly = true
  output_name = "smart-door-memory-server-unittest"

  sources = [ "tests/smart_door_memory_server_unittest.cc" ]

  deps = [
    ":lib",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}
