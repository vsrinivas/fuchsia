# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fuzzing/fuzzer.gni")
import("//build/test.gni")
import("//src/sys/build/fuchsia_unittest_package.gni")

group("tests") {
  testonly = true
  deps = [
    ":kernel-cmdline-fuzzers",
    ":kernel-cmdline-test-pkg",
  ]
}

test("kernel-cmdline") {
  output_name = "kernel-cmdline-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "kernel_cmdline_test.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/kernel/lib/cmdline",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zxtest",
  ]
}

fuzzer("kernel-cmdline-fuzzer") {
  sources = [ "kernel_cmdline_fuzztest.cc" ]
  deps = [
    "//zircon/kernel/lib/cmdline",
    "//zircon/public/lib/fbl",
  ]
}

fuzzers_package("kernel-cmdline-fuzzers") {
  cpp_fuzzers = [ ":kernel-cmdline-fuzzer" ]
}

fuchsia_unittest_package("kernel-cmdline-test-pkg") {
  package_name = "kernel-cmdline-test"
  deps = [ ":kernel-cmdline" ]
}
