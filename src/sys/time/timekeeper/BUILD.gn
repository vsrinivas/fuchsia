# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

timekeeper_deps = [
  ":timekeeper-config",
  "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
  "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
  "//sdk/fidl/fuchsia.hardware.rtc:fuchsia.hardware.rtc_rust",
  "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
  "//sdk/fidl/fuchsia.metrics:fuchsia.metrics_rust",
  "//sdk/fidl/fuchsia.time:fuchsia.time_rust",
  "//sdk/fidl/fuchsia.time.external:fuchsia.time.external_rust",
  "//src/lib/cobalt/rust/builders",
  "//src/lib/diagnostics/inspect/runtime/rust",
  "//src/lib/diagnostics/inspect/rust",
  "//src/lib/fdio/rust:fdio",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fidl/rust/fidl-contrib",
  "//src/lib/fuchsia",
  "//src/lib/fuchsia-async",
  "//src/lib/fuchsia-component",
  "//src/lib/zircon/rust:fuchsia-zircon",
  "//src/sys/time/lib/inspect-writable",
  "//src/sys/time/lib/time-util",
  "//src/sys/time/lib/time_metrics_registry",
  "//third_party/cobalt/src/lib/client/rust:cobalt-client",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:async-trait",
  "//third_party/rust_crates:chrono",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:lazy_static",
  "//third_party/rust_crates:parking_lot",
  "//third_party/rust_crates:thiserror",
  "//third_party/rust_crates:tracing",
]

timekeeper_sources = [
  "src/clock_manager.rs",
  "src/diagnostics/cobalt.rs",
  "src/diagnostics/composite.rs",
  "src/diagnostics/fake.rs",
  "src/diagnostics/inspect.rs",
  "src/diagnostics/mod.rs",
  "src/enums.rs",
  "src/estimator/frequency.rs",
  "src/estimator/kalman_filter.rs",
  "src/estimator/mod.rs",
  "src/main.rs",
  "src/rtc.rs",
  "src/time_source.rs",
  "src/time_source_manager.rs",
]

rustc_binary("bin") {
  name = "timekeeper"
  edition = "2021"
  with_unit_tests = true

  deps = timekeeper_deps

  sources = timekeeper_sources

  test_deps = [
    "//src/lib/fuchsia",
    "//src/lib/test_util",
  ]
}

rustc_binary("bin_with_fake_time") {
  name = "timekeeper_fake_time"
  edition = "2021"
  with_unit_tests = false
  testonly = true

  deps = timekeeper_deps

  sources = timekeeper_sources

  non_rust_deps = [ "//src/lib/fake-clock/lib" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "timekeeper"
  manifest = "meta/service.cml"
}

fuchsia_structured_config_rust_lib("timekeeper-config") {
  cm_label = ":manifest"
}

fuchsia_structured_config_values("timekeeper-config-values") {
  cm_label = ":manifest"
  values_source = "config/values.json5"
}

fuchsia_component("service") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_package("timekeeper") {
  deps = [
    ":service",
    ":timekeeper-config-values",
  ]
}

resource("testdata") {
  sources = [
    "test/end-of-unix-time",
    "test/y2k",
  ]
  outputs = [ "data/{{source_file_part}}" ]
}

fuchsia_unittest_package("timekeeper-tests") {
  manifest = "meta/unittests.cml"
  deps = [
    ":bin_test",
    ":testdata",
    "//src/connectivity/network/dns:component",
    "//src/connectivity/network/netstack:component",
  ]
  test_specs = {
    environments = [ nuc_env ]
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":timekeeper-tests" ]
}
