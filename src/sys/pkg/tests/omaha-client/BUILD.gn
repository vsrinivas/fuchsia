# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

rustc_test("driver") {
  name = "omaha_client_integration_test"
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver-rustc",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg-rustc",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket-rustc",
    "//sdk/fidl/fuchsia.update:fuchsia.update-rustc",
    "//sdk/fidl/fuchsia.update.channelcontrol:fuchsia.update.channelcontrol-rustc",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/pkg/fidl/fuchsia.update.installer:fuchsia.update.installer-rustc",
    "//src/sys/pkg/lib/fidl-fuchsia-update-installer-ext",
    "//src/sys/pkg/lib/fuchsia-pkg-testing",
    "//src/sys/pkg/testing/mock-installer",
    "//src/sys/pkg/testing/mock-omaha-server",
    "//src/sys/pkg/testing/mock-paver",
    "//src/sys/pkg/testing/mock-reboot",
    "//src/sys/pkg/testing/mock-resolver",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hex",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tempfile",
  ]

  sources = [ "src/lib.rs" ]
}

test_package("omaha-client-integration-tests") {
  deps = [
    ":driver",
    "//src/sys/pkg/bin/grand-swd-binary",
    "//src/sys/pkg/bin/omaha-client:bin",
    "//src/sys/pkg/bin/system-updater:bin",
  ]

  binaries = [
    {
      name = "omaha_client_service"
      dest = "omaha-client-service"
    },
    {
      name = "system_updater"
      dest = "system-updater"
    },
    {
      name = "grand_swd_binary"
      dest = "system-update-committer"
    },
  ]

  meta = [
    {
      path = rebase_path(
              "//src/sys/pkg/bin/omaha-client/meta/omaha-client-service-for-integration-test.cmx")
      dest = "omaha-client-service-for-integration-test.cmx"
    },
    {
      path = rebase_path(
              "//src/sys/pkg/bin/system-updater/meta/system-updater-isolated.cmx")
      dest = "system-updater-isolated.cmx"
    },
    {
      path = rebase_path(
              "//src/sys/pkg/tests/system-update-committer/meta/system-update-committer.cmx")
      dest = "system-update-committer.cmx"
    },
  ]

  tests = [
    {
      name = "omaha_client_integration_test"
      dest = "omaha-client-integration-test"
      log_settings = {
        max_severity = "ERROR"
      }
    },
  ]
}

group("tests") {
  testonly = true
  public_deps = [ ":omaha-client-integration-tests" ]
}
