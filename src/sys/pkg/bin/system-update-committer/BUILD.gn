# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//src/sys/build/components.gni")

# `system-update-committer` is conceptually a binary, but it built as a lib here so
# it can be aggregated into //src/sys/pkg/bin/grand-swd-binary to save disk space.
rustc_library("lib") {
  name = "system-update-committer"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.paver:fuchsia.paver-rustc",
    "//sdk/fidl/fuchsia.update:fuchsia.update-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
  ]
  test_deps = [
    "//src/sys/pkg/testing/mock-paver",
    "//third_party/rust_crates:matches",
  ]

  source_root = "src/main.rs"
  sources = [
    "src/check_and_set.rs",
    "src/config.rs",
    "src/fidl.rs",
    "src/main.rs",
  ]

  visibility = [
    ":*",
    "//src/sys/pkg/bin/grand-swd-binary:*",
  ]
}

resource("renamed-grand-swd-binary") {
  deps = [ "//src/sys/pkg/bin/grand-swd-binary" ]
  data_deps = [ "//src/sys/pkg/bin/grand-swd-binary" ]
  sources = [ "$root_out_dir/grand_swd_binary" ]
  outputs = [ "bin/system-update-committer" ]
  visibility = [
    ":*",
    "//src/sys/pkg/tests/system-update-committer/*",
  ]
}

fuchsia_component("component") {
  component_name = "system-update-committer"
  manifest = "meta/system-update-committer.cml"
  deps = [ ":renamed-grand-swd-binary" ]
}

fuchsia_package("package") {
  package_name = "system-update-committer"
  deps = [ ":component" ]
}

fuchsia_component("test-component") {
  testonly = true
  component_name = "system-update-committer-test"
  manifest = "meta/system-update-committer-test.cml"
  deps = [ ":lib_test" ]
}

fuchsia_test_package("test-package") {
  package_name = "system-update-committer-tests"
  test_components = [ ":test-component" ]
}

group("tests") {
  testonly = true
  public_deps = [ ":test-package" ]
}
