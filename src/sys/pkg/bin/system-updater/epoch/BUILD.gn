# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

if (is_host) {
  generator_deps = [
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
  ]
  generator_test_deps = [
    "//third_party/rust_crates:indoc",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:proptest",
  ]
  generator_sources = [
    "src/args.rs",
    "src/epoch.rs",
    "src/history.rs",
    "src/main.rs",
  ]

  rustc_binary("generator") {
    name = "epoch_generator"
    edition = "2018"
    deps = generator_deps
    sources = generator_sources
  }

  rustc_test("epoch-generator-tests") {
    deps = generator_deps + generator_test_deps
    source_root = "src/main.rs"
    sources = generator_sources
  }
}

group("host_tests") {
  testonly = true
  deps = [ ":epoch-generator-tests($host_toolchain)" ]
}
