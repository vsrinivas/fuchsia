# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//build/testing/environments.gni")

rustc_library("isolated-swd") {
  version = "0.1.0"
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_rust",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//sdk/fidl/fuchsia.space:fuchsia.space_rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-hyper",
    "//src/lib/fuchsia-url",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/pkg/fidl/fuchsia.update.installer:fuchsia.update.installer_rust",
    "//src/sys/pkg/lib/fidl-fuchsia-update-installer-ext",
    "//src/sys/pkg/lib/omaha-client",
    "//src/sys/pkg/lib/version",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hyper",
    "//third_party/rust_crates:hyper-rustls",
    "//third_party/rust_crates:tempfile",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/sys/lib/fidl-fuchsia-pkg-ext",
    "//src/sys/pkg/lib/fuchsia-merkle",
    "//src/sys/pkg/lib/fuchsia-pkg-testing",
    "//src/sys/pkg/testing/blobfs-ramdisk",
    "//src/sys/pkg/testing/mock-paver",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:hyper",
    "//third_party/rust_crates:serde_json",
  ]

  sources = [
    "src/cache.rs",
    "src/lib.rs",
    "src/omaha.rs",
    "src/omaha/http_request.rs",
    "src/omaha/install_plan.rs",
    "src/omaha/installer.rs",
    "src/omaha/policy.rs",
    "src/omaha/timer.rs",
    "src/resolver.rs",
    "src/updater.rs",
  ]
}

fuchsia_test_component("isolated-swd-tests-component") {
  component_name = "isolated-swd-tests"
  manifest = "meta/isolated-swd-tests.cml"
  deps = [ ":isolated-swd_test" ]
}

isolated_swd_components = [
  "//src/sys/pkg/bin/pkg-cache:component-ignore-system-image",
  "//src/sys/pkg/bin/pkg-cache:config_values_from_gn-ignore-system-image",
  "//src/sys/pkg/bin/pkg-resolver:component",
  "//src/sys/pkg/bin/system-updater:isolated",
]

fuchsia_component("pkg-component") {
  component_name = "pkg"
  manifest = "//src/sys/pkg/meta/pkg-recovery.cml"
  deps = [
    "//src/sys/pkg/bin/fake-system-update-committer",
    "//src/sys/pkg/bin/pkg-cache:component-ignore-system-image",
    "//src/sys/pkg/bin/pkg-cache:config_values_from_gn-ignore-system-image",
    "//src/sys/pkg/bin/pkg-resolver:component",
  ]
}

fuchsia_test_package("isolated-swd-tests") {
  test_components = [ ":isolated-swd-tests-component" ]

  deps = [
    "//src/connectivity/network/dns:component",
    "//src/connectivity/network/netstack:component",
    "//src/storage/bin/blobfs",
    "//src/storage/testing:storage_driver_test_realm",
    "//src/sys/pkg/bin/fake-system-update-committer",
    "//src/sys/pkg/bin/pm:pm_bin",
    "//src/sys/pkg/lib/fuchsia-pkg-testing/certs",
    "//src/sys/pkg/tests/pkg-resolver:empty-repo",
  ]
  deps += isolated_swd_components
  deps += [ ":pkg-component" ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_package("isolated-swd-components") {
  deps = isolated_swd_components
}

group("tests") {
  testonly = true
  public_deps = [ ":isolated-swd-tests" ]
}
