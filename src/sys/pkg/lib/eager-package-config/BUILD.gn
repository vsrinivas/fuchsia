# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

rustc_library("eager-package-config") {
  version = "0.0.1"
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//src/lib/fuchsia-url",
    "//src/sys/pkg/lib/channel-config",
    "//src/sys/pkg/lib/omaha-client",
    "//src/sys/pkg/lib/version",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tracing",
  ]
  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
      "//src/lib/fuchsia-fs",
      "//src/lib/zircon/rust:fuchsia-zircon",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:thiserror",
    ]
  }

  test_deps = [
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [
    "src/lib.rs",
    "src/omaha_client.rs",
    "src/pkg_resolver.rs",
  ]
}

fuchsia_unittest_package("eager-package-config-lib-tests") {
  manifest = "meta/eager-package-config-lib-test.cml"
  deps = [ ":eager-package-config_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":eager-package-config-lib-tests" ]
}
