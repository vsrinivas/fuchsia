# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")

# Only needed on host for now.
if (is_host) {
  rustc_library("fuchsia-repo") {
    version = "0.1.0"
    edition = "2021"
    with_unit_tests = true

    deps = [
      "//sdk/fidl/fuchsia.developer.ffx:fuchsia.developer.ffx_rust",
      "//src/developer/ffx/lib/fidl-fuchsia-developer-ffx-ext",
      "//src/lib/fuchsia-async",
      "//src/lib/fuchsia-fs",
      "//src/lib/fuchsia-url",
      "//src/lib/gcs",
      "//src/lib/http-sse:http_sse",
      "//src/sys/lib/fidl-fuchsia-pkg-ext",
      "//src/sys/pkg/lib/far/rust:fuchsia-archive",
      "//src/sys/pkg/lib/fuchsia-merkle",
      "//src/sys/pkg/lib/fuchsia-pkg",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:async-fs",
      "//third_party/rust_crates:async-lock",
      "//third_party/rust_crates:async-net",
      "//third_party/rust_crates:async-trait",
      "//third_party/rust_crates:bytes",
      "//third_party/rust_crates:camino",
      "//third_party/rust_crates:chrono",
      "//third_party/rust_crates:data-encoding",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:http",
      "//third_party/rust_crates:hyper",
      "//third_party/rust_crates:maplit",
      "//third_party/rust_crates:notify",
      "//third_party/rust_crates:parking_lot",
      "//third_party/rust_crates:pin-project",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:tempfile",
      "//third_party/rust_crates:thiserror",
      "//third_party/rust_crates:tokio",
      "//third_party/rust_crates:tracing",
      "//third_party/rust_crates:tuf",
      "//third_party/rust_crates:url",
      "//third_party/rust_crates:walkdir",
    ]

    test_deps = [
      "//src/developer/ffx/lib/pkg:empty-repo-keys",
      "//src/developer/ffx/lib/pkg:empty-repo-metadata",
      "//src/lib/fuchsia",
      "//src/lib/fuchsia-hyper",
      "//src/sys/pkg/lib/fuchsia-pkg",
      "//third_party/rust_crates:assert_matches",
      "//third_party/rust_crates:maplit",
      "//third_party/rust_crates:pretty_assertions",
      "//third_party/rust_crates:proptest",
      "//third_party/rust_crates:tempfile",
    ]

    sources = [
      "src/async_spooled.rs",
      "src/lib.rs",
      "src/manager.rs",
      "src/range.rs",
      "src/repo_client.rs",
      "src/repo_keys.rs",
      "src/repo_storage.rs",
      "src/repository.rs",
      "src/repository/file_system.rs",
      "src/repository/gcs_repository.rs",
      "src/repository/http_repository.rs",
      "src/repository/pm.rs",
      "src/repository/repo_tests.rs",
      "src/resolve.rs",
      "src/resource.rs",
      "src/server.rs",
      "src/test_utils.rs",
      "src/util.rs",
    ]
  }

  group("tests") {
    testonly = true
    deps = [ ":fuchsia-repo_test" ]
  }
} else {
  group("fuchsia-repo") {
    deps = [ ":fuchsia-repo($host_toolchain)" ]
  }

  group("tests") {
    testonly = true
    deps = [ ":fuchsia-repo_test($host_toolchain)" ]
  }
}
