# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")

go_library("host-target-testing") {
  testonly = true
  name = "go.fuchsia.dev/fuchsia/src/sys/pkg/testing/host-target-testing/..."
  deps = [
    "//garnet/go/src/far:far",
    "//src/sys/pkg/bin/pm:pm_lib",
    "//src/sys/pkg/lib/repo",
    "//third_party/golibs:github.com/flynn/go-tuf",
    "//third_party/golibs:golang.org/x/crypto",
    "//tools/lib/retry",
    "//tools/net/sshutil",
  ]
  non_go_deps = [
    ":copy_avbtool",
    ":copy_zbitool",
  ]
}

test_packages = [
  "avb",
  "packages",
  "paver",
  "omaha",
  "zbi",
]
foreach(testpkg, test_packages) {
  go_test("host-target-testing-${testpkg}-test") {
    gopackages = [ "go.fuchsia.dev/fuchsia/src/sys/pkg/testing/host-target-testing/${testpkg}" ]
    deps = [ ":host-target-testing" ]
  }
}

copy("copy_avbtool") {
  testonly = true
  sources = [ "//zircon/third_party/tools/android/avb/avbtool" ]
  outputs = [ "$root_out_dir/test_data/system-tests/{{source_file_part}}" ]
}

zircon_tools_dir =
    get_label_info("//zircon/public/tool($host_toolchain)", "root_out_dir")
copy("copy_zbitool") {
  testonly = true
  sources = [ "${zircon_tools_dir}/zbi" ]
  outputs = [ "$root_out_dir/test_data/system-tests/{{source_file_part}}" ]
  deps = [ "//zircon/public/tool/zbi($host_toolchain)" ]
}

group("tests") {
  testonly = true

  public_deps = []
  foreach(testpkg, test_packages) {
    public_deps += [ ":host-target-testing-${testpkg}-test($host_toolchain)" ]
  }
}
