# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/fuchsia_component.gni")

# Defines a Fuchsia component that contains a Rust program.
# See: https://fuchsia.dev/fuchsia-src/development/components/build
#
# This template is a combination of `fuchsia_component()` and `rustc_binary()`.
# It takes the same parameters as `fuchsia_component()` does, and forwards the
# rest to `rustc_binary()`.
# In addition, the output of the `rustc_binary()` will automatically be
# included in the component's resources under the path "bin/executable_name",
# where executable_name is either the `output_name` parameter if provided or the
# target name.
# For legacy reasons, `name` is also accepted as an alias of `output_name`.
#
# See also:
# * //src/sys/build/fuchsia_component.gni
# * //build/rust/rustc_binary.gni
#
# Example:
# ```
# fuchsia_rust_component("my-component") {
#   sources = [ "main.cc" ]
#   manifest = "manifest.cml"
# }
#
# fuchsia_package("my-package") {
#   deps = [ ":my-component" ]
#   ...
# }
# ```
# The component above will have the following launch URL:
# `fuchsia-pkg://fuchsia.com/my-package#meta/my-component.cm`
#
# Parameters
#
#   manifest (required)
#     The component manifest.
#     Type: path
#
#   component_name (optional)
#     The name of the component.
#     Type: string
#     Default: target_name
#
#   resources (optional)
#     Resources to include. See documentation for `fuchsia_component()`.
#     Type: list(scopes)
#
#   rustc_binary (optional)
#     Reference an existing rustc_binary target rather than creating one.
#     Type: label
#
#   data_deps
#   deps
#   public_deps
#   testonly
#   visibility
#
#   Additional parameters will be forwarded to `rustc_binary()`.
template("fuchsia_rust_component") {
  assert(
      defined(invoker.manifest),
      "A `manifest` argument was missing when calling fuchsia_rust_component($target_name)")
  assert(
      !(defined(invoker.output_name) && defined(invoker.name)),
      "Can't specify both `output_name` and `name` for fuchsia_rust_unittest($target_name)")

  output_name = target_name
  if (defined(invoker.output_name)) {
    output_name = invoker.output_name
  } else if (defined(invoker.name)) {
    output_name = invoker.name
  }

  # Rust tools don't like dashes ¯\_(ツ)_/¯
  output_name = string_replace(output_name, "-", "_")

  if (defined(invoker.rustc_binary)) {
    rustc_binary_target = invoker.rustc_binary
  } else {
    rustc_binary_target = "${target_name}_rustc_binary"
    import("//build/rust/rustc_binary.gni")
    rustc_binary(rustc_binary_target) {
      forward_variables_from(invoker,
                             "*",
                             [
                               "component_name",
                               "manifest",
                               "name",
                               "output_name",
                               "resources",
                               "target_name",
                               "visibility",
                             ])
      name = output_name
      visibility = [ ":*" ]
    }
    rustc_binary_target = ":$rustc_binary_target"
  }

  fuchsia_component(target_name) {
    forward_variables_from(invoker,
                           [
                             "component_name",
                             "data_deps",
                             "deps",
                             "manifest",
                             "public_deps",
                             "resources",
                             "testonly",
                             "visibility",
                           ])
    if (!defined(deps)) {
      deps = []
    }
    deps += [ rustc_binary_target ]
    if (!defined(resources)) {
      resources = []
    }
    resources += [
      {
        source = "$root_out_dir/$output_name"
        destination = "bin/$output_name"
      },
    ]
  }
}
