# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("fuchsia_component.gni")
import("fuchsia_test_package.gni")

# Internal template. Do not use outside of fuchsia_*_unittest templates.
template("fuchsia_unittest_internal") {
  if (defined(invoker.manifest_contents)) {
    generated_target = "${target_name}_generated_manifest"
    generated_file(generated_target) {
      contents = invoker.manifest_contents
      outputs = [ "$target_out_dir/${generated_target}.cml" ]
      output_conversion = "json"
      testonly = true
      visibility = [ ":*" ]
    }

    manifest = get_target_outputs(":$generated_target")
    manifest = manifest[0]
  }

  component_target = "${target_name}_component"
  fuchsia_component(component_target) {
    forward_variables_from(invoker,
                           [
                             "manifest",
                             "resources",
                           ])
    component_name = invoker.target_name
    testonly = true
    visibility = [ ":*" ]

    deps = [ invoker.executable_target ]
    if (defined(generated_target)) {
      deps += [ ":$generated_target" ]
    }

    if (!defined(resources)) {
      resources = []
    }
    resources += invoker.additional_resources
  }

  fuchsia_test_package(target_name) {
    forward_variables_from(invoker,
                           [
                             "test_specs",
                             "visibility",
                           ])
    tests = [ ":$component_target" ]
    package_name = invoker.target_name
  }
}
