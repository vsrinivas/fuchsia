# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/components.gni")

# Run all tests with:
# fx test //src/sys/build/tests
group("tests") {
  deps = [
    ":echo-integration-test",
    ":readfile-unittest",
    ":return_zero_package",
    ":twoplustwo-cpp-unittest",
    ":twoplustwo-rust-unittest",
  ]
  testonly = true
}

fuchsia_cpp_component("return_zero_component") {
  sources = [ "return_zero.cc" ]
  output_name = "return_zero"
  manifest = "return_zero.cmx"
  component_name = "return-zero"
  testonly = true
}

fuchsia_test_package("return_zero_package") {
  package_name = "return-zero-package"
  test_components = [ ":return_zero_component" ]
}

fuchsia_cpp_unittest("twoplustwo-cpp-unittest") {
  sources = [ "two_plus_two.cc" ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_rust_unittest("twoplustwo-rust-unittest") {
  source_root = "two_plus_two.rs"
}

fuchsia_rust_unittest("readfile-unittest") {
  source_root = "read_file.rs"
  resources = [
    {
      source = "hello_world.txt"
      destination = "data/hello_world.txt"
    },
  ]
}

fuchsia_component("echo-server") {
  manifest = "//garnet/examples/fidl/meta/echo_server.cmx"
  resources = [
    {
      source = "$root_out_dir/echo_server_go"
      destination = "bin/echo_server"
    },
  ]
  deps = [ "//garnet/examples/fidl/echo_server_go:echo_server_go_bin" ]
}

fuchsia_rust_component("echo-client-test") {
  source_root = "echo_client.rs"
  manifest = "echo_client_test.cmx"
  deps = [
    "//garnet/examples/fidl/services:echo-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_test_package("echo-integration-test") {
  test_components = [
    ":echo-client-test",

    # Demonstrate that you can include multiple tests in the same package
    ":return_zero_component",
  ]
  components = [ ":echo-server" ]
}
