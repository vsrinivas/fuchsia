# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/package/component.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

rustc_library("lib") {
  name = "test_manager_lib"
  with_unit_tests = true
  edition = "2018"

  # Only for internal use.
  visibility = [
    ":*",
    "tests/*",
  ]

  deps = [
    "//garnet/lib/rust/io_util",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//sdk/fidl/fuchsia.test:fuchsia.test-rustc",
    "//src/lib/test_executor/rust:test_executor",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures",
    "//zircon/system/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

rustc_binary("bin") {
  name = "test_manager_bin"
  with_unit_tests = true
  edition = "2018"

  deps = [
    ":lib",
    "//garnet/public/rust/fuchsia-async",
  ]
}

package("test_manager") {
  testonly = true
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "test_manager"
      source = "test_manager_bin"
      shell = true
    },
  ]

  meta = [
    {
      path = rebase_path("meta/test_manager.cml")
      dest = "test_manager.cm"
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    "tests",
  ]
}
