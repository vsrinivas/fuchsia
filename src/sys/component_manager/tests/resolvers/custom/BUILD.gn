# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("resolvers") {
  testonly = true
  deps = [ ":tests" ]
}

group("tests") {
  testonly = true
  deps = [ ":package" ]
}

# The binary for the test resolver.
rustc_binary("resolver_bin") {
  edition = "2021"
  testonly = true

  name = "component_manager_test_resolvers_resolver"

  source_root = "src/resolver.rs"

  deps = [
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.component.resolution:fuchsia.component.resolution_rust",
    "//sdk/fidl/fuchsia.data:fuchsia.data_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.mem:fuchsia.mem_rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/resolver.rs" ]
}

# The integration test binary.
rustc_test("integration_test_bin") {
  edition = "2021"
  name = "component_manager_test_resolvers_integration_test"

  source_root = "src/integration_test.rs"

  deps = [
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/sys/component_manager/tests/fidl:components_rust",
  ]

  sources = [ "src/integration_test.rs" ]
}

fuchsia_component("root") {
  testonly = true
  manifest = "meta/root.cml"
}

fuchsia_component("resolver") {
  testonly = true
  manifest = "meta/resolver.cml"
  deps = [ ":resolver_bin" ]
}

fuchsia_component("integration_test") {
  testonly = true
  manifest = "meta/integration_test.cml"
  deps = [ ":integration_test_bin" ]
}

fuchsia_test_package("package") {
  package_name = "component-manager-test-resolver"
  test_components = [ ":root" ]
  deps = [
    ":integration_test",
    ":resolver",

    # fxbug.dev/101490: Dep included for affected tests presubmit
    "//src/sys/component_manager:component-manager-realm-builder-cmp",
    "//src/sys/component_manager/tests/resolvers:trigger-bin",
  ]
}
