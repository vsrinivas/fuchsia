# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/rust/rustc_test.gni")

group("boot") {
  testonly = true
  deps = [ ":tests" ]
}

group("tests") {
  testonly = true
  deps = []

  # TODO(https://fxbug.dev/77163): This test launches a nested component manager
  # and doesn't route debug data service, causing the test to log on error
  # level, which fails because of restricted log severity in tests.
  if (!is_coverage) {
    deps += [ ":boot-resolver-routing-tests" ]
  }
}

rustc_test("integration-test-bin") {
  testonly = true
  deps = [
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/sys/component_manager/tests/fidl:components-rustc",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]
  source_root = "src/integration_test.rs"
  sources = [ "src/integration_test.rs" ]
}

component_config("config") {
  testonly = true
  sources = [ "config.json5" ]
  dest = "data/component_manager_config"
}

fuchsia_component("component-manager-cmp") {
  testonly = true
  component_name = "component_manager"
  manifest = "meta/component_manager.cml"
  deps = [
    ":config",
    "//src/sys/component_manager:bin",
  ]
}

fuchsia_component("boot-cmp") {
  testonly = true
  component_name = "boot"
  manifest = "meta/boot.cml"
}

fuchsia_component("integration-test-cmp") {
  testonly = true
  component_name = "integration_test"
  manifest = "meta/integration_test.cml"
  deps = [ ":integration-test-bin" ]
}

fuchsia_test_package("boot-resolver-routing-tests") {
  test_components = [ ":integration-test-cmp" ]
  deps = [
    ":boot-cmp",
    ":component-manager-cmp",
    "//src/sys/component_manager/tests/resolvers:trigger-cmp",
  ]
}
