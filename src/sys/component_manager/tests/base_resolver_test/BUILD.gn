# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

rustc_test("base_resolver_test_bin") {
  name = "base_resolver_test"
  edition = "2018"
  source_root = "base_resolver_test.rs"

  deps = [
    "//garnet/examples/fidl/services:echo-rustc",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/sys/component_manager/testing:test_utils_lib",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "base_resolver_test.rs" ]
}

rustc_binary("root_component_bin") {
  name = "root_component"
  edition = "2018"
  source_root = "root_component.rs"

  deps = [
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "root_component.rs" ]
}

test_package("base_resolver_test") {
  deps = [
    ":base_resolver_test_bin",
    ":root_component_bin",
    "//garnet/examples/fidl/echo_server_rust:echo_server_rust_bin",
    "//src/sys/component_manager:bin",
    "//src/sys/component_manager:component_manager_test_config",
  ]

  resources = [
    {
      path = "${root_gen_dir}/src/sys/component_manager/component_manager_test_config"
      dest = "component_manager_config"
    },
  ]

  binaries = [
    {
      name = "root_component"
    },
    {
      name = "echo_server_rust"
      dest = "echo_server"
    },
    {
      name = "component_manager"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/component_manager_without_loader.cmx")
      dest = "component_manager_without_loader.cmx"
    },
    {
      path = rebase_path("meta/root.cml")
      dest = "root.cm"
    },
    {
      path = rebase_path("meta/echo_server.cml")
      dest = "echo_server.cm"
    },
    {
      path =
          rebase_path("//src/sys/component_manager/meta/component_manager.cmx")
      dest = "component_manager.cmx"
    },
  ]

  tests = [
    {
      name = "base_resolver_test"
    },
  ]
}
