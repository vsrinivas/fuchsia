# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("component_manager_panic_test_bin") {
  name = "component_manager_panic_test"
  edition = "2018"
  source_root = "component_manager_panic.rs"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/sys/lib/component-events",
  ]

  sources = [ "component_manager_panic.rs" ]
}

fuchsia_component("component_manager") {
  testonly = true
  manifest = "meta/component_manager.cml"
  deps = [
    ":component_manager_config",
    "//src/sys/component_manager:bin",
  ]
}

fuchsia_component("component_manager_panic_test_cmp") {
  testonly = true
  manifest = "meta/component_manager_panic_test.cml"
  deps = [ ":component_manager_panic_test_bin" ]
}

fuchsia_test_package("component_manager_panic_test") {
  test_components = [ ":component_manager_panic_test_cmp" ]
  deps = [ ":component_manager" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

component_config("component_manager_config") {
  sources = [ "config.json5" ]
}
