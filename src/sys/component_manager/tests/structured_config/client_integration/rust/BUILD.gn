# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_binary("bin") {
  edition = "2021"
  testonly = true
  name = "structured_config_rust_receiver"
  sources = [ "src/main.rs" ]
  deps = [
    ":config_lib",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/sys/component_manager/tests/structured_config/fidl:test.structuredconfig.receiver_rust",
    "//third_party/rust_crates:futures",
  ]
}

fuchsia_component_manifest("manifest") {
  testonly = true
  component_name = "rust_receiver"
  manifest = "meta/receiver.cml"
}

fuchsia_structured_config_rust_lib("config_lib") {
  testonly = true
  name = "receiver_config"
  cm_label = ":manifest"
}

fuchsia_component("component") {
  testonly = true
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_structured_config_values("values") {
  testonly = true
  cm_label = ":manifest"
  values_source = "../config/values.json5"
}

rustc_test("rust_sc_client_test_bin") {
  edition = "2021"
  source_root = "src/test.rs"
  sources = [ "src/test.rs" ]
  deps = [
    "..:sc_client_integration_support",
    "//src/lib/fuchsia",
  ]
}

fuchsia_test_component("test_component") {
  manifest = "meta/test.cml"
  deps = [ ":rust_sc_client_test_bin" ]
}

fuchsia_test_package("rust") {
  package_name = "sc_rust_client_integration_test"
  test_components = [ ":test_component" ]
  deps = [
    ":component",
    ":values",

    # fxbug.dev/101490: Dep included for affected tests presubmit
    "//src/sys/component_manager:component-manager-realm-builder-cmp",
  ]
}
