# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [ ":component-manager-services-tests" ]
}

rustc_binary("service-routing-branch-bin") {
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]

  source_root = "src/branch.rs"
  sources = [ "src/branch.rs" ]
}

fuchsia_test_component("service-routing-branch") {
  manifest = "meta/service-routing-branch.cml"
  restricted_features = [ "services" ]
  deps = [ ":service-routing-branch-bin" ]
}

rustc_test("service-routing-test-bin") {
  deps = [
    "//examples/components/services:fuchsia.examples.services-rustc",
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]

  source_root = "src/service_routing_test.rs"
  sources = [ "src/service_routing_test.rs" ]
}

fuchsia_test_component("service-routing-test") {
  manifest = "meta/service-routing-test.cml"
  restricted_features = [ "services" ]
  deps = [ ":service-routing-test-bin" ]
}

fuchsia_test_package("component-manager-services-tests") {
  test_components = [ ":service-routing-test" ]

  deps = [
    ":service-routing-branch",
    "//examples/components/services:provider-a-cmp",
    "//examples/components/services:provider-b-cmp",
  ]
}
