# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//src/sys/build/components.gni")

group("tests") {
  testonly = true

  deps = [ ":routing_integration_test" ]
}

rustc_test("routing_integration_test_bin") {
  name = "routing_integration_test"
  edition = "2018"
  source_root = "integration_test.rs"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/sys/component_manager/tests/fidl:components-rustc",
  ]

  sources = [ "integration_test.rs" ]
}

fuchsia_component("trigger") {
  testonly = true
  deps = [ "//src/sys/component_manager/tests:trigger_bin" ]
  manifest = "meta/trigger.cml"
}

fuchsia_unittest_package("routing_integration_test") {
  manifest = "meta/routing_integration_test.cml"
  deps = [
    ":routing_integration_test_bin",
    ":trigger",
    "//examples/components/routing/echo_server:echo_server_component",
  ]
}
