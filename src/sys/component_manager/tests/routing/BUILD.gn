# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

group("tests") {
  testonly = true

  deps = [ ":routing_integration_test" ]
}

rustc_test("routing_integration_test_bin") {
  name = "routing_integration_test"
  edition = "2018"
  source_root = "routing_integration_test.rs"
  deps = [
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/component_manager/tests:test_utils",
    "//third_party/rust_crates:anyhow",
    "//zircon/system/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

test_package("routing_integration_test") {
  deps = [
    ":routing_integration_test_bin",
    "//examples/components/routing/echo_client",
    "//examples/components/routing/echo_server",
  ]

  meta = [
    {
      path = rebase_path("//examples/components/routing/meta/echo_server.cml")
      dest = "echo_server.cm"
    },
    {
      path = rebase_path("//examples/components/routing/meta/echo_client.cml")
      dest = "echo_client.cm"
    },
    {
      path = rebase_path("meta/echo_realm.cml")
      dest = "echo_realm.cm"
    },
  ]

  binaries = [
    {
      name = "echo_server"
    },
    {
      name = "echo_client"
    },
  ]

  tests = [
    {
      name = "routing_integration_test"
    },
  ]
}
