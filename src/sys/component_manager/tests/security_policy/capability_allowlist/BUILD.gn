# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [ ":security-policy-capability-allowlist-integration-test" ]
}

rustc_test("security_policy_capability_allowlist_integration_test_bin") {
  name = "security_policy_capability_allowlist_integration_test"
  edition = "2021"
  source_root = "security_policy_capability_allowlist_integration_test.rs"
  deps = [
    ":test.policy_rust",
    "../util:security_policy_test_util",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "security_policy_capability_allowlist_integration_test.rs" ]
}

fidl("test.policy") {
  name = "test.policy"
  sources = [ "policy.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

rustc_binary("policy_service_bin") {
  edition = "2021"
  source_root = "policy_service.rs"
  deps = [
    ":test.policy_rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "policy_service.rs" ]
}

executable("dir_service_bin") {
  sources = [ "dir_service.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//src/lib/storage/vfs/cpp",
    "//src/storage/memfs",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/svc",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

rustc_binary("cap_getter_bin") {
  edition = "2021"
  source_root = "cap_getter.rs"
  deps = [
    ":test.policy_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "cap_getter.rs" ]
}

component_config("cm_config") {
  sources = [ "cm_config.json5" ]
}

fuchsia_component("test_root") {
  testonly = true
  manifest = "meta/test_root.cml"
}

fuchsia_component("policy_service") {
  testonly = true
  manifest = "meta/policy_service.cml"
  deps = [ ":policy_service_bin" ]
}

fuchsia_component("dir_service") {
  testonly = true
  manifest = "meta/dir_service.cml"
  deps = [ ":dir_service_bin" ]
}

fuchsia_component("cap_getter") {
  testonly = true
  manifest = "meta/cap_getter.cml"
  deps = [ ":cap_getter_bin" ]
}

fuchsia_unittest_package(
    "security-policy-capability-allowlist-integration-test") {
  manifest = "meta/security-policy-capability-allowlist-integration-test.cml"
  deps = [
    ":cap_getter",
    ":cm_config",
    ":dir_service",
    ":policy_service",
    ":security_policy_capability_allowlist_integration_test_bin",
    ":test_root",
    "//src/sys/component_manager/tests/security_policy:cm_for_test",
  ]
}
