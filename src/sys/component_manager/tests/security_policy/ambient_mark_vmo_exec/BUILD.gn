# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//src/sys/build/components.gni")

group("tests") {
  testonly = true
  deps = [ ":security-policy-vmex-integration-test" ]
}

rustc_test("security_policy_vmex_integration_test_bin") {
  name = "security_policy_vmex_integration_test"
  edition = "2018"
  source_root = "security_policy_vmex_integration_test.rs"
  deps = [
    ":test.policy-rustc",
    "../util:security_policy_test_util",
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:matches",
  ]

  sources = [ "security_policy_vmex_integration_test.rs" ]
}

fidl("test.policy") {
  name = "test.policy"
  sources = [ "policy.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

rustc_binary("protected_ops_util") {
  edition = "2018"
  source_root = "protected_ops_util.rs"
  deps = [
    ":test.policy-rustc",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "protected_ops_util.rs" ]
}

component_config("cm_config") {
  sources = [ "cm_config.json" ]
}

fuchsia_component("protected_ops") {
  testonly = true
  deps = [ ":protected_ops_util" ]
  manifest = "meta/protected_ops.cml"
}

fuchsia_component("test_root") {
  testonly = true
  manifest = "meta/test_root.cml"
}

fuchsia_component("protected_ops_no_request") {
  testonly = true
  deps = [ ":protected_ops_util" ]
  manifest = "meta/protected_ops_no_request.cml"
}

# This test needs a slightly customized version of component_manager.cmx that includes
# deprecated-ambient-replace-as-executable, so that the test ELF runner can actually grant the
# job policy we're testing. The config file `cm_config` can only be loaded from the same package.
fuchsia_component("cm_for_test") {
  testonly = true
  deps = [ "//src/sys/component_manager:bin" ]
  manifest = "meta/cm_for_test.cmx"
}

fuchsia_unittest_package("security-policy-vmex-integration-test") {
  manifest = "meta/security-policy-vmex-integration-test.cmx"
  deps = [
    ":cm_config",
    ":cm_for_test",
    ":protected_ops",
    ":protected_ops_no_request",
    ":security_policy_vmex_integration_test_bin",
    ":test_root",
  ]
}
