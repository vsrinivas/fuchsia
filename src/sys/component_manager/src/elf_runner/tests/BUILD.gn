# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [ ":elf_runner_lifecycle_test" ]
}

rustc_test("lifecycle") {
  source_root = "lifecycle.rs"
  name = "lifecycle_integration_test"
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/component_manager/tests:test_utils_lib",
    "//src/sys/component_manager/tests/fidl:events-rustc",
    "//third_party/rust_crates:futures-util",
    "//zircon/system/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

package("elf_runner_lifecycle_test") {
  testonly = true
  deps = [
    ":lifecycle",
    "//examples/components/basic:lifecycle_full",
    "//src/sys/test_adapters/rust",
  ]

  binaries = [
    {
      name = "lifecycle_full"
    },
    {
      name = "rust_test_adapter"
    },
  ]

  tests = [
    {
      name = "lifecycle_integration_test"
    },
  ]

  meta = [
    {
      path = rebase_path("//examples/components/basic/meta/lifecycle_full.cml")
      dest = "lifecycle_full.cm"
    },
    {
      path = rebase_path("meta/lifecycle.cml")
      dest = "lifecycle_integration_test.cm"
    },
  ]
}
