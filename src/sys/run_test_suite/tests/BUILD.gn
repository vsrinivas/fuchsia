# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_test("tests_bin") {
  edition = "2018"
  source_root = "main.rs"
  name = "run_test_suite_integration_tests"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/sys/run_test_suite:lib",
    "//src/sys/run_test_suite/directory",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tempfile",
  ]

  sources = [ "main.rs" ]
}

rustc_test("error_logging_test_bin") {
  source_root = "error_logging_test.rs"
  name = "error_logging_test"
  deps = [
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fuchsia",
    "//third_party/rust_crates:tracing",
  ]
  sources = [ "error_logging_test.rs" ]
}

rustc_test("logging_test_bin") {
  source_root = "logging_test.rs"
  name = "logging_test"
  deps = [
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fuchsia",
    "//third_party/rust_crates:tracing",
  ]
  sources = [ "logging_test.rs" ]
}

rustc_test("stdout_ansi_test_bin") {
  source_root = "stdout_ansi_test.rs"
  name = "stdout_ansi_test"
  deps = [
    "//src/lib/fuchsia",
    "//third_party/rust_crates:ansi_term",
    "//third_party/rust_crates:tracing",
  ]
  sources = [ "stdout_ansi_test.rs" ]
}

executable("long_running_test_bin") {
  output_name = "long_running_test"
  testonly = true
  sources = [ "long_running_test.cc" ]

  deps = [
    "//garnet/public/lib/gtest",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_component("long_running_test") {
  testonly = true
  deps = [ ":long_running_test_bin" ]
  manifest = "meta/long_running_test.cml"
}

fuchsia_component("logging_test") {
  testonly = true
  deps = [ ":logging_test_bin" ]
  manifest = "meta/logging_test.cml"
}

fuchsia_component("stdout_ansi_test") {
  testonly = true
  deps = [ ":stdout_ansi_test_bin" ]
  manifest = "meta/stdout_ansi_test.cml"
}

fuchsia_component("error_logging_test") {
  testonly = true
  deps = [ ":error_logging_test_bin" ]
  manifest = "meta/error_logging_test.cml"
}

fuchsia_unittest_package("run_test_suite_integration_tests") {
  manifest = "meta/run_test_suite_integration_tests.cmx"
  deps = [
    ":error_logging_test",
    ":logging_test",
    ":long_running_test",
    ":stdout_ansi_test",
    ":tests_bin",
    "//examples/components/routing/echo_server:echo_server_component",
    "//examples/tests:disabled-test-example",
    "//examples/tests:echo_test_client",
    "//examples/tests:echo_test_realm",
    "//examples/tests:failing-test-example",
    "//examples/tests:get-tests-closes-channel-example",
    "//examples/tests:get-tests-hangs-example",
    "//examples/tests:huge-test-example",
    "//examples/tests:incomplete-test-example",
    "//examples/tests:invalid-test-example",
    "//examples/tests:no-onfinished-after-test-example",
    "//examples/tests:no-test-example",
    "//examples/tests:passing-test-example",
    "//examples/tests:run-closes-channel-example",
    "//examples/tests:run-hangs-example",
    "//examples/tests:test-with-stderr",
  ]

  # There's expected error logs that happen due to races given that the logging component just
  # logs and exits quickly.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":run_test_suite_integration_tests",
    "//examples/tests",
  ]
}
