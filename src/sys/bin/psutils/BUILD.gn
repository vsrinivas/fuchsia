# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("psutils") {
  deps = [
    ":handles",
    ":kill",
    ":killall",
    ":kstats",
    ":memgraph",
    ":ps",
    ":signal",
    ":threads",
    ":top",
    ":vmaps",
    ":vmos",
  ]
}

source_set("ps_internal") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "ps_internal.c" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
    "//zircon/system/ulib/task-utils",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

source_set("handles_internal") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "handles-internal.cc",
    "object-utils.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
    "//zircon/system/ulib/task-utils",
  ]
}

executable("ps") {
  output_dir = "$root_out_dir/zircon-migrated"
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "ps.c" ]
  deps = [
    ":ps_internal",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
    "//zircon/system/ulib/task-utils",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("top") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "top.c" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
    "//zircon/system/ulib/task-utils",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("kill") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "kill.c" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/task-utils",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("killall") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "killall.c" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/task-utils",
  ]
}

executable("vmaps") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "vmaps.c" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
    "//zircon/system/ulib/task-utils",
  ]
}

executable("vmos") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "vmo-utils.c",
    "vmos.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
    "//zircon/system/ulib/task-utils",
  ]
}

executable("memgraph") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "memgraph.cc",
    "object-utils.cc",
    "resources.c",
    "vmo-utils.c",
  ]
  include_dirs = [ target_gen_dir ]
  deps = [
    ":gen-memgraph-schema",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/task-utils",
  ]
}

# Generate an include file that contains the schema JSON.
action("gen-memgraph-schema") {
  outputs = [ "$target_gen_dir/memgraph-schema.h" ]
  sources = [ "memgraph-schema.json" ]
  script = "gen-memgraph-schema-header.sh"
  args = rebase_path(outputs, root_build_dir) +
         rebase_path(sources, root_build_dir)
}

executable("kstats") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "kstats.c",
    "resources.c",
  ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("threads") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "threads.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/inspector",
    "//zircon/system/ulib/task-utils",
  ]
}

executable("signal") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "signal.c" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/task-utils",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("handles") {
  output_dir = "$root_out_dir/zircon-migrated"
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "handles.cc" ]
  deps = [
    ":handles_internal",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_c",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/task-utils",
  ]
}
