# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  output_name = "debug-dash-launcher"
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_rust",
    "//sdk/fidl/fuchsia.dash:fuchsia.dash_rust",
    "//sdk/fidl/fuchsia.device:fuchsia.device_rust",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//sdk/fidl/fuchsia.process:fuchsia.process_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/library_loader",
    "//src/sys/lib/moniker",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [ "//third_party/rust_crates:tempfile" ]

  sources = [
    "src/launch.rs",
    "src/layout.rs",
    "src/main.rs",
    "src/socket.rs",
  ]
}

fuchsia_component("component") {
  component_name = "debug_dash_launcher"
  manifest = "meta/debug_dash_launcher.cml"
  deps = [ ":bin" ]
}

fuchsia_unittest_package("debug-dash-launcher-unittests") {
  manifest = "meta/debug_dash_launcher_unittests.cml"
  deps = [
    ":bin_test",
    "//src/bringup/bin/ptysvc:component",
  ]
}

fuchsia_package("debug-dash-launcher") {
  deps = [
    ":component",
    "//third_party/sbase:bins",
    "//zircon/third_party/uapp/dash",
  ]

  # This package is added by product assembly into non-eng builds only.
  # It should not be added manually anywhere else.
  visibility = [ "//bundles/assembly/*" ]
}

group("tests") {
  testonly = true
  deps = [
    ":debug-dash-launcher-unittests",
    "integration_test",
  ]
}
