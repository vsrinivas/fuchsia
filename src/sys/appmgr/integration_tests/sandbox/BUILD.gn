# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//src/sys/appmgr/integration_tests/sandbox/sandbox_test_package.gni")

group("sandbox") {
  testonly = true
  deps = [
    ":has_pkg",
    ":path-traversal-escape-package",
    "features",
    "services",
  ]
}

source_set("namespace_test") {
  testonly = true

  sources = [
    "namespace_test.cc",
    "namespace_test.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_wire",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]
}

sandbox_test_package("has_pkg") {
  sources = [ "has_pkg.cc" ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//src/sys/appmgr/integration_tests/sandbox:namespace_test",
  ]
  environments = basic_envs
}

test("path_traversal_escape") {
  sources = [ "path_traversal_escape.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.sys:fuchsia.sys_cpp_wire",
    "//sdk/lib/fidl",
    "//sdk/lib/sys/component/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}

fuchsia_test_component("path-traversal-escape") {
  manifest = "meta/path-traversal-escape.cml"
  deps = [ ":path_traversal_escape" ]
}

# This is specifically a CFv1 component to test the path behavior of appmgr. Do
# not convert this component to CFv2. This can be deleted when appmgr is
# deleted.
fuchsia_test_component("path-traversal-escape-child") {
  manifest = "meta/path-traversal-escape-child.cmx"
  deps = [ ":path_traversal_escape" ]
}

fuchsia_test_package("path-traversal-escape-package") {
  package_name = "path-traversal-escape"
  test_components = [ ":path-traversal-escape" ]
  deps = [ ":path-traversal-escape-child" ]
}
