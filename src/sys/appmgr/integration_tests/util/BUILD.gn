# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/package.gni")

group("util") {
  testonly = true
  deps = [ ":persistent_storage_test_util" ]
}

fidl("data_file_reader_writer") {
  # TODO(fxb/35879): Remove lint exclusions by fixing known FIDL lint violations in this target
  excluded_checks = [ "string-bounds-not-specified" ]

  testonly = true
  name = "test.appmgr.integration"

  sources = [ "data_file_reader_writer.fidl" ]

  public_deps = [ "//zircon/vdso/zx" ]
}

source_set("data_file_reader_writer_util") {
  testonly = true

  sources = [
    "data_file_reader_writer_util.cc",
    "data_file_reader_writer_util.h",
  ]

  deps = [
    ":data_file_reader_writer",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/lib/fxl",
  ]

  # TODO(44385): UBSan has found an instance of undefined behavior in this target.
  # Disable UBSan for this target temporarily until it is migrated into CI/CQ.
  public_configs = [ "//build/config:temporarily_disable_ubsan_do_not_use" ]
}

executable("persistent_storage_test_util_bin") {
  testonly = true
  output_name = "persistent_storage_test_util"

  sources = [ "persistent_storage_test_util.cc" ]

  deps = [
    ":data_file_reader_writer",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

package("persistent_storage_test_util") {
  testonly = true

  meta = [
    # Same test util manifest gets added into package with two different names
    # to enable testing isolation of components based on URL
    {
      path = "meta/util.cmx"
      dest = "util.cmx"
    },
    {
      path = "meta/util.cmx"
      dest = "util2.cmx"
    },
  ]

  binaries = [
    {
      name = "persistent_storage_test_util"
    },
  ]

  deps = [ ":persistent_storage_test_util_bin" ]
}
