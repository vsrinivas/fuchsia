# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")

group("util") {
  testonly = true
  deps = [ ":persistent_storage_test_util" ]
}

fidl("data_file_reader_writer") {
  # TODO(fxbug.dev/35879): Remove lint exclusions by fixing known FIDL lint violations in this target
  excluded_checks = [ "string-bounds-not-specified" ]

  testonly = true
  name = "test.appmgr.integration"

  sources = [ "data_file_reader_writer.fidl" ]

  public_deps = [ "//zircon/vdso/zx" ]
}

source_set("data_file_reader_writer_util") {
  testonly = true

  sources = [
    "data_file_reader_writer_util.cc",
    "data_file_reader_writer_util.h",
  ]

  deps = [
    ":data_file_reader_writer_hlcpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//src/lib/fxl",
  ]
}

executable("persistent_storage_test_util_bin") {
  testonly = true
  output_name = "persistent_storage_test_util"

  sources = [ "persistent_storage_test_util.cc" ]

  deps = [
    ":data_file_reader_writer_hlcpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_component("util1") {
  component_name = "util"
  testonly = true
  deps = [ ":persistent_storage_test_util_bin" ]
  manifest = "meta/util.cmx"
}

fuchsia_component("util2") {
  testonly = true
  deps = [ ":persistent_storage_test_util_bin" ]
  manifest = "meta/util.cmx"
}

fuchsia_package("persistent_storage_test_util") {
  testonly = true

  # Same test util manifest gets added into package with two different names
  # to enable testing isolation of components based on URL
  deps = [
    ":util1",
    ":util2",
  ]
}
