# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  testonly = true
  name = "dart_wrapper_runner"
  with_unit_tests = true
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//src/lib/async-utils",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/runner",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [ "//third_party/rust_crates:assert_matches" ]

  sources = [ "src/main.rs" ]
}

fuchsia_component("dart_aot_wrapper_runner") {
  testonly = true
  manifest = "meta/dart-aot-wrapper-runner.cml"
  component_name = "dart-aot-wrapper-runner"
  deps = [ ":bin" ]
}

fuchsia_package("dart-aot-wrapper-runner") {
  testonly = true
  deps = [ ":dart_aot_wrapper_runner" ]
}

fuchsia_component("dart_jit_wrapper_runner") {
  testonly = true
  manifest = "meta/dart-jit-wrapper-runner.cml"
  component_name = "dart-jit-wrapper-runner"
  deps = [ ":bin" ]
}

fuchsia_package("dart-jit-wrapper-runner") {
  testonly = true
  deps = [ ":dart_jit_wrapper_runner" ]
}

fuchsia_unittest_package("dart-wrapper-runner-unittests") {
  deps = [ ":bin_test" ]
}

group("dart_wrapper") {
  testonly = true
  deps = [
    ":dart-aot-wrapper-runner",
    ":dart-jit-wrapper-runner",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":dart-wrapper-runner-unittests" ]
}
