# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("tests_bin") {
  edition = "2021"
  source_root = "main.rs"
  name = "stress_runner_integration_test"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [ "main.rs" ]
}

fuchsia_component("stress_runner_integration_test") {
  testonly = true
  manifest = "meta/stress_runner_integration_test.cml"
  deps = [ ":tests_bin" ]
}

fuchsia_test_package("stress-runner-integration-test") {
  test_components = [ ":stress_runner_integration_test" ]

  # This includes the `always_fail`, `always_pass`, `timeout` and `echo` stress tests
  # used in this integration test.
  deps = [
    "test_data",
    "//src/sys/test_manager:test_manager_for_test_cmp",
  ]

  # TODO(fxbug.dev/114727): Flaky test disabled on coverage.
  # Please fix the flake and remove the condition below.
  if (is_coverage) {
    test_specs = {
      environments = [
        {
          dimensions = qemu_env
          tags = [ "disabled" ]
        },
      ]
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":stress-runner-integration-test" ]
}
