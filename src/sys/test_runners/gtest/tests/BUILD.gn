# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("tests_bin") {
  edition = "2018"
  source_root = "tests.rs"
  name = "gtest_runner_integration_test"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:maplit",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [
    "lib.rs",
    "tests.rs",
  ]

  inputs = [ "../test_data/sample_tests_golden_events.rsf" ]
}

fuchsia_component("gtest_runner_integration_test") {
  testonly = true
  manifest = "meta/gtest_runner_integration_test.cml"
  deps = [ ":tests_bin" ]
}

rustc_test("stress_tests_bin") {
  edition = "2018"
  source_root = "stress_tests.rs"
  name = "gtest_runner_stress_test"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [
    "lib.rs",
    "stress_tests.rs",
  ]
}

fuchsia_component("gtest_runner_stress_test") {
  testonly = true
  manifest = "meta/gtest_runner_stress_test.cml"
  deps = [ ":stress_tests_bin" ]
}

fuchsia_test_package("gtest-runner-integration-test") {
  package_deps_for_infra_only = [
    "//src/sys/test_runners/gtest/test_data:gtest-runner-example-tests",
    "//src/sys/test_runners/starnix:starnix-test-runner",
  ]
  test_components = [
    ":gtest_runner_integration_test",
    ":gtest_runner_stress_test",
  ]
  deps = [ "//src/sys/test_manager:test_manager_for_test_cmp" ]

  # fxbug.dev/74652: This stress test fails on coverage bot.
  if (is_coverage) {
    test_components -= [ ":gtest_runner_stress_test" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":gtest-runner-integration-test",
    "../test_data:gtest-runner-example-tests",
  ]
}
