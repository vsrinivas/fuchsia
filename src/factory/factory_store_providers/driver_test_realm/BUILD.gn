# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//tools/cmc/build/expect_includes.gni")

group("driver_test_realm") {
  testonly = true
  deps = [ ":component" ]
}

expect_includes("client_includes") {
  includes = [ "meta/client.shard.cml" ]
}

executable("bin") {
  testonly = true
  output_name = "factory_driver_test_realm"
  sources = [ "ram_disk_setup.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_cpp_wire",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/sys/component/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/files:files",
    "//src/lib/fsl",
    "//src/lib/storage/block_client/cpp",
    "//src/storage/testing:storage_driver_test_support",
  ]
}

fuchsia_component("component") {
  testonly = true
  component_name = "factory_driver_test_realm"
  manifest = "meta/factory_driver_test_realm.cml"
  deps = [
    ":bin",
    "//sdk/lib/driver_test_realm",
    "//src/devices/bus/drivers/platform",
    "//src/storage/testing:storage_drivers",
  ]
}
