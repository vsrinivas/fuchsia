# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//garnet/lib/magma/gnbuild/magma.gni")
import("//packages/package.gni")

package("magma-test-build-only") {
  testonly = true
  deps = []
  if (build_libvulkan_arm_mali || build_intel_gen) {
    deps += [ ":tests" ]
    if (build_intel_gen) {
      deps += [
        "$mesa_build_root:tests",
        "$msd_intel_gen_build_root:msd_intel_test",
        "$msd_intel_gen_build_root:tests",
      ]
    }
  }
  if (build_msd_arm_mali) {
    deps += [ "//garnet/drivers/gpu/msd-arm-mali:tests" ]
  }
}

package("magma-test") {
  testonly = true
  deps = []
  tests = []
  if (build_libvulkan_arm_mali || build_intel_gen) {
    deps += [
      ":autorun",
      ":tests",
    ]

    tests += [
      {
        name = "magma_sys_unit_tests"
      },

      {
        name = "magma_integration_tests"
      },

      {
        name = "magma_abi_conformance_tests"
      },

      {
        name = "vkreadback"
      },

      {
        name = "vkext"
      },
    ]

    if (build_intel_gen) {
      deps += [
        ":autorun",
        "$mesa_build_root:tests",
        "$msd_intel_gen_build_root:tests",
      ]

      tests += [
        {
          name = "msd_intel_gen_nonhardware_tests"
        },

        {
          name = "mesa_unit_tests"
        },

        {
          name = "test_wsi_magma"
        },

        {
          name = "state_pool"
        },

        {
          name = "state_pool_free_list_only"
        },

        {
          name = "state_pool_no_free"
        },
      ]

      binaries = [ {
            name = "autorun"
            dest = "magma_autorun"
          } ]
    }
  }

  if (build_msd_arm_mali) {
    deps += [ "//garnet/drivers/gpu/msd-arm-mali:tests" ]
    tests += [ {
          name = "msd_arm_mali_nonhardware_tests"
        } ]
  }
}

if (build_libvulkan_arm_mali || build_intel_gen) {
  copy("autorun") {
    sources = [
      "$magma_build_root/scripts/autorun",
    ]
    outputs = [
      "$root_out_dir/autorun",
    ]
  }

  group("tests") {
    testonly = true

    public_configs = [ "$magma_build_root:magma_tests_include_config" ]

    public_deps = [
      "integration",
      "unit_tests:magma_abi_conformance_tests",
      "unit_tests:magma_sys_unit_tests",
      "vkcube",
      "vkext",
      "vkreadback",
    ]
  }
}
