# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//garnet/lib/magma/gnbuild/magma.gni")
import("//build/package.gni")

package("magma-test-build-only") {
  testonly = true
  deprecated_system_image = true

  deps = [
    "unit_tests",
  ]

  if (have_libvulkan || use_vulkan_loader_for_tests) {
    deps += [
      "benchmark",
      "vkcube",
      "vkext",
      "vkloop",
      "vkpriority",
      "vkreadback",
    ]
  }

  if (build_intel_gen) {
    deps += [
      "$mesa_build_root:tests",
      "$msd_intel_gen_build_root:msd_intel_test",
      "$msd_intel_gen_build_root:tests",
      "//garnet/drivers/gpu/msd-intel-gen/tests/integration",
    ]
  }
  if (build_msd_arm_mali) {
    deps += [
      "//garnet/drivers/gpu/msd-arm-mali:msd_arm_test",
      "//garnet/drivers/gpu/msd-arm-mali:tests",
      "//garnet/drivers/gpu/msd-arm-mali/tests/integration",
    ]
  }
  if (build_vsl_gc) {
    deps += [ "//garnet/drivers/gpu/msd-vsl-gc:msd_vsl_test" ]
  }
}

package("nonhardware-tests") {
  testonly = true
  package_name = "magma_nonhardware_tests"

  deps = [
    "unit_tests",
  ]

  tests = [
    {
      name = "magma_unit_tests"
    },
  ]
}

package("tests") {
  testonly = true
  deprecated_system_image = true

  deps = [
    "integration",
  ]

  tests = [
    {
      name = "magma_abi_conformance_tests"
    },
  ]
}

package("vulkan-tests") {
  testonly = true

  if (have_libvulkan || use_vulkan_loader_for_tests) {
    deps = [
      "benchmark",
      "vkcube",
      "vkext",
      "vkloop",
      "vkpriority",
      "vkreadback",
    ]

    tests = [
      {
        name = "magma_memcpy"
      },
      {
        name = "vkreadback"
      },
      {
        name = "vkloop"
      },
      {
        name = "vkext"
      },
      {
        name = "vkcopy"
      },
      {
        name = "vkpriority"
      },
    ]

    meta = [
      {
        path = rebase_path("meta/vulkan-tests.cmx")
        dest = "vulkan-tests.cmx"
      },
    ]
  }
}
