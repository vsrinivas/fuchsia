# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//magma/gnbuild/magma.gni")

declare_args() {
  magma_indriver_test = ""
}

source_set("main") {
  sources = [
    "driver_main.cpp",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$magma_build_root/src/sys_driver",
    "$mesa_build_root:magma_vulkan",
  ]

  libs = [
    "driver",
    "ddk",
    "magenta",
  ]
}

source_set("test_driver") {
  testonly = true

  configs += [ "$magma_build_root:magma_tests_include_config" ]

  sources = [
    "driver_main.cpp",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$magma_build_root/src/sys_driver",
    "$magma_build_root/tests/helper:platform_device_helper",
    "$mesa_build_root:magma_vulkan",
  ]

  defines = []

  if (magma_indriver_test == "msd_unit_tests") {
    # Don't create the device, let the unit tests create and destroy it.
    defines += [ "MAGMA_CREATE_DEVICE=0" ]
    defines += [ "MAGMA_INDRIVER_TEST=1" ]
    sources += [ "driver_test_gtest.cc" ]
    deps += [
      "$magma_build_root/tests/unit_tests:magma_platform_tests",
      "$magma_build_root/tests/unit_tests:msd_abi_conformance_tests",
      "$msd_gtest_target",
      "//third_party/gtest",
    ]
  } else if (magma_indriver_test == "sys_abi_tests") {
    defines += [ "MAGMA_INDRIVER_TEST=1" ]
    sources += [ "driver_test_gtest.cc" ]
    deps += [
      "$magma_build_root/tests/unit_tests:sys_abi_conformance_tests",
      "//third_party/gtest",
    ]
  } else if (magma_indriver_test == "glreadback") {
    defines += [ "MAGMA_INDRIVER_TEST=1" ]
    sources += [ "driver_test_readback.cc" ]
    deps -= [ "$mesa_build_root:magma_vulkan" ]
    deps += [ "$magma_build_root/tests/gpureadback" ]
  } else if (magma_indriver_test == "spinning_cube") {
    defines += [ "MAGMA_INDRIVER_TEST=1" ]
    sources += [ "driver_test_spinning_cube.cc" ]
    deps -= [ "$mesa_build_root:magma_vulkan" ]
    deps += [ "$magma_build_root/tests/spinning_cube" ]
  } else if (magma_indriver_test == "vkreadback") {
    defines += [ "MAGMA_INDRIVER_TEST=1" ]
    sources += [ "driver_test_vkreadback.cc" ]
    deps += [ "$magma_build_root/tests/vkreadback" ]
  } else if (magma_indriver_test == "vkcube") {
    defines += [ "MAGMA_INDRIVER_TEST=1" ]
    sources += [ "driver_test_vkcube.cc" ]
    deps += [ "$magma_build_root/tests/vkcube" ]
  } else if (magma_indriver_test != "") {
    assert(false)
  }

  libs = [
    "driver",
    "ddk",
    "magenta",
  ]
}
