# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//magma/gnbuild/magma.gni")

group("magma_service_driver") {
  deps = [
    "$msd_intel_gen_build_root",
  ]
}

config("libdrm_api") {
  include_dirs = [ "third_party/libdrm" ]
}

source_set("libdrm") {
  sources = [
    "third_party/libdrm/libdrm_intel_gen.cpp",
    "third_party/libdrm/libdrm_intel_gen.h",
  ]

  public_configs = [ ":libdrm_api" ]

  deps = [
    ":util",
  ]

  public_deps = [
    "include:msd_abi",
  ]
}

source_set("libmad") {
  sources = [
    "src/app_driver/magma.cc",
    "src/app_driver/magma_buffer.cc",
    "src/app_driver/magma_buffer.h",
    "src/app_driver/magma_connection.cc",
    "src/app_driver/magma_connection.h",
  ]

  public_deps = [
    "include:mad_abi",
  ]

  deps = [
    ":libdrm",
    ":libmsd",
    ":util",
    "include:mad_abi",
    "src/sys_driver:magma_system_interface",
  ]
}

# This config allows inclusion of headers via relative paths from src
config("magma_src_include_config") {
  include_dirs = [ "src" ]
}

# This config allows inclusion of headers via relative paths from tests
config("magma_tests_include_config") {
  include_dirs = [ "tests" ]
}

# TODO(MA-29): remove this, just use src/magma_util instead
group("util") {
  public_deps = [
    "src/magma_util",
  ]
}

# internal only
source_set("libmsd") {
  deps = [
    "src/sys_driver",
  ]
  public_deps = [
    "include:msd_abi",
  ]
}

group("magma_tests") {
  deps = [
    "tests",
  ]
}
