# Copyright 2016 The Fuchsia Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//magma/gnbuild/magma.gni")

group("magma_service_driver") {
  deps = [
    "$msd_intel_gen_build_root",
  ]
}

config("libmad_api") {
  include_dirs = [ "include" ]
}

config("libmsd_api") {
  include_dirs = [
    "include",
    "include/sys-driver",
    "include/sys-driver/abi",
    "src/sys-driver/platform_buffer",
  ]
}

config("util_api") {
  include_dirs = [ "src/include" ]
}

source_set("util") {
  public_configs = [ ":util_api" ]
}

config("libdrm_api") {
  include_dirs = [ "third_party/libdrm" ]
}

source_set("libdrm") {
  sources = [
    "third_party/libdrm/libdrm_intel_gen.cpp",
    "third_party/libdrm/libdrm_intel_gen.h",
  ]

  public_configs = [ ":libdrm_api" ]

  configs += [ ":libmsd_api" ]

  deps = [
    ":util",
  ]
}

source_set("libmad") {
  public_configs = [ ":libmad_api" ]

  sources = [
    "include/common/magma_defs.h",
    "include/magma.h",
    "src/app-driver/magma.cpp",
    "src/app-driver/magma_device.cpp",
    "src/app-driver/magma_device.h",
  ]

  deps = [
    ":libdrm",
    ":libmsd",
  ]
}

# internal only
source_set("libmsd") {
  public_configs = [ ":libmsd_api" ]

  public_deps = [
    ":util",
    ":platform_buffer",
  ]

  include_dirs = [ "third_party" ]

  sources = [
    "include/magma_driver.h",
    "include/magma_sys_defs.h",
    "include/magma_sys_driver.h",
    "include/magma_system.h",
    "src/sys-driver/magma_system.cpp",
  ]
}

source_set("platform_buffer") {
  public_configs = [ ":libmsd_api" ]

  public_deps = [
    ":util",
  ]

  sources = [
    "src/sys-driver/platform_buffer/platform_buffer.h",
    "src/sys-driver/platform_buffer/platform_buffer.cc",
    "src/sys-driver/platform_buffer/magenta_platform_buffer.cc",
  ]

  libs = [
    "ddk"
  ]
}

group("magma_tests") {
  deps = [
    "tests",
  ]
}
