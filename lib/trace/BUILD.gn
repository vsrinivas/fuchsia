# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Declares common trace data types.
source_set("types") {
  sources = [
    "ticks.cc",
    "ticks.h",
    "types.h",
  ]

  deps = [ "//lib/ftl" ]
}

# Declares the trace macros and writer API and offers only a do-nothing stub
# implementation with weak linkage.
#
# To enable tracing in your application also include the 'provider' target
# in your application and make sure to register a TraceProvider when your
# application starts up.
#
# This target only depends on ftl, standard library, and system headers
# so that it can safely be referenced from most low-level libraries with
# minimum impact.
source_set("trace") {
  sources = [
    "event.h",
    "internal/event_helpers.h",
    "writer_stub.cc",
    "writer.h",
  ]

  deps = [
    ":internal_fields",
    "//lib/ftl",
  ]

  public_deps = [
    ":types",
    "//magenta/system/ulib/mx",
  ]
}

# Implements the trace writer API.
# This target includes the trace engine which needs a message loop to operate.
source_set("writer") {
  sources = [
    "internal/trace_engine.cc",
    "internal/trace_engine.h",
    "writer.cc",
  ]

  deps = [
    "//lib/ftl",
    "//lib/mtl/handles",
    "//lib/mtl/tasks",
    "//lib/mtl/vmo",
  ]

  public_deps = [
    ":trace",
  ]
}

# Implements the trace provider API.
# This target includes the fidl-based TraceProvider front-end together
# with the trace writer.
source_set("provider") {
  sources = [
    "internal/trace_provider_impl.cc",
    "internal/trace_provider_impl.h",
    "dump.cc",
    "dump.h",
    "provider.cc",
    "provider.h",
    "settings.cc",
    "settings.h",
  ]

  deps = [
    "//apps/modular/lib/app",
    "//apps/tracing/services",
    "//lib/ftl",
    "//lib/mtl",
  ]

  public_deps = [
    ":writer",
  ]
}

# Implements the trace reader API.
source_set("reader") {
  sources = [
    "reader.cc",
    "reader.h",
  ]

  deps = [
    ":internal_fields",
    "//lib/ftl",
  ]

  public_deps = [
    ":types",
  ]
}

# Declares internal fields which describe the trace format.
source_set("internal_fields") {
  sources = [
    "internal/fields.h",
  ]

  public_deps = [
    ":types",
  ]
}

executable("trace_tests") {
  testonly = true

  sources = [
    "event_unittest.cc",
    "internal/fields_unittest.cc",
    "settings_unittest.cc",
    "writer_unittest.cc",
  ]

  deps = [
    ":internal_fields",
    ":provider",
    ":reader",
    ":trace",
    ":types",
    "//apps/tracing/services",
    "//lib/mtl",
    "//lib/mtl/test",
    "//third_party/gtest",
    "//third_party/rapidjson",
  ]
}

executable("trace_stub_tests") {
  testonly = true

  sources = [
    "writer_stub_unittest.cc",
  ]

  deps = [
    ":trace",
    "//lib/mtl/test",
    "//third_party/gtest",
  ]
}
