# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("cpp") {
  testonly = true
  deps = [
    ":archive_reader",
    ":read_visitor",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":inspect_contrib_cpp_tests",
    "tests",
  ]
}

source_set("read_visitor") {
  sources = [ "read_visitor.h" ]

  public_deps = [
    "//zircon/public/lib/fit",
    "//zircon/system/ulib/inspect",
  ]
}

source_set("archive_reader") {
  sources = [
    "archive_reader.cc",
    "archive_reader.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.diagnostics",

    # TODO(57392): Move it back to //third_party once unification completes.
    "//zircon/third_party/rapidjson",
  ]

  deps = [
    "//src/lib/fsl",
    "//src/lib/fxl",
  ]
}

executable("read_visitor_test") {
  testonly = true
  output_name = "read_visitor_test"
  sources = [ "read_visitor_unittest.cc" ]
  deps = [
    ":read_visitor",
    "//zircon/public/lib/zxtest",
  ]
}

executable("archive_reader_test") {
  testonly = true
  output_name = "archive_reader_test"
  sources = [ "archive_reader_unittest.cc" ]
  deps = [
    ":archive_reader",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//zircon/public/lib/zxtest",
  ]
}

unittest_package("inspect_contrib_cpp_tests") {
  tests = [
    {
      name = "read_visitor_test"
      environments = basic_envs
    },
    {
      name = "archive_reader_test"
      environments = basic_envs
    },
  ]

  deps = [
    ":archive_reader_test",
    ":read_visitor_test",
  ]
}
