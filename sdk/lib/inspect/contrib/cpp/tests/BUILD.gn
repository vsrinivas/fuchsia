#Copyright 2018 The Fuchsia Authors.All rights reserved.
#Use of this source code is governed by a BSD - style license that can be
#found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  deps = [ ":archive_reader_integration_tests" ]
}

executable("bin") {
  output_name = "archive_reader_test_app"

  sources = [ "program.cc" ]

  deps = [
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("test") {
  testonly = true
  output_name = "archive_reader_integration_tests"

  sources = [ "test.cc" ]

  deps = [
    "//sdk/lib/inspect/contrib/cpp:archive_reader",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
  ]
}

test_package("archive_reader_integration_tests") {
  deps = [
    ":bin",
    ":test",
  ]

  binaries = [
    {
      name = "archive_reader_test_app"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/archive_reader_test_app.cmx")
      dest = "archive_reader_test_app.cmx"
    },
    {
      path = rebase_path("meta/archive_reader_test_app_2.cmx")
      dest = "archive_reader_test_app_2.cmx"
    },
  ]

  tests = [
    {
      name = "archive_reader_integration_tests"
      environments = basic_envs
    },
  ]
}
