# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/sdk_source_set.gni")

sdk_source_set("cpp") {
  category = "partner"

  sdk_name = "scenic_cpp"

  include_base = "//sdk"

  public_configs = [ "//sdk/config" ]

  sources = [
    "commands.cc",
    "commands.h",
    "commands_sizing.cc",
    "commands_sizing.h",
    "id.h",
    "resources.cc",
    "resources.h",
    "session.cc",
    "session.h",
    "view_ref_pair.cc",
    "view_ref_pair.h",
    "view_token_pair.cc",
    "view_token_pair.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.images",
    "//sdk/fidl/fuchsia.ui.gfx",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/images/cpp",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/syslog",
  ]

  non_sdk_deps = [ ":gen_command_sizing" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

compiled_action("gen_command_sizing") {
  tool = "//tools/fidl/measure-tape/src:measure-tape"

  check_file = "$target_out_dir/scenic-measure-tape.checked"
  outputs = [ "$check_file" ]

  args = [
    "--json",
    rebase_path(
        "$root_build_dir/fidling/gen/sdk/fidl/fuchsia.images/fuchsia.images.fidl.json",
        root_build_dir),
    "--json",
    rebase_path(
        "$root_build_dir/fidling/gen/sdk/fidl/fuchsia.ui.gfx/fuchsia.ui.gfx.fidl.json",
        root_build_dir),
    "--json",
    rebase_path(
        "$root_build_dir/fidling/gen/sdk/fidl/fuchsia.ui.input/fuchsia.ui.input.fidl.json",
        root_build_dir),
    "--json",
    rebase_path(
        "$root_build_dir/fidling/gen/sdk/fidl/fuchsia.ui.scenic/fuchsia.ui.scenic.fidl.json",
        root_build_dir),
    "--json",
    rebase_path(
        "$root_build_dir/fidling/gen/sdk/fidl/fuchsia.ui.views/fuchsia.ui.views.fidl.json",
        root_build_dir),
    "--target-binding",
    "hlcpp",
    "--target-type",
    "fuchsia.ui.scenic/Command",
    "--out-h",
    rebase_path("commands_sizing.h"),
    "--h-include-path",
    "lib/ui/scenic/cpp/commands_sizing.h",
    "--out-cc",
    rebase_path("commands_sizing.cc"),
    "--only-check-to-file",
    rebase_path("$check_file"),
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.images",
    "//sdk/fidl/fuchsia.ui.gfx",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.ui.views",
  ]
}
