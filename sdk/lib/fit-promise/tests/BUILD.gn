# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":fit-promise-test-pkg",
    ":fit-promise-tests($host_toolchain)",
  ]
}

# TODO: Convert these to zxtest and move them into the test above.
test("fit-promise-tests") {
  sources = [
    "barrier_tests.cc",
    "bridge_tests.cc",
    "examples/promise_example1.cc",
    "examples/promise_example2.cc",
    "examples/utils.cc",
    "future_tests.cc",
    "pending_task_tests.cc",
    "promise_examples.cc",
    "promise_tests.cc",
    "result_examples.cc",
    "result_tests.cc",
    "scheduler_tests.cc",
    "scope_tests.cc",
    "sequencer_tests.cc",
    "single_threaded_executor_tests.cc",
    "suspended_task_tests.cc",
  ]
  deps = [
    "//sdk/lib/fit-promise",
    "//zircon/system/ulib/zxtest",
  ]
  configs += [ ":shadow" ]
  if (is_fuchsia) {
    deps += [ "//sdk/lib/fdio" ]
  }

  # TODO(fxbug.dev/48668): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

config("shadow") {
  # chromium uses this library, and enables the following flag,
  # so these tests should work with this flag enabled
  cflags_cc = [ "-Wshadow" ]
}

fuchsia_unittest_package("fit-promise-test-pkg") {
  package_name = "fit-promise-tests"
  deps = [ ":fit-promise-tests" ]
}
