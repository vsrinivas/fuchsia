# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/fidl/fidl.gni")

sdk_source_set("cpp") {
  category = "partner"

  sdk_name = "modular_cpp"

  include_base = "//sdk"

  public_configs = [ "//sdk/config" ]

  sources = [
    "agent.cc",
    "agent.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular:fuchsia.modular_hlcpp",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys_hlcpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
  ]
}

fidl("test.modular.agent") {
  sources = [ "agent.test.fidl" ]
}

executable("modular_cpp_tests_bin") {
  testonly = true
  sources = [ "agent_unittest.cc" ]

  deps = [
    ":cpp",
    ":test.modular.agent_hlcpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("modular_cpp_tests") {
  deps = [ ":modular_cpp_tests_bin" ]
  manifest = "meta/modular_cpp_tests_bin.cml"
}
