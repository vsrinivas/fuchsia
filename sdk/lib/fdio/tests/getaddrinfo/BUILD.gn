# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

executable("getaddrinfo_test") {
  testonly = true

  sources = [ "getaddrinfo_test.cc" ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxc",
  ]
}

rustc_binary("lookup_server") {
  edition = "2021"

  sources = [ "src/main.rs" ]

  deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.name:fuchsia.net.name_rust",
    "//src/connectivity/lib/net-declare",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:futures",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_test_component("getaddrinfo_test_component") {
  component_name = "getaddrinfo_test"
  deps = [ ":getaddrinfo_test" ]
  manifest = "meta/getaddrinfo_test.cml"
}

fuchsia_component("lookup_server_component") {
  component_name = "lookup_server"
  deps = [ ":lookup_server" ]
  manifest = "meta/lookup_server.cml"
}

fuchsia_test_package("getaddrinfo_tests") {
  test_components = [ ":getaddrinfo_test_component" ]
  deps = [ ":lookup_server_component" ]
}

group("tests") {
  testonly = true
  deps = [ ":getaddrinfo_tests" ]
}
