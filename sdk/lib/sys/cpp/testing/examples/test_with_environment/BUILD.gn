# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

executable("fake_echo_app") {
  testonly = true
  output_name = "fake_echo_app"

  sources = [
    "fake_echo.cc",
    "fake_echo.h",
  ]

  deps = [
    "//garnet/examples/fidl/services:echo",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("test_with_environment_example_test_bin") {
  testonly = true
  output_name = "test_with_environment_example_test"

  sources = [
    "fake_echo.h",
    "test_with_environment_example_test.cc",
  ]

  deps = [
    "//garnet/examples/fidl/services:echo",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/lib/fxl/test:gtest_main",
  ]
}

test_package("test_with_environment_example_test") {
  deps = [
    ":fake_echo_app",
    ":test_with_environment_example_test_bin",
  ]

  binaries = [
    {
      name = "fake_echo_app"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/fake_echo_app.cmx")
      dest = "fake_echo_app.cmx"
    },
  ]

  tests = [
    {
      name = "test_with_environment_example_test"
      environments = basic_envs
    },
  ]
}
