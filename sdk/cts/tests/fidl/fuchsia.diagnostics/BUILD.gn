# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//sdk/cts/build/cts.gni")

cts_executable("diagnostics-test-bin") {
  output_name = "diagnostics-test"
  testonly = true

  sources = [ "accessor_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.diagnostics",
    "//sdk/lib/fit",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]

  non_cts_deps = [
    "//sdk/fidl/fuchsia.sys2",
    "//sdk/lib/inspect/contrib/cpp:archive_reader",

    # TODO(81398): Include this in the SDK.
    "//sdk/lib/sys/cpp/testing:realm_builder",
    "//third_party/rapidjson",
  ]
}

cts_fuchsia_component("diagnostics-test") {
  manifest = "meta/diagnostics-test.cml"
  testonly = true

  deps = [
    ":diagnostics-test-bin",
    "//sdk/fidl/fuchsia.component",
  ]
}

cts_executable("inspect-publisher-bin") {
  output_name = "inspect-publisher"
  testonly = true

  sources = [ "inspect_publisher.cc" ]

  deps = [
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

cts_fuchsia_component("inspect-publisher") {
  manifest = "meta/inspect-publisher.cml"
  testonly = true
  deps = [ ":inspect-publisher-bin" ]
}

cts_fuchsia_test_package("fuchsia-diagnostics-tests") {
  package_name = "fuchsia-diagnostics-tests"
  test_components = [ ":diagnostics-test" ]
  deps = [ ":inspect-publisher" ]
}

fuchsia_package_archive("fuchsia-diagnostics-tests_archive") {
  testonly = true
  package = ":fuchsia-diagnostics-tests"
}

cts_artifacts("prebuilts") {
  testonly = true
  targets = [ ":fuchsia-diagnostics-tests_archive" ]
}

cts_copy_to_sdk("build") {
  testonly = true
  sources = [ "BUILD.gn" ]
}

group("tests") {
  testonly = true
  deps = [ ":fuchsia-diagnostics-tests" ]
}

sdk_molecule("test_sdks") {
  testonly = true
  deps = [
    ":build_sdk",
    ":diagnostics-test-bin_sdk",
    ":diagnostics-test_sdk",
    ":inspect-publisher-bin_sdk",
    ":inspect-publisher_sdk",
    ":prebuilts",
  ]
}
