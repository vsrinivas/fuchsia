# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//sdk/cts/build/cts.gni")

cts_executable("element-manager-test-bin") {
  output_name = "element-manager-test"
  testonly = true

  sources = [ "element_manager_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.element",
    "//sdk/lib/sys/cpp",
    "//sdk/testing/fidl:fidl_test",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]
}

cts_fuchsia_component("element-manager-test") {
  manifest = "meta/element-manager-test.cmx"
  testonly = true

  deps = [ ":element-manager-test-bin" ]
}

cts_fuchsia_test_package("fuchsia-element-tests") {
  package_name = "fuchsia-element-tests"

  # TODO(fxbug.dev/70941): Remove workaround for teardown race causing unrelated ERROR
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":element-manager-test" ]
}

fuchsia_package_archive("fuchsia-element-tests_archive") {
  testonly = true
  package = ":fuchsia-element-tests"
}

cts_artifacts("prebuilts") {
  testonly = true
  package_archives = [ ":fuchsia-element-tests_archive" ]
}

cts_executable("reference-element-bin") {
  output_name = "reference-element"
  testonly = true

  sources = [ "reference_element.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

cts_fuchsia_component("reference-element") {
  manifest = "meta/reference-element.cml"
  testonly = true
  deps = [ ":reference-element-bin" ]
}

cts_executable("reference-graphical-presenter-bin") {
  output_name = "reference-graphical-presenter"
  testonly = true

  sources = [ "reference_graphical_presenter.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.element",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

cts_fuchsia_component("reference-graphical-presenter") {
  manifest = "meta/reference-graphical-presenter.cml"
  testonly = true
  deps = [ ":reference-graphical-presenter-bin" ]
}

cts_executable("reference-session-bin") {
  output_name = "reference-session"
  testonly = true

  sources = [ "reference_session.cc" ]

  deps = [
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

cts_fuchsia_component("reference-session") {
  manifest = "meta/reference-session.cml"
  testonly = true
  deps = [ ":reference-session-bin" ]
}

cts_fuchsia_package("fuchsia-element-reference") {
  package_name = "fuchsia-element-reference"
  testonly = true
  deps = [
    ":reference-element",
    ":reference-graphical-presenter",
    ":reference-session",
  ]
}

cts_session_config("reference-session-config") {
  config = "reference_session_config.json"
}

cts_copy_to_sdk("build") {
  testonly = true
  sources = [ "BUILD.gn" ]
}

group("tests") {
  testonly = true
  deps = [
    ":fuchsia-element-tests",

    # Required base packages for tests to run in CQ.
    ":fuchsia-element-reference",
    ":reference-session-config",
    "//src/cobalt/bin/testing/mock_cobalt",
    "//src/session/bin/component_manager:component_manager_sfw",
    "//src/session/bin/element_manager",
    "//src/session/bin/session_manager",
  ]
}

sdk_molecule("test_sdks") {
  testonly = true
  deps = [
    ":build_sdk",
    ":element-manager-test-bin_sdk",
    ":element-manager-test_sdk",
    ":reference-element-bin_sdk",
    ":reference-element_sdk",
    ":reference-graphical-presenter-bin_sdk",
    ":reference-graphical-presenter_sdk",
    ":reference-session-bin_sdk",
    ":reference-session-config_sdk",
    ":reference-session_sdk",
  ]
}
