# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//sdk/cts/build/cts.gni")
import("//src/session/build/session_config.gni")
import("//src/sys/build/fuchsia_package.gni")

cts_executable("element-manager-test-bin") {
  output_name = "element-manager-test"
  testonly = true

  sources = [ "element_manager_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.element",
    "//sdk/lib/fidl/cpp/test:fidl_test",
    "//sdk/lib/sys/cpp",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

cts_fuchsia_component("element-manager-test") {
  manifest = "meta/element-manager-test.cmx"
  testonly = true

  deps = [ ":element-manager-test-bin" ]
}

cts_fuchsia_test_package("fuchsia-element-tests") {
  test_components = [ ":element-manager-test" ]
}

executable("reference-element-bin") {
  output_name = "reference-element"
  testonly = true

  sources = [ "reference_element.cc" ]

  deps = [
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("reference-element") {
  manifest = "meta/reference-element.cml"
  testonly = true
  deps = [ ":reference-element-bin" ]
}

executable("reference-session-bin") {
  output_name = "reference-session"
  testonly = true

  sources = [ "reference_session.cc" ]

  deps = [
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("reference-session") {
  manifest = "meta/reference-session.cml"
  testonly = true
  deps = [ ":reference-session-bin" ]
}

fuchsia_package("fuchsia-element-reference") {
  testonly = true
  deps = [
    ":reference-element",
    ":reference-session",
  ]
}

session_config("reference-session-config") {
  config = "reference_session_config.json"
}

group("tests") {
  testonly = true
  deps = [
    ":fuchsia-element-tests",

    # Required base packages for tests to run in CQ.
    ":fuchsia-element-reference",
    ":reference-session-config",
    "//src/cobalt/bin/testing/mock_cobalt",
    "//src/session/bin/component_manager:component_manager_sfw",
    "//src/session/bin/element_manager",
    "//src/session/bin/session_manager",
  ]
}
