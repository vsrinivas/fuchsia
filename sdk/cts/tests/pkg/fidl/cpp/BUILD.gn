# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//sdk/cts/build/cts.gni")
import("//src/tests/fidl/conformance_suite/gidl-conformance-suite.gni")
import("//tools/fidl/gidl/gidl.gni")

gidl_hlcpp_conformance("conformance_test_sources") {
  testonly = true
  inputs = conformance_suite_gidl_files
  fidl = conformance_suite_fidl_target
  deps = [
    ":test_util",
    "//zircon/system/ulib/zxtest",
  ]
  if (is_fuchsia) {
    deps += [ ":handle_util" ]
  }
}

cts_source_set("host_test_sources") {
  testonly = true

  sources = [
    "array_unittest.cc",
    "bits_unittest.cc",
    "bitset_unittest.cc",
    "clone_unittest.cc",
    "comparison_unittest.cc",
    "defaults_unittest.cc",
    "enum_unittest.cc",
    "fidl_test.cc",
    "object_coding_unittest.cc",
    "roundtrip_test.cc",
    "string_unittest.cc",
    "table_unittest.cc",
    "vector_unittest.cc",
    "xunion_unittest.cc",
  ]

  deps = [
    ":test_util",
    "//sdk/lib/fidl/cpp:cpp_base",
    "//sdk/lib/stdcompat",
    "//sdk/testing/fidl:fidl_test",
    "//zircon/system/ulib/zxtest",
  ]
}

cts_source_set("test_sources") {
  testonly = true

  # TODO(jamesr): Make this an sdk source set
  # sdk_name = "fidl_cpp_tests"
  # include_base = "//sdk"

  sources = [
    "binding_set_unittest.cc",
    "binding_unittest.cc",
    "collision_unittest.cc",
    "decode_handle_rights_test.cc",
    "encode_decode_unittest.cc",
    "event_sender_unittest.cc",
    "interface_handle_unittest.cc",
    "interface_ptr_set_unittest.cc",
    "interface_ptr_unittest.cc",
    "interface_request_unittest.cc",
    "message_reader_unittest.cc",
    "message_unittest.cc",
    "proxy_controller_unittest.cc",
    "stub_controller_unittest.cc",
    "synchronous_interface_ptr_unittest.cc",
    "thread_safe_binding_set_unittest.cc",
    "wire_format_v2_decode_test.cc",
  ]

  deps = [
    ":test_util",
    ":union_migration_test_fidl",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/fit-promise",
    "//sdk/testing/fidl:collision_test",
    "//sdk/testing/fidl:fidl_test",
    "//sdk/testing/fidl:fidl_test_handles",
    "//sdk/testing/fidl:test",
    "//sdk/testing/fidl:test_support",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]

  public_configs = [ "//sdk/config" ]
}

cts_executable("fidl_hlcpp_conformance_tests_bin") {
  visibility = [ ":*" ]
  testonly = true

  output_name = "fidl_hlcpp_conformance_tests"

  deps = [
    "//sdk/testing/fidl:test",
    "//sdk/testing/fidl:test_support",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(75000): Add GIDL to the CTS archive.
  non_cts_deps = [ ":conformance_test_sources" ]
}

cts_fuchsia_component("fidl_hlcpp_conformance_tests") {
  manifest = "meta/fidl_hlcpp_conformance_tests.cml"
  testonly = true
  visibility = [ ":*" ]
  deps = [ ":fidl_hlcpp_conformance_tests_bin" ]
}

cts_executable("fidl_hlcpp_unit_tests_bin") {
  visibility = [ ":*" ]
  testonly = true

  output_name = "fidl_hlcpp_unit_tests"

  deps = [
    ":host_test_sources",
    ":test_sources",
    "//sdk/testing/fidl:test",
    "//sdk/testing/fidl:test_support",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]
}

cts_fuchsia_component("fidl_hlcpp_unit_tests") {
  manifest = "meta/fidl_hlcpp_unit_tests.cml"
  testonly = true
  visibility = [ ":*" ]
  deps = [ ":fidl_hlcpp_unit_tests_bin" ]
}

cts_fuchsia_test_package("fidl_hlcpp_tests") {
  package_name = "fidl_hlcpp_tests"
  test_components = [
    ":fidl_hlcpp_conformance_tests",
    ":fidl_hlcpp_unit_tests",
  ]
}

if (is_host) {
  # TODO(johnshamoon): Add cts_test() and do this in the template.
  current_dir = get_label_info(":fidl_hlcpp_unit_tests_host", "dir")
  is_prebuilt_cts =
      string_replace(current_dir, "//prebuilt/cts", "") != current_dir

  test("fidl_hlcpp_unit_tests_host") {
    output_name = "fidl_hlcpp_unit_tests"
    if (is_prebuilt_cts) {
      output_dir = "${root_out_dir}/host_${current_cpu}/prebuilt/cts"
    }
    deps = [
      ":host_test_sources",
      "//zircon/system/ulib/zxtest",
    ]
  }

  test("fidl_hlcpp_conformance_tests_host") {
    output_name = "fidl_hlcpp_conformance_tests"
    if (is_prebuilt_cts) {
      output_dir = "${root_out_dir}/host_${current_cpu}/prebuilt/cts"
    }
    deps = [
      ":conformance_test_sources",
      "//zircon/system/ulib/zxtest",
    ]
  }
}

cts_fidl("union_migration_test_fidl") {
  name = "fidl.test.unionmigration"

  sources = [ "union_migration.test.fidl" ]
}

cts_source_set("test_util") {
  testonly = true

  sources = [ "test/test_util.h" ]

  public_deps = [ "//sdk/lib/fidl/cpp:cpp_base" ]
}

cts_source_set("handle_util") {
  testonly = true

  sources = [
    "test/handle_util.cc",
    "test/handle_util.h",
  ]

  public_deps = [ "//zircon/system/ulib/zx" ]
}

cts_copy_to_sdk("build") {
  testonly = true
  sources = [ "BUILD.gn" ]
}

group("tests") {
  testonly = true
  deps = [ ":fidl_hlcpp_tests" ]

  current_dir = get_label_info(":${target_name}", "dir")
  is_prebuilt_cts =
      string_replace(current_dir, "//prebuilt/cts", "") != current_dir
  if (!is_prebuilt_cts) {
    deps += [
      ":fidl_hlcpp_conformance_tests_host($host_toolchain)",
      ":fidl_hlcpp_unit_tests_host($host_toolchain)",
    ]
  }
}

sdk_molecule("test_sdks") {
  testonly = true
  deps = [
    ":build_sdk",
    ":fidl_hlcpp_conformance_tests_bin_sdk",
    ":fidl_hlcpp_conformance_tests_sdk",
    ":fidl_hlcpp_unit_tests_bin_sdk",
    ":fidl_hlcpp_unit_tests_sdk",
    ":handle_util_sdk",
    ":host_test_sources_sdk",
    ":test_sources_sdk",
    ":test_util_sdk",
  ]
}
