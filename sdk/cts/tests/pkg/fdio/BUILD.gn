# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//sdk/cts/build/cts.gni")

shebang = "#!resolve fuchsia-pkg://fuchsia.com/fdio-spawn-tests"
if (defined(cts_version) && cts_version != "") {
  shebang += "_${cts_version}"
}

cts_executable("fdio-spawn-tests-bin") {
  output_name = "fdio-spawn-tests-bin"
  testonly = true
  visibility = [ ":*" ]

  sources = [
    "resolve_test.cc",
    "shebang_test.cc",
    "spawn_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    ":fake-launcher-util-bin",
    "//sdk/fidl/fuchsia.io",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/zxtest",
  ]
}

cts_fuchsia_component("fdio-spawn-tests-component") {
  testonly = true
  manifest = "meta/fdio-spawn-tests-component.cmx"
  deps = [ ":fdio-spawn-tests-bin" ]
}

# A test utility used to test various features of fdio_spawn, extensively in spawn_test.cc.
cts_executable("spawn-child-util-bin") {
  testonly = true
  visibility = [ ":*" ]

  output_name = "spawn_child_util"
  sources = [ "spawn_child_util.cc" ]
  deps = [ "//sdk/lib/fdio" ]
}

cts_fuchsia_component("spawn-child-util-component") {
  testonly = true
  manifest = "meta/spawn-child-util-component.cmx"
  deps = [ ":spawn-child-util-bin" ]
}

# A test utility that attempts to use zx_process_create (mimicking a launcher implementation).
cts_executable("fake-launcher-util-bin") {
  testonly = true
  visibility = [ ":*" ]

  output_name = "fake_launcher_util"
  sources = [
    "fake_launcher_util.cc",
    "fake_launcher_util.h",
  ]
  deps = [ "//sdk/lib/fdio" ]
}

cts_fuchsia_component("fake-launcher-util-component") {
  testonly = true
  manifest = "meta/fake-launcher-util-component.cmx"
  deps = [ ":fake-launcher-util-bin" ]
}

# A simple test utility that returns the integer value provided to it in its first argument.
cts_executable("return-arg-test-util-bin") {
  testonly = true
  output_name = "return_arg_test_util"
  sources = [ "return_arg_util.cc" ]
}

cts_fuchsia_component("return-arg-test-util-component") {
  testonly = true
  manifest = "meta/return-arg-test-util-component.cmx"
  deps = [ ":return-arg-test-util-bin" ]
}

# A simple program that prints the arguments with newline delimiter.
cts_executable("echo-arguments-bin") {
  output_name = "echo_arguments_bin"
  sources = [ "echo_arguments.cc" ]
}

cts_fuchsia_component("echo-arguments-bin-component") {
  testonly = true
  manifest = "meta/echo-arguments-bin-component.cmx"
  deps = [ ":echo-arguments-bin" ]
}

generated_file("resolve-once-file") {
  outputs = [ "${target_gen_dir}/test_bins/resolve_once" ]
  contents = "${shebang}#bin/return_arg_test_util"
}

# The 'binaries' in the components below are really just text files with either
# a "#!resolve" or "#!" directive.
cts_resource("resolve-once-bin") {
  sources = get_target_outputs(":resolve-once-file")
  outputs = [ "bin/resolve_once" ]
  deps = [ ":resolve-once-file" ]
}

cts_fuchsia_component("resolve-once-component") {
  testonly = true
  manifest = "meta/resolve-once-component.cmx"
  deps = [ ":resolve-once-bin" ]
}

generated_file("resolve-twice-file") {
  outputs = [ "${target_gen_dir}/test_bins/resolve_twice" ]
  contents = "${shebang}#bin/resolve_once"
}

cts_resource("resolve-twice-bin") {
  sources = get_target_outputs(":resolve-twice-file")
  outputs = [ "bin/resolve_twice" ]
  deps = [ ":resolve-twice-file" ]
}

cts_fuchsia_component("resolve-twice-component") {
  testonly = true
  manifest = "meta/resolve-twice-component.cmx"
  deps = [ ":resolve-twice-bin" ]
}

generated_file("resolve-infinite-loop-file") {
  outputs = [ "${target_gen_dir}/test_bins/resolve_infinite_loop" ]
  contents = "${shebang}#bin/resolve_infinite_loop"
}

cts_resource("resolve-infinite-loop-bin") {
  sources = get_target_outputs(":resolve-infinite-loop-file")
  outputs = [ "bin/resolve_infinite_loop" ]
  deps = [ ":resolve-infinite-loop-file" ]
}

cts_fuchsia_component("resolve-infinite-loop-component") {
  testonly = true
  manifest = "meta/resolve-infinite-loop-component.cmx"
  deps = [ ":resolve-infinite-loop-bin" ]
}

cts_resource("resolve-to-not-found-bin") {
  sources = [ "test_bins/resolve_to_not_found" ]
  outputs = [ "bin/resolve_to_not_found" ]
}

cts_fuchsia_component("resolve-to-not-found-component") {
  testonly = true
  manifest = "meta/resolve-to-not-found-component.cmx"
  deps = [ ":resolve-to-not-found-bin" ]
}

cts_resource("shebang-echo-arguments-bin") {
  sources = [ "test_bins/shebang_echo_arguments" ]
  outputs = [ "bin/shebang_echo_arguments" ]
}

cts_fuchsia_component("shebang-echo-arguments-component") {
  testonly = true
  manifest = "meta/shebang-echo-arguments-component.cmx"
  deps = [ ":shebang-echo-arguments-bin" ]
}

cts_resource("use-script-as-interpreter-bin") {
  sources = [ "test_bins/use_script_as_interpreter" ]
  outputs = [ "bin/use_script_as_interpreter" ]
}

cts_fuchsia_component("use-script-as-interpreter-component") {
  testonly = true
  manifest = "meta/use-script-as-interpreter-component.cmx"
  deps = [ ":use-script-as-interpreter-bin" ]
}

cts_resource("shebang-infinite-loop-bin") {
  sources = [ "test_bins/shebang_infinite_loop" ]
  outputs = [ "bin/shebang_infinite_loop" ]
}

cts_fuchsia_component("shebang-infinite-loop-component") {
  testonly = true
  manifest = "meta/shebang-infinite-loop-component.cmx"
  deps = [ ":shebang-infinite-loop-bin" ]
}

cts_resource("attempt-use-shell-outside-package-script") {
  sources = [ "test_bins/attempt_use_shell_outside_package.sh" ]
  outputs = [ "bin/attempt_use_shell_outside_package.sh" ]
}

cts_fuchsia_component("attempt-use-shell-outside-package-component") {
  testonly = true
  manifest = "meta/attempt-use-shell-outside-package-component.cmx"
  deps = [ ":attempt-use-shell-outside-package-script" ]
}

cts_resource("too-long-shebang-bin") {
  sources = [ "test_bins/too_long_shebang" ]
  outputs = [ "bin/too_long_shebang" ]
}

cts_fuchsia_component("too-long-shebang-component") {
  testonly = true
  manifest = "meta/too-long-shebang-component.cmx"
  deps = [ ":too-long-shebang-bin" ]
}

cts_resource("use-resolve-from-shebang-bin") {
  sources = [ "test_bins/use_resolve_from_shebang" ]
  outputs = [ "bin/use_resolve_from_shebang" ]
}

cts_fuchsia_component("use-resolve-from-shebang-component") {
  testonly = true
  manifest = "meta/use-resolve-from-shebang-component.cmx"
  deps = [ ":use-resolve-from-shebang-bin" ]
}

generated_file("use-shebang-from-resolve-file") {
  outputs = [ "${target_gen_dir}/test_bins/use-shebang-from-resolve" ]
  contents = "${shebang}#bin/shebang_echo_arguments"
}

cts_resource("use-shebang-from-resolve-bin") {
  sources = get_target_outputs(":use-shebang-from-resolve-file")
  outputs = [ "bin/use_shebang_from_resolve" ]
  deps = [ ":use-shebang-from-resolve-file" ]
}

cts_fuchsia_component("use-shebang-from-resolve-component") {
  testonly = true
  manifest = "meta/use-shebang-from-resolve-component.cmx"
  deps = [ ":use-shebang-from-resolve-bin" ]
}

cts_fuchsia_test_package("fdio-spawn-tests") {
  package_name = "fdio-spawn-tests"
  test_components = [ ":fdio-spawn-tests-component" ]

  deps = [
    ":attempt-use-shell-outside-package-component",
    ":echo-arguments-bin-component",
    ":fake-launcher-util-component",
    ":resolve-infinite-loop-component",
    ":resolve-once-component",
    ":resolve-to-not-found-component",
    ":resolve-twice-component",
    ":return-arg-test-util-component",
    ":shebang-echo-arguments-component",
    ":shebang-infinite-loop-component",
    ":spawn-child-util-component",
    ":too-long-shebang-component",
    ":use-resolve-from-shebang-component",
    ":use-script-as-interpreter-component",
    ":use-shebang-from-resolve-component",
  ]
}

fuchsia_package_archive("fdio-spawn-tests_archive") {
  testonly = true
  package = ":fdio-spawn-tests"
}

cts_artifacts("prebuilts") {
  testonly = true
  targets = [ ":fdio-spawn-tests_archive" ]
}

group("tests") {
  testonly = true
  deps = [ ":fdio-spawn-tests" ]
}

cts_copy_to_sdk("build") {
  testonly = true
  sources = [ "BUILD.gn" ]
}

sdk_molecule("test_sdks") {
  testonly = true
  deps = [
    ":attempt-use-shell-outside-package-component_sdk",
    ":attempt-use-shell-outside-package-script_sdk",
    ":build_sdk",
    ":echo-arguments-bin-component_sdk",
    ":echo-arguments-bin_sdk",
    ":fake-launcher-util-bin_sdk",
    ":fake-launcher-util-component_sdk",
    ":fdio-spawn-tests-bin_sdk",
    ":fdio-spawn-tests-component_sdk",
    ":prebuilts",
    ":resolve-infinite-loop-bin_sdk",
    ":resolve-infinite-loop-component_sdk",
    ":resolve-once-bin_sdk",
    ":resolve-once-component_sdk",
    ":resolve-to-not-found-bin_sdk",
    ":resolve-to-not-found-component_sdk",
    ":resolve-twice-bin_sdk",
    ":resolve-twice-component_sdk",
    ":return-arg-test-util-bin_sdk",
    ":return-arg-test-util-component_sdk",
    ":shebang-echo-arguments-bin_sdk",
    ":shebang-echo-arguments-component_sdk",
    ":shebang-infinite-loop-bin_sdk",
    ":shebang-infinite-loop-component_sdk",
    ":spawn-child-util-bin_sdk",
    ":spawn-child-util-component_sdk",
    ":too-long-shebang-bin_sdk",
    ":too-long-shebang-component_sdk",
    ":use-resolve-from-shebang-bin_sdk",
    ":use-resolve-from-shebang-component_sdk",
    ":use-script-as-interpreter-bin_sdk",
    ":use-script-as-interpreter-component_sdk",
    ":use-shebang-from-resolve-bin_sdk",
    ":use-shebang-from-resolve-component_sdk",
  ]
}
