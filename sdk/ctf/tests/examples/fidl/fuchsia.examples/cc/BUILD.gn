# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# [START build]

import("//sdk/ctf/build/ctf.gni")

group("tests") {
  testonly = true
  deps = [ ":fuchsia.examples.echo_test" ]
}

ctf_executable("test_main") {
  output_name = "fuchsia.examples.echo_test_main"
  sources = [ "main.cc" ]
  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_cpp",
    "//sdk/lib/sys/component/cpp:cpp",
    "//sdk/lib/syslog/cpp:cpp-macros",
    "//zircon/system/ulib/zxtest",
  ]
  testonly = true
}

ctf_fuchsia_component("fuchsia.examples.echo_test_component") {
  component_name = "fuchsia.examples.echo_test"
  manifest = "meta/fuchsia.examples.echo_test.cml"
  deps = [ ":test_main" ]
  testonly = true
}

ctf_fuchsia_test_package("fuchsia.examples.echo_test") {
  enable_ctf_test_realms = true
  package_name = "fuchsia.examples.echo_test"
  test_components = [ ":fuchsia.examples.echo_test_component" ]
}
# [END build]
