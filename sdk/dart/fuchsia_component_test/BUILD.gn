# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dart/dart_library.gni")
import("//build/dart/dart_test_component.gni")

group("dart_realm_builder_deps") {
  testonly = true
  deps = [
    ":includes",
    "//src/lib/fuchsia-component-test/realm_builder_server:realm_builder_server_component",
  ]
  public_deps = [ ":fuchsia_component_test" ]
}

dart_library("fuchsia_component_test") {
  package_name = "fuchsia_component_test"
  null_safe = true

  sdk_category = "partner"

  sources = [
    "realm_builder.dart",
    "src/error.dart",
    "src/internal/local_component.dart",
    "src/internal/local_component_runner.dart",
    "src/local_component_handles.dart",
    "src/realm_builder.dart",
  ]

  deps = [
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/fuchsia_services",
    "//sdk/dart/fuchsia_vfs",
    "//sdk/dart/zircon",
    "//sdk/fidl/fuchsia.component:fuchsia.component_dart",
    "//sdk/fidl/fuchsia.component.config:fuchsia.component.config_dart",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_dart",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_dart",
    "//sdk/fidl/fuchsia.component.test:fuchsia.component.test_dart",
    "//sdk/fidl/fuchsia.data:fuchsia.data_dart",
    "//sdk/fidl/fuchsia.diagnostics.types:fuchsia.diagnostics.types_dart",
    "//sdk/fidl/fuchsia.io:fuchsia.io_dart",
    "//third_party/dart-pkg/pub/collection",
  ]
}

expect_includes("includes") {
  includes = [ "//sdk/lib/sys/component/realm_builder.shard.cml" ]
}

# Run these tests using:
#   fx set ... --with //sdk/dart/fuchsia_component_test:tests
#   fx test dart_realm_builder_unittests
dart_test_component("fuchsia-component-test-test") {
  manifest = "meta/fuchsia-component-test-test.cml"

  null_safe = true

  sources = [ "fuchsia_component_test_test.dart" ]

  deps = [
    "//examples/components/routing/fidl:echo_dart",
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia",
    "//sdk/dart/fuchsia_component_test:dart_realm_builder_deps",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/fuchsia_services",
    "//sdk/dart/zircon",
    "//sdk/fidl/fuchsia.component:fuchsia.component_dart",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_dart",
    "//sdk/fidl/fuchsia.component.test:fuchsia.component.test_dart",
    "//sdk/fidl/fuchsia.intl:fuchsia.intl_dart",
    "//sdk/fidl/fuchsia.io:fuchsia.io_dart",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_dart",
    "//third_party/dart-pkg/pub/mockito",
    "//third_party/dart-pkg/pub/test",
  ]
}

fuchsia_component("echo_client_with_binder_component") {
  component_name = "echo_client_with_binder"
  manifest = "meta/echo_client_with_binder.cml"
  deps = [ "//examples/components/routing/rust/echo_client" ]
}

fuchsia_test_package("dart_realm_builder_unittests") {
  test_components = [ ":fuchsia-component-test-test" ]
  deps = [
    ":echo_client_with_binder_component",
    "//examples/components/realm_builder:echo_client_component",
    "//examples/components/realm_builder:echo_server_component",
    "//examples/components/realm_builder:echo_server_legacy_component",
    "//src/lib/fuchsia-component-test/tests/echo_client_sc",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":dart_realm_builder_unittests",
    dart_default_build_cfg.runner_dep,
  ]
}
