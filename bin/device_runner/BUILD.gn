# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//peridot/build/executable_package.gni")
import("//peridot/build/tests_package.gni")
import("//third_party/flatbuffers/flatbuffer.gni")

flatbuffer("users") {
  sources = [
    "users.fbs",
  ]
}

executable("device_runner_bin") {
  output_name = "device_runner_bin"

  sources = [
    "device_runner.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/fidl/fuchsia.sys",
    "//garnet/public/lib/fxl",
    "//garnet/public/fidl/fuchsia.ui.policy",
    "//garnet/public/fidl/fuchsia.ui.viewsv1",
    "//peridot/bin/device_runner/cobalt",
    "//peridot/lib/common:async_holder",
    "//peridot/lib/common:teardown",
    "//peridot/lib/fidl:app_client",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/fidl:clone",
    "//peridot/lib/fidl:scope",
    "//peridot/lib/ledger_client:constants",
    "//peridot/lib/util:filesystem",
    "//peridot/public/fidl/fuchsia.modular",
    "//peridot/public/fidl/fuchsia.modular.auth",
    "//peridot/public/fidl/fuchsia.modular.internal",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/trace-provider",
  ]
}

package("device_runner") {
  deps = [
    ":device_runner_bin",
    "//peridot/bin/device_runner/cobalt:generate_device_runner_config",
  ]

  meta = [
    {
      path = "meta/device_runner.cmx"
      dest = "device_runner.cmx"
    },
  ]

  resources = [
    {
      path = rebase_path(
              get_label_info(
                             "//peridot/bin/device_runner/cobalt:generate_device_runner_config",
                             "target_gen_dir")
              + "/cobalt_config.binproto")
      dest = "cobalt_config.binproto"
    },
  ]

  binary = "device_runner_bin"
}

tests_package("device_runner_unittests") {
  deps = [
    ":user_controller_impl_unittest",
  ]
}

source_set("lib") {
  sources = [
    "user_controller_impl.cc",
    "user_controller_impl.h",
    "user_provider_impl.cc",
    "user_provider_impl.h",
  ]

  deps = [
    ":users",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/fidl/fuchsia.sys",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/fidl/fuchsia.ui.policy",
    "//garnet/public/fidl/fuchsia.ui.viewsv1",
    "//peridot/lib/common:async_holder",
    "//peridot/lib/common:names",
    "//peridot/lib/common:teardown",
    "//peridot/lib/common:xdr",
    "//peridot/lib/fidl:app_client",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/fidl:clone",
    "//peridot/lib/fidl:json_xdr",
    "//peridot/lib/fidl:scope",
    "//peridot/lib/util:filesystem",
    "//peridot/public/fidl/fuchsia.modular",
    "//peridot/public/fidl/fuchsia.modular.auth",
    "//peridot/public/fidl/fuchsia.modular.internal",
    "//zircon/public/lib/trace",
  ]
}

executable_package("dev_device_shell") {
  sources = [
    "dev_device_shell.cc",
  ]

  deps = [
    "//garnet/public/lib/callback",
    "//garnet/public/lib/fxl",
    "//garnet/public/fidl/fuchsia.ui.viewsv1",
    "//peridot/lib/fidl:single_service_app",
    "//peridot/lib/testing",
    "//peridot/public/fidl/fuchsia.modular",
    "//peridot/public/lib/app_driver/cpp",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

executable("user_controller_impl_unittest") {
  testonly = true

  sources = [
    "user_controller_impl_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/component/cpp/testing:fake_launcher",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/gtest",
    "//peridot/lib/fidl:clone",
    "//third_party/googletest:gtest_main",
  ]
}
