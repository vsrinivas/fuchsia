# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//packages/package.gni")

import("//third_party/flatbuffers/flatbuffer.gni")

package("device_runner") {
  system_image = true

  deps = [
    ":device_runner_bin",
  ]

  binaries = [
    {
      name = "device_runner"
    },
  ]
}

package("dev_device_shell") {
  system_image = true

  deps = [
    ":dev_device_shell_bin",
  ]

  binaries = [
    {
      name = "dev_device_shell"
    },
  ]
}

flatbuffer("users") {
  sources = [
    "users.fbs",
  ]
}

executable("device_runner_bin") {
  output_name = "device_runner"

  sources = [
    "device_runner.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/app/fidl",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/ui/presentation/fidl",
    "//garnet/public/lib/ui/views/fidl",
    "//peridot/lib/common:async_holder",
    "//peridot/lib/common:teardown",
    "//peridot/lib/fidl:app_client",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/fidl:scope",
    "//peridot/lib/ledger_client:constants",
    "//peridot/lib/util:filesystem",
    "//peridot/public/lib/auth/fidl",
    "//peridot/public/lib/config/fidl",
    "//peridot/public/lib/device/fidl",
    "//peridot/public/lib/user/fidl:private",
    "//zircon/system/ulib/trace-provider",
  ]
}

source_set("lib") {
  sources = [
    "user_controller_impl.cc",
    "user_controller_impl.h",
    "user_provider_impl.cc",
    "user_provider_impl.h",
  ]

  deps = [
    ":users",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/app/fidl",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/ui/presentation/fidl",
    "//garnet/public/lib/ui/views/fidl",
    "//peridot/lib/common:async_holder",
    "//peridot/lib/common:xdr",
    "//peridot/lib/common:teardown",
    "//peridot/lib/fidl:app_client",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/fidl:json_xdr",
    "//peridot/lib/fidl:scope",
    "//peridot/lib/util:filesystem",
    "//peridot/public/lib/auth/fidl",
    "//peridot/public/lib/config/fidl",
    "//peridot/public/lib/device/fidl",
    "//peridot/public/lib/user/fidl:private",
    "//zircon/system/ulib/trace",
  ]
}

executable("dev_device_shell_bin") {
  output_name = "dev_device_shell"

  sources = [
    "dev_device_shell.cc",
  ]

  deps = [
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/ui/views/fidl",
    "//peridot/lib/fidl:single_service_app",
    "//peridot/lib/testing",
    "//peridot/public/lib/app_driver/cpp",
    "//peridot/public/lib/device/fidl",
  ]
}

source_set("unittests") {
  testonly = true

  sources = [
    "user_controller_impl_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//peridot/lib/testing:fake_application_launcher",
    "//peridot/lib/testing:test_with_message_loop",
    "//third_party/gtest",
  ]
}
