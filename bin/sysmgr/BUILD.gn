# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  sources = [
    "app.cc",
    "app.h",
    "config.cc",
    "config.h",
    "delegating_loader.cc",
    "delegating_loader.h",
  ]

  public_deps = [
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fidl/cpp",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/svc/cpp",
    "//third_party/rapidjson",
  ]
}

executable("bin") {
  output_name = "sysmgr"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/fxl",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

package("sysmgr") {
  deps = [
    ":bin",
  ]

  binary = "sysmgr"

  meta = [
    {
      path = rebase_path("meta/sysmgr.cmx")
      dest = "sysmgr.cmx"
    },
  ]
}

template("config_package") {
  package(target_name) {
    deprecated_system_image = true
    resources = [
      {
        dest = "sysmgr/${invoker.name}"
        path = rebase_path("config/${invoker.name}")
      },
    ]
  }
}

config_package("network_config") {
  name = "network.config"
}

config_package("services_config") {
  name = "services.config"
}
