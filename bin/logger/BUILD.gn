# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rust_binary.gni")
import("//build/package.gni")

rust_binary("bin") {
  with_tests = true

  name = "logger"

  deps = [
    "//garnet/public/rust/fidl_crates/garnet_public_lib_app_fidl",
    "//garnet/public/rust/fidl_crates/garnet_public_lib_logger_fidl",
  ]
}

executable("logger_integration_tests") {
  testonly = true

  sources = [
    "test/logger_tests.cc",
    "test/run_all_tests.cc",
  ]
  deps = [
    "//third_party/gtest",
    "//zircon/public/lib/syslog",
  ]
}

package("logger") {
  deps = [
    ":bin",
  ]

  binary = "logger"

  meta = [{
    path = rebase_path("//garnet/bin/appmgr/legacy_flat_exported_dir")
    dest = "legacy_flat_exported_dir"
  }]
}

package("logger_tests") {
  system_image = true
  testonly = true

  deps = [
    ":bin",
    ":logger_integration_tests",
  ]

  tests = [
    {
      name = "logger-bin-test"
    },
    {
      name = "logger_integration_tests"
    },
  ]
}
