# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//garnet/public/build/test.gni")
import("//packages/package.gni")

package("net_media_service") {
  system_image = true

  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "net_media_service"
    },
  ]
}

executable("bin") {
  output_name = "net_media_service"

  sources = [
    "main.cc",
    "net_media_player_impl.cc",
    "net_media_player_impl.h",
    "net_media_player_messages.cc",
    "net_media_player_messages.h",
    "net_media_player_net_proxy.cc",
    "net_media_player_net_proxy.h",
    "net_media_player_net_stub.cc",
    "net_media_player_net_stub.h",
    "net_media_service_impl.cc",
    "net_media_service_impl.h",
  ]

  deps = [
    ":serialization",
    "//garnet/bin/media/media_service",
    "//garnet/bin/media/util",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fidl/cpp/bindings",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/media/fidl",
    "//garnet/public/lib/media/timeline",
    "//garnet/public/lib/netconnector/cpp",
    "//garnet/public/lib/netconnector/fidl",
    "//garnet/public/lib/network/fidl",
    "//garnet/public/lib/url",
  ]
}

source_set("serialization") {
  sources = [
    "serialization.cc",
    "serialization.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
  ]
}

test("tests") {
  output_name = "net_media_service_tests"

  testonly = true

  sources = [
    "test/serialization_test.cc",
  ]

  deps = [
    ":serialization",
  ]
}
