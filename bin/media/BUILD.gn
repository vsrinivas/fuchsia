# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

group("services") {
  public_deps = [
    "//garnet/bin/media/codec_factory",
    "//garnet/bin/media/media_player",
    "//garnet/bin/media/net_media_service",
  ]
}

group("tools") {
  public_deps = [
    "//garnet/bin/media/signal_generator",
    "//garnet/bin/media/vol",
  ]
}

package("audio_server") {
  deps = [
    "//garnet/bin/media/audio_server",
  ]

  binary = "audio_server"

  meta = [
    {
      path = rebase_path("audio_server/meta/audio_server.cmx")
      dest = "audio_server.cmx"
    },
  ]
}

package("codec_factory") {
  deps = [
    "//garnet/bin/media/codec_factory:codec_factory",
  ]

  binary = "codec_factory"

  meta = [
    {
      path = rebase_path("codec_factory/meta/codec_factory.cmx")
      dest = "codec_factory.cmx"
    },
  ]
}

# This package is only really expected to be used by the codec_factory package.
package("codec_runner_sw_omx") {
  deps = [
    "//garnet/bin/media/codecs/sw/omx/codec_runner_sw_omx:codec_runner_sw_omx",

    # Codec loadable_module(s)
    "//garnet/bin/media/codecs/sw/omx/dec/aac:libcodec_sw_omx_dec_aac",
  ]

  binary = "codec_runner_sw_omx"

  loadable_modules = [
    {
      name = "libcodec_sw_omx_dec_aac.so"
    },
  ]
}

package("media_client") {
  deprecated_system_image = true

  deps = [
    "//garnet/public/lib/media/c",
  ]

  libraries = [
    {
      name = "libmedia_client.so"
    },
  ]
}

package("tests") {
  testonly = true
  deprecated_system_image = true

  package_name = "media_tests"

  deps = [
    "//garnet/bin/media/audio_server:tests",
    "//garnet/bin/media/audio_server/mixer:tests",
    "//garnet/bin/media/media_player:tests",
    "//garnet/bin/media/media_player/demux:tests",
    "//garnet/bin/media/media_player/player:tests",
    "//garnet/bin/media/media_player/util:tests",
    "//garnet/bin/media/net_media_service:tests",
    "//garnet/public/lib/media/c:tests",
    "//garnet/public/lib/media/timeline:tests",
    "//garnet/public/lib/media/transport:tests",
  ]

  tests = [
    {
      name = "audio_mixer_tests"
    },

    {
      name = "audio_server_tests"
    },

    {
      name = "media_client_tests"
    },

    {
      name = "media_demux_tests"
    },

    {
      name = "media_lib_timeline_tests"
    },

    {
      name = "media_lib_transport_tests"
    },

    {
      name = "media_player_tests"
    },

    {
      name = "media_util_tests"
    },

    {
      name = "net_media_service_tests"
    },

    {
      name = "player_tests"
    },
  ]
}
