# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//packages/package.gni")

executable("trace_tests_bin") {
  testonly = true

  output_name = "trace_tests"

  deps = [
    "lib/measure:unittests",
    "lib/trace:unittests",
    "src/trace:unittests",
    "//apps/test_runner/src:gtest_main",
  ]
}

package("trace_manager") {
  archive = true

  deps = [
    "src/trace_manager",
  ]

  binaries = [ {
        name = "trace_manager"
        dest = "app"
      } ]

  resources = [ {
        path = rebase_path("src/trace_manager/tracing.config")
        dest = "tracing.config"
      } ]
}

package("ktrace_provider") {
  archive = true

  deps = [
    "src/ktrace_provider",
  ]

  binaries = [ {
        name = "ktrace_provider"
        dest = "app"
      } ]

  meta = [ {
        path = rebase_path("src/ktrace_provider/meta/sandbox")
        dest = "sandbox"
      } ]
}

package("trace_tests") {
  testonly = true

  deps = [
    ":trace_tests_bin",
    "lib/trace:ctrace_stub_tests",
    "lib/trace:trace_stub_tests",
  ]

  tests = [
    {
      name = "trace_tests"
      # Disabled because several tests are crashing on release builds.
      disabled = true
    },
    {
      name = "trace_stub_tests"
    },
    {
      name = "ctrace_stub_tests"
    },
  ]
}

package("trace") {
  deps = [
    "src/trace",
  ]

  binaries = [ {
        name = "trace"
      } ]
}

group("host_utils") {
  deps = [
    "//apps/tracing/src/traceutil($host_toolchain)",
  ]
}
