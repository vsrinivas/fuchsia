# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")

executable("bin") {
  visibility = [ ":*" ]
  output_name = "guest_integration_tests"
  testonly = true

  sources = [
    "main.cc",
    "test_serial.cc",
    "test_serial.h",
  ]

  deps = [
    "//garnet/public/fidl/fuchsia.guest",
    "//garnet/public/lib/component/cpp/testing",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/svc/cpp",
    "//zircon/public/fidl/zircon-sysinfo:zircon-sysinfo_c",
    "//zircon/public/lib/fbl",
  ]
}

test_package("guest_integration_tests") {
  deps = [
    ":bin",
  ]

  tests = [
    {
      name = "guest_integration_tests"
      
      # There is currently only x64 hardware available for testing.
      if (target_cpu == "x64") {
        environments = [
          {
            device = {
              type = "Intel NUC Kit NUC7i5DNHE"
            }
          },
        ]
      }
    },
  ]
}

executable("virtio_block_test_util") {
  output_name = "virtio_block_test_util"

  deps = [
    "//garnet/public/lib/fxl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
  ]

  sources = [
    "virtio_block_test_util.cc",
  ]
}

package("guest_integration_tests_utils") {
  deprecated_system_image = true
  deps = [
    ":virtio_block_test_util",
  ]

  binaries = [
    {
      name = "virtio_block_test_util"
    },
  ]
}
