# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "guest_config.cc",
    "guest_config.h",
    "guest_view.cc",
    "guest_view.h",
    "kernel.cc",
    "kernel.h",
    "linux.cc",
    "linux.h",
    "zircon.cc",
    "zircon.h",
  ]

  public_deps = [
    "//garnet/bin/guest/third_party/fdt",
    "//garnet/lib/machina",
    "//garnet/public/lib/fsl",
    "//garnet/public/fidl/fuchsia.guest",
    "//garnet/public/lib/ui/view_framework:view_provider",
    "//third_party/rapidjson",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/trace-provider",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

executable("bin") {
  output_name = "vmm"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

package("vmm") {
  deps = [
    ":bin",
  ]

  binary = "vmm"

  meta = [
    {
      path = rebase_path("//garnet/bin/guest/vmm/meta/vmm.cmx")
      dest = "vmm.cmx"
    },
  ]

  if (target_cpu == "arm64") {
    resources = [
      {
        path = rebase_path("//garnet/lib/machina/arch/arm64/dts/board.dtb")
        dest = "board.dtb"
      },
    ]
  } else if (current_cpu == "x64") {
    resources = [
      {
        path = rebase_path("//garnet/lib/machina/arch/x86/asl/dsdt.aml")
        dest = "dsdt.aml"
      },
      {
        path = rebase_path("//garnet/lib/machina/arch/x86/asl/mcfg.aml")
        dest = "mcfg.aml"
      },
    ]
  }
}

executable("vmm_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "guest_config-unittest.cc",
  ]

  deps = [
    ":lib",
    "//third_party/googletest:gtest_main",
    "//zircon/public/lib/fbl",
  ]
}

package("vmm_tests") {
  testonly = true
  deprecated_system_image = true

  deps = [
    ":vmm_unittests",
  ]

  tests = [
    {
      name = "vmm_unittests"
    },
  ]
}
