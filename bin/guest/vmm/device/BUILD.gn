# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  sources = [
    "device_base.h",
    "stream_base.h",
  ]

  public_deps = [
    "//garnet/lib/machina/device",
    "//garnet/lib/machina/fidl:fuchsia.guest.device",
    "//garnet/public/lib/component/cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/trace-provider",
  ]
}

executable("virtio_balloon_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_balloon"
  sources = [ "virtio_balloon.cc" ]
  deps = [ ":lib" ]
}

package("virtio_balloon") {
  binary = "virtio_balloon"
  deps = [ ":virtio_balloon_bin" ]
}

executable("virtio_console_bin") {
  visibility = [ ":*" ]

  output_name = "virtio_console"
  sources = [ "virtio_console.cc" ]
  deps = [ ":lib" ]
}

package("virtio_console") {
  binary = "virtio_console"
  deps = [ ":virtio_console_bin" ]
}

executable("device_tests") {
  visibility = [ "//garnet/bin/guest/vmm:*" ]
  testonly = true

  sources = [
    "test_with_device.cc",
    "test_with_device.h",
    "virtio_balloon_test.cc",
    "virtio_console_test.cc",
    "virtio_queue_fake.cc",
    "virtio_queue_fake.h",
  ]

  deps = [
    "//garnet/lib/machina/device",
    "//garnet/lib/machina/fidl:fuchsia.guest.device",
    "//garnet/public/lib/component/cpp/testing",
    "//third_party/googletest:gtest_main",
    "//zircon/public/lib/virtio",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}
