# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "environment_controller_impl.cc",
    "environment_controller_impl.h",
    "environment_manager_impl.cc",
    "environment_manager_impl.h",
    "guest_component.cc",
    "guest_component.h",
    "guest_services.cc",
    "guest_services.h",
    "guest_vsock_endpoint.cc",
    "guest_vsock_endpoint.h",
    "host_vsock_endpoint.cc",
    "host_vsock_endpoint.h",
  ]

  public_deps = [
    "//garnet/bin/guest/vmm/fidl:fuchsia.guest.vmm",
    "//garnet/public/fidl/fuchsia.guest",
    "//garnet/public/fidl/fuchsia.sys",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/svc/cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/bitmap",
  ]
}

executable("bin") {
  output_name = "guestmgr"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
  ]
}

package("guestmgr") {
  deps = [
    ":bin",
  ]
  binary = "guestmgr"
  meta = [
    {
      path = rebase_path("meta/guestmgr.cmx")
      dest = "guestmgr.cmx"
    },
  ]
}

executable("guestmgr_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "host_vsock_endpoint_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/gtest",
    "//third_party/googletest:gtest_main",
  ]
}

package("guestmgr_tests") {
  testonly = true

  deps = [
    ":guestmgr_unittests",
  ]

  tests = [
    {
      name = "guestmgr_unittests"
    },
  ]
}
