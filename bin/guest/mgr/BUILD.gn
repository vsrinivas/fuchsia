# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "guest_environment_impl.cc",
    "guest_environment_impl.h",
    "guest_holder.cc",
    "guest_holder.h",
    "guest_manager_impl.cc",
    "guest_manager_impl.h",
    "host_vsock_endpoint.cc",
    "host_vsock_endpoint.h",
    "remote_vsock_endpoint.cc",
    "remote_vsock_endpoint.h",
    "vsock_endpoint.cc",
    "vsock_endpoint.h",
    "vsock_server.cc",
    "vsock_server.h",
  ]

  public_deps = [
    "//garnet/public/lib/component/cpp",
    "//garnet/public/fidl/fuchsia.sys",
    "//garnet/public/lib/fxl",
    "//garnet/public/fidl/fuchsia.guest",
    "//garnet/public/lib/svc/cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/bitmap",
  ]
}

executable("bin") {
  output_name = "guestmgr"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
  ]
}

package("guestmgr") {
  deps = [
    ":bin",
  ]
  binary = "guestmgr"
}

executable("guestmgr_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "vsock_server-unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/gtest",
    "//third_party/googletest:gtest_main",
  ]
}

package("guestmgr_tests") {
  testonly = true

  deps = [
    ":guestmgr_unittests",
  ]

  tests = [
    {
      name = "guestmgr_unittests"
    },
  ]
}
