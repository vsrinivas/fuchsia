# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/bootdata.gni")
import("//build/config/fuchsia/zircon.gni")
import("//garnet/bin/guest/pkg/guest_package.gni")

action("gpt_image") {
  visibility = [ ":*" ]
  gpt_image = "$target_out_dir/zircon.gpt"

  script = "//garnet/bin/guest/pkg/zircon_guest/mkgpt.sh"
  args = [
    "-o",
    rebase_path("$target_out_dir"),
    target_cpu,
  ]

  outputs = [
    gpt_image,
  ]
}

bootdata("guest-bootdata") {
  visibility = [ ":*" ]
  boot = true
  inputs = zircon_boot_manifests
  deps = []
  foreach(label, deps) {
    inputs += get_target_outputs(label)
  }
}

guest_package("zircon_guest") {
  deps = [
    ":gpt_image",
    ":guest-bootdata",
  ]

  cmdline = "TERM=uart console.path=/dev/class/console/virtio-console"
  bootdata_outputs = get_target_outputs(":guest-bootdata")
  ramdisk = bootdata_outputs[0]
  zircon = "$zircon_build_dir/zircon.bin"

  gpt_outputs = get_target_outputs(":gpt_image")
  gpt_filename = "gpt.img"
  resources = [
    {
      path = rebase_path(gpt_outputs[0])
      dest = gpt_filename
    },
  ]
  block_devices = [ "/guest/data/${gpt_filename},ro" ]
}
