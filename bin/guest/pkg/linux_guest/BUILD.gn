# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//garnet/bin/guest/pkg/guest_package.gni")

disk_image = "$target_out_dir/linux/disk.img"
linux_image = "$target_out_dir/linux/Image"

action("block") {
  visibility = [ ":*" ]
  script = "check_image.sh"
  depfile = "$target_out_dir/block.d"
  args = [
    rebase_path("images/${target_cpu}/disk.img", root_build_dir),
    rebase_path(disk_image, root_build_dir),
    rebase_path(depfile, root_build_dir)
  ]
  outputs = [ disk_image ]
}

action("kernel") {
  visibility = [ ":*" ]
  script = "check_image.sh"
  depfile = "$target_out_dir/kernel.d"
  args = [
    rebase_path("images/${target_cpu}/Image", root_build_dir),
    rebase_path(linux_image, root_build_dir),
    rebase_path(depfile, root_build_dir)
  ]
  outputs = [ linux_image ]
}

guest_package("linux_guest") {
  deps = [
    ":block",
    ":kernel",
  ]

  if (target_cpu == "arm64") {
    cmdline = "earlycon=pl011,0x808300000"
  } else if (target_cpu == "x64") {
    cmdline = "earlycon=uart,io,0x3f8"
  }
  cmdline += " console=hvc0 console=tty0 root=/dev/vda rw"

  linux = "$target_out_dir/linux/Image"

  disk_name = "filesystem.img"
  resources = [
    {
      path = rebase_path("$target_out_dir/linux/disk.img")
      dest = "${disk_name}"
    },
  ]
  block_devices = [ "/guest/data/${disk_name},ro,volatile" ]
}
