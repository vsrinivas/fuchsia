# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "efi.cc",
    "guest_config.cc",
    "guest_config.h",
    "guest_view.cc",
    "guest_view.h",
    "linux.cc",
    "zircon.cc",
  ]

  public_deps = [
    "//garnet/lib/machina",
    "//garnet/public/lib/ui/view_framework:view_provider",
    "//third_party/rapidjson",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hypervisor",
    "//zircon/third_party/lib/fdt",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

executable("bin") {
  output_name = "guest"

  sources = [
    "guest.cc",
  ]

  deps = [
    ":lib",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

executable("guest_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "guest_config-unittest.cc",
    "run_all_unittests.cc",
  ]

  deps = [
    ":lib",
    "//third_party/gtest",
    "//zircon/public/lib/fbl",
  ]
}

package("guest_tests") {
  testonly = true
  system_image = true

  deps = [
    ":guest_unittests",
  ]

  tests = [
    {
      name = "guest_unittests"
    },
  ]
}
