# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "kernel.cc",
    "kernel.h",
    "guest_config.cc",
    "guest_config.h",
    "guest_view.cc",
    "guest_view.h",
    "linux.cc",
    "linux.h",
    "zircon.cc",
    "zircon.h",
  ]

  public_deps = [
    "//garnet/bin/guest/third_party/fdt",
    "//garnet/lib/machina",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/ui/view_framework:view_provider",
    "//third_party/rapidjson",
    "//zircon/public/lib/fbl",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

executable("bin") {
  output_name = "guest"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

package("guest") {
  deps = [
    ":bin",
  ]

  binary = "guest"

  meta = [
    {
      path = rebase_path("//garnet/bin/guest/meta/sandbox")
      dest = "sandbox"
    }
  ]

  if (target_cpu == "arm64") {
    resources = [
      {
        path = rebase_path("//garnet/lib/machina/arch/arm64/dts/board.dtb")
        dest = "board.dtb"
      },
    ]
  } else if (current_cpu == "x64") {
    resources = [
      {
        path = rebase_path("//garnet/lib/machina/arch/x86/asl/dsdt.aml")
        dest = "dsdt.aml"
      },
      {
        path = rebase_path("//garnet/lib/machina/arch/x86/asl/mcfg.aml")
        dest = "mcfg.aml"
      },
    ]
  }
}

executable("guest_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "guest_config-unittest.cc",
    "run_all_unittests.cc",
  ]

  deps = [
    ":lib",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
  ]
}

package("guest_tests") {
  testonly = true
  deprecated_system_image = true

  deps = [
    ":guest_unittests",
  ]

  tests = [
    {
      name = "guest_unittests"
    },
  ]
}
