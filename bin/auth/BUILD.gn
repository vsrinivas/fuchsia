# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

package("token_manager") {
  deps = [
    "//garnet/bin/auth/token_manager",
  ]

  binary = "token_manager"

  meta = [
    {
      path = rebase_path("meta/token_manager.cmx")
      dest = "token_manager.cmx"
    },
  ]
}

package("token_manager_rust") {
  deps = [
    "//garnet/bin/auth/rust/token_manager:token_manager_rust",
  ]

  binary = "rust_crates/token_manager_rust"

  meta = [
    {
      path = rebase_path("rust/meta/token_manager.cmx")
      dest = "token_manager.cmx"
    },
  ]
}

package("oauth_examples") {
  testonly = true
  deprecated_system_image = true

  deps = [
    "//garnet/bin/auth/testing/e2e_manual:google_oauth_demo",
  ]

  binaries = [
    {
      name = "google_oauth_demo"
    },
  ]
}

package("rust_tests") {
  testonly = true
  deprecated_system_image = true

  deps = [
    "rust/cache:cache",
    "rust/store:store",
    "rust/testing/dev_auth_provider:dev_auth_provider_rust_bin",
  ]

  tests = [
    { name = "auth_cache_lib_test_rustc" },
    { name = "auth_store_lib_test_rustc" },
    { name = "dev_auth_provider_rust_bin_test_rustc"},
  ]
}

package("tests") {
  testonly = true
  deprecated_system_image = true

  package_name = "token_manager_tests"

  deps = [
    ":token_manager_unittests",
    "//garnet/bin/auth/testing/e2e_local:e2e_dev",
    "//garnet/bin/auth/rust/testing/dev_auth_provider:dev_auth_provider_rust_bin"
  ]

  binaries = [
    {
      name = "dev_auth_provider_rust"
    },
  ]

  tests = [
    {
      name = "token_manager_unittests"
    },
    {
      name = "dev_token_mgr_e2e_test"
    },
  ]
}

executable("token_manager_unittests") {
  testonly = true

  deps = [
    "//garnet/bin/auth/cache:unittests",
    "//garnet/bin/auth/store:unittests",
    "//garnet/public/lib/fxl:fxl_printers",
    "//third_party/googletest:gtest_main",
  ]
}
