# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//apps/mozart/config.gni")

group("scene") {
  deps = [
    ":scene_manager_service",
  ]
}

executable("scene_manager_service") {
  output_name = "scene_manager_service"

  sources = [
    "main.cc",
    "scene_manager_app.cc",
    "scene_manager_app.h",
  ]

  deps = [
    ":common",
  ]
}

config("common_include_dirs") {
  include_dirs = [
    "//lib/escher",
    "//third_party/glm",
    "//lib",
  ]
}

source_set("common") {
  sources = [
    "print_op.cc",
    "print_op.h",
    "renderer/renderer.cc",
    "renderer/renderer.h",
    "resources/gpu_memory.cc",
    "resources/gpu_memory.h",
    "resources/host_memory.cc",
    "resources/host_memory.h",
    "resources/image.cc",
    "resources/image.h",
    "resources/link.cc",
    "resources/link.h",
    "resources/material.cc",
    "resources/material.h",
    "resources/memory.cc",
    "resources/memory.h",
    "resources/nodes/entity_node.cc",
    "resources/nodes/entity_node.h",
    "resources/nodes/node.cc",
    "resources/nodes/node.h",
    "resources/nodes/shape_node.cc",
    "resources/nodes/shape_node.h",
    "resources/nodes/tag_node.cc",
    "resources/nodes/tag_node.h",
    "resources/resource.cc",
    "resources/resource.h",
    "resources/resource_linker.cc",
    "resources/resource_linker.h",
    "resources/resource_map.cc",
    "resources/resource_map.h",
    "resources/resource_type_info.h",
    "resources/resource_visitor.cc",
    "resources/resource_visitor.h",
    "resources/shapes/circle_shape.cc",
    "resources/shapes/circle_shape.h",
    "resources/shapes/shape.cc",
    "resources/shapes/shape.h",
    "scene_manager_impl.cc",
    "scene_manager_impl.h",
    "session/session.cc",
    "session/session.h",
    "session/session_context.h",
    "session/session_handler.cc",
    "session/session_handler.h",
    "util/error_reporter.cc",
    "util/error_reporter.h",
  ]

  public_deps = [
    "//application/lib/app",
    "//apps/mozart/services/geometry",
    "//apps/mozart/services/geometry/cpp",
    "//apps/mozart/services/scene",
    "//apps/tracing/lib/trace:provider",
    "//lib/escher/escher",
    "//lib/fidl/cpp/bindings",
    "//lib/ftl",
    "//lib/mtl",
  ]

  public_configs = [ ":common_include_dirs" ]

  deps = [
    "//magma:vulkan",
  ]
}

source_set("testing_common") {
  testonly = true

  sources = [
    "tests/main.cc",
  ]

  public_deps = [
    ":common",
    "//apps/mozart/lib/scene:session_helpers",
    "//apps/mozart/lib/tests",
    "//third_party/gtest",
  ]
}

executable("apptests") {
  output_name = "scene_manager_apptests"

  testonly = true

  sources = [
    "tests/scene_manager_apptest.cc",
    "tests/scene_manager_test.cc",
    "tests/scene_manager_test.h",
  ]

  deps = [
    ":testing_common",
  ]
}

executable("unittests") {
  output_name = "scene_manager_unittests"

  testonly = true

  sources = [
    "tests/node_unittest.cc",
    "tests/resource_linker_unittest.cc",
    "tests/session_test.cc",
    "tests/session_test.h",
    "tests/session_unittest.cc",
    "tests/shape_unittest.cc",
    "tests/tag_unittest.cc",
  ]

  deps = [
    ":testing_common",
  ]
}

group("tests") {
  testonly = true

  deps = [
    ":apptests",
    ":unittests",
  ]
}
