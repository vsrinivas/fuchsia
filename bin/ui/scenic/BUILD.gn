# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# To build a different Scenic-y executable, cut'n'paste the
# declare_args() and executable("scenic") into a different
# BUILD.gn and then modify the args and the executable name,
# as desired.

declare_args() {
  enable_value_subsystem = false
  enable_scenic_subsystem = true
  enable_views_subsystem = true
  enable_sketchy_subsystem = false
  enable_dummy_subsystem = true
}

executable("scenic") {
  sources = [
    "//garnet/bin/ui/scenic/app.cc",
    "//garnet/bin/ui/scenic/app.h",
    "//garnet/bin/ui/scenic/main.cc",
  ]

  deps = [
    "//garnet/bin/ui/scenic:base_deps",
  ]
  defines = []

  if (enable_scenic_subsystem) {
    defines += [ "SCENIC_ENABLE_GFX_SUBSYSTEM" ]
    deps += [ "//garnet/lib/ui/gfx" ]
  }

  if (enable_views_subsystem) {
    defines += [ "SCENIC_ENABLE_VIEWS_SUBSYSTEM" ]
    deps += [ "//garnet/lib/ui/views" ]
  }

  if (enable_dummy_subsystem) {
    defines += [ "SCENIC_ENABLE_DUMMY_SUBSYSTEM" ]
    deps += [ "//garnet/lib/ui/scenic/tests:dummy_system" ]
  }
}

source_set("base_deps") {
  public_deps = [
    "//garnet/lib/ui/scenic",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fidl/cpp/bindings",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//zircon/public/lib/trace-provider",
  ]
}
