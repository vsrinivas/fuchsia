# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("errors") {
  sources = [
    "net_error_list.h",
    "net_errors.cc",
    "net_errors.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
  ]
}

source_set("lib") {
  # if you don't need HTTPS, comment out NETWORK_SERVICE_USE_HTTPS in |defines| below.
  defines = [ "NETWORK_SERVICE_USE_HTTPS" ]

  sources = [
    "http_client.h",
    "net_adapters.cc",
    "net_adapters.h",
    "network_service_delegate.cc",
    "network_service_delegate.h",
    "network_service_impl.cc",
    "network_service_impl.h",
    "upload_element_reader.cc",
    "upload_element_reader.h",
    "url_loader_impl.cc",
    "url_loader_impl.h",
  ]

  deps = [
    "//garnet/public/lib/netstack/fidl",
    "//garnet/public/lib/network/fidl",
    "//garnet/public/lib/url",
    "//third_party/asio",
    "//third_party/boringssl:boringssl",
    "//zircon/public/lib/zx",
  ]

  public_deps = [
    ":errors",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
  ]
}

executable("bin") {
  output_name = "network"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
  ]
}

package("network") {
  deps = [
    ":bin",
  ]

  binary = "network"

  meta = [
    {
      path = rebase_path("meta/network.cmx")
      dest = "network.cmx"
    },
  ]
}

executable("network_service_tests_bin") {
  testonly = true

  sources = [
    "upload_element_reader_unittest.cc",
  ]

  output_name = "network_service_tests"

  deps = [
    ":lib",
    "//garnet/public/lib/test_runner/cpp:gtest_main",
  ]
}

package("network_service_tests") {
  testonly = true
  deprecated_system_image = true

  deps = [
    ":network_service_tests_bin",
  ]

  tests = [
    {
      name = "network_service_tests"
    },
  ]
}
