# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//packages/package.gni")

executable("network") {
  # if you don't need HTTPS, comment out NETWORK_SERVICE_USE_HTTPS in |defines| below.
  defines = [ "NETWORK_SERVICE_USE_HTTPS" ]

  sources = [
    "http_client.h",
    "main.cc",
    "net_adapters.cc",
    "net_adapters.h",
    "net_errors.cc",
    "net_errors.h",
    "network_service_delegate.cc",
    "network_service_delegate.h",
    "network_service_impl.cc",
    "network_service_impl.h",
    "upload_element_reader.cc",
    "upload_element_reader.h",
    "url_loader_impl.cc",
    "url_loader_impl.h",
  ]

  deps = [
    "//application/lib/app",
    "//apps/network/services",
    "//garnet/public/lib/ftl",
    "//garnet/public/lib/mtl",
    "//garnet/public/lib/netstack/fidl:net_address",
    "//garnet/public/lib/url",
    "//magenta/system/ulib/mx",
    "//third_party/asio",
    "//third_party/boringssl:boringssl",
  ]
}

# TODO: This should really be called just 'network' but the executable target 'network' is also
# being used as a dependency elsewhere in the tree to get access to net_errors.h. This is wrong -
# targets shouldn't declare a dependency on an executable just to get access to their header files.
# The right fix is to define an API target with the errors, preferably in a fidl file, and have
# other targets depend on that so the name of the executable target can be a detail internal to the
# package target.
package("network_pkg") {
  package_name = "network"

  app = true

  deps = [
    ":network",
    "//apps/network/application_loader",
  ]

  binaries = [
    {
      name = "network"
    },

    {
      name = "network_application_loader"
    },
  ]
}
