# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("console") {
  sources = [
    "command.cc",
    "command.h",
    "command_parser.cc",
    "command_parser.h",
    "command_utils.cc",
    "command_utils.h",
    "main_loop.cc",
    "main_loop.h",
    "noun_memory.cc",
    "noun_process.cc",
    "noun_zxdb.cc",
    "output_buffer.cc",
    "output_buffer.h",
  ]

  public_deps = [
    "//garnet/bin/zxdb/client",
  ]

  if (current_toolchain == host_toolchain) {
    public_deps += [ ":linenoise" ]
  } else {
    public_deps += [ "//zircon/public/lib/linenoise" ]
  }
}

source_set("tests") {
  testonly = true
  sources = [
    "command_parser_unittest.cc",
    "command_utils_unittest.cc",
  ]

  deps = [
    ":console",
    "//third_party/gtest",
  ]
}

# Host build of linenoise.
# TODO(brettw) remove this when host builds of Zircon libraries are
# available. See ZX-1676.
if (current_toolchain == host_toolchain) {
  config("linenoise_config") {
    include_dirs = [ "//zircon/third_party/ulib/linenoise/include" ]
  }

  static_library("linenoise") {
    sources = [
      "//zircon/third_party/ulib/linenoise/linenoise.c",
    ]
    public_configs = [ ":linenoise_config" ]
  }
}
