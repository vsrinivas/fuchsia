# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("symbols") {
  # The public API for this target refers to the API for the zxdb/client
  # module, of which this is a submodule. The client library one directory up
  # instantiates the "Impl" classes using the non-public headers.
  friend = [
    "../*",
  ]

  public = [
    "base_type.h",
    "file_line.h",
    "lazy_symbol.h",
    "line_details.h",
    "loaded_module_symbols.h",
    "location.h",
    "modified_type.h",
    "module_symbol_status.h",
    "module_symbols.h",
    "process_symbols.h",
    "system_symbols.h",
    "target_symbols.h",
    "symbol.h",
    "symbol_factory.h",
    "type.h",
  ]

  sources = [
    "base_type.cc",
    "dwarf_die_decoder.cc",
    "dwarf_die_decoder.h",
    "dwarf_symbol_factory.cc",
    "dwarf_symbol_factory.h",
    "file_line.cc",
    "lazy_symbol.cc",
    "line_details.cc",
    "loaded_module_symbols.cc",
    "loaded_module_symbols_impl.cc",
    "loaded_module_symbols_impl.h",
    "location.cc",
    "modified_type.cc",
    "module_symbol_index.cc",
    "module_symbol_index.h",
    "module_symbol_index_node.cc",
    "module_symbol_index_node.h",
    "module_symbols.cc",
    "module_symbols_impl.cc",
    "module_symbols_impl.h",
    "process_symbols.cc",
    "process_symbols_impl.cc",
    "process_symbols_impl.h",
    "system_symbols.cc",
    "target_symbols.cc",
    "target_symbols_impl.cc",
    "target_symbols_impl.h",
    "symbol.cc",
    "type.cc",
  ]

  # This library can not use any part of the debugger other than the "common"
  # base helpers.
  public_deps = [
    "..:common",
    "//garnet/lib/debug_ipc:client",
    "//garnet/public/lib/fxl",
  ]

  deps = [
    "//third_party/llvm/lib/DebugInfo",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "mock_module_symbols.cc",
    "mock_module_symbols.h",
    "mock_process_symbols.cc",
    "mock_process_symbols.h",
  ]

  deps = [
    ":symbols",
  ]
}

source_set("tests") {
  testonly = true

  sources = [
    "module_symbol_index_unittest.cc",
    "module_symbol_index_node_unittest.cc",
    "module_symbols_impl_unittest.cc",
    "process_symbols_impl_unittest.cc",
    "system_symbols_unittest.cc",
    "test_symbol_module.cc",
  ]

  deps = [
    ":symbols",
    ":test_support",
    "//third_party/googletest:gtest",
    "//third_party/llvm/lib/DebugInfo",
  ]

  data_deps = [
    "//garnet/bin/zxdb/client/test_data:copy_test_so(${default_toolchain}-shared)",
  ]
}
