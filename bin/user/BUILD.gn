# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # if false, bypasses Kronk launch
  kronk = true

  # if true, uses the "kronk-dev" agent, which is the unstable,
  # under-development version
  kronk_dev = false
}

source_set("user") {
  deps = [
    ":maxwell_main",
  ]
}

executable("maxwell_main") {
  output_name = "maxwell"

  sources = [
    "maxwell_main.cc",
  ]

  deps = [
    ":user_intelligence_provider",
    "//application/lib/app",
    "//apps/maxwell/services/user",
    "//lib/mtl",
  ]
}

source_set("user_intelligence_provider") {
  sources = [
    "user_intelligence_provider_impl.cc",
    "user_intelligence_provider_impl.h",
  ]

  public_deps = [
    "//application/lib/app",
  ]

  deps = [
    ":agent_launcher",
    ":intelligence_services",
    "//apps/maxwell/services/action_log",
    "//apps/maxwell/services/context",
    "//apps/maxwell/services/context:debug",
    "//apps/maxwell/services/resolver",
    "//apps/maxwell/services/suggestion",
    "//apps/maxwell/services/user",
    "//apps/maxwell/services/user:scope",
    "//apps/maxwell/src/acquirers/story_info:initializer",
    "//apps/network/services",
  ]

  if (kronk) {
    defines = [ "KRONK" ]
    if (kronk_dev) {
      defines += [ "KRONK_DEV" ]
    }
  }
}

source_set("intelligence_services") {
  sources = [
    "intelligence_services_impl.cc",
    "intelligence_services_impl.h",
  ]

  deps = [
    "//apps/maxwell/services/action_log",
    "//apps/maxwell/services/context",
    "//apps/maxwell/services/suggestion",
    "//apps/maxwell/services/user",
    "//apps/maxwell/services/user:scope",
  ]
}

source_set("agent_launcher") {
  sources = [
    "agent_launcher.cc",
    "agent_launcher.h",
  ]

  public_deps = [
    "//application/lib/app",
  ]

  deps = [
    "//apps/maxwell/src:application_environment_host_impl",
  ]
}
