# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

executable("bin") {
  output_name = "crashpad_analyzer"

  deps = [
    ":crashpad_analyzer_impl",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/syslog/cpp",
    "//zircon/public/fidl/fuchsia-crash",
    "//zircon/public/lib/async-loop-cpp",
  ]

  sources = [
    "main.cc",
  ]
}

package("crashpad_analyzer") {
  deps = [
    ":bin",
  ]

  binary = "crashpad_analyzer"

  meta = [
    {
      path = rebase_path("meta/crashpad_analyzer.cmx")
      dest = "crashpad_analyzer.cmx"
    },
  ]
}

source_set("crashpad_analyzer_impl") {
  sources = [
    "crashpad_analyzer_impl.cc",
    "crashpad_analyzer_impl.h",
  ]

  public_deps = [
    "//garnet/public/lib/fxl",
    "//third_party/crashpad/client",
    "//third_party/crashpad/util",
    "//zircon/public/fidl/fuchsia-crash",
    "//zircon/public/fidl/fuchsia-mem",
    "//zircon/public/lib/zx",
  ]

  deps = [
    ":report_annotations",
    "//garnet/public/lib/syslog/cpp",
    "//third_party/crashpad/handler",
    "//third_party/crashpad/minidump",
    "//third_party/crashpad/third_party/mini_chromium/mini_chromium/base",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
  ]
}

source_set("report_annotations") {
  sources = [
    "report_annotations.cc",
    "report_annotations.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/syslog/cpp",
    "//zircon/public/fidl/zircon-sysinfo:zircon-sysinfo_c",
    "//zircon/public/lib/zx",
  ]
}
