# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# A collection of utilities useful to debuggers and similar programs.
# This library is "public" in the sense that it's available to be used
# by anyone outside of this directory.
# TODO(dje): Living in bin/foo is suboptimal.
static_library("debugger-utils") {
  sources = [
    "build-ids.cc",
    "build-ids.h",
    "byte-block-file.cc",
    "byte-block-file.h",
    "byte-block.h",
    "elf-reader.cc",
    "elf-reader.h",
    "elf-symtab.cc",
    "elf-symtab.h",
    "ktrace-reader.cc",
    "ktrace-reader.h",
    "load-maps.cc",
    "load-maps.h",
    "util.cc",
    "util.h",
  ]

  if (is_fuchsia) {
    sources += [
      "dso-list.cc",
      "dso-list.h",
      "util-mx.cc",
    ]
  }

  if (current_cpu == "x64") {
    sources += [
      "x86-cpuid.cc",
      "x86-cpuid.h",
      "x86-pt.cc",
      "x86-pt.h",
    ]
  }

  deps = [
    "//garnet/public/lib/fxl",
  ]

  if (is_fuchsia) {
    deps += [ "//magenta/system/ulib/mx" ]
  } else {
    deps += [ "//magenta/system/public" ]
  }
}

group("tests") {
  testonly = true

  deps = [
    ":util-unittests",
  ]
}

executable("util-unittests") {
  testonly = true

  sources = [
    "../../test/run-all-unittests.cc",
    "util-mx.cc",
    "util-unittest.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
    "//third_party/gtest",
  ]

  libs = [ "magenta" ]
}
