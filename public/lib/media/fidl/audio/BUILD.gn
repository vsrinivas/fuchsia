# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/tools/bindings/mojom.gni")

group("audio") {
  deps = [
    ":audio_server",
  ]
}

mojo_native_application("audio_server") {
  deps = [
    "//base",
    "//mojo/application",
    "//mojo/services/flog/cpp",
    "//mojo/services/media/audio/interfaces",
    "//mojo/services/media/common/cpp",
    "//mojo/services/media/common/interfaces",
    "//services/media/common",
  ]

  sources = [
    "audio_output.cc",
    "audio_output_manager.cc",
    "audio_pipe.cc",
    "audio_server_app.cc",
    "audio_server_impl.cc",
    "audio_track_impl.cc",
    "audio_track_to_output_link.cc",
    "gain.cc",
    "platform/generic/mixer.cc",
    "platform/generic/mixers/linear_sampler.cc",
    "platform/generic/mixers/no_op.cc",
    "platform/generic/mixers/point_sampler.cc",
    "platform/generic/output_formatter.cc",
    "platform/generic/standard_output_base.cc",
    "platform/generic/throttle_output.cc",
  ]

  libs = []

  if (is_linux && !is_android) {
    sources += [ "platform/linux/alsa_output.cc" ]
    if (is_fnl) {
      sources += [ "platform/linux/alsa_output_tinyalsa.cc" ]
      libs += [ "tinyalsa" ]
    } else {
      sources += [ "platform/linux/alsa_output_desktop.cc" ]
      libs += [ "asound" ]
    }
  } else {
    sources += [ "platform/stubs/alsa_output_stub.cc" ]
  }
}
