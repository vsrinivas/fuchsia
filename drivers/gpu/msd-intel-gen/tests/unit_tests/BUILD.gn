# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//magma/gnbuild/magma.gni")

executable("msd_intel_gen_nonhardware_tests") {
  testonly = true

  sources = [
    "main.cc",
  ]

  public_deps = [
    ":tests_not_requiring_hardware",
    "//third_party/gtest",
  ]
}

# These tests don't require Intel graphics hardware to run, so they
# can be run under QEMU.
source_set("tests_not_requiring_hardware") {
  testonly = true

  sources = [
    "modeset/test_displayport.cc",
    "modeset/test_edid.cc",
    "test_buffer.cc",
    "test_cache_config.cc",
    "test_context.cc",
    "test_engine_command_streamer.cc",
    "test_gtt.cc",
    "test_hardware_status_page.cc",
    "test_instructions.cc",
    "test_ppgtt.cc",
    "test_render_init_batch.cc",
    "test_ringbuffer.cc",
    "test_scheduler.cc",
    "test_semaphore.cc",
    "test_sequencer.cc",
  ]

  deps = [
    ":test_deps",
  ]
}

# These tests do require Intel graphics hardware to run.  They can be
# run inside the test build of the driver.
source_set("tests_requiring_hardware") {
  testonly = true

  sources = [
    "test_command_buffer.cc",
    "test_device.cc",
    "test_display.cc",
    "test_forcewake.cc",
  ]

  deps = [
    ":test_deps",
  ]
}

group("test_deps") {
  testonly = true

  public_deps = [
    "$magma_build_root/include:msd_abi",
    "$magma_build_root/src/magma_util",
    "$magma_build_root/tests/helper:command_buffer_helper",
    "$magma_build_root/tests/helper:platform_device_helper",
    "$magma_build_root/tests/mock:mmio",
    "$msd_intel_gen_build_root/src",
    "$msd_intel_gen_build_root/tests/mock",
    "//third_party/gtest",
  ]
}
