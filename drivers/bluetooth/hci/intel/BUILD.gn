# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")
import("//garnet/tools/cipd.gni")

driver_module("btintel_driver") {
  output_name = "btintel"

  sources = [
    "binding.c",
    "device.cpp",
    "device.h",
    "driver.cpp",
    "firmware_loader.cpp",
    "firmware_loader.h",
    "logging.h",
    "vendor_hci.cpp",
    "vendor_hci.h",
  ]

  deps = [
    "//garnet/drivers/bluetooth/lib/gap",
    "//garnet/drivers/bluetooth/lib/hci",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
  ]

  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
}

if (internal_access) {
  copy("btintel-firmware") {
    sources = [
      "firmware/ibt-11-5.sfi",
      "firmware/ibt-12-16.sfi",
      "firmware/ibt-18-2.sfi",
      "firmware/ibt-hw-37.8.10-fw-22.50.19.14.f.bseq",
      "firmware/ibt-hw-37.8.bseq",
    ]
    outputs = [
      "$root_out_dir/firmware/{{source_file_part}}",
    ]
  }
}

package("btintel") {
  deprecated_system_image = true

  deps = [
    ":btintel_driver",
  ]

  drivers = [
    {
      name = "btintel.so"
    },
  ]

  if (internal_access) {
    deps += [
      # The copy step has to be in the shared-library toolchain to use the
      # expected output directory, since we treat firmware blobs as if they
      # were libraries which always use the shared-library toolchain to build.
      ":btintel-firmware($shlib_toolchain)",
    ]

    libraries = [
      {
        name = "firmware/ibt-11-5.sfi"
      },
      {
        name = "firmware/ibt-12-16.sfi"
      },
      {
        name = "firmware/ibt-18-2.sfi"
      },
      {
        name = "firmware/ibt-hw-37.8.10-fw-22.50.19.14.f.bseq"
      },
      {
        name = "firmware/ibt-hw-37.8.bseq"
      },
    ]
  }
}
