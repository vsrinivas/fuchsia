# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("maxwell_integration") {
  testonly = true

  public_deps = [
    ":context_engine_test",
    ":suggestion_engine_test",
    ":resolver_test",
  ]
}

executable("context_engine_test") {
  testonly = true

  sources = [
    "context_engine_test.cc",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    ":context_common",
    "//peridot/bin/context_engine:scope_utils",
    "//peridot/public/lib/context/cpp:context_metadata_builder",
    "//peridot/public/lib/context/cpp:formatting",
    "//peridot/public/lib/context/fidl",
  ]
}

executable("resolver_test") {
  testonly = true

  sources = [
    "resolver_test.cc",
  ]

  deps = [
    ":common",
    "//peridot/public/lib/resolver/fidl",
  ]
}

executable("suggestion_engine_test") {
  testonly = true

  sources = [
    "suggestion_engine_test.cc",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    ":context_common",
    ":test_suggestion_listener",
    "//peridot/bin/acquirers/mock:gps",
    "//peridot/bin/agents:carmen_sandiego",
    "//peridot/bin/agents:ideas",
    "//peridot/lib/rapidjson",
    "//peridot/lib/testing:story_provider_mock",
    "//peridot/public/lib/context/fidl",
    "//peridot/public/lib/suggestion/fidl",
    "//peridot/public/lib/suggestion/fidl:debug",
  ]
}

# TODO(rosswang): move utils to a subdirectory

source_set("context_common") {
  testonly = true

  sources = [
    "context_engine_test_base.h",
  ]

  deps = [
    ":common",
  ]

  public_deps = [
    "//peridot/public/lib/context/fidl",
    "//peridot/public/lib/user_intelligence/fidl:scope",
    "//third_party/gtest",
  ]
}

source_set("common") {
  testonly = true

  sources = [
    "test.cc",
    "test.h",
  ]

  deps = [
    "//garnet/public/lib/fsl",
    "//peridot/bin/user:agent_launcher",
    "//peridot/lib/environment_host",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
    "//third_party/gtest",
  ]
}

source_set("test_suggestion_listener") {
  testonly = true

  sources = [
    "test_suggestion_listener.cc",
    "test_suggestion_listener.h",
  ]

  deps = [
    "//peridot/public/lib/suggestion/cpp:formatting",
    "//peridot/public/lib/suggestion/fidl",
    "//peridot/public/lib/suggestion/fidl:debug",
  ]

  public_deps = [
    "//third_party/gtest",
  ]
}
