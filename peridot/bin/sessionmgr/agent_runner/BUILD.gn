# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/environments.gni")
import("//peridot/build/tests_package.gni")

source_set("agent_runner") {
  sources = [
    "agent_context_impl.cc",
    "agent_context_impl.h",
    "agent_runner.cc",
    "agent_runner_storage_impl.cc",
    "agent_runner_storage_impl.h",
    "agent_service_index.h",
    "map_agent_service_index.h",
  ]

  public_deps = [
    ":public",
  ]

  deps = [
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/json",
    "//peridot/bin/sessionmgr:component_context",
    "//peridot/bin/sessionmgr/message_queue",
    "//peridot/bin/sessionmgr/storage:constants_and_utils",
    "//peridot/lib/common:teardown",
    "//peridot/lib/fidl:app_client",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/fidl:json_xdr",
    "//peridot/lib/ledger_client:page_client",
    "//peridot/lib/ledger_client:types",
    "//peridot/public/lib/async/cpp:operation",
    "//sdk/fidl/fuchsia.ledger",
    "//sdk/fidl/fuchsia.modular",
    "//sdk/fidl/fuchsia.modular.auth",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fidl/cpp",
    "//src/lib/fxl",
  ]
}

executable("agent_runner_unittest") {
  testonly = true

  sources = [
    "agent_runner_unittest.cc",
  ]

  deps = [
    ":agent_runner",
    ":public",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/component/cpp/testing:fake_launcher",
    "//garnet/public/lib/fsl",
    "//peridot/bin/sessionmgr/entity_provider_runner",
    "//peridot/bin/sessionmgr/message_queue",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/ledger_client:page_client",
    "//peridot/lib/testing:fake_agent_runner_storage",
    "//peridot/lib/testing:mock_base",
    "//peridot/lib/testing:test_with_ledger",
    "//sdk/fidl/fuchsia.modular",
    "//sdk/fidl/fuchsia.modular.auth",
    "//sdk/fidl/fuchsia.sys",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/trace",
  ]
}

tests_package("agent_runner_unittests") {
  deps = [
    ":agent_runner_unittest",
  ]
  environments = basic_envs
}

# Broken out for component to depend on w/o creating circles.
source_set("public") {
  sources = [
    "agent_runner.h",
    "agent_runner_storage.cc",
    "agent_runner_storage.h",
    "agent_service_index.h",
  ]

  deps = [
    "//peridot/bin/sessionmgr/entity_provider_runner",
    "//peridot/public/lib/async/cpp:operation",
    "//sdk/fidl/fuchsia.ledger",
    "//sdk/fidl/fuchsia.modular",
    "//sdk/fidl/fuchsia.modular.auth",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fidl/cpp",
    "//src/ledger/bin/fidl",
    "//src/lib/fxl",
  ]
}
