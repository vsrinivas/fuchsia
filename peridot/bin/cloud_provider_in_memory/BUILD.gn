# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

visibility = [
  "//peridot/bin/ledger/*",
  ":*",
]

source_set("lib") {
  sources = [
    "fake_cloud_provider.cc",
    "fake_cloud_provider.h",
    "fake_device_set.cc",
    "fake_device_set.h",
    "fake_page_cloud.cc",
    "fake_page_cloud.h",
    "types.h",
  ]

  public_deps = [
    "//garnet/public/lib/callback",
    "//garnet/public/lib/fxl",
    "//peridot/bin/ledger/fidl/include",
    "//peridot/lib/commit_pack",
  ]

  deps = [
    "//peridot/bin/ledger/fidl_helpers",
    "//peridot/lib/convert",
    "//peridot/public/fidl/fuchsia.ledger.cloud",
    "//third_party/murmurhash",
  ]

  configs += [ "//peridot/bin/ledger:ledger_config" ]
}

executable("app") {
  output_name = "cloud_provider_in_memory"

  sources = [
    "app.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fidl/cpp",
    "//garnet/public/lib/fxl",
    "//peridot/lib/rng:system",
    "//peridot/public/fidl/fuchsia.modular",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/trace-provider",
  ]
}

package("cloud_provider_in_memory") {
  deps = [
    ":app",
  ]

  binary = "cloud_provider_in_memory"

  meta = [
    {
      path = rebase_path("cloud_provider_in_memory.cmx")
      dest = "cloud_provider_in_memory.cmx"
    },
  ]
}

group("tests") {
  testonly = true

  public_deps = [
    "//peridot/bin/cloud_provider_in_memory/validation",
  ]
}
