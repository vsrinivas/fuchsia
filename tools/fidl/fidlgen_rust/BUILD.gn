# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/host.gni")
import("//build/testing/host_test_data.gni")

go_library("lib") {
  name = "go.fuchsia.dev/fuchsia/tools/fidl/fidlgen_rust/..."
}

go_binary("fidlgen_rust") {
  gopackage = "go.fuchsia.dev/fuchsia/tools/fidl/fidlgen_rust"

  sdk_category = "partner"

  deps = [
    ":lib",
    "//garnet/go/src/fidl:compiler",
  ]
}

install_host_tools("host") {
  deps = [ ":fidlgen_rust" ]
  outputs = [ "fidlgen_rust" ]
}

if (is_host) {
  go_test("fidlgen_rust_test") {
    gopackages = [ "go.fuchsia.dev/fuchsia/tools/fidl/fidlgen_rust/codegen" ]

    deps = [
      ":lib",
      "//garnet/go/src/fidl:fidlgen_test_lib",
    ]
    non_go_deps = [ ":test_data" ]
  }

  go_test("fidlgen_rust_ir_test") {
    gopackages = [ "go.fuchsia.dev/fuchsia/tools/fidl/fidlgen_rust/ir" ]

    deps = [
      ":lib",
      "//garnet/go/src/fidl:fidlgen_test_lib",
    ]
  }

  host_test_data("test_data") {
    sources = [
      "//prebuilt/third_party/rust_tools/$host_platform/bin/rustfmt",
      "//rustfmt.toml",
    ]
    outputs = [ "$root_out_dir/test_data/fidlgen_rust/{{source_file_part}}" ]
  }
}
