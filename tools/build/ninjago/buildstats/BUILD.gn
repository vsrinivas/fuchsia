# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/testing/host_test_data.gni")

if (is_host) {
  go_library("main") {
    sources = [
      "main.go",
      "main_test.go",
    ]
    deps = [
      "//tools/build/ninjago/compdb",
      "//tools/build/ninjago/ninjagraph",
      "//tools/build/ninjago/ninjalog",
      "//tools/lib/color",
      "//tools/lib/logger",
    ]
  }

  go_binary("buildstats") {
    library = ":main"
  }

  _testdata_path = "$target_gen_dir/testdata"

  host_test_data("testdata") {
    sources = [
      "//tools/build/ninjago/test_data/compdb.json.gz",
      "//tools/build/ninjago/test_data/graph.dot.gz",
      "//tools/build/ninjago/test_data/ninja_log.gz",
    ]
    outputs = [ "${_testdata_path}/{{source_file_part}}" ]
  }

  go_test("buildstats_tests") {
    library = ":main"
    output_name = "buildstats_tests"
    args = [
      "--test_data_dir",
      rebase_path(_testdata_path, root_build_dir),
    ]
    deps = [ "//third_party/golibs:github.com/google/go-cmp" ]
    non_go_deps = [ ":testdata" ]
  }
}
