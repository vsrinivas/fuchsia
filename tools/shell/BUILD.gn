# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

test("shell_tests_bin") {
  deps = [
    "//sdk/lib/fidl/cpp/test",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//tools/shell/console:tests",
    "//tools/shell/lib:tests",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
  ]
}

# Generate a manifest providing all of the .fidl.json files exported by the
# core SDK (and any other FIDL files we might want).
action("generate_fidl_json") {
  script = "generate_fidl_json.sh"
  generated_output = "$target_gen_dir/sdk_fidl_json.manifest"
  outputs = [
    "$generated_output",
  ]
  testonly = true
  deps = [
    "//garnet/examples/fidl/services:echo",
    "//sdk:core",
    "//sdk:core_fidl_json",
    "//sdk/fidl/fuchsia.power:fuchsia.power",
    "//third_party/quickjs:qjs($host_toolchain)",
    "//zircon/system/fidl/fuchsia-device-manager",
    "//zircon/system/fidl/fuchsia-kernel",
  ]
  args = [
    "--qjs=" + rebase_path("$root_build_dir/host_$host_cpu/qjs"),
    "--fidl-json-file=" +
        rebase_path("$root_gen_dir/sdk/core.fidl_json.txt", root_build_dir),
    "--out-file=" + rebase_path("$generated_output", root_build_dir),
    rebase_path(
        "$root_build_dir/fidling/gen/garnet/examples/fidl/services/echo.fidl.json",
        root_build_dir),
    rebase_path(
        "$root_build_dir/fidling/gen/sdk/fidl/fuchsia.power/fuchsia.power.fidl.json",
        root_build_dir),
    rebase_path(
        "$root_build_dir/fidling/gen/zircon/system/fidl/fuchsia-kernel/fuchsia-kernel.fidl.json",
        root_build_dir),
    rebase_path(
        "$root_build_dir/fidling/gen/zircon/system/fidl/fuchsia-device-manager/fuchsia-device-manager.fidl.json",
        root_build_dir),
    rebase_path(
        "$root_build_dir/fidling/gen/zircon/system/fidl/fuchsia-boot/fuchsia-boot.fidl.json",
        root_build_dir),
  ]
}

unittest_package("shell_tests") {
  deps = [
    ":shell_tests_bin",
  ]
  tests = [
    {
      name = "shell_tests_bin"
      environments = basic_envs
    },
  ]
}

executable("bin") {
  output_name = "josh"

  sources = [
    "main.cc",
  ]

  deps = [
    "//tools/shell/console",
    "//tools/shell/lib",
  ]

  libs = [ "zircon" ]
}

# "josh" is Javascript Object SHell.  We can change the name later.
package("josh") {
  # TODO(jeremymanson): This is only testonly because sdk:core is.  We should undo that.
  testonly = true
  deps = [
    ":bin",
    ":generate_fidl_json",
  ]
  binaries = [
    {
      name = "josh"
      shell = true
    },
  ]
  extra = get_target_outputs(":generate_fidl_json")
}
