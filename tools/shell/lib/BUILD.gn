# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/quickjs/compiled_js.gni")

static_library("lib") {
  sources = [
    "fdio.cc",
    "fdio.h",
    "fidl.cc",
    "fidl.h",
    "qjs_util.h",
    "runtime.cc",
    "runtime.h",
    "zx.cc",
    "zx.h",
  ]

  public_deps = [
    ":fidl",
    ":zx",
    "//src/lib/fidl_codec",
    "//src/lib/fxl",
    "//third_party/quickjs:qjs-lib",
    "//third_party/rapidjson",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fidl_base",
    "//zircon/public/lib/zx",
  ]
}

compiled_js("fidl") {
  source = "fidl.js"
  module = true
  extra_args = [
    "-M",
    "fidl_internal",
  ]
}

compiled_js("zx") {
  source = "zx.js"
  module = true
  extra_args = [
    "-M",
    "zx_internal",
  ]
}

action("js_libs") {
  script = "generate_js_libs.sh"
  sources = [
    "ns.js",
    "util.js",
  ]
  generated_output = "$target_gen_dir/libs.manifest"
  outputs = [
    "$generated_output",
  ]
  args = [ rebase_path("$generated_output", root_build_dir) ] +
         rebase_path(sources, root_build_dir)
}

source_set("tests") {
  testonly = true
  sources = [
    "fidl_test.cc",
    "js_testing_utils.h",
    "ns_test.cc",
    "zx_test.cc",
  ]
  deps = [
    ":lib",
    "//sdk/lib/fidl/cpp/test",
    "//src/lib/fidl_codec:json_for_test",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/memfs",
  ]
}
