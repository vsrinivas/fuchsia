# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")
import("//build/testing/host_test_data.gni")

group("tests") {
  testonly = true
  deps = [ ":cargo_gnaw_golden_integration_test($host_toolchain)" ]
}

if (is_host) {
  rustc_test("cargo_gnaw_golden_integration_test") {
    edition = "2018"
    source_root = "golden.rs"
    deps = [
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:pretty_assertions",
      "//third_party/rust_crates:tempfile",
      "//tools/cargo-gnaw:cargo-gnaw-lib",
    ]

    # Host tests are tested on different workers from those the artifacts were
    # built on.  This means, if we want to use binaries from the source tree,
    # we need to add them to test dependencies explicitly.
    non_rust_deps = [
      ":cargo_gnaw_gn",
      ":cargo_gnaw_rust",
      ":cargo_gnaw_tests",
    ]

    sources = [ "golden.rs" ]
  }

  host_test_data("cargo_gnaw_rust") {
    sources = [ "//prebuilt/third_party/rust/${host_platform}" ]
    outputs = [ "$root_out_dir/test_data/tools/cargo-gnaw/runfiles/rust" ]
  }

  host_test_data("cargo_gnaw_tests") {
    sources = [ "//tools/cargo-gnaw/tests" ]
    outputs = [ "$root_out_dir/test_data/tools/cargo-gnaw/tests" ]
  }

  host_test_data("cargo_gnaw_gn") {
    sources = [ "//prebuilt/third_party/gn/${host_platform}/gn" ]
    outputs = [ "$root_out_dir/test_data/tools/cargo-gnaw/runfiles/gn" ]
  }
}
