# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")

go_binary("check-licenses") {
  gopackage = "go.fuchsia.dev/fuchsia/scripts/check-licenses/cmd"
  deps = [
    ":main",
    ":templates",
  ]
}

go_library("main") {
  name = "go.fuchsia.dev/fuchsia/scripts/check-licenses/cmd"
  source_dir = "cmd"
  deps = [
    ":lib",
    "//third_party/golibs:cloud.google.com/go/storage",
    "//third_party/golibs:github.com/google/subcommands",
    "//tools/lib/logger",
  ]
}

go_library("templates") {
  name = "go.fuchsia.dev/fuchsia/scripts/check-licenses/templates"
  source_dir = "templates"
}

go_library("lib") {
  name = "go.fuchsia.dev/fuchsia/scripts/check-licenses/lib"
  deps = [ "//tools/lib/logger" ]
  source_dir = "lib"
}

install_host_tools("host") {
  deps = [ ":check-licenses" ]
  outputs = [ "check-licenses" ]
}

group("tests") {
  testonly = true
  deps = [
    ":check-licenses_lib_tests($host_toolchain)",
    ":check-licenses_main_tests($host_toolchain)",
  ]
}

go_test("check-licenses_main_tests") {
  gopackages = [ "go.fuchsia.dev/fuchsia/scripts/check-licenses/cmd" ]
  deps = [ ":main" ]
}

go_test("check-licenses_lib_tests") {
  gopackages = [ "go.fuchsia.dev/fuchsia/scripts/check-licenses/lib" ]
  deps = [ ":lib" ]
}
