# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")

go_library("util") {
  sources = [
    "gen.go",
    "git.go",
    "gn.go",
    "gn_test.go",
    "init.go",
    "target.go",
  ]
  deps = [
    "//third_party/golibs:github.com/google/go-cmp",
    "//tools/lib/logger",
  ]
}

group("tests") {
  testonly = true

  deps = [ ":check-licenses_util_tests($host_toolchain)" ]
}

if (is_host) {
  go_test("check-licenses_util_tests") {
    library = ":util"
    args = [
      "--gn_path",
      rebase_path("//prebuilt/third_party/gn/${host_os}-${host_cpu}/gn",
                  root_build_dir),
      "--build_dir",
      rebase_path(root_out_dir, root_build_dir),
      "--test_data_dir",
      rebase_path("$target_gen_dir/testdata", root_build_dir),
    ]
    non_go_deps = [ "testdata" ]
  }
}
