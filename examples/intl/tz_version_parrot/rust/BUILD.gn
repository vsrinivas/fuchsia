# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

group("rust") {
  testonly = true
  deps = [ ":tz-version-parrot-rust" ]
}

group("tests") {
  testonly = true
  deps = [ ":rust" ]
}

rustc_test("bin") {
  name = "tz_version_parrot_rust"

  deps = [
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",

    # [START icudata_library]
    "//src/lib/icu_data/rust/icu_data",

    # [END icudata_library]
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:rust_icu_ucal",
  ]

  sources = [ "src/lib.rs" ]
}

fuchsia_component("tz_version_parrot_rust") {
  testonly = true
  manifest = "meta/tz_version_parrot.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("tz-version-parrot-rust") {
  test_components = [ ":tz_version_parrot_rust" ]
  deps = [
    "//examples/intl/tz_version_parrot:icu_tz_data_for_tz_version_parrot",
    "//examples/intl/tz_version_parrot:tz_version_parrot_resources",

    # [START icudata_resource]
    "//src/intl:icudtl",

    # [END icudata_resource]
  ]
}
