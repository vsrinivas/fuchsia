# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "client"
  edition = "2018"

  deps = [
    "//examples/diagnostics/inspect/codelab/fidl:fuchsia.examples.inspect-rustc",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

part_ids = [
  1,
  2,
  3,
  4,
  5,
]
client_components = []
foreach(part_id, part_ids) {
  client_name = "client_part_" + part_id
  client_components += [ ":$client_name" ]

  fuchsia_component(client_name) {
    manifest = "meta/" + client_name + ".cml"
    deps = [ ":bin" ]
  }
}

group("components") {
  deps = client_components
}
