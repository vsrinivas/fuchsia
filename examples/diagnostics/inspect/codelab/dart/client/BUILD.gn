# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/dart_component.gni")
import("//build/dart/dart_library.gni")

dart_library("lib") {
  package_name = "inspect_dart_codelab"
  sources = [ "main.dart" ]
  source_dir = "lib"
  deps = [
    "//examples/diagnostics/inspect/codelab/fidl:fuchsia.examples.inspect_dart",
    "//sdk/dart/fuchsia",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/fuchsia_services",
    "//sdk/fidl/fuchsia.component:fuchsia.component_dart",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_dart",
    "//sdk/fidl/fuchsia.io:fuchsia.io_dart",
  ]
}

part_ids = [
  1,
  2,
  3,
  4,
  5,
]
client_components = []
foreach(part_id, part_ids) {
  client_name = "client_part_" + part_id
  target_name = "inspect_dart_codelab_" + client_name

  client_components += [ ":" + target_name ]

  dart_component(target_name) {
    main_package = "inspect_dart_codelab"
    component_name = client_name
    manifest = "meta/" + client_name + ".cml"
    deps = [ ":lib" ]
  }
}

group("client") {
  deps = client_components
}
