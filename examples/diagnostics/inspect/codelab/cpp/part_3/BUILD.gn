# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/components.gni")

source_set("lib") {
  sources = [
    "reverser.cc",
    "reverser.h",
  ]

  public_deps = [
    "//examples/diagnostics/inspect/codelab/fidl:fuchsia.examples.inspect",
    "//sdk/lib/sys/inspect/cpp",
  ]
}

executable("bin") {
  output_name = "inspect_cpp_codelab_part_3_bin"
  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("inspect_cpp_codelab_part_3") {
  manifest = "meta/inspect_cpp_codelab_part_3.cmx"
  deps = [ ":bin" ]
}

executable("unittests") {
  testonly = true
  output_name = "inspect_cpp_codelab_part_3_unittests"
  sources = [ "reverser_unittests.cc" ]

  deps = [
    ":lib",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",

    # CODELAB: Add the testing library.
  ]
}

fuchsia_unittest_component("inspect_cpp_codelab_part_3_unittests") {
  deps = [ ":unittests" ]
}

fuchsia_test("test_spec") {
  package =
      "//examples/diagnostics/inspect/codelab/cpp:inspect_cpp_codelab_unittests"
  component = ":inspect_cpp_codelab_part_3_unittests"
}
