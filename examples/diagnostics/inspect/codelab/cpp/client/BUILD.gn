# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

executable("bin") {
  output_name = "inspect_cpp_codelab_client"
  sources = [ "main.cc" ]

  deps = [
    "//examples/diagnostics/inspect/codelab/fidl:fuchsia.examples.inspect_hlcpp",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp:cpp-macros",
    "//zircon/system/ulib/zxc",
  ]
}

part_ids = [
  1,
  2,
  3,
  4,
  5,
]
client_components = []
foreach(part_id, part_ids) {
  client_name = "client_part_" + part_id
  client_components += [ ":$client_name" ]

  fuchsia_component(client_name) {
    manifest = "meta/" + client_name + ".cml"
    deps = [ ":bin" ]
  }
}

group("components") {
  deps = client_components
}
