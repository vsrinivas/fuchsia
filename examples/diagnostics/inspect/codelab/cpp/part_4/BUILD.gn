# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

source_set("lib") {
  sources = [
    "reverser.cc",
    "reverser.h",
  ]

  public_deps = [
    "//examples/diagnostics/inspect/codelab/fidl:fuchsia.examples.inspect_hlcpp",
    "//sdk/lib/sys/inspect/cpp",
  ]
}

executable("bin") {
  output_name = "inspect_cpp_codelab_part_4"
  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("part_4") {
  manifest = "meta/part_4.cml"
  deps = [ ":bin" ]
}

executable("unittests") {
  testonly = true
  output_name = "inspect_cpp_codelab_part_4_unittests"
  sources = [ "reverser_unittests.cc" ]

  deps = [
    ":lib",
    "//sdk/lib/fit-promise",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/async:async-cpp",
  ]
}

fuchsia_unittest_component("inspect_cpp_codelab_part_4_unittests") {
  deps = [ ":unittests" ]
}

fuchsia_test("test_spec") {
  package =
      "//examples/diagnostics/inspect/codelab/cpp:inspect_cpp_codelab_unittests"
  component = ":inspect_cpp_codelab_part_4_unittests"
}

executable("integration_test") {
  testonly = true
  output_name = "inspect_cpp_codelab_part_4_integration_test"
  sources = [ "tests/integration_test.cc" ]

  public_deps = [
    "//examples/diagnostics/inspect/codelab/cpp/testing:inspect-cpp-codelab-testing",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_hlcpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_component("inspect_cpp_codelab_part_4_integration_test") {
  testonly = true
  manifest = "meta/integration_test.cml"
  deps = [ ":integration_test" ]
}

fuchsia_test("integration_test_spec") {
  package = "//examples/diagnostics/inspect/codelab/cpp:inspect_cpp_codelab_integration_tests"
  component = ":inspect_cpp_codelab_part_4_integration_test"
}
