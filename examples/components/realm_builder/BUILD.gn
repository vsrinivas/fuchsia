# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dart/dart_test_component.gni")

group("tests") {
  testonly = true
  deps = [ ":package" ]

  if (dart_default_build_cfg.is_aot) {
    deps += [ "//src/dart:dart_aot_runner" ]
  } else {
    deps += [ "//src/dart:dart_jit_runner" ]
  }
}

fuchsia_component("cpp-component") {
  testonly = true
  manifest = "cpp/meta/sample_realm.cml"
  component_name = "cpp"
  deps = [ "cpp:bin" ]
}

fuchsia_component("rust-component") {
  testonly = true
  manifest = "rust/meta/sample_realm.cml"
  component_name = "rust"
  deps = [ "rust:bin" ]
}

dart_test_component("dart-component") {
  package_root = "dart/"
  manifest = "dart/meta/sample_realm.cml"

  null_safe = true

  sources = [ "sample.dart" ]

  # [START realm_builder_dep_dart]
  deps = [
    "//sdk/dart/fuchsia_component_test",

    # [START_EXCLUDE]
    "//examples/components/routing/fidl:echo_dart",
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/fuchsia_services",
    "//sdk/fidl/fuchsia.io:fuchsia.io_dart",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_dart",
    "//third_party/dart-pkg/pub/test",

    # [END_EXCLUDE]
  ]

  # [END realm_builder_dep_dart]
}

fuchsia_component("echo_client_component") {
  component_name = "echo_client"
  manifest = "meta/echo_client.cml"
  deps = [ "//examples/components/routing/rust/echo_client" ]
}

fuchsia_component("echo_server_component") {
  component_name = "echo_server"
  manifest = "meta/echo_server.cml"
  deps = [ "//examples/components/routing/rust/echo_server" ]
}

fuchsia_component("echo_server_legacy_component") {
  component_name = "echo_server"
  manifest = "meta/echo_server.cmx"
  deps = [ "//examples/components/routing/rust/echo_server" ]
}

# Run with `fx test realm-builder-examples`.
fuchsia_test_package("package") {
  package_name = "realm-builder-examples"
  test_components = [
    ":cpp-component",
    ":dart-component",
    ":rust-component",
  ]

  deps = [
    ":echo_client_component",
    ":echo_server_component",
    ":echo_server_legacy_component",
  ]
}
