# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/toolchain.gni")

go_binary("echo_client_go_bin") {
  output_name = "echo_client_go"
  gopackage = "echo_client_go"

  deps = [ ":echo_client_go_lib" ]
}

go_library("echo_client_go_lib") {
  name = "echo_client_go"

  sources = [ "echo_client.go" ]

  deps = [
    "//examples/fidl/legacy/services:echo($go_toolchain)",
    "//sdk/fidl/fuchsia.sys($go_toolchain)",
    "//src/lib/component",
  ]
}

resource("echo_client_file") {
  sources = [ "${root_build_dir}/echo_client_go" ]
  outputs = [ "bin/echo_client" ]
  deps = [ ":echo_client_go_bin" ]

  # data_deps is necessary to include shared libraries in package.
  data_deps = [ ":echo_client_go_bin" ]
}

fuchsia_package_with_single_component("echo_client_go") {
  manifest = "../meta/echo_client.cmx"
  deps = [ ":echo_client_file" ]
}
