# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/toolchain.gni")

go_binary("echo_server_go_bin") {
  output_name = "echo_server_go"
  gopackage = "echo_server_go"
  deps = [ ":echo_server_go_lib" ]
}

go_library("echo_server_go_lib") {
  name = "echo_server_go"

  sources = [ "echo_server.go" ]

  deps = [
    "//examples/fidl/legacy/services:echo($go_toolchain)",
    "//sdk/fidl/fuchsia.sys($go_toolchain)",
    "//src/lib/component",
  ]
}

resource("echo_server_file") {
  sources = [ "${root_build_dir}/echo_server_go" ]
  outputs = [ "bin/echo_server" ]
  deps = [ ":echo_server_go_bin" ]

  # data_deps is necessary to include shared libraries in package.
  data_deps = [ ":echo_server_go_bin" ]
}

fuchsia_package_with_single_component("echo_server_go") {
  manifest = "../meta/echo_server.cmx"
  deps = [ ":echo_server_file" ]
}
