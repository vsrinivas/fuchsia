# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  sources = [
    "echo_server_app.cc",
    "echo_server_app.h",
  ]

  public_deps = [
    "//garnet/examples/fidl/services:echo2",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fidl/cpp",
  ]
}

executable("bin") {
  output_name = "echo2_server_cpp"

  sources = [
    "echo2_server.cc",
  ]

  deps = [
    ":lib",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

package("echo2_server_cpp") {
  deps = [
    ":bin",
  ]

  binary = "echo2_server_cpp"
}

executable("echo2_server_cpp_unittests") {
  testonly = true

  sources = [
    "echo_server_app_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/component/cpp/testing",
    "//garnet/public/lib/fxl/test:gtest_main",
  ]
}

package("echo2_server_cpp_tests") {
  testonly = true

  deps = [
    ":echo2_server_cpp_unittests",
  ]

  tests = [
    {
      name = "echo2_server_cpp_unittests"
    },
  ]
}
