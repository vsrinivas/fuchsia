# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# [START imports]
import("//build/components.gni")

# [END imports]

# [START bin]
executable("bin") {
  output_name = "fidl_echo_cpp_client_sync"
  sources = [ "main.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_cpp",

    # This library is used to log messages.
    "//sdk/lib/syslog/cpp",

    # This library is used to consume capabilities, e.g. protocols,
    # from the component's incoming directory.
    "//sdk/lib/sys/component/cpp",
  ]
}

# [END bin]

# [START component]
fuchsia_component("echo-client") {
  component_name = "echo_client"
  manifest = "meta/client.cml"
  deps = [ ":bin" ]
}

# [END component]

# [START package]
# C++ sync client and server example package
fuchsia_package("echo-cpp-client-sync") {
  deps = [
    ":echo-client",
    "//examples/fidl/cpp/server:echo-server",
    "//examples/fidl/echo-realm:echo_realm",
  ]
}

# [END package]

group("client_sync") {
  testonly = true
  deps = [ ":echo-cpp-client-sync" ]
}
