# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/zbi.gni")
import("//build/images/assemble_system.gni")
import("//garnet/build/pkgfs.gni")

recovery_packages = [
  "//garnet/go/src/amber:pkgfs",
  "//garnet/packages/prod:amber",
  "//garnet/packages/prod:build_info",
  "//garnet/packages/prod:netcfg",
  "//garnet/packages/prod:netstack",
  "//garnet/packages/prod:runtime",
  "//garnet/packages/prod:wlancfg",
  "//garnet/packages/prod:wlanstack",
  "//src/recovery/system",
  pkgfs_package_label,
]

shell_commands("shell-commands") {
  testonly = true
  visibility = [ ":*" ]
  deps = recovery_packages
}

config_package("config-data") {
  testonly = true
  visibility = [ ":*" ]
  deps = recovery_packages
}

recovery_packages += [
  ":shell-commands",
  ":config-data",
]

assemble_system("recovery") {
  base_packages = recovery_packages
  netboot = true
}
