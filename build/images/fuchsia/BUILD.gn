# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/assembled_system.gni")
import("//build/assembly/generated_assembly_inputs.gni")
import("//build/assembly/generated_partitions_config.gni")
import("//build/assembly/legacy_image_metadata_overrides.gni")
import("//build/assembly/update_package.gni")
import("//build/images/args.gni")
import("//build/images/filesystem_limits.gni")
import("//build/images/vboot/vboot.gni")
import("//build/product.gni")
import("//build/sdk/product_bundle.gni")
import("//build/security.gni")
import("//build/security/policies.gni")
import("//build/security/policies_swd.gni")
import("//build/security/verifier/scrutiny_verifiers.gni")
import("//build/testing/host_test_data.gni")
import("//build/testing/verify_files_match.gni")
import("//src/sys/core/build/core.gni")

if (is_host) {
  host_test_data("fuchsia_zbi_for_host_tests") {
    sources = [ "$root_build_dir/fuchsia.zbi" ]
    # This dep cannot be specified, because it creates a dependency cycle.  It
    # could be added if all tests using this were not in any package set (and
    # were just in host_labels)
    #deps = [ ":fuchsia($default_toolchain)" ]
  }
}

# The assembly operation should only be defined in the default (fuchsia)
# toolchain.
if (is_fuchsia) {
  assert(current_toolchain == default_toolchain,
         "//build/images/* are only valid in the Fuchsia toolchain")

  # This catches inadvertent dependencies on the "fucshia" assembly implementation
  # in bringup-only configurations.
  assert(
      !use_bringup_assembly,
      "The bringup product config cannot rely on the fuchsia assembly.  Bringup has it's own in //build/images/bringup")

  ##################################################
  # Shared parameters across assemblies in this file
  ##################################################

  files = {
    images_config = "$target_out_dir/fuchsia.images_config.json"
    partitions_config = "$target_out_dir/partitions_config.json"
    assembly_inputs = "$target_out_dir/fuchsia/assembly_inputs.json"
    platform_aib_dir = get_label_info("//bundles/assembly", "target_out_dir")

    # Outputs of assembly.
    outdir = target_out_dir + "/fuchsia"
    zbi = "${outdir}/fuchsia.zbi"
    zbi_signed = "${outdir}/fuchsia.zbi.signed"
    vbmeta = "${outdir}/fuchsia.vbmeta"
    minfs = "${outdir}/data.blk"
    blobfs = "${outdir}/blob.blk"
    fvm = "${outdir}/fvm.blk"
    fvm_sparse = "${outdir}/fvm.sparse.blk"
    fvm_blob_sparse = "${outdir}/fvm.blob.sparse.blk"
    fvm_fastboot = "${outdir}/fvm.fastboot.blk"
    packages_json = "${outdir}/packages.json"
    images_json = "${outdir}/images.json"

    # Outputs of prime assembly.
    outdir_prime = target_out_dir + "/fuchsia_prime_assembly"
    images_prime_json = "${outdir_prime}/images.json"
    packages_prime_json = "${outdir_prime}/packages.json"

    # Outputs of recovery.
    recovery_name = get_label_info(recovery_label, "name")
    recovery_outdir =
        get_label_info(recovery_label, "target_out_dir") + "/" + recovery_name
    recovery_zbi = "${recovery_outdir}/${recovery_name}.zbi"
    recovery_zbi_signed = "${recovery_outdir}/${recovery_name}.zbi.signed"
    recovery_vbmeta = "${recovery_outdir}/${recovery_name}.vbmeta"
    recovery_blobfs = "${recovery_outdir}/blob.blk"
    recovery_fvm = "${recovery_outdir}/fvm.blk"
    recovery_fvm_sparse = "${recovery_outdir}/fvm.sparse.blk"
    recovery_fvm_blob_sparse = "${recovery_outdir}/fvm.blob.sparse.blk"
    recovery_fvm_fastboot = "${recovery_outdir}/fvm.fastboot.blk"
    recovery_images_json = "${recovery_outdir}/images.json"

    esp_image_path = "${root_build_dir}/fuchsia.esp.blk"

    if (target_cpu == "x64") {
      qemu_kernel = "$root_build_dir/multiboot.bin"
    } else {
      qemu_kernel = "$root_build_dir/qemu-boot-shim.bin"
    }
  }

  fuchsia_base = {
    testonly = fuchsia_zbi_testonly
    generate_fvm = true
    board_name = board_name
    output_dir = root_build_dir
    fshost_config = board_fshost_config

    check_production_tag = check_production_eligibility

    # In non-prod builds, allow use of the eng platform bundles.
    allow_eng_platform_bundle_use = !check_production_eligibility

    include_component_id_index = true

    # Use the GN arg for including shell commands.
    include_shell_commands = include_shell_commands_package

    # If the product definition is specifying a product assembly config file, then
    # pass that to the assembled_system() call(s).
    if (fuchsia_product_assembly_config_file != false) {
      product_assembly_config_file = fuchsia_product_assembly_config_file
    } else {
      product_assembly_config_file =
          "//build/assembly/empty_product_config.json"
    }
    if (fuchsia_product_assembly_config_label != false) {
      product_assembly_config_dep = fuchsia_product_assembly_config_label
    }

    cmdline_deps = [ "//build/input:bootfs" ]

    base_packages = [
      "//:developer_base_driver_packages",
      "//:developer_base_packages",
      "//:legacy_base_packages",
      ":core_realm",
    ]

    # Include the product-defined meta package labels.
    meta_packages = meta_package_labels

    # Add the base package labels for the selected SWD policy.
    if (defined(policy_labels.swd)) {
      foreach(policy, policies_swd) {
        if (policy.name == policy_labels.swd) {
          base_packages += policy.base_package_deps
        }
      }
    }

    # Add the base package labels for the selected SWD policy.
    extra_core_realm_shards = []
    if (defined(policy_labels.update_checker)) {
      foreach(policy, policies_update_checker) {
        if (policy.name == policy_labels.update_checker) {
          # TODO(fxbug.dev/93695) Remove the need for
          # `if_board_supports_update_configurator` and
          # `board_supports_update_configurator`, replacing them with an
          # optional capability route when that feature is available.
          if (board_supports_update_configurator) {
            inner = policy.if_board_supports_update_configurator

            extra_core_realm_shards += inner.core_realm_shards
          }
        }
      }
    }

    base_driver_packages = [
      "//:developer_base_driver_packages",
      "//:legacy_base_driver_packages",
    ]
    cache_packages = [
      "//:developer_cache_packages",
      "//:legacy_cache_packages",
    ]
    system_image_deps = [ "//build/input:system_image" ]
    universe_packages = [
      "//:developer_universe_packages",
      "//:legacy_universe_packages",
    ]
    bootfs_labels = [ "//build/input:bootfs" ]
    bootfs_package_labels = product_bootfs_packages

    # minfs arguments
    if (minfs_product_minimum_data_bytes != false) {
      minfs_minimum_data_bytes = minfs_product_minimum_data_bytes
    } else if (minfs_board_minimum_data_bytes != false) {
      minfs_minimum_data_bytes = minfs_board_minimum_data_bytes
    }
    if (minfs_product_maximum_bytes != false) {
      minfs_maximum_bytes = minfs_product_maximum_bytes
    } else if (minfs_board_maximum_bytes != false) {
      minfs_maximum_bytes = minfs_board_maximum_bytes
    }

    # blobfs arguments
    assembly_blob_layout_format = blob_layout_format
    assembly_compress_blobs = compress_blobs
    if (blobfs_product_minimum_inodes != false) {
      blobfs_minimum_inodes = blobfs_product_minimum_inodes
    } else if (blobfs_board_minimum_inodes != false) {
      blobfs_minimum_inodes = blobfs_board_minimum_inodes
    }
    if (blobfs_product_minimum_data_bytes != false) {
      blobfs_minimum_data_bytes = blobfs_product_minimum_data_bytes
    } else if (blobfs_board_minimum_data_bytes != false) {
      blobfs_minimum_data_bytes = blobfs_board_minimum_data_bytes
    }
    if (blobfs_product_maximum_bytes != false) {
      blobfs_maximum_bytes = blobfs_product_maximum_bytes
    } else if (blobfs_board_maximum_bytes != false) {
      blobfs_maximum_bytes = blobfs_board_maximum_bytes
    }
    if (max_blob_contents_size != false) {
      blobfs_maximum_contents_size = max_blob_contents_size
    }

    # fvm arguments
    assembly_include_account_in_fvm = include_account_in_fvm
    assembly_fvm_slice_size = fvm_slice_size

    # zbi arguments
    if (custom_signing_script != "") {
      zbi_signing_script = custom_signing_script
      zbi_signing_script_deps = [ "//build/images/custom_signing:deps" ]
    } else if (use_vboot) {
      zbi_signing_script = vboot_action.script
      zbi_signing_args = vboot_action.args
      zbi_signing_script_deps = vboot_action.deps
      inputs = vboot_action.inputs
    }
  }

  core_realm("core_realm") {
    package_name = core_realm_package_name
    deps = core_realm_shards + board_core_realm_shards +
           fuchsia_base.extra_core_realm_shards
    restrict_persistent_storage = core_realm_restrict_persistent_storage
    restrict_core_env_to_base = !auto_update_packages
  }

  ##################################################
  # Fuchsia
  ##################################################

  # Copy the blob.blk to the old location for assembled images.
  # TODO(fxbug.dev/82077): Delete this once clients do not depend on the old path.
  copy("copy_blobfs") {
    testonly = fuchsia_zbi_testonly
    sources = [ "${target_out_dir}/fuchsia/blob.blk" ]
    outputs = [ old_blobfs_path ]
    deps = [ ":fuchsia_assembly" ]
  }

  # Copy the fvm.blob.sparse.blk to the old location for assembled images.
  # TODO(fxbug.dev/82077): Delete this once clients do not depend on the old path.
  copy("copy_fvm_blob_sparse") {
    testonly = fuchsia_zbi_testonly
    sources = [ "${target_out_dir}/fuchsia/fvm.blob.sparse.blk" ]
    outputs = [ old_fvm_blob_sparse_path ]
    deps = [ ":fuchsia_assembly" ]
  }

  group("fuchsia") {
    testonly = fuchsia_zbi_testonly
    public_deps = [
      ":assembly_inputs",
      ":copy_blobfs",
      ":fuchsia_assembly",
      ":product_bundle",
      ":update",
    ]

    if (include_fvm_blob_sparse) {
      public_deps += [ ":copy_fvm_blob_sparse" ]
    }

    deps = [ ":scrutiny_verifiers" ]

    metadata = {
      # Only include the update package(s) manifest(s) in metadata collection,
      # the base & cache packages all need to be taken from the `images.json`
      # output of assembly.
      package_barrier = [ ":update" ]
      assembly_inputs = [
        {
          path = rebase_path(files.assembly_inputs, root_build_dir)
        },
      ]
    }
  }

  assembled_system("fuchsia_assembly") {
    forward_variables_from(fuchsia_base, "*")
    image_name = "fuchsia"

    create_legacy_aib_package = !is_debug
    create_legacy_aib_archive = !is_debug

    image_metadata_overrides = legacy_fuchsia_image_metadata_overrides
  }

  generated_partitions_config("partitions_config") {
    testonly = fuchsia_zbi_testonly
    output_path = files.partitions_config
    hw_revision = board_name
    bootstrap_partitions = bootstrap_files
    unlock_credentials = board_fastboot_unlock_credentials

    if (use_gigaboot) {
      esp_image_path = files.esp_image_path
      deps = [ "//build/images/flash:esp" ]
    }
  }

  # Create a list of the files that are referenced by the partitions config.
  _partitions_contents = board_fastboot_unlock_credentials
  foreach(file, bootstrap_files) {
    _partitions_contents += [ file.path ]
  }
  foreach(file, firmware_prebuilts) {
    _partitions_contents += [ "${file.path}${firmware_prebuilts_path_suffix}" ]
  }
  if (use_gigaboot) {
    _partitions_contents += [ files.esp_image_path ]
  }

  update_package("update") {
    testonly = fuchsia_zbi_testonly
    deps = [
      ":fuchsia_assembly",
      ":partitions_config",
      "//build/info:latest-commit-date",
      recovery_label,
    ]
    partitions = files.partitions_config
    packages = files.packages_json
    system_a = files.images_json
    system_r = files.recovery_images_json
    board_name = board_name
    version_file = build_info_files.version
    epoch = "1"
  }

  product_bundle("product_bundle") {
    testonly = fuchsia_zbi_testonly
    deps = [
      ":fuchsia_assembly",
      ":partitions_config",
      recovery_label,
    ]
    partitions = files.partitions_config
    system_a = files.images_json
    system_r = files.recovery_images_json

    # Create lists of the files that are referenced by the assembly manifests.
    _assembly_contents = [
      files.blobfs,
      files.fvm,
      files.fvm_sparse,
      files.fvm_blob_sparse,
      files.qemu_kernel,
    ]
    _recovery_contents = [ files.qemu_kernel ]
    if (files.recovery_name != "zedboot") {
      _recovery_contents += [
        files.recovery_blobfs,
        files.recovery_fvm,
        files.recovery_fvm_sparse,
        files.recovery_fvm_blob_sparse,
      ]
    }
    if (fvm_emmc_partition_size != false || fvm_ftl_nand_block_count != false) {
      _assembly_contents += [ files.fvm_fastboot ]
      if (files.recovery_name != "zedboot") {
        _recovery_contents += [ files.recovery_fvm_fastboot ]
      }
    }
    if (custom_signing_script != "" || use_vboot) {
      _assembly_contents += [ files.zbi_signed ]
      _recovery_contents += [ files.recovery_zbi_signed ]
    } else {
      _assembly_contents += [ files.zbi ]
      _recovery_contents += [ files.recovery_zbi ]
    }
    if (use_vbmeta) {
      _assembly_contents += [ files.vbmeta ]
      _recovery_contents += [ files.recovery_vbmeta ]
    }

    # List the contents of each input so that the outputs can be calculated.
    partitions_contents = _partitions_contents
    system_a_contents = _assembly_contents
    system_r_contents = _recovery_contents
  }

  generated_assembly_inputs("assembly_inputs") {
    testonly = fuchsia_zbi_testonly
    images_config = files.images_config
    partitions_config = files.partitions_config
    output_path = files.assembly_inputs

    sources = [
      "${files.outdir}/legacy",
      "${files.platform_aib_dir}/common_minimal",
      "${files.platform_aib_dir}/common_minimal_eng",
      "${files.platform_aib_dir}/emulator_support",
    ]

    deps = [
      ":fuchsia_assembly",
      ":partitions_config",
    ]

    product_config = fuchsia_base.product_assembly_config_file
    if (fuchsia_product_assembly_config_label != false) {
      deps += [ fuchsia_product_assembly_config_label ]
    }
  }

  ##################################################
  # Fuchsia Prime
  ##################################################

  # Generate a fuchsia_prime system, which is the same as a fuchsia system, but
  # with the addition of a dummy file in the system_image package. This can be
  # used for OTA tests.
  resource("dummy_resources") {
    sources = [ "dummy.txt" ]
    outputs = [ "dummy.txt" ]
    visibility = [ ":*" ]
  }
  fuchsia_package("dummy") {
    deps = [ ":dummy_resources" ]
    visibility = [ ":*" ]
  }

  assembled_system("fuchsia_prime_assembly") {
    forward_variables_from(fuchsia_base, "*")
    system_image_deps += [ ":dummy" ]
    base_package_name = "system_image_prime"
  }

  update_package("update_prime") {
    testonly = fuchsia_zbi_testonly
    deps = [
      ":fuchsia_prime_assembly",
      ":partitions_config",
      "//build/info:latest-commit-date",
      recovery_label,
    ]
    name = "update_prime"
    partitions = files.partitions_config
    packages = files.packages_prime_json
    system_a = files.images_prime_json
    system_r = files.recovery_images_json
    board_name = board_name
    version_file = build_info_files.version
    epoch = "1"
  }

  group("fuchsia_prime") {
    testonly = fuchsia_zbi_testonly
    public_deps = [
      ":fuchsia_prime_assembly",
      ":update_prime",
    ]
    metadata = {
      # This is created by the `:fuchsia_prime_assembly` target, but
      # assembled_system() doesn't produce any metadata about the packages that
      # it makes, as we don't have enough information about that at GN time.
      #
      # So here we note what the package_manifest created by our dep will be,
      # and that works because this area is fairly tightly-coupled.
      package_output_manifests = [
        "${target_out_dir}/fuchsia_prime_assembly/base/package_manifest.json",
      ]
    }
  }

  ##################################################
  # Netboot
  ##################################################

  # This rolls the primary ZBI together with a compressed RAMDISK image of
  # fvm.blk into a fat ZBI that boots the full system without using any real
  # storage.  The system decompresses the fvm.blk image into memory and then
  # sees that RAM disk just as if it were a real disk on the device.
  assembled_system("netboot") {
    forward_variables_from(fuchsia_base, "*")
    ramdisk_fvm_in_zbi = true

    image_metadata_overrides = {
      zbi = {
        name = "netboot"
        archive = enable_netboot
        bootserver_netboot = [ "--boot" ]
      }
    }
  }

  scrutiny_verifiers("scrutiny_verifiers") {
    assembly_image_name = "fuchsia"
    update_package_target = ":update"

    route_sources_config = fuchsia_route_sources_config
    static_pkgs_goldens = fuchsia_static_pkgs_goldens
    structured_config_policy = fuchsia_structured_config_policy
    verify_component_resolvers_allowlist =
        fuchsia_verify_component_resolvers_allowlist
    verify_routes_component_tree_config =
        fuchsia_verify_routes_component_tree_config
    verify_routes_exceptions_allowlist =
        fuchsia_verify_routes_exceptions_allowlist
    verify_routes_exceptions_allowlist_bootfs =
        fuchsia_verify_routes_exceptions_allowlist_bootfs
    verify_routes_exceptions_allowlist_product =
        fuchsia_verify_routes_exceptions_allowlist_product
    zbi_bootfs_filelist_goldens = fuchsia_zbi_bootfs_filelist_goldens
    zbi_bootfs_packages_goldens = fuchsia_zbi_bootfs_packages_goldens
    zbi_kernel_cmdline_goldens = fuchsia_zbi_kernel_cmdline_goldens

    forward_variables_from(fuchsia_base,
                           [
                             "product_assembly_config_dep",
                             "testonly",
                             "product_assembly_config_file",
                           ])
  }
}
