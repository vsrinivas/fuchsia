# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dev.gni")
import("//src/developer/ffx/build/ffx_action.gni")

# Extract the structured configuration values in a system image.
#
# Parameters
#
#   product_bundle (required)
#     [label] The label of a product_bundle target.
#
#   output (required)
#     [path] The JSON file to which to write the extracted values.
#
#   deps, public_deps, data_deps (optional)
#     Usual GN meaning.
template("extract_assembled_structured_config") {
  assert(defined(invoker.product_bundle),
         "extract_assembled_structured_config() must specify product_bundle")
  assert(defined(invoker.output),
         "extract_assembled_structured_config() must specify an output")

  ffx_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])
    depfile = "$target_gen_dir/$target_name.d"

    pb_name = get_label_info(invoker.product_bundle, "name")
    pb_out = get_label_info(invoker.product_bundle, "target_out_dir")
    pb_dir = "$pb_out/$pb_name"
    pb_manifest = "$pb_dir/product_bundle.json"

    inputs = [ pb_manifest ]
    outputs = [ invoker.output ]

    args = [
      "scrutiny",
      "extract",
      "structured-config",
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--build-path",
      rebase_path(root_build_dir, root_build_dir),
      "--product-bundle",
      rebase_path(pb_dir, root_build_dir),
      "--output",
      rebase_path(invoker.output, root_build_dir),
    ]

    if (!defined(invoker.deps)) {
      deps = []
    }
    deps += [ invoker.product_bundle ]
  }
}

# Check structured configuration values in a system image, asserting they match
# the provided policy file.
#
# Parameters
#
#   product_bundle (required)
#     [label] The label of a product_bundle target.
#
#   policy (required)
#     [path] The JSON policy file containing allowed configuration values for the build.
#
#   deps, public_deps, data_deps (optional)
#     Usual GN meaning.
template("verify_assembled_structured_config") {
  assert(defined(invoker.product_bundle),
         "verify_assembled_structured_config() must specify product_bundle")
  assert(defined(invoker.policy),
         "verify_assembled_structured_config() must specify a policy file")

  ffx_action(target_name) {
    no_output_dir_leaks = false

    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])
    stamp_file = "$target_gen_dir/$target_name.verified"
    depfile = "$target_gen_dir/$target_name.d"
    tmp_dir = "$target_out_dir/$target_name/tmp"

    pb_name = get_label_info(invoker.product_bundle, "name")
    pb_out = get_label_info(invoker.product_bundle, "target_out_dir")
    pb_dir = "$pb_out/$pb_name"
    pb_manifest = "$pb_dir/product_bundle.json"

    hermetic_action_ignored_prefixes = [ tmp_dir ]

    inputs = [
      pb_manifest,
      invoker.policy,
    ]
    outputs = [ stamp_file ]

    args = [
      "scrutiny",
      "verify",
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--stamp",
      rebase_path(stamp_file, root_build_dir),
      "--tmp-dir",
      rebase_path(tmp_dir, root_build_dir),
      "structured-config",
      "--policy",
      rebase_path(invoker.policy, root_build_dir),
      "--product-bundle",
      rebase_path(pb_dir, root_build_dir),
    ]

    if (!defined(invoker.deps)) {
      deps = []
    }
    deps += [ invoker.product_bundle ]
  }
}
