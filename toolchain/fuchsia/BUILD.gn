# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/clang_toolchain.gni")
import("//packages/package.gni")

clang_toolchain_suite(target_cpu) {
  toolchain_cpu = target_cpu
  toolchain_os = target_os

  use_strip = true
}

package("cpp") {
  files = exec_script("//build/clang_print_file_name.py",
                      [
                        "--target=${clang_target}",
                        "--prefix=${clang_prefix}",
                        "libc++.so.2",
                        "libc++abi.so.1",
                        "libunwind.so.1",
                      ],
                      "trim list lines")
  libraries = []
  foreach(file, files) {
    libraries += [ {
          name = get_path_info(file, "file")
          source = file
          used_by_early_boot_drivers = true
        } ]
  }
}
