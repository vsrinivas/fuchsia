# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/clang_toolchain.gni")
import("//build/config/fuchsia/config.gni")
import("//packages/package.gni")

template("fuchsia_toolchain") {
  clang_toolchain(target_name) {
    use_strip = true
    forward_variables_from(invoker, "*")

    toolchain_cpu = target_cpu
    toolchain_os = target_os
  }
}

fuchsia_toolchain(target_cpu) {
}

fuchsia_toolchain("${target_cpu}-shared") {
  is_pic_default = true
}

if (current_cpu == "arm64") {
  clang_target = "aarch64-fuchsia"
} else if (current_cpu == "x64") {
  clang_target = "x86_64-fuchsia"
} else {
  assert(false, "Unsupported architecture")
}

package("cpp") {
  files = exec_script("//build/clang_print_file_name.py",
                      [
                        "--target=${clang_target}",
                        "--prefix=${clang_prefix}",
                        "libc++.so.2",
                        "libc++abi.so.1",
                        "libunwind.so.1",
                      ],
                      "trim list lines")
  libraries = []
  foreach(file, files) {
    libraries += [
      {
        name = get_path_info(file, "file")
        source = file
        used_by_early_boot_drivers = true
      }
    ]
  }
}
