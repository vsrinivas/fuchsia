# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/variant.gni")

config("frame_pointers") {
  cflags = [ "-fno-omit-frame-pointer" ]
  ldflags = cflags
}

variant("asan") {
  common_flags = [ "-fsanitize=address" ]

  # ASan wants frame pointers because it captures stack traces
  # on allocations and such, not just on errors.
  configs = [ ":frame_pointers" ]
}

variant("ubsan") {
  common_flags = [ "-fsanitize=undefined" ]
}

variant("fuzzer") {
  common_flags = [ "-fsanitize=fuzzer" ]
}

variant("sancov") {
  common_flags = [ "-fsanitize-coverage=trace-pc-guard" ]
}

# asan_no_detect_leaks is just asan, but it acts by default as if
# ASAN_OPTIONS=detect_leaks=0 were in the environment at runtime.
variant("asan_no_detect_leaks") {
  configs = [ ":asan" ]
  deps = [ ":no_detect_leaks" ]
}

source_set("no_detect_leaks") {
  visibility = [ ":*" ]
  sources = [ "asan_no_detect_leaks.c" ]
}
