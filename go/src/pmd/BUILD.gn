# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/go/toolchain.gni")
import("//build/host.gni")
import("//build/package.gni")

go_library("pmd") {
  name = "fuchsia.googlesource.com/pmd"

  deps = [
    "//garnet/go/src/far",
    "//garnet/go/src/merkle",
    "//garnet/go/src/pm:pm_lib",
    "//garnet/go/src/thinfs:thinfs_lib",
    "//garnet/public/fidl/fuchsia.amber($go_toolchain)",
    "//garnet/public/fidl/fuchsia.sys($go_toolchain)",
    "//garnet/public/lib/component/go/src/app",
    "//garnet/public/lib/fidl/go/src/fidl/bindings",
  ]
}

go_binary("pkgsvr_bin") {
  output_name = "pkgsvr"
  gopackage = "fuchsia.googlesource.com/pmd/cmd/pkgsvr"
  extra_dependencies = [
    ":pmd",
  ]
}

go_binary("installblob_bin") {
  output_name = "installblob"
  gopackage = "fuchsia.googlesource.com/pmd/cmd/installblob"
  extra_dependencies = [ ":pmd" ]
}

test_packages = [
  "pkgfs",
  "index",
]
foreach(testpkg, test_packages) {
  go_test("pmd_${testpkg}_test") {
    gopackage = "fuchsia.googlesource.com/pmd/${testpkg}"
    extra_dependencies = [ ":pmd", ]
    non_go_deps = [
      "//zircon/public/lib/fs-management",
    ]
  }
}

package("pkgsvr") {
  deps = [
    ":pkgsvr_bin",
  ]

  binaries = [
    {
      name = "pkgsvr"
    },
  ]
}

package("installblob") {
  deprecated_system_image = true

  deps = [
    ":installblob_bin",
  ]

  binaries = [
    {
      name = "installblob"
    },
  ]
}

package("pmd_tests") {
  testonly = true

  deps = []
  tests = []

  foreach(testpkg, test_packages) {
    deps += [ ":pmd_${testpkg}_test" ]
    tests += [
      {
        name = "pmd_${testpkg}_test"
      },
    ]
  }
}
