# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_test.gni")
import("//build/go/toolchain.gni")
import("//build/host.gni")
import("//build/package.gni")

common_dependencies = [
  {
    package = "fuchsia.googlesource.com/pmd"
  },
  {
    package = "fuchsia.googlesource.com/pm"
    source = "//garnet/go/src/pm"
  },
  {
    package = "fuchsia.googlesource.com/far"
    source = "//garnet/go/src/far"
  },
  {
    package = "fuchsia.googlesource.com/merkle"
    source = "//garnet/go/src/merkle"
  },
  {
    package = "thinfs"
    source = "//garnet/go/src/thinfs"
  },
  {
    package = "golang.org/x/crypto"
    source = "//third_party/golang/crypto"
  },
]

go_binary("pkgsvr_bin") {
  output_name = "pkgsvr"
  gopackage = "fuchsia.googlesource.com/pmd/cmd/pkgsvr"
  extra_dependencies = [
    "//garnet/public/lib/amber/fidl($go_toolchain)",
    "//garnet/public/lib/app/fidl($go_toolchain)",
    "//garnet/public/lib/app/go/src/app",
    "//garnet/public/lib/fidl/go/src/fidl/bindings",
  ]
  go_dependencies = common_dependencies
}

go_binary("installblob_bin") {
  output_name = "installblob"
  gopackage = "fuchsia.googlesource.com/pmd/cmd/installblob"
  go_dependencies = common_dependencies
}

test_packages = [
  "pkgfs",
  "index",
]
foreach(testpkg, test_packages) {
  go_test("pmd_${testpkg}_test") {
    gopackage = "fuchsia.googlesource.com/pmd/${testpkg}"
    extra_dependencies = [
      "//garnet/public/lib/amber/fidl($go_toolchain)",
      "//garnet/public/lib/app/fidl($go_toolchain)",
      "//garnet/public/lib/app/go/src/app",
      "//garnet/public/lib/fidl/go/src/fidl/bindings",
    ]
    deps = [
      "//zircon/public/lib/fs-management",
    ]
    go_dependencies = common_dependencies
  }
}

package("pkgsvr") {
  deps = [
    ":pkgsvr_bin",
  ]

  binaries = [
    {
      name = "pkgsvr"
    },
  ]
}

package("installblob") {
  deprecated_system_image = true

  deps = [
    ":installblob_bin",
  ]

  binaries = [
    {
      name = "installblob"
    },
  ]
}

package("pmd_tests") {
  testonly = true

  deps = []
  tests = []

  foreach(testpkg, test_packages) {
    deps += [ ":pmd_${testpkg}_test" ]
    tests += [
      {
        name = "pmd_${testpkg}_test"
      },
    ]
  }
}
