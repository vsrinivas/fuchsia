# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")

template("fidl_example") {
  not_needed(invoker, "*")
  fidl(target_name) {
    nick = target_name
    if (defined(invoker.nick)) {
      nick = invoker.nick
    }
    name = "fidl.examples.$nick"

    sources = [
      "//zircon/system/host/fidl/examples/${target_name}.fidl"
    ]
  }
}

fidl_example("alignment") {}
fidl_example("empty") {}
fidl_example("enums") {}
fidl_example("events") {}
fidl_example("example-0") { nick = "example0" }
fidl_example("example-1") { nick = "example1" }
fidl_example("example-2") { nick = "example2" }
fidl_example("example-3") { nick = "example3" }
fidl_example("example-4") { nick = "example4" }
fidl_example("example-5") { nick = "example5" }
fidl_example("example-6") { nick = "example6" }
fidl_example("example-7") { nick = "example7" }
fidl_example("example-8") { nick = "example8" }
fidl_example("example-9") { nick = "example9" }
fidl_example("interface-ordinals") { nick = "ordinals" }
fidl_example("tables") {}
fidl_example("test") {}
fidl_example("types") {}

fidl("point") {
  name = "fidl.examples.libraryA"

  sources = [
    "//zircon/system/host/fidl/examples/library-a/point.fidl"
  ]
}

fidl("view") {
  name = "fidl.examples.libraryB"

  sources = [
    "//zircon/system/host/fidl/examples/library-b/view.fidl"
  ]

  deps = [
    ":point",
  ]
}

group("examples") {
  deps = [
    ":alignment",
    ":enums",
    ":events",
    ":example-0",
    ":example-1",
    ":example-2",
    ":example-3",
    ":example-4",
    ":example-5",
    ":example-6",
    ":example-7",
    ":example-8",
    ":example-9",
    ":interface-ordinals",
    ":tables",
    ":test",
    ":types",
    ":point",
    ":view",
  ]
}
