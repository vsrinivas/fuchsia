# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/package.gni")
import("//build/testing/environments.gni")

executable("tests") {
  testonly = true

  output_name = "libdriver-integration-test"

  sources = [
    "action-list.cc",
    "basic-lifecycle-test.cc",
    "integration-test.cc",
    "main.cc",
    "mock-device-hooks.cc",
    "mock-device-thread.cc",
    "mock-device.cc",
    "root-mock-device.cc",
  ]

  deps = [
    ":mock-device",
    "//garnet/public/lib/async_promise",
    "//garnet/public/lib/gtest",
    "//zircon/public/fidl/fuchsia-device-test:fuchsia-device-test",
    "//zircon/public/fidl/fuchsia-device-test:fuchsia-device-test_c",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/devmgr-integration-test",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/fit",
  ]
}

package("libdriver-integration-test") {
  testonly = true

  deps = [
    ":tests",
  ]
  tests = [
    {
      name = "libdriver-integration-test"
      environments = basic_envs
    },
  ]
}

fidl("mock-device") {
  name = "fuchsia.device.mock"

  sources = [
    "//zircon/system/dev/test/mock-device/mock-device.fidl",
  ]

  deps = []
}
