# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//garnet/lib/magma/gnbuild/magma.gni")

source_set("address_range") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_address_range.cc",
  ]

  deps = [
    "//garnet/lib/magma/src/magma_util",
    "//zircon/public/lib/zx",
  ]

  libs = [ "zircon" ]
}

source_set("buffer") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_buffer.cc",
    "zircon_platform_buffer.h",
  ]

  public_deps = [
    ":handle",
    "..:buffer_header",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$magma_build_root/src/magma_util/platform:trace",
    "..:object",
  ]
}

source_set("bus_mapper") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_bus_mapper.cc",
    "zircon_platform_bus_mapper.h",
  ]

  public_deps = [
    ":buffer",
    "//garnet/lib/magma/src/magma_util/platform:bus_mapper_header",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$magma_build_root/src/magma_util/platform:trace",
    "$zircon_build_root/public/lib/ddk",
    "..:object",
  ]
}

source_set("interrupt") {
  sources = [
    "zircon_platform_interrupt.h",
  ]

  public_deps = [
    "$zircon_build_root/public/lib/zx",
  ]
}

source_set("device") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_device.cc",
    "zircon_platform_device.h",
    "zircon_platform_interrupt.h",
    "zircon_platform_mmio.cc",
    "zircon_platform_mmio.h",
    "zircon_platform_pci_device.cc",
    "zircon_platform_pci_device.h",
  ]

  deps = [
    ":bus_mapper",
    ":interrupt",
    "$magma_build_root/src/magma_util",
    "$zircon_build_root/public/lib/ddk",
    "$zircon_build_root/public/lib/zx",
    "..:device_header",
    "//src/devices/host:driver",
    "//zircon/public/lib/device-protocol-pci",
    "//zircon/public/lib/device-protocol-platform-device",
    "//zircon/system/banjo/ddk.protocol.composite",
    "//zircon/system/banjo/ddk.protocol.pci",
    "//zircon/system/banjo/ddk.protocol.platform.device",
  ]
}

source_set("futex") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_futex.cc",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$zircon_build_root/public/lib/zx",
  ]
}

source_set("connection_client") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_connection_client.cc",
  ]

  deps = [
    "$magma_build_root/include:msd_abi",
    "$magma_build_root/src/magma_util",
    "$zircon_build_root/public/lib/fdio",
    "$zircon_build_root/public/lib/zx",
    "..:connection_header",
    "..:trace",
    "//sdk/fidl/fuchsia.gpu.magma",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_c",
  ]
}

source_set("connection") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_connection.cc",
  ]

  deps = [
    "$magma_build_root/include:msd_abi",
    "$magma_build_root/src/magma_util",
    "$zircon_build_root/public/lib/async-loop-cpp",
    "$zircon_build_root/public/lib/async-loop-default",
    "$zircon_build_root/public/lib/zx",
    "..:connection_header",
    "//sdk/fidl/fuchsia.gpu.magma",
  ]
}

source_set("device_client") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_device_client.cc",
  ]

  deps = [
    "..:device_client_header",
    "//garnet/lib/magma/include:msd_abi",
    "//garnet/lib/magma/src/magma_util",
    "//sdk/fidl/fuchsia.gpu.magma",
    "//zircon/public/lib/zx",
  ]
}

source_set("sysmem_connection") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_sysmem_connection.cc",
  ]

  deps = [
    ":buffer",
    "..:sysmem_connection_header",
    "..:trace",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-sysmem",
  ]
}

source_set("thread") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_thread.cc",
  ]

  deps = [
    ":handle",
    "..:object",
    "..:thread_header",
  ]
}

source_set("event") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_event.cc",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$zircon_build_root/public/lib/zx",
  ]
}

source_set("handle") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_handle.cc",
    "zircon_platform_handle.h",
  ]

  public_deps = [
    "$zircon_build_root/public/lib/zx",
    "..:handle_header",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
  ]
}

source_set("object") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_object.cc",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
  ]
}

source_set("semaphore") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_semaphore.cc",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$magma_build_root/src/magma_util/platform:trace",
    "$zircon_build_root/public/lib/zx",
    "..:object",
  ]
}

source_set("port") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_port.cc",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$zircon_build_root/public/lib/fit",
    "$zircon_build_root/public/lib/zx",
  ]
}

source_set("trace") {
  configs += [
    "..:platform_include_config",
    "..:tracing",
  ]

  sources = [
    "zircon_platform_trace.cc",
    "zircon_platform_trace.h",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "..:trace_header",
  ]

  public_deps = [
    "//zircon/public/lib/fit",
  ]

  if (magma_enable_tracing) {
    deps += [
      "$zircon_build_root/public/lib/async-cpp",
      "$zircon_build_root/public/lib/async-loop-cpp",
      "$zircon_build_root/public/lib/async-loop-default",
      "$zircon_build_root/public/lib/trace-vthread-with-static-engine",
      "$zircon_build_root/public/lib/trace-with-static-engine",
    ]
  }
}

source_set("trace_provider") {
  configs += [
    "..:platform_include_config",
    "..:tracing",
  ]

  sources = [
    "zircon_platform_trace_provider.cc",
    "zircon_platform_trace_provider.h",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "..:trace_provider_header",
  ]

  public_deps = [
    "//zircon/public/lib/fit",
  ]

  if (magma_enable_tracing) {
    deps += [ "$zircon_build_root/public/lib/async-cpp" ]
    public_deps += [
      "$zircon_build_root/public/lib/async-loop-cpp",
      "$zircon_build_root/public/lib/async-loop-default",
      "//zircon/public/lib/trace-provider-with-static-engine",
    ]
  }
  assert_no_deps = [ "//zircon/public/lib/fdio" ]
}

source_set("trace_provider_with_fdio") {
  configs += [
    "..:platform_include_config",
    "..:tracing",
  ]

  sources = [
    "zircon_platform_trace_provider_with_fdio.cc",
  ]

  deps = [
    ":trace_provider",
    "$magma_build_root/src/magma_util",
    "..:trace_provider_with_fdio_header",
    "//zircon/public/lib/fdio",
  ]

  public_deps = [
    "//zircon/public/lib/fit",
  ]
}

source_set("random") {
  deps = [
    "..:random_header",
  ]
  sources = [
    "zircon_platform_random.cc",
  ]
}
