# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//garnet/lib/magma/gnbuild/magma.gni")

group("magma-vulkan") {
  if (magma_enable_developer_build) {
    testonly = true
  }
  public_deps = [
    ":magma-qcom-adreno",
  ]
  if (build_libvulkan_qcom_adreno != []) {
    public_deps += [ ":magma-qcom-adreno-config" ]
  }
}

package("magma-qcom-adreno") {
  deprecated_system_image = true
  if (magma_enable_developer_build) {
    testonly = true
  }

  if (build_libvulkan_qcom_adreno != []) {
    deps = build_libvulkan_qcom_adreno
    libraries = [
      {
        name = "libvulkan_adreno.so"
      },
    ]
  }
}

if (build_libvulkan_qcom_adreno != []) {
  # api_version must be present but is unused
  icd_data = [
    "{",
    "\"file_format_version\": \"1.0.0\",",
    "\"ICD\": {",
    "\"library_path\": \"libvulkan_adreno.so\",",
    "\"api_version\": \"0.0.0\"",
    "}",
    "}",
  ]
  write_file("$target_gen_dir/vulkan/icd.d/libvulkan_adreno.json", icd_data)
  config_data("magma-qcom-adreno-config") {
    for_pkg = "vulkan-icd"
    sources = [
      rebase_path("$target_gen_dir/vulkan/icd.d/libvulkan_adreno.json"),
    ]
    outputs = [
      "icd.d/{{source_file_part}}",
    ]
  }
}
