# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_binary("intl_property_manager_bin") {
  name = "intl_property_manager"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//garnet/examples/intl/manager/fidl:fuchsia.examples.intl.manager-rustc",
    "//garnet/lib/rust/fidl_fuchsia_intl_ext",
    "//sdk/fidl/fuchsia.intl:fuchsia.intl-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:clap",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:structopt",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/main.rs" ]
}

package("intl_property_manager") {
  deps = [ ":intl_property_manager_bin" ]
  binaries = [
    {
      name = "intl_property_manager"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/intl_property_manager.cmx")
      dest = "intl_property_manager.cmx"
    },
    {
      path = rebase_path("meta/intl_property_manager_without_flags.cmx")
      dest = "intl_property_manager_without_flags.cmx"
    },
  ]
}

test_package("intl_property_manager_tests") {
  deps = [ ":intl_property_manager_bin_test" ]

  tests = [
    {
      name = "intl_property_manager_bin_test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":intl_property_manager",
    ":intl_property_manager_tests",
  ]
}
