# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")

source_set("use_media_decoder_source_set") {
  testonly = true

  sources = [
    "in_stream.cc",
    "in_stream_file.cc",
    "in_stream_http.cc",
    "in_stream_peeker.cc",
    "test/use_video_decoder_test.cc",
    "use_aac_decoder.cc",
    "use_aac_decoder.h",
    "use_video_decoder.cc",
    "use_video_decoder.h",
    "util.cc",
    "util.h",
  ]

  public_deps = [
    "//garnet/lib/media/codec_impl:fourcc",
    "//sdk/fidl/fuchsia.mediacodec",
    "//sdk/fidl/fuchsia.net.oldhttp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//third_party/boringssl:crypto",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fbl",
  ]

  deps = [
    "//garnet/lib/media/codec_impl:fourcc",
    "//garnet/lib/media/raw_video_writer:raw_video_writer",
    "//garnet/lib/media/test:codec_client",
    "//garnet/lib/media/test:frame_sink",
    "//garnet/public/lib/ui/base_view/cpp",
    "//garnet/public/lib/ui/scenic/cpp",
    "//sdk/lib/sys/cpp",
    "//src/media/audio/lib/wav_writer:wav_writer",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fbl",
  ]
}

executable("use_media_decoder") {
  testonly = true

  output_name = "use_media_decoder"

  sources = [
    "main.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
    "//garnet/lib/media/test:frame_sink",
  ]
}

# This test target uses the AAC decoder the same way as the example program and
# then also verifies the output data (without writing an output file).
executable("use_aac_decoder_test") {
  testonly = true
  output_name = "use_aac_decoder_test"

  sources = [
    "test/use_aac_decoder_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
    "//src/lib/fxl/test:gtest_main",
  ]
}

# This test target uses the h264 decoder the same way as the example program and
# then also verifies the output data (without writing an output file).
test("use_h264_decoder_test") {
  testonly = true

  output_name = "use_h264_decoder_test"

  sources = [
    "test/use_h264_decoder_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # In environments without an h264 HW decoder, this test will fall back to a SW
  # decoder.
  environments = [ astro_env ] + basic_envs
}

test("use_vp9_decoder_test") {
  testonly = true

  output_name = "use_vp9_decoder_test"

  sources = [
    "test/use_vp9_decoder_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # In contrast to use_h264_decoder_test, there is no SW fallback for VP9 at the
  # moment, so for now only list envs with HW decode.
  environments = [ astro_env ]
}

test("vp9_decoder_conformance_test") {
  testonly = true

  output_name = "vp9_decoder_conformance_test"

  sources = [
    "test/vp9_decoder_conformance_test.cc",
  ]

  deps = [
    ":use_media_decoder_source_set",
  ]

  # No VP9 SW fallback for now, so the test can only pass running on envs with
  # HW decode.
  environments = [ astro_env ]
}
