# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("timeline") {
  sources = [
    "type_converters.cc",
    "type_converters.h",
  ]

  public_deps = [
    ":no_converters",
    "//sdk/fidl/fuchsia.mediaplayer",
  ]
}

source_set("no_converters") {
  sources = [
    "timeline.h",
    "timeline_function.cc",
    "timeline_function.h",
    "timeline_rate.cc",
    "timeline_rate.h",
  ]

  public_deps = [
    "//garnet/public/lib/fxl",
  ]

  public_configs = [
    "//garnet/public:config",
  ]
}

executable("test_bin") {
  testonly = true
  output_name = "media_lib_timeline_tests"

  sources = [
    "test/timeline_function_test.cc",
    "test/timeline_rate_test.cc",
  ]

  deps = [
    ":timeline",
    "//garnet/public/lib/fsl",
    "//third_party/googletest:gtest_main",
  ]
}

test_package("media_lib_timeline_tests") {
  deps = [
    ":test_bin",
  ]

  tests = [
    {
      name = "media_lib_timeline_tests"
      environments = basic_envs
    },
  ]
}
