# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("transport") {
  sources = [
    "fifo_allocator.cc",
    "fifo_allocator.h",
    "mapped_shared_buffer.cc",
    "mapped_shared_buffer.h",
    "shared_buffer_set.cc",
    "shared_buffer_set.h",
    "shared_buffer_set_allocator.cc",
    "shared_buffer_set_allocator.h",
  ]

  public_deps = [
    "//garnet/public/lib/fxl",
    "//zircon/public/lib/zx",
  ]
}

executable("test_bin") {
  testonly = true
  output_name = "media_lib_transport_tests"

  sources = [
    "test/shared_buffer_set_allocator_test.cc",
    "test/shared_buffer_set_test.cc",
  ]

  deps = [
    ":transport",
    "//third_party/googletest:gtest_main",
  ]
}

test_package("media_lib_transport_tests") {
  deps = [
    ":test_bin",
  ]

  tests = [
    {
      name = "media_lib_transport_tests"
      environments = basic_envs
    },
  ]
}
