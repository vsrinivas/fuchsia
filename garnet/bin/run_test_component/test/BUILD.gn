# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test.gni")
import("//src/sys/component_index/component_index.gni")

group("test") {
  testonly = true
  deps = [ ":run_test_component_test-package" ]
}

test("bin") {
  output_name = "run_test_component_test"

  sources = [ "run_test_component_test.cc" ]

  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.debugdata",
    "//sdk/fidl/fuchsia.process",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/fdio",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_test_component("run_test_component_test-component") {
  component_name = "run_test_component_test"
  deps = [ ":bin" ]
  manifest = "meta/run_test_component_test.cmx"
}

# NOTE: This is not a test, and must be installed under bin/
# so use executable() instead of test() below.
executable("coverage_component") {
  testonly = true
  sources = [ "coverage_component.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.debugdata",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("coverage_component-component") {
  testonly = true
  component_name = "coverage_component"
  deps = [ ":coverage_component" ]
  manifest = "meta/coverage_component.cmx"
}

rustc_binary("component_to_test_standard_out") {
  name = "component_to_test_standard_out"
  source_root = "./component_to_test_standard_out.rs"
  edition = "2018"

  deps = [
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:log",
  ]

  sources = [ "component_to_test_standard_out.rs" ]
}

fuchsia_component("component_to_test_standard_out-component") {
  component_name = "component_to_test_standard_out"
  deps = [ ":component_to_test_standard_out" ]
  manifest = "meta/component_to_test_standard_out.cmx"
}

rustc_binary("error_logging_component") {
  name = "error_logging_component"
  source_root = "./error_logging_component.rs"
  edition = "2018"

  deps = [
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:log",
  ]

  sources = [ "error_logging_component.rs" ]
}

fuchsia_component("error_logging_component-component") {
  component_name = "error_logging_component"
  deps = [ ":error_logging_component" ]
  manifest = "meta/error_logging_component.cmx"
}

fuchsia_component("logging_component-component") {
  component_name = "logging_component"
  manifest = "//src/diagnostics/archivist/tests/logging-component/logging_component.cmx"
  deps = [ "//src/diagnostics/archivist/tests/logging-component:bin" ]
}

fuchsia_test_package("run_test_component_test-package") {
  package_name = "run_test_component_test"
  test_components = [ ":run_test_component_test-component" ]

  deps = [
    # These components are needed at runtime, but are not tests.
    ":component_to_test_standard_out-component",
    ":coverage_component-component",
    ":error_logging_component-component",
    ":logging_component-component",
  ]
}
