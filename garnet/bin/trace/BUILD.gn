# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

executable("bin") {
  output_name = "trace"

  sources = [ "main.cc" ]

  deps = [ ":trace_app" ]

  # TODO(fxbug.dev/46850): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

source_set("trace_app") {
  sources = [
    "app.cc",
    "app.h",
    "cmd_utils.cc",
    "cmd_utils.h",
    "command.cc",
    "command.h",
    "commands/list_categories.cc",
    "commands/list_categories.h",
    "commands/record.cc",
    "commands/record.h",
    "commands/time.cc",
    "commands/time.h",
    "tracer.cc",
    "tracer.h",
    "utils.cc",
    "utils.h",
  ]

  public_deps = [
    "//garnet/lib/trace_converters:chromium",
    "//sdk/fidl/fuchsia.tracing.controller",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
    "//third_party/zlib:zfstream",
    "//zircon/public/lib/fit",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace-reader",
  ]

  deps = [ ":options" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

source_set("options") {
  sources = [
    "options.cc",
    "options.h",
  ]
}

package("trace") {
  deps = [ ":bin" ]

  binaries = [
    {
      name = "trace"
      shell = true
    },
  ]

  meta = [
    {
      path = rebase_path("meta/trace.cmx")
      dest = "trace.cmx"
    },
  ]

  # If your build includes the target trace tool, your $host_tools_dir
  # contains the utilities to work with traces copied from the target.
  data_deps = [ "//garnet/bin/traceutil" ]
}
