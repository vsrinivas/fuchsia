# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")

executable("bin") {
  output_name = "sysmem-assistant"

  sources = [
    "main.cc",
  ]

  deps = [
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fxl",
    "//sdk/lib/fidl/cpp",
    "//zircon/public/fidl/fuchsia-hardware-tee",
    "//zircon/public/fidl/fuchsia-sysmem:fuchsia-sysmem_c",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/tee-client-api",
  ]
}

package("sysmem-assistant") {
  deps = [
    ":bin",
  ]

  binary = "sysmem-assistant"

  meta = [
    {
      path = rebase_path("meta/sysmem-assistant.cmx")
      dest = "sysmem-assistant.cmx"
    },
  ]
}

declare_args() {
  # The secmem TA must be obtained elsewhere and put into the firmware
  # directory.
  have_secmem_ta = false
}

package("ta-image") {
  deprecated_system_image = true

  if (have_secmem_ta) {
    secmem_ta_uuid = "2c1a33c0-44cc-11e5-bc3b-0002a5d5c51b"
    libraries = [
      {
        source = rebase_path("firmware/$secmem_ta_uuid.ta")
        name = "firmware/$secmem_ta_uuid.ta"
      },
    ]
  }
}

config_data("config") {
  # Don't launch without a TA to use.
  if (have_secmem_ta) {
    for_pkg = "sysmgr"
    outputs = [
      "sysmem-assistant.config",
    ]
    sources = [
      "sysmem-assistant.config",
    ]
  } else {
    for_pkg = ""
    outputs = []
    sources = []
  }
}
