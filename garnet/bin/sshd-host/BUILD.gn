# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("lib") {
  sources = [ "service.cc" ]
  public_deps = [
    "//sdk/fidl/fuchsia.boot",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/sys/lib/chrealm",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("bin") {
  output_name = "sshd-host"

  sources = [ "sshd-host.cc" ]

  deps = [
    ":lib",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

package("sshd-host") {
  deps = [
    ":bin",
    "//build/validate:non_production_tag",
    "//third_party/openssh-portable:hostkeygen",
    "//third_party/openssh-portable:scp",
    "//third_party/openssh-portable:sftp-server",
    "//third_party/openssh-portable:sshd",
  ]

  binaries = [
    {
      name = "sshd-host"
    },
    {
      name = "hostkeygen"
    },
    {
      name = "scp"
    },
    {
      name = "sftp-server"
    },
    {
      name = "sshd"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/sshd-host.cmx")
      dest = "sshd-host.cmx"
    },
  ]

  # Used by programs that need to load ICU locale data, such as tz-util.
  resources = [
    {
      path = rebase_path("//third_party/icu/common/icudtl.dat")
      dest = "icudtl.dat"
    },
  ]
}

config_data("config") {
  for_pkg = "sshd-host"
  sources =
      [ rebase_path("//third_party/openssh-portable/fuchsia/sshd_config") ]
  outputs = [ "sshd_config" ]
}

executable("test") {
  testonly = true

  output_name = "sshd-host-test"

  sources = [ "test.cc" ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.boot",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test_package("sshd-host-tests") {
  deps = [ ":test" ]

  tests = [
    {
      name = "sshd-host-test"
      environments = basic_envs
    },
  ]
}
