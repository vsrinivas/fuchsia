# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/sdk_executable.gni")
import("//build/host.gni")
import("//build/package.gni")
import("//build/test.gni")
import("//build/testing/environments.gni")

group("fidlcat") {
  testonly = true
  deps = [
    ":fidlcat_host_tests($host_toolchain)",
    ":fidlcat_lib_tests",
    ":fidlcat_tests",
    "lib",
  ]
}

if (current_toolchain == host_toolchain) {
  sdk_executable("bin") {
    category = "partner"

    output_name = "fidlcat"

    sources = [
      "main.cc",
    ]
    deps = [
      "//garnet/bin/fidlcat/lib",
      "//garnet/bin/fidlcat/lib:host_lib",
      "//src/lib/fxl",
      "//src/developer/debug/ipc:client",
      "//src/developer/debug/zxdb/client",
      "//src/developer/debug/zxdb/common",
      "//src/developer/debug/zxdb/console",
      "//third_party/rapidjson",
      "//zircon/public/lib/fidl_base",
    ]
  }

  test("fidlcat_host_tests") {
    deps = [
      "//garnet/bin/fidlcat/lib:host_tests",
      "//third_party/googletest:gtest_main",
    ]
  }
}

executable("fidlcat_lib_tests") {
  testonly = true
  output_name = "fidlcat_lib_tests"
  sources = []
  deps = [
    "//garnet/bin/fidlcat/lib:tests",
  ]
}

package("fidlcat_tests") {
  testonly = true
  deps = [
    ":fidlcat_lib_tests",
  ]
  tests = [
    {
      name = "fidlcat_lib_tests"
      environments = basic_envs
    },
  ]
}

install_host_tools("fidlcat_host") {
  deps = [
    ":bin",
  ]
  outputs = [
    "fidlcat",
  ]
}
