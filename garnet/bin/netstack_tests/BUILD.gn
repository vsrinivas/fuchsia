# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

executable("netstack_integration_test") {
  testonly = true

  sources = [
    "netstack_add_eth_test.cc",
    "netstack_filter_test.cc",
    "netstack_ioctl_test.cc",
  ]

  deps = [
    "//garnet/bin/netstack_tests/test_ioctl_client",
    "//garnet/lib/inet",
    "//garnet/public/lib/component/cpp/testing",
    "//garnet/public/lib/netemul/network:ethertap",
    "//garnet/public/lib/netstack/c",
    "//sdk/fidl/fuchsia.net.filter",
    "//sdk/fidl/fuchsia.netstack",
    "//sdk/lib/sys/cpp",
    "//third_party/googletest:gtest_main",
    "//zircon/public/banjo/ddk-protocol-ethernet",
    "//zircon/public/fidl/fuchsia-hardware-ethernet",
    "//zircon/public/fidl/fuchsia-net-stack",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
  ]
}

test("netstack_bsdsocket_c_api_test") {
  if (is_linux || is_fuchsia) {
    sources = [
      "bsdsocket_test.cc",
      "util.cc",
      "util.h",
    ]
  }

  deps = [
    "//third_party/googletest:gtest_main",
  ]
}

test("netstack_fuchsia_c_api_test") {
  sources = [
    "fdio_test.cc",
    "nodename_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//third_party/googletest:gtest_main",
    "//zircon/public/fidl/fuchsia-net:fuchsia-net_c",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/sync",
  ]
}

test_package("netstack_integration_tests") {
  deps = [
    ":netstack_integration_test",
  ]

  tests = [
    {
      name = "netstack_integration_test"
      environments = basic_envs
    },
  ]
}

test_package("netstack_c_api_tests") {
  deps = [
    ":netstack_bsdsocket_c_api_test",
    ":netstack_fuchsia_c_api_test",
  ]

  tests = [
    {
      name = "netstack_bsdsocket_c_api_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_fuchsia_c_api_test"
      environments = [ qemu_env ]
    },
  ]
}
