# Copyright 2019 The Fuchsia Authors.All rights reserved.
# Use of this source code is governed by a BSD - style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")

group("sw") {
  testonly = true
  deps = [
    ":codec_adapter_sw",
    ":codec_runner_sw",
    ":mpsc_queue",
    ":mpsc_queue_test_bin",
    ":mpsc_queue_tests",
    "ffmpeg",
    "omx",
  ]
}

config("codec_runner_sw_include_config") {
  include_dirs = [ "." ]
}

source_set("codec_runner_sw") {
  sources = [
    "local_single_codec_factory.h",
  ]

  public = [
    "codec_runner_app.h",
  ]

  deps = [
    "//garnet/lib/media/codec_impl",
    "//garnet/public/lib/component/cpp",
  ]
}

config("codec_adapter_sw_include_config") {
  include_dirs = [ "." ]
}

source_set("codec_adapter_sw") {
  sources = [
    "codec_adapter_sw.cc",
  ]

  public = [
    "codec_adapter_sw.h",
  ]

  public_configs = [ ":codec_adapter_sw_include_config" ]

  public_deps = [
    ":mpsc_queue",
    "//garnet/lib/media/codec_impl",
    "//src/lib/fxl",
    "//sdk/fidl/fuchsia.mediacodec",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

config("mpsc_queue_include_config") {
  include_dirs = [ "." ]
}

source_set("mpsc_queue") {
  visibility = [ "//garnet/bin/media/codecs/sw/*" ]

  public = [
    "mpsc_queue.h",
  ]

  public_configs = [ ":mpsc_queue_include_config" ]

  public_deps = [
    "//zircon/public/lib/fit",
  ]
}

executable("mpsc_queue_test_bin") {
  output_name = "mpsc_queue_tests"

  testonly = true

  sources = [
    "test/mpsc_queue_tests.cc",
  ]

  deps = [
    ":mpsc_queue",
    "//garnet/public/lib/fsl",
    "//third_party/googletest:gtest_main",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

test_package("mpsc_queue_tests") {
  deps = [
    ":mpsc_queue_test_bin",
  ]

  tests = [
    {
      name = "mpsc_queue_tests"
      environments = basic_envs
    },
  ]
}
