# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")

# Define pkg_resolver configuration data to be included in the
# config-data package.
#
#   disable_dynamic_configuration
#     [bool] Disable editing package URL rewrite rules and repository configs.
#
template("pkg_resolver_config") {
  cfg = {
    forward_variables_from(invoker, [ "disable_dynamic_configuration" ])
    assert(defined(disable_dynamic_configuration),
           "disable_dynamic_configuration must be defined")
  }

  config_path = "$target_gen_dir/pkg_resolver_config.json"

  write_file(config_path, cfg, "json")

  config_data(target_name) {
    for_pkg = "pkg_resolver"
    outputs = [
      "config.json",
    ]
    sources = [
      config_path,
    ]
  }
}
