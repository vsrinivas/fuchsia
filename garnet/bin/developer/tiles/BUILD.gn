# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("tiles_lib") {
  sources = [
    "tiles.cc",
    "tiles.h",
  ]

  public_deps = [
    "//garnet/public/lib/svc/cpp",
    "//sdk/fidl/fuchsia.developer.tiles",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/fidl/fuchsia.ui.viewsv1",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//zircon/public/lib/async-cpp:async-cpp",
    "//zircon/public/lib/zx",
  ]

  deps = [
    "//src/lib/fxl",
  ]
}

executable("bin") {
  output_name = "tiles"

  sources = [
    "main.cc",
  ]

  deps = [
    ":tiles_lib",
    "//src/lib/fxl",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/lib/sys/cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/trace-provider",
  ]
}

package("tiles") {
  meta = [
    {
      path = "meta/tiles.cmx"
      dest = "tiles.cmx"
    },
  ]

  binary = "tiles"

  deps = [
    ":bin",
  ]
}

executable("tiles_unittests") {
  testonly = true

  sources = [
    "tiles_unittest.cc",
  ]

  deps = [
    ":tiles_lib",
    "//src/lib/fxl",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//third_party/googletest:gtest_main",
  ]
}

test_package("tiles_tests") {
  deps = [
    ":tiles_unittests",
  ]

  tests = [
    {
      name = "tiles_unittests"
      environments = basic_envs
    },
  ]
}
