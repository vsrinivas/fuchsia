# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("integration_tests") {
  testonly = true
  deps = [
    ":appmgr_integration_tests",
    ":hub_tests",
    ":realm_tests",
    "components",
    "fidl",
    "inspect",
    "mock_runner",
    "sandbox",
    "util",
  ]
}

executable("realm_tests") {
  testonly = true

  output_name = "appmgr_realm_integration_tests"

  sources = [
    "mock_runner_registry.cc",
    "mock_runner_registry.h",
    "realm_integration_test.cc",
    "realm_runner_integration_test.cc",
  ]

  deps = [
    "fidl:test.component.mockrunner",
    "mock_runner:appmgr_mock_runner",
    "util:data_file_reader_writer",
    "util:data_file_reader_writer_util",
    "//garnet//examples/fidl/services:echo",
    "//garnet/bin/appmgr:lib",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

executable("hub_tests") {
  testonly = true

  output_name = "appmgr_hub_integration_tests"

  sources = [
    "hub_integration_test.cc",
  ]

  deps = [
    "//garnet/bin/sysmgr:lib",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration",
    "//third_party/googletest:gmock",
  ]
}

test_package("appmgr_integration_tests") {
  deps = [
    ":hub_tests",
    ":realm_tests",
  ]

  tests = [
    {
      name = "appmgr_realm_integration_tests"
      environments = basic_envs
    },
    {
      name = "appmgr_hub_integration_tests"
      environments = basic_envs
    },
  ]
}
