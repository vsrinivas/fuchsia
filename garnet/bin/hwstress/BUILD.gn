# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")

source_set("lib") {
  sources = [
    "args.cc",
    "cpu.cc",
    "hwstress.cc",
    "util.cc",
  ]

  public_deps = [
    "//src/lib/fxl",
    "//zircon/public/lib/cmdline",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxc",
  ]
}

executable("bin") {
  output_name = "hwstress"

  sources = [ "main.cc" ]

  deps = [ ":lib" ]
}

package("hwstress") {
  deps = [ ":bin" ]

  binaries = [
    {
      shell = true
      name = "hwstress"
    },
  ]
}

executable("hwstress_test") {
  testonly = true

  sources = [
    "args_test.cc",
    "cpu_test.cc",
    "hwstress_test.cc",
    "util_test.cc",
  ]

  deps = [
    ":lib",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/public/lib/zx",
  ]
}

test_package("hwstress_test_pkg") {
  package_name = "hwstress-tests"

  deps = [ ":hwstress_test" ]

  tests = [
    {
      name = "hwstress_test"
    },
  ]
}

group("tests") {
  testonly = true
  public_deps = [ ":hwstress_test_pkg" ]
}
