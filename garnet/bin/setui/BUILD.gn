# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("setui") {
  testonly = true
  deps = [
    ":bin",
    ":setui_service",
    ":setui_service_tests",
  ]
}

rustc_binary("bin") {
  name = "setui_service"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.setui:fuchsia.setui-rustc",
    "//sdk/fidl/fuchsia.timezone:fuchsia.timezone-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_derive",
    "//third_party/rust_crates:serde_json",
  ]
}

package("setui_service") {
  deps = [
    ":bin",
  ]

  binary = "setui_service"

  meta = [
    {
      path = rebase_path("meta/setui_service.cmx")
      dest = "setui_service.cmx"
    },
  ]
}

unittest_package("setui_service_tests") {
  deps = [
    ":bin",
  ]

  tests = [
    {
      name = "setui_service_bin_test"
      environments = basic_envs
    },
  ]
}
