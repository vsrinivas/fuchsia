# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//garnet/lib/magma/gnbuild/magma.gni")
import("//src/graphics/lib/vulkan/layers.gni")

group("ui") {
  testonly = true
  deps = [
    ":root_presenter",
    ":scenic_tests",
    ":set_renderer_params",
    "benchmarks",
    "gltf_export",
    "input",
    "input_reader",
    "present_view",
    "presentation_mode",
    "print_input",
    "root_presenter",
    "screencap",
    "set_renderer_params_HACK",
    "snapshot",
    "tests",
    "text",
    "//src/ui/bin/ime:ime_service",
    "//src/ui/bin/ime:ime_test",
    "//src/ui/scenic",
  ]
}

test_package("scenic_tests") {
  deps = [
    "input_reader/tests",
    "present_view/tests",
    "presentation_mode/tests",
    "root_presenter/tests",
    "//src/ui/scenic/lib/display/tests",
    "//src/ui/scenic/lib/flatland/tests",
    "//src/ui/scenic/lib/gfx/tests",
    "//src/ui/scenic/lib/gfx/util/tests",
    "//src/ui/scenic/lib/input/tests",
    "//src/ui/scenic/lib/scenic/tests",
  ]

  tests = [
    {
      name = "gfx_apptests"
      environments = basic_envs
    },
    {
      name = "gfx_pixeltests"
      environments = magma_libvulkan_hardware_envs
    },
    {
      name = "gfx_unittests"
      environments = basic_envs + magma_libvulkan_hardware_envs
    },
    {
      name = "gfx_util_unittests"
      environments = basic_envs
    },
    {
      name = "gfx_viewstate_apptests"
      environments = magma_libvulkan_hardware_envs
    },
    {
      name = "input_apptests"
      environments = basic_envs
    },
    {
      name = "input_reader_unittests"
      environments = basic_envs
    },
    {
      name = "input_unittests"
      environments = basic_envs
    },
    {
      name = "presentation_mode_unittests"
      environments = basic_envs
    },
    {
      name = "present_view_unittests"
      environments = basic_envs
    },
    {
      name = "present_view_apptests"
      environments = basic_envs
    },
    {
      name = "root_presenter_tests"
      environments = basic_envs
    },
    {
      name = "scenic_unittests"
      environments = basic_envs
    },
    {
      name = "flatland_unittests"
      environments = basic_envs
    },
    {
      name = "display_unittests"
      environments = basic_envs
    },
  ]

  public_deps = vulkan_validation_layers.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules
  resources = vulkan_validation_layers.resources
}

package("root_presenter") {
  deps = [
    "root_presenter",
  ]

  binaries = [
    {
      name = "root_presenter"
    },
  ]

  resources = [
    {
      path = rebase_path("root_presenter/resources/cursor32.png")
      dest = "cursor32.png"
    },
  ]

  meta = [
    {
      path = rebase_path("root_presenter/meta/root_presenter.cmx")
      dest = "root_presenter.cmx"
    },
  ]
}

# Component shell tools can be run as shell command or as components via a
# component URI. The following list contains the names of such components that
# follow the pattern:
# name/meta/name.cmx contains the component manifest
# name is the name of a subdirectory that builds the output binary `name`
component_shell_tools = [
  "gltf_export",
  "present_view",
  "screencap",
  "snapshot",
]
foreach(tool, component_shell_tools) {
  package(tool) {
    meta = [
      {
        path = rebase_path("$tool/meta/$tool.cmx")
        dest = "$tool.cmx"
      },
    ]

    deps = [
      tool,
    ]

    binaries = [
      {
        name = tool
        shell = true
      },
    ]
  }
}

package("set_renderer_params") {
  deps = [
    "set_renderer_params_HACK:set_renderer_params",
  ]

  binaries = [
    {
      name = "set_renderer_params"
      shell = true
    },
  ]

  meta = [
    {
      path =
          rebase_path("set_renderer_params_HACK/meta/set_renderer_params.cmx")
      dest = "set_renderer_params.cmx"
    },
  ]
}

# Non-component tools contains the names of shell command tools that do not
# have any CMX.
non_component_tools = [
  "input",
  "print_input",
]

foreach(tool, non_component_tools) {
  package(tool) {
    deps = [
      tool,
    ]
    binaries = [
      {
        name = tool
        shell = true
      },
    ]
  }
}
