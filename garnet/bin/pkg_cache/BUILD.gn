# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_binary("bin") {
  name = "pkg_cache"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//garnet/lib/rust/fidl_fuchsia_pkg_ext",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

package("pkg_cache") {
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "rust_crates/pkg_cache"
      dest = "pkg_cache"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/pkg_cache.cmx")
      dest = "pkg_cache.cmx"
    },
  ]
}

test_package("pkg_cache_tests") {
  deps = [
    ":bin",
  ]

  tests = [
    {
      name = "pkg_cache_bin_test"
      environments = basic_envs
    },
  ]
}
