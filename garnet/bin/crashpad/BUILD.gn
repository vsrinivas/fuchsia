# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/package/component.gni")

package("crashpad_analyzer") {
  components = [ ":component" ]
}

fuchsia_component("component") {
  deprecated_global_persistent_storage = "//build"
  binary = "crashpad_analyzer"

  manifest = "meta/crashpad_analyzer.cmx"

  resources = [
    {
      path = "configs/default.json"
      dest = "default_config.json"
    },
  ]

  deps = [
    ":main",
  ]
}

executable("main") {
  output_name = "crashpad_analyzer"

  sources = [
    "main.cc",
  ]

  deps = [
    ":src",
    "//garnet/public/lib/syslog/cpp",
    "//sdk/lib/sys/cpp",
    "//zircon/public/fidl/fuchsia-crash",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

source_set("src") {
  sources = [
    "config.cc",
    "config.h",
    "crashpad_analyzer_impl.cc",
    "crashpad_analyzer_impl.h",
    "report_annotations.cc",
    "report_annotations.h",
    "report_attachments.cc",
    "report_attachments.h",
    "scoped_unlink.h",
  ]

  public_deps = [
    "//garnet/public/lib/fxl",
    "//sdk/lib/fidl/cpp",
    "//third_party/crashpad/client",
    "//third_party/crashpad/util",
    "//zircon/public/fidl/fuchsia-crash",
    "//zircon/public/fidl/fuchsia-mem",
    "//zircon/public/lib/zx",
  ]

  deps = [
    "//garnet/public/lib/syslog/cpp",
    "//third_party/crashpad/handler",
    "//third_party/crashpad/minidump",
    "//third_party/crashpad/third_party/mini_chromium/mini_chromium/base",
    "//third_party/rapidjson",
    "//zircon/public/fidl/fuchsia-sysinfo:fuchsia-sysinfo_c",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
  ]
}

package("upload_to_prod_server_config") {
  deprecated_system_image = true

  resources = [
    {
      path = "configs/upload_to_prod_server.json"
      dest = "crashpad_analyzer/override_config.json"
    },
  ]
}
