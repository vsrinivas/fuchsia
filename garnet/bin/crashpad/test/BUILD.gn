# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

test_package("crashpad_analyzer_tests") {
  tests = [
    {
      name = "config_unittest"
      environments = basic_envs
    },
    {
      name = "crashpad_analyzer_impl_unittest"
      environments = basic_envs
    },
    {
      name = "crashpad_analyzer_integration_test"
    },
  ]

  binaries = [
    # needed for crashpad_analyzer_impl_unittest
    {
      name = "crasher_exe"
    },
  ]

  resources = [
    # needed for config_unittest
    {
      path = "configs/valid_no_upload.json"
      dest = "valid_config_no_upload.json"
    },

    # needed for config_unittest
    {
      path = "configs/valid_upload.json"
      dest = "valid_config_upload.json"
    },

    # needed for config_unittest
    {
      path = "configs/valid_no_upload_spurious_server.json"
      dest = "valid_config_no_upload_spurious_server.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_spurious_field.json"
      dest = "bad_schema_spurious_field_config.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_missing_required_field.json"
      dest = "bad_schema_missing_required_field_config.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_missing_server.json"
      dest = "bad_schema_missing_server_config.json"
    },
  ]

  deps = [
    ":config_unittest",
    ":crasher_exe",
    ":crashpad_analyzer_impl_unittest",
    ":crashpad_analyzer_integration_test",
  ]
}

executable("config_unittest") {
  testonly = true

  sources = [
    "config_unittest.cc",
  ]

  deps = [
    "//garnet/bin/crashpad:src",
    "//garnet/public/lib/syslog/cpp",
    "//third_party/googletest:gtest_main",
  ]
}

executable("crasher_exe") {
  testonly = true

  sources = [
    "crasher_exe.cc",
  ]
}

executable("crashpad_analyzer_impl_unittest") {
  testonly = true

  sources = [
    "crashpad_analyzer_impl_unittest.cc",
  ]

  deps = [
    "//garnet/bin/crashpad:src",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/syslog/cpp",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest_main",
    "//zircon/public/fidl/fuchsia-crash",
    "//zircon/public/fidl/fuchsia-mem",
    "//zircon/public/lib/zx",
  ]
}

executable("crashpad_analyzer_integration_test") {
  testonly = true

  sources = [
    "crashpad_analyzer_integration_test.cc",
  ]

  deps = [
    "//garnet/public/lib/component/cpp/testing",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/gtest",
    "//garnet/public/lib/syslog/cpp",
    "//third_party/googletest:gtest_main",
    "//zircon/public/fidl/fuchsia-crash",
    "//zircon/public/fidl/fuchsia-mem",
    "//zircon/public/lib/zx",
  ]
}
