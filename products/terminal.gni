# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//products/core.gni")

core_realm_package_name = "core-terminal"

legacy_base_package_labels += [
  "//src/fonts",
  "//src/intl/intl_services:pkg",
]

# Remove intl_services default shards that were added in core.gni.
core_realm_shards += [ "//src/intl/intl_services:intl_services_shard" ]
core_realm_shards -= [ "//src/intl/intl_services:intl_services_small_shard" ]

# Include sysmgr.
# TODO(fxbug.dev/108016, fxbug.dev/100732): Remove once tiles is migrated.
legacy_base_package_labels -= [ "//src/sys/appmgr" ]
legacy_base_package_labels += [
  "//src/sys/appmgr:appmgr_with_sysmgr",
  "//src/sys/sysmgr",
]

# Add build type config data
legacy_base_package_labels += [
  "//src/cobalt/bin/app:build_type_eng",
  "//src/fonts:build_type_eng",
]

# Add UI core shard and packages on terminal.
core_realm_shards += [ "//src/ui:ui_core_shard" ]
legacy_cache_package_labels += [
  "//src/fonts/collections:small-open-fonts-collection",
  "//src/ui:ui_packages",
  "//src/ui/bin/terminal",
  "//src/ui/tools/tiles",

  # Used to mock out display hardware in UI integration tests.
  "//src/ui/bin/hardware_display_controller_provider",
]

# End to end (E2E) `dart_inspect_benchmarks` test requires the dart runner.
core_realm_shards += [ "//src/dart:dart_runner_core_shard" ]

# Build with both JIT and AOT non-product dart runners. JIT is used for debug
# builds, and AOT is used for release builds.
legacy_base_package_labels += [
  "//src/dart:dart_jit",
  "//src/dart:dart_aot",
]

# End to end (E2E) product test runtime dependencies. Must be added to cache,
# not universe, so the test can run on bots where there is no package server.
#
# TODO(fxbug.dev/9892): Support a package server on bots so this can be added to
# universe like the tests.
legacy_cache_package_labels += [
  # Used for Chromium tests for example:
  #    //src/chromium/web_runner_tests:tests
  "//src/testing/fidl/intl_property_manager",

  # General end to end dependencies.
  "//src/tests/end_to_end/bundles:end_to_end_deps",
  "//src/tests/end_to_end/bundles:terminal_end_to_end_deps",

  # CFv2 test framework.
  "//src/sys/run_test_suite",

  # Fake build info service
  "//src/developer/build_info/testing:testing",

  # Archivist for manual embedding in tests. Note that this archivist won't attribute logs by
  # moniker and is unable to ingest Inspect. Used in Chromium tests.
  "//src/diagnostics/archivist:archivist-without-attribution",
]

# Add SDK tools set to "cache" so these tools are automatically available in
# QEMU images generated for the SDK, but in cache, so they can be updated.
legacy_cache_package_labels += [ "//sdk/bundles:tools" ]

core_realm_shards += [
  "//src/media/codec:codec_factory_shard",
  "//src/media/audio/audio_core:core_shard",
]

# Add cfv2 sl4f to base.
core_realm_shards += [ "//src/testing/sl4f:sl4f_terminal_core_shard" ]
legacy_base_package_labels += [ "//src/sys/bin/start_sl4f" ]

legacy_universe_package_labels += [
  # E2E Product Tests. They don't produce fuchsia packages, but host
  # executables, and must just be included somewhere in the build configuration
  # of the product.
  "//src/tests/end_to_end/sl4f:test",
  "//src/tests/end_to_end/perf:test",
  "//src/tests/end_to_end/time_sync:test",

  # Only for products in which Flutter is supported.
  "//src/tests/intl/timezone:tests-for-flutter",
]

# Add Test UI Stack for use in tests.
# TODO(fxbug.dev/105105): Remove once we migrate client usage to subpackages.
legacy_cache_package_labels +=
    [ "//src/ui/testing/test_ui_stack:test-ui-stack-flavors" ]

# Use the product assembly config file to specify the product configuration.
#
# This should be overridden by products which inherit from minimal.
fuchsia_product_assembly_config_label = "//products/minimal"
fuchsia_product_assembly_config_file = "${root_build_dir}/obj/products/minimal/minimal/product_assembly_config.json"

fuchsia_structured_config_policy =
    "//products/minimal/minimal_user_structured_config_policy.json5"
