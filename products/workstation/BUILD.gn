# Copyright 2022 The Fuchsia Authors.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/product_assembly_configuration.gni")
import("//build/bazel/bazel_inputs.gni")

assert(
    is_fuchsia,
    "Product definitions and assemblies should only be defined in the fuchsia toolchain")

workstation_platform_config = {
  build_type = "eng"
  identity = {
    password_pinweaver = "required"
  }
  input = {
    supported_input_devices = [
      "keyboard",
      "mouse",
      "touchscreen",
    ]
  }
}

workstation_product_config = {
  session_url = "fuchsia-pkg://fuchsia.com/workstation_session#meta/workstation_routing.cm"
}

product_assembly_configuration("workstation") {
  platform = workstation_platform_config
  product = workstation_product_config
}

bazel_input_resource("workstation_product_config") {
  sources = [ "${target_out_dir}/workstation/product_assembly_config.json" ]
  outputs = [ "products/workstation/workstation/product_assembly_config.json" ]
  deps = [ ":workstation" ]
}

# TODO(https://fxbug.dev/107089) remove this product once workstation_eng can start paused
product_assembly_configuration("workstation_paused") {
  platform = workstation_platform_config
  product = {
    # We want to inherit all of workstation_eng's config except the session URL.
    forward_variables_from(workstation_product_config, "*", [ "session_url" ])

    session_url = ""
  }
}
